; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > s.ModHead

        ASSERT  (.=Module_BaseAddr)

MySWIBase       *       Module_SWISystemBase + FilterSWI * Module_SWIChunkSize

        DCD     0                 ;Start - Module_BaseAddr
        DCD     Init              - Module_BaseAddr
        DCD     Die               - Module_BaseAddr
        DCD     Service           - Module_BaseAddr
        DCD     Title             - Module_BaseAddr
        DCD     Helpstr           - Module_BaseAddr
        DCD     Helptable         - Module_BaseAddr
        DCD     MySWIBase
        DCD     Filter_SWIdecode  - Module_BaseAddr
        DCD     Filter_SWInames   - Module_BaseAddr
        DCD     0
      [ international_help
        DCD     message_file_name - Module_BaseAddr
      |
        DCD     0
      ]
      [ :LNOT: No32bitCode
        DCD     ModuleFlags       - Module_BaseAddr
      ]

; ----------------------------------------------------------------------------------------------------------------------
Title   DCB     "FilterManager",0
Helpstr DCB     "Filter Manager",9,"$Module_HelpVersion"
        DCB     0
        ALIGN
        
; ----------------------------------------------------------------------------------------------------------------------

prefilter_token         DCB     "PreFT",0
postfilter_token        DCB     "PostFT",0
rectfilter_token        DCB     "RectFT",0
copyfilter_token        DCB     "CopyFT",0
postrectfilter_token    DCB     "PostRectFT",0
posticonfilter_token    DCB     "PostIconFT",0
filter_token            DCB     "Filter",0
task_token              DCB     "Task",0
mask_token              DCB     "Mask",0
all_token               DCB     "All",0
                        ALIGN

      [ :LNOT: No32bitCode
ModuleFlags             DCD     ModuleFlag_32bit
      ]

min_gap         *       3
min_tab1        *       20
min_tab2        *       24
min_tab3        *       8

; ---------------------------------------------------------------------------------------------------------------------
;       Handle *Filters - List all attached filters with the tasks they are attached to.
Filters_Code
        Push    "r7-r8,LR"
        Debug   xx,"*Filters called listing pre_filters"

        LDR     wsptr,[R12]

        ADR     r1, prefilter_token
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        Pull    "r7-r8,PC",VS

        MOV     r4, #0                  ; r4 = how much space we are using on the stack to hold column headings.

        ADR     r1, filter_token
        BL      lookuptostack
        BVS     %FT05
        STR     sp, filter_text
        ADD     r5, r3, #min_gap        ; r5 = width of column 1
        CMP     r5, #min_tab1
        MOVCC   r5, #min_tab1

        ADR     r1, task_token
        BL      lookuptostack
        BVS     %FT05
        STR     sp, task_text
        ADD     r6, r3, #min_gap        ; r6 = width of column 2
        CMP     r6, #min_tab2
        MOVCC   r6, #min_tab2

        ADR     r1, mask_token
        BL      lookuptostack
        BVS     %FT05
        STR     sp, mask_text
        ADD     r7, r3, #min_gap        ; r7 = width of column 3
        CMP     r7, #min_tab3
        MOVCC   r7, #min_tab3

        ADR     r1, all_token
        BL      lookuptostack
        BVS     %FT05
        STR     sp, all_text

        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05
        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield_dashed
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield_dashed
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, pre_filters
        B       %FT02
01
        LDR     r0, [r3, #f_name]
        MOV     r1, r5
        BL      writefield
        BVS     %FT05

        LDR     r0, [r3, #f_task]
        CMP     r0, #0
        LDREQ   r0, all_text
        SWINE   XTaskManager_TaskNameFromHandle
        BVS     %FT05
        MOV     r1, r6
        BL      writefield
        SWI     XOS_NewLine
        BVS     %FT05

        LDR     r3, [r3, #next_ptr]
02
        CMP     r3, #0
        BNE     %BT01

        SWI     XOS_NewLine

        ADRL    r1, postfilter_token
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield
        BVS     %FT05
        LDR     r0, mask_text
        MOV     r1, r7
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05
        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield_dashed
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield_dashed
        BVS     %FT05
        LDR     r0, mask_text
        MOV     r1, r7
        BL      writefield_dashed
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, post_filters
        B       %FT04
03
        LDR     r0, [r3, #f_name]
        MOV     r1, r5
        BL      writefield
        BVS     %FT05

        LDR     r0, [r3, #f_task]
        CMP     r0, #0
        LDREQ   r0, all_text
        SWINE   XTaskManager_TaskNameFromHandle
        MOVVC   r1, r6
        BLVC    writefield
        BVS     %FT05

        LDR     r0, [r3, #f_mask]
        ADR     r1, convert_buffer
        MOV     r2, #10
        SWI     XOS_ConvertHex8
        SWIVC   XOS_Write0
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, [r3, #next_ptr]
04
        CMP     r3, #0
        BNE     %BT03

        SWI     XOS_NewLine
        BVS     %FT05

        ADRL    r1, rectfilter_token
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05
        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield_dashed
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield_dashed
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, rect_filters
        B       %FT11

10
        LDR     r0, [r3, #f_name]
        MOV     r1, r5
        BL      writefield
        BVS     %FT05

        LDR     r0, [r3, #f_task]
        CMP     r0, #0
        LDREQ   r0, all_text
        SWINE   XTaskManager_TaskNameFromHandle
        MOVVC   r1, r6
        BLVC    writefield
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, [r3, #next_ptr]
11
        CMP     r3, #0
        BNE     %BT10

        SWI     XOS_NewLine
        BVS     %FT05

        ADRL    r1, postrectfilter_token
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05
        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield_dashed
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield_dashed
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, post_rect_filters
        B       %FT31

30
        LDR     r0, [r3, #f_name]
        MOV     r1, r5
        BL      writefield
        BVS     %FT05

        LDR     r0, [r3, #f_task]
        CMP     r0, #0
        LDREQ   r0, all_text
        SWINE   XTaskManager_TaskNameFromHandle
        MOVVC   r1, r6
        BLVC    writefield
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, [r3, #next_ptr]
31
        CMP     r3, #0
        BNE     %BT30

        SWI     XOS_NewLine
        BVS     %FT05


        ADRL    r1, posticonfilter_token
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05
        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield_dashed
        BVS     %FT05
        LDR     r0, task_text
        MOV     r1, r6
        BL      writefield_dashed
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, post_icon_filters
        B       %FT41

40
        LDR     r0, [r3, #f_name]
        MOV     r1, r5
        BL      writefield
        BVS     %FT05

        LDR     r0, [r3, #f_task]
        CMP     r0, #0
        LDREQ   r0, all_text
        SWINE   XTaskManager_TaskNameFromHandle
        MOVVC   r1, r6
        BLVC    writefield
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, [r3, #next_ptr]
41
        CMP     r3, #0
        BNE     %BT40

        SWI     XOS_NewLine
        BVS     %FT05


        ADRL    r1, copyfilter_token
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05
        LDR     r0, filter_text
        MOV     r1, r5
        BL      writefield_dashed
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, copy_filters
        B       %FT13
12
        LDR     r0, [r3, #f_name]
        MOV     r1, r5
        BL      writefield
        SWIVC   XOS_NewLine
        BVS     %FT05

        LDR     r3, [r3, #next_ptr]
13
        CMP     r3, #0
        BNE     %BT12
05
        ADD     sp, sp, r4
        Pull    "r7-r8,PC"

lookuptostack
; In:   r1->token
;       r4=current size of data on stack
; Out:  r1,r2 corrupted
;       r3=length of text found
;       r4=new size of data on stack
;       sp->text
        MOV     r8, lr                  ; Don't use stack in here (BLs are OK though)
        MOV     r2, #0
        BL      MsgTrans_Lookup
        MOVVS   pc, r8

        ADD     r1, r3, #4              ; Word align length (allow for terminator).
        BIC     r1, r1, #3
        SUB     sp, sp, r1              ; Allocate space on stack.
        ADD     r4, r4, r1

        MOV     r1, sp
06
        LDRB    r0, [r2], #1
        CMP     r0, #" "
        STRCSB  r0, [r1], #1
        BCS     %BT06
        MOV     r0, #0
        STRB    r0, [r1]
        MOV     pc, r8

writefield
; Write control-terminated string, padded with spaces to field width
; In:   r0 = text to print
;       r1 = field width
; Out:  r0,r2 corrupted
        Push    "lr"
        MOV     r2, r0
        BL      Write32
        Pull    "pc",VS
        SUB     r0, r0, r2
        SUB     r0, r1, r0
07
        SWI     XOS_WriteI+" "
        Pull    "pc",VS
        SUBS    r0, r0, #1
        BGE     %BT07
        Pull    "pc"

Write32
; Output a control character terminated string to fit in a given field width
; In:   R0 -> Ctrl-terminated string
;       R1 =  field width
        Push    "R1,R2,LR"
;
        SUB     R1,R1,#min_gap
        MOV     R2,R0
01      LDRB    R0,[R2],#1
        CMP     R0,#32
        SUBHSS  R1,R1,#1
        MOVLO   R0,R2
        Pull    "R1,R2,PC",LO
        SWI     XOS_WriteC
        BVC     %BT01
        Pull    "R1,R2,PC"

writefield_dashed
; Write dashes for the length of the given control-terminated string, and pad with spaces to field width
; In:   r0 = text string
;       r1 = field width
; Out:  r0,r2 corrupted
        Push    "lr"
        MOV     r2, r0
01
        LDRB    lr, [r0], #1
        CMP     lr, #32
        SWIHS   XOS_WriteI+"-"
        Pull    "pc",VS
        BHS     %BT01
        SUB     r0, r0, r2
        SUB     r0, r1, r0
07
        SWI     XOS_WriteI+" "
        Pull    "pc",VS
        SUBS    r0, r0, #1
        BGE     %BT07
        Pull    "pc"

; ----------------------------------------------------------------------------------------------------------------------
Helptable
      [ international_help
        Command "Filters", 0, 0, International_Help
        DCB     0
      |
        Command "Filters", 0, 0, 0
        DCB     0
      ]
; ----------------------------------------------------------------------------------------------------------------------
      [ international_help
Filters_Help    DCB     "HFLMFLT", 0
Filters_Syntax  DCB     "SFLMFLT", 0
      |
Filters_Help
        DCB     "*Filters displays all Wimp filters currently active.", 0
Filters_Syntax
        DCB     "Syntax: *Filters", 0
      ]
        ALIGN
; ----------------------------------------------------------------------------------------------------------------------
;       Module initialisation point
Init
        Push    "r0-r3,LR"

        LDR     r2, [r12]
        CMP     r2, #0                  ; clears V
        BNE     %FT01

        MOV     r0, #ModHandReason_Claim
        MOV     r3, #max_running_work
        SWI     XOS_Module
        ADDVS   sp,sp,#4
        Pull    "r1-r3,PC",VS

        STR     r2, [r12]
01
        MOV     r12, r2

; Clear list pointers

        MOV     r0, #0
        STR     r0, pre_filters
        STR     r0, post_filters
        STR     r0, rect_filters
        STR     r0, copy_filters
        STR     r0, post_rect_filters
        STR     r0, post_icon_filters
        STR     r0, flags               ; Clear flags.
        [ No26bitCode
        STR     r0, fake_postrect
        ]

; If we're on a Wimp earlier than 3.86, then we need to fake post-rectangle filters
        MOV     r0, #7
        SWI     Wimp_ReadSysInfo
        SUB     r0, r0, #256
        [ :LNOT: No26bitCode
        CMP     r0, #386-256
        MOVLT   r1, #1
        MOVGE   r1, #0
        STR     r1, fake_postrect
        ]
; If we're on a Wimp earlier than 3.99, then we cannot rely on getting handles from r10
        CMP     r0, #399-256
        MOVLT   r1, #0
        MOVGE   r1, #1
        STR     r1, get_handle_from_r10

        [ standalone
        BL      declareresourcefsfiles
        ]

        ADR     R0,servicecallback
        MOV     R1,WP                   ; workspace pointer
        SWI     XOS_AddCallBack         ;   install the call back to broadcast we are here message
        MOVVC   R0,#-1
        STRVC   R0,callbackpending      ; and then setup a suitable flag to indicate we have a callback pending
        Pull    "r0-r3,LR"              ; No errors
        RETURNVC                        ; clear V

;;-----------------------------------------------------------------------------
;; When the CallBack is granted we then broadcast a service call so that
;;   all Filters can be registered that are required.
;;-----------------------------------------------------------------------------

servicecallback
        EntryS  "R0-R1"

        MOV     R1,#0                   ; the callback has been granted, so flag as no longer pending
        STR     R1,callbackpending

        LDR     R0,=Module_Version
        MOV     R1,#Service_FilterManagerInstalled
        SWI     XOS_ServiceCall         ; broadcast the service

        EXITS
        LTORG

; ----------------------------------------------------------------------------------------------------------------------
;       Module service entry point
ServiceTable
        ASSERT  Service_ResourceFSStarting < Service_WimpRegisterFilters
        DCD     0                               ; flags word
        DCD     Service2 - Module_BaseAddr
      [ standalone
        DCD     Service_ResourceFSStarting
      ]
        DCD     Service_WimpRegisterFilters
        DCD     0                               ; terminator

        DCD     ServiceTable - Module_BaseAddr
Service
        MOV     R0, R0                          ; flag service table to aware kernels
        TEQ     R1,#Service_WimpRegisterFilters
      [ standalone
        TEQNE   R1,#Service_ResourceFSStarting
      ]
        MOVNE   PC,LR
Service2
      [ standalone
        TEQ     R1,#Service_ResourceFSStarting
        BEQ     serviceresourcefsstarting
      ]

        LDR     R12, [R12]
        CMP     R12, #0
        MOVEQ   PC, LR                  ; Exit if no workspace

;-------------------------------------
; Register our filters with the Wimp
; In:
;  R12 -> workspace
;
; Out:
;   VC if filters registered

RegisterFilters
        Push    "r0-r2,LR"

        [ :LNOT: No26bitCode
        LDR     LR,fake_postrect
        TEQ     LR,#0
        MOVNE   R0,#0
        STRNE   R0,int_get_rectangle_reentry_addr
        ]

        LDR     LR,pre_filters
        CMP     LR,#0                   ; Any pre-filters?
        MOVNE   R0,#0                   ; Pre_filter
        ADRNEL  R1,pre_filter_handler
        MOVNE   R2,R12

        Debug   register,"Register pre-filter",R0,R1,R2,LR

        SWINE   XWimp_RegisterFilter    ; Yes then register

        DebugE  register,"Cant register pre-filter "

        ADDVS   r13,r13,#4
        Pull    "r1-r2,PC",VS

        LDR     LR,rect_filters
        CMP     LR,#0                   ; Any rect-filters?
        BNE     %FT01

        LDR     LR,post_rect_filters
        CMP     LR,#0                   ; how about post-rect-filters?
        BEQ     %FT02
        LDR     LR,fake_postrect
        CMP     LR,#0

01      MOVNE   R0,#3                   ; rect_filter
        ADRNEL  R1,rect_filter_handler
        MOVNE   R2,R12

        Debug   register,"Register rect-filter",R0,R1,R2,LR

        SWINE   XWimp_RegisterFilter    ; Yes then register

        DebugE  register,"Cant register rect-filter "

        ADDVS   r13,r13,#4
        Pull    "r1-r2,PC",VS

02      LDR     LR,copy_filters
        CMP     LR,#0                   ; Any copy-filters?
        MOVNE   R0,#2                   ; copy_filter
        ADRNEL  R1,copy_filter_handler
        MOVNE   R2,R12

        Debug   register,"Register copy-filter",R0,R1,R2,LR

        SWINE   XWimp_RegisterFilter    ; Yes then register

        DebugE  register,"Cant register copy-filter "

        ADDVS   r13,r13,#4
        Pull    "r1-r2,PC",VS

        LDR     LR,post_filters
        CMP     LR,#0                   ; Any post-filters?
        MOVNE   R0,#WimpFilter_PostPoll
        ADRNEL  R1,post_filter_handler
        MOVNE   R2,R12

        Debug   register,"Register post-filter",R0,R1,R2,LR

        SWINE   XWimp_RegisterFilter    ; Yes then register

        DebugE  register,"Cant register post-filter "

        ADDVS   r13,r13,#4
        Pull    "r1-r2,PC",VS

        LDR     LR,post_icon_filters
        CMP     LR,#0                   ; Any post-icon-filters?
        MOVNE   R0,#WimpFilter_PostIconGetRectangle
        ADRNEL  R1,post_icon_filter_handler
        MOVNE   R2,R12

        Debug   register,"Register post-icon-filter",R0,R1,R2,LR

        SWINE   XWimp_RegisterFilter    ; Yes then register

        DebugE  register,"Cant register post-icon-filter "

        ADDVS   r13,r13,#4
        Pull    "r1-r2,PC",VS

        LDR     LR,fake_postrect
        TEQ     LR,#0
        Pull    "r0-r2,PC",NE

        LDR     LR,post_rect_filters
        CMP     LR,#0                   ; Any post-rect-filters?
        MOVNE   R0,#4                   ; post_rect_filter
        ADRNEL  R1,post_rect_filter_handler
        MOVNE   R2,R12

        Debug   register,"Register post-rect-filter",R0,R1,R2,LR

        SWINE   XWimp_RegisterFilter
        STRVS   r0,[r13]

        DebugE  register,"Can't register post-rect-filter "

        Pull    "r0-r2,LR"
        RETURNVC

      [ standalone
; ResourceFS has been reloaded - redeclare resource files
; In    R2 -> address to call
;       R3 -> workspace for ResourceFS module

serviceresourcefsstarting
        Push    "r0,LR"
        ADRL    R0,resourcefsfiles
        MOV     LR,PC                   ; LR -> return address
        MOV     PC,R2                   ; R2 -> address to call
        NOP
        Pull    "r0,LR"
        RETURN
      ]

; ----------------------------------------------------------------------------------------------------------------------
;       RMKill'ing the module  -  Release filter handlers. Release linked lists of filters.
Die
;
        LDR     r12, [r12]
        CMP     r12, #0
        RETURN  EQ

        Push    "r0-r11,LR"

        ADR     r2, pre_filters
        BL      free_list               ; Free list of pre filters
        ADR     r2, rect_filters
        BL      free_list               ; Free list of rect filters
        ADR     r2, post_filters
        BL      free_list               ; Free list of post filters
        ADR     r2, post_rect_filters
        BL      free_list               ; Free list of post rect filters
        ADR     r2, post_icon_filters
        BL      free_list               ; Free list of post icon filters
        ADR     r2, copy_filters
        BL      free_list               ; Free list of copy filters

        MOV     R0,#WimpFilter_PrePoll
        MOV     R1,#0
        MOV     R2,#0
        SWI     XWimp_RegisterFilter    ; Ignore errors.

        MOV     R0,#WimpFilter_GetRectangle
        MOV     R1,#0
        MOV     R2,#0
        SWI     XWimp_RegisterFilter    ; Ignore errors.

        MOV     R0,#WimpFilter_PostPoll
        MOV     R1,#0
        MOV     R2,#0
        SWI     XWimp_RegisterFilter    ; Ignore errors.

        MOV     R0,#WimpFilter_BlockCopy
        MOV     R1,#0
        MOV     R2,#0
        SWI     XWimp_RegisterFilter    ; Ignore errors.

        MOV     R0,#WimpFilter_PostGetRectangle
        MOV     R1,#0
        MOV     R2,#0
        SWI     XWimp_RegisterFilter    ; Ignore errors.

        MOV     R0,#WimpFilter_PostIconGetRectangle
        MOV     R1,#0
        MOV     R2,#0
        SWI     XWimp_RegisterFilter    ; Ignore errors.

        LDR     R0,callbackpending
        CMP     R0,#0                   ; do we have a callback pending? (clears V)
        ADRNE   R0,servicecallback      ;   yes, so remove it before its granted
        MOVNE   R1,WP                   ;   as we are going to die
        SWINE   XOS_RemoveCallBack

        MOV     R0,#0
        STR     R0,callbackpending      ; and then remove it

        MOV     R1,#Service_FilterManagerDying
        SWI     XOS_ServiceCall         ; broadcast that the module is dying so you are no longer installed

      [ standalone
        ADRL    R0,resourcefsfiles
        SWI     XResourceFS_DeregisterFiles ; ignore errors
      ]

        BL      MsgTrans_CloseFile      ; Make sure Messages file is closed.

        Pull    "R0-R11,LR"
        RETURNVC                        ; clear V - don't refuse to die 

; ----------------------------------------------------------------------------------------------------------------------

        LNK     s.SWIs
