; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > Common

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: common_open
;
; in:   r0  = device interrupt number to claim
;       r1  = address of device interrupt handler
;       r2  = external handle (used for calling DeviceFS)
;       r3  = flags for opening the file
;       r6 -> special field that has been decoded
;
; out:  r1  = internal handle (used in communication from DeviceFS)
;
; This call is made to allow the parallel device driver to initialise itself, ie.
; claiming any vectors it may need.
;



common_open
                EntryS  "r2-r4"

                Debug   open,"common_open, DeviceFS handle = ",r2

                LDRB    r4, Flags
                TST     r4, #f_OpenForRead :OR: f_OpenForWrite
                ADRNEL   r0, ErrorBlock_Parallel_InUse
                PullEnv NE

              [ international
                BNE     MakeErrorWithDeviceName
              |
                RETURNVS NE
              ]

                STR     r2, StreamHandle                        ; stash the stream handle

                TST     r4, #f_VectorOwned                      ; do I own the vectors?
                BNE     %FT00

                MOV     r2, wp                                  ; -> code and workspace
                SWI     XOS_ClaimDeviceVector
                EXIT    VS                                      ; return any error from ClaimDeviceVector

                ORR     r4, r4, #f_VectorOwned
00
                TST     r3, #&01
                ORREQ   r4, r4, #f_OpenForRead
                ORRNE   r4, r4, #f_OpenForWrite
                STRB    r4, Flags                               ; update to indicate that the vectors are owned

                EXITS



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: common_close
;
; in:   r0  = device interrupt number to release
;       r1  = address of device interrupt handler
;       r2  = address of busy check callback handler (or 0)
;
; out:  -
;
; This call is made to allow the device to close down the routines associated
; with printing characters.
;

common_close
                EntryS  "r2,r3,r8"

                Debug   close,"common_close"

                SETPSR  I_bit,lr,,r8                            ; disable IRQs

                TEQ     r2, #0
                LDRNEB  r3, IRQFlags
                TSTNE   r3, #if_PendingCallAfter
                BEQ     %05

                Push    "r0, r1"
                MOV     r0, r2
                MOV     r1, wp
                SWI     XOS_RemoveTickerEvent
                Pull    "r0, r1"

                BIC     r3, r3, #if_PendingCallAfter
                STRB    r3, IRQFlags

05
                LDRB    r3, Flags
                TST     r3, #f_VectorOwned                      ; is the vector owned at the moment
                BEQ     %10

                Debug   close," removing IRQ routine..."

                MOV     r2, wp                                  ; -> routines
                SWI     XOS_ReleaseDeviceVector
                BVS     %FT15

                BIC     r3, r3, #f_VectorOwned                  ; mark as vector no longer owned

                Debug   close," removed"
10
                BIC     r3, r3, #f_OpenForRead :OR: f_OpenForWrite
                STRB    r3, Flags

                MOV     r3, #0
                STR     r3, StreamHandle                        ; clear the output handle
                STR     r3, BufferHandle                        ; and buffer handle

                Debug   close,"finished closing stream"

                EXITS   ,cf                                     ; exit restoring all flags (including I)

15
                ORR     r8, r8, #V_bit
                RestPSR r8,,cf                                  ; ensure I bit is restored and V is set
                EXIT                                            ; return any error from ReleaseDeviceVector


; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;
; call: common_createTX
;
; in:   r0 = reason code (6)
;       r2 = internal stream handle
;       r3 = flags for buffer
;       r4 = size for buffer
;       r5 = buffer handle (usually -1)
;       r6 = threshold for buffer
;
; out:  above registers modified
;
; This routine is called just before the output stream is created.
;

common_createTX
                Debug   open,"common_createTX"

                CMP     r5, #-1
                MOVEQ   r5, #Buff_Print         ; set the buffer handle
                MOV     pc, lr



; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; call: common_createRX
;
; in:   r0 = reason code (7)
;       r2 = internal stream handle
;       r3 = flags for buffer
;       r4 = size for buffer
;       r5 = buffer handle (usually -1)
;       r6 = threshold for buffer
;
; out:  above registers modified
;
; This entry point is called just before the input stream buffer is going to
; be created.  The flags are modified so that the Halt and Resume entry points
; are called and the threshold is set.
;

common_createRX EntryS  "r0-r2"

                Debug   open,"common_createRX"

                ORR     r3, r3, #BufferFlags_SendThresholdUpCalls ; change flags for parallel buffer

                CMP     r5, #-1
                MOVEQ   r5, #Buff_Print         ; use parallel buffer
                MOV     r6, #Parallel_Threshold

                EXITS

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; call: common_get_loop_factor
;
; in:   -
;
; out:  r0 = looping factor
;
; This call is made to find a looping factor. This is a measurement of how
; fast the processor is.

ncpuloops * 2048        ; loop this many times

common_get_loop_factor ROUT

                Entry   "r0-r3,r8"

                ; We load Timer0 with &7FFE, and set it going. Then we go into
                ; a tight loop for ncpuloops times. Then we reread the timer
                ; and see how much it has gone down
                ; We then return 16384 (&4000) divided by this number so that we
                ; return numbers which are higher when the processor is faster

                Debug   open,"common_get_loop_factor"

                ; This whole code sequence is naughty and should really use
                ; the HAL timer API. For now, just poke the timer directly.
                MOV     r0, #9
                MOV     r1, #4:SHL:8            ; Address of SSpace (IOMD)
                SWI     XOS_Memory
                MOVVS   r3, #IOC                ; Address of the IO controller pre-HAL era
                MOVVC   r3, r1

                LDR     R1, =&7FFE              ; 32K @@ 2MHz = ~16ms limit

                SETPSR  I_bit,lr,,r8            ; disable IRQs

                MOV     r0, r1, LSR #8
                STRB    r1, [r3, #Timer1LL]
                STRB    r0, [r3, #Timer1LH]
                LDR     r0, =ncpuloops
                STRB    r0, [r3, #Timer1GO]     ; start the timer NOW
                B       %FT25                   ; Looks superfluous, but is required
                                                ; to get ncpuloops pipeline breaks
25
                SUBS    r0, r0, #1              ; 1S
                BNE     %BT25                   ; 1N + 2S

                STRB    r0, [r3, #Timer1LR]     ; latch count NOW
                LDRB    r2, [r3, #Timer1CL]
                LDRB    r0, [r3, #Timer1CH]
                ADD     r2, r2, r0, LSL #8      ; count after looping is ...

                SUB     r2, r1, r2              ; decrements !, &7FFE - current time = time it takes to do loops

                MOV     R1, #10*&4000           ; Instead of dividing by 4 (done at site of use), multiply
                                                ; by 10 (here). The only requirement for this number is that
                                                ; it is big enough to avoid the callback code being called
                                                ; in normal use. JRC 27 May 1997
                DivRem  R0, R1, R2, R3          ; R0 := 16384 DIV R2 (R3 is temp)

                STR     r0, LoopFactor          ; save for next time

                RestPSR r8,,cf                  ; restore flags and reenable IRQ's
                NOP

                EXIT                            
        [ AdvancedErrorHandling

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; call: common_check_upcall
;
; in:   r0 = UpCall reason code
;            other registers depend on the reason code in r0
;       r12 = our workspace pointer
;
; out:  -
;
; This call checks whether the UpCall we just received is UpCall_StreamCreated,
; and if so, whether the DeviceFS handle corresponds to the one we are currently
; using. If so, we can obtain the FileSwitch handle for our stream!
;
common_check_upcall
                TEQ     r0, #UpCall_StreamCreated
                MOVNE   pc, lr

                ; UpCall_StreamCreated
                ; r0 = 10 (Stream created)
                ; r1 = device driver's handle
                ; r2 = 0 if created for reception (else created for transmission)
                ; r3 = file handle for stream
                ; r4 = DeviceFS stream handle as passed to us on initialisation

                Entry   "r0"

                Debug   adverr, "UpCall_StreamCreated, DeviceFS handle = ", r4

                LDR     r0, StreamHandle        ; the DeviceFS stream handle of our current stream (if any, else 0)
                TEQ     r0, r4                  ; is it the handle we just got?
                STREQ   r3, FileSwitchHandle    ; yes, so let us keep the FileSwitch handle!

              [ debug
                BNE     %FT00
                Debug   adverr, "Storing FileSwitch handle ", r3
00
              ]
                EXIT
        ]

                END
