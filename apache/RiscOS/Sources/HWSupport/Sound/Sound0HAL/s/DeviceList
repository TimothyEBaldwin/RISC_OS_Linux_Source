; Copyright 2015 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

DeviceList_Remove ROUT
        ; In:
        ;   r2 -> HAL device
        ;   r12 = SoundRMA
        Entry   "r0-r4"
        Debug   devlist,"DeviceList_Remove",r2
        ; See if this is in our list of devices
        BL      DeviceList_FindByDevice
        CMP     r4, #0
        DebugIf EQ,devlist,"Not found"
        EXIT    EQ
        ; Found it; now get rid of it
        LDR     r0, [r4, #DeviceList_Next]
        STR     r0, [r3, #DeviceList_Next] ; Unlink
      [ SupportSoftMix
        LDR     r0, [r4, #DeviceList_SoftMixDevice]
        CMP     r0, #0
        BLNE    SoftMixShutdown
      ]
        MOV     r0, #ModHandReason_Free
        MOV     r2, r4
        SWI     XOS_Module        
        Debug   devlist,"DeviceList_Remove done"
        EXIT

DeviceList_Add ROUT
        ; In:
        ;   r2 -> Audio HAL device with compatible API version
        ;   r12 = SoundRMA
        ; Out:
        ;   r0 = device list entry, or error
        Entry   "r0-r4"
        Debug   devlist,"DeviceList_Add",r2
        ; Add to our device list, and create a software mixer if necessary
        ; ID strings are generated from the ID & Location fields of the device struct, and are of the form 'SoundDMA#HAL_xxxx_yyyyyyyy'
        MOV     r0, #ModHandReason_Claim
        MOV     r3, #DeviceList_ID + (:LEN: "SoundDMA#HAL_xxxx_yyyyyyyy") + 1 
        SWI     XOS_Module
        FRAMSTR r0, VS
        EXIT    VS
        FRAMLDR r3,,r2
        STR     r3, [r2, #DeviceList_AudioDevice]
        ; Generate ID string
        ADD     r1, r2, #DeviceList_ID
        ADR     r0, DeviceIDProto
10
        LDRB    lr, [r0], #1
        CMP     lr, #0
        STRNEB  lr, [r1], #1
        BNE     %BT10
      [ UseLDRSH
        LDRH    r0, [r3, #HALDevice_ID]
      |
        LDRB    r0, [r3, #HALDevice_ID]
        LDRB    lr, [r3, #HALDevice_ID+1]
        ORR     r0, r0, lr, LSL #8
      ]
        Push    "r2"
        MOV     r2, #5
        SWI     XOS_ConvertHex4
        MOV     r2, #'_'
        STRB    r2, [r1], #1
        LDR     r0, [r3, #HALDevice_Location]
        MOV     r2, #9
        SWI     XOS_ConvertHex8
        Pull    "r2"
      [ SupportSoftMix
        ; Software mixer needed?
        LDR     r0, [r3, #HALDevice_AudioMixer]
        CMP     r0, #0
        MOVNE   r0, #0
        BLEQ    SoftMixInit
        STRVC   r0, [r2, #DeviceList_SoftMixDevice]
        BVC     %FT50
        Debug   devlist,"SoftMixDevice=",r0
        FRAMSTR r0
        MOV     r0, #ModHandReason_Free
        SWI     XOS_Module
        SETV
        EXIT
50
      ]
        ; Link into our list
        LDR     r0, [r12, #DeviceList]
        STR     r0, [r2, #DeviceList_Next]
        STR     r2, [r12, #DeviceList]
        FRAMSTR r2,,r0
        Debug   devlist,"DeviceList_Add done"
        EXIT

DeviceIDProto
        DCB     "SoundDMA#HAL_", 0
        ALIGN

Initialise_DeviceList ROUT
        ; Do the initial scan for devices and initialise the device list
        Entry   "r0-r4,r8"
        Debug   devlist,"Initialise_DeviceList"
        MOV     r1, #0
        MOV     r8, #OSHW_DeviceEnumerate
10      LDR     r0, =MaxDeviceVersion:SHL:16 :OR: HALDeviceType_Audio :OR: HALDeviceAudio_AudC
        SWI     XOS_Hardware
        DebugIf VS,devlist,"Initialise_DeviceList err"
        EXIT    VS
        CMP     r1, #-1
        DebugIf EQ,devlist,"Initialise_DeviceList done"
        EXIT    EQ
        ; Check device meets minimum version requirement
        LDR     r0, [r2, #HALDevice_Version]
        CMP     r0, #MinDeviceVersion:SHL:16
        BLO     %BT10
        ; Check device not already on the list (can happen if we create a software mixer - will cause everything returned by OSHW_DeviceEnumerate to get shifted along one)
        ; Note that we could switch to EnumerateChrono, but then (because we insert at the front of our device list) the list will end up with the newest device at the front, which may cause issues with selecting the default device (generally, aim for the oldest device -> most likely to be motherboard audio, and so most likely to always be present)
        ; Changing DeviceList_Add to insert at the rear would fix that, but slow down device addition in general, whereas this check here will only slow us down when we first initialise the list.
        BL      DeviceList_FindByDevice
        CMP     r4, #0
        BLEQ    DeviceList_Add
        B       %BT10

Finalise_DeviceList ROUT
        ; Free all device list entries
        Entry   "r2"
        Debug   devlist,"Finalise_DeviceList"
10
        LDR     r2, [r12, #DeviceList]
        CMP     r2, #0
        DebugIf EQ,devlist,"Finalise_DeviceList done"
        EXIT    EQ
        LDR     r2, [r2, #DeviceList_AudioDevice]
        BL      DeviceList_Remove
        B       %BT10

DeviceList_FindByDevice ROUT
        ; In:
        ;  r2 = Audio device ptr
        ;  r12 = SoundRMA
        ; Out:
        ;  r3 = previous entry in list
        ;  r4 = device list entry, or 0 if not found
        Entry
        Debug   devlist, "FindByDevice", r2
        ADD     r3, r12, #DeviceList-DeviceList_Next
10
        LDR     r4, [r3, #DeviceList_Next]
        CMP     r4, #0
        DebugIf EQ,devlist,"Not found"
        EXIT    EQ
        LDR     lr, [r4, #DeviceList_AudioDevice]
        CMP     lr, r2
        MOVNE   r3, r4
        BNE     %BT10
        Debug   devlist,"Found",r3,r4
        EXIT

DeviceList_FindByID ROUT
        ; In:
        ;  r0 = space/control-terminated device ID string
        ;  r12 = SoundRMA
        ; Out:
        ;  r3 = device list entry, or 0 if not found
        Entry   "r0-r2"
        DebugS  devlist, "FindByID", r0
        ADD     r3, r12, #DeviceList-DeviceList_Next
10
        LDR     r3, [r3, #DeviceList_Next]
        CMP     r3, #0
        DebugIf EQ,devlist,"Not found"
        EXIT    EQ
        ADD     r1, r3, #DeviceList_ID
        FRAMLDR r0
20
        LDRB    r2, [r0], #1
        CMP     r2, #32
        MOVLS   r2, #0
        LDRB    lr, [r1], #1
        CMP     r2, lr
        BNE     %BT10
        CMP     r2, #0
        BNE     %BT20
        Debug   devlist,"Found",r3
        EXIT

        END
