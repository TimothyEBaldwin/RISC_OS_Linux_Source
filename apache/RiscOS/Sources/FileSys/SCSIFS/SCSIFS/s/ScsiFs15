; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;>ScsiFs15

        LTORG

; =============
; LowLevelEntry
; =============

;entry
; R1  reason
; R2  disc address top 3 bits drive
; R3  RAM ptr
; R4  length
; R5  -> disc record
; R6  -> defect list or NIL during Mount
; R12 -> private word

LowLevelEntry ROUT      ;entry point for FileCore
        getSB
LowLevelOp              ;internal entry point
        Push    "R0-R8,R9,LR"
LowLevelOp2             ;another internal entry point
      [ Debug3
        wrhex   R1
        wrhex   R2
        wrhex   R3
        wrhex   R4
        wrhex   R5
        wrhex   R6
        mess    ,"*>LowLevel",NL
      ]

      [ Debug3
        mess    , "LowLevelEntry/LowLevelOp - "
      ]

        MOV     LR,R2,LSR #(32-3)
        DrvRecPtr LR,LR
        mapDrvToDevLo LR,LR,LR
        CMP     LR,#-1
        BEQ     BadDrive

        CMPS    R4, #0                  ; IF 0 length
        BICEQ   R7, R1, #NoTimeOut :OR: NoEscape :OR: ScatterBit :OR: AltDefectBit
        ASSERT  DiscOp_Verify < DiscOp_ReadTrk
        ASSERT  DiscOp_ReadSecs < DiscOp_ReadTrk
        ASSERT  DiscOp_WriteSecs < DiscOp_ReadTrk
        CMPEQS  R7, #DiscOp_ReadTrk     ; AND verify, read/write secs, not background
        Pull    "R0-R8,R9,PC",LO        ; THEN nothing to do

        sbaddr  R7, BlockSizes          ; if not examined drive yet

        LDRB    R8, [R7,R2, LSR #(32-3)]
        CMPS    R8, #0                  ; V=0
        BLEQ    Examine                 ; (R2,R7->R0,R8,V) then do it now
        BVS     LowError

        LDR     R7, FCFeatures
        ANDS    R5, R1, #OpMask
        CMPS    R5, #DiscOp_Restore+1        ; moan if bad reason code
; LDRCCB R0, Winnies
;>>> RSBCCS R0, R0, R2, LSR #29     ;     or if bad drive
;>>>what about highest floppy number?
        BCS     BadParameters

        MOV     R0, R2, LSR #29


        mapDrvToDevLo R0, R0            ; map drive onto device
        AND     LR, R1, #ScatterBit :OR: NoEscape :OR: NoTimeOut :OR: BackgroundOp
        ORR     R0, R0, LR, LSL #CTL_cnvt
        ASSERT  DiscOp_StepIn > DiscOp_Restore
        ASSERT  DiscOp_StepOut > DiscOp_Restore
        ASSERT  DiscOp_CachedReadSecs > DiscOp_Restore
        ASSERT  DiscOp_Specify > DiscOp_Restore
        CMPS    R5, #DiscOp_Restore
        BHS     %FT20
        ;convert byte disc address to block address
        BIC     LR, R2, #DiscBits
        TST     R7, #FC_Big
        MOVNE   R7, LR
        BNE     %FT20
        MOV     R7, LR, LSR R8          ; block number
        TEQS    LR, R7, LSL R8
        BNE     BadParameters

20
      [ AllowPartitions
        MOV     LR, R2, LSR #(32-3)     ; disc number
        ADD     LR, SB, LR, LSL #2      ; to word address
        LDR     LR, [LR, #:INDEX:PartitionBaseAdd-:INDEX:SCSIFSWorkBase]
        ADD     R7, R7, LR              ; offset by base sector offset
      ]
        ADD     PC, PC, R5, LSL #2      ; dispatch on reason
        NOP

        B       VerifyCase
        B       ReadSecsCase
        B       WriteSecsCase
        B       ReadTrackCase           ; may be read ids ?
        B       WriteTrackCase          ; whatever needed for formatting
        B       SeekCase
        B       RestoreCase

VerifyCase
      [ Debug3
        mess    , "Verify", NL
      ]
        MOV     R1, #&2F                ; VERIFY command
        B       OpCommon

ReadSecsCase
      [ Debug3
        mess    , "ReadSectors", NL
      ]
        ORR     R0, R0, #CTL_TXREAD
        MOV     R1, #&28                ; READ extended command
        B       OpCommon

WriteSecsCase
      [ Debug3
        mess    , "WriteSectors", NL
      ]
        ORR     R0, R0, #CTL_TXWRITE
        MOV     R1, #&2A                ; WRITE extended command
        B       OpCommon

ReadTrackCase
      [ Debug3
        mess    , "ReadTrack !!NOT SUPPORTED - NOP !!", NL
      ]
        B       DoNOP

WriteTrackCase
      [ Debug3
        mess    , "WriteTrack !!NOT SUPPORTED - NOP!!", NL
      ]
        B       DoNOP

SeekCase
        MOV     R1, R0
        BL      UnLockMedia
    [ SeekIsNOP
      [ Debug3
        mess    , "Seek !!NOT SUPPORTED - NOP !!", NL
      ]
        B       DoNOP
    |
      [ Debug3
        mess    , "Seek", NL
      ]
        MOV     R1, #&2B                ; SEEK command
        MOV     R4, #0                  ; Ensure cdb[7]..cdb[8] are zero
        B       OpCommon
    ]

RestoreCase
      [ Debug3
        mess    , "Restore !!NOT SUPPORTED - NOP !!", NL
      ]
        B       DoNOP

;
; Build a SCSI CDB and the registers needed for the SWI SCSI_Op
; on the stack
;
; On entry
;   R0  composite of deviceID & flags (remapping already applied)
;   R1  bytes 0..1 of required CDB
;   R2  disc address top 3 bits drive
;   R3  RAM ptr
;   R4  length
;   R7  disc address expressed as block number
;   R8  Log2BlockSize
;
; The UseSpecialScatterBlk switch when ON means that the Driver is not relied upon
; to source data for sector writes.  This is done by expanding a single RAMBlk
; write into a ScatterBlock write, where the first entry in the scatter list is
; the original write, and the second is a section of RAM (starting at address 0)
; large enough to make the total transfer precisely equal to the size of the
; transfer indicated in the CDB.  The total size of the transfer (R4) is also
; adjusted appropriately.
;

OpCommon
        MOV     R5, #0                  ; Default timeout
      [ UseSpecialScatterBlk
        STR     R5, ScatterFlag         ; Default: dont use scatter method
      ]
        SUB     SP,SP,#12               ; Largest CBD is 12 bytes

        MOV     R2,SP                   ; ->CDB built on stack
        Push    "R2,R3,R4,R9"

        RevBytes R7, R5,R6              ; Change sex of disc block address

;
; The given Xfer length is in bytes, the CDB needs length in blocks, if the
; byte length is not a whole number of blocks, round up the CDB block count
; and rely on SCSIdriver sinking/sourcing the excess.
; NOTE: this note only applies if the UseSpecialScatterBlk switch is OFF
;
        MOV     R5, R4, LSR R8          ; Xfer length in blocks (was in bytes)
        TEQ     R4, R5, LSL R8
        ADDNE   R5, R5, #1              ; Inc if Xfer length not a whole number
    [ UseSpecialScatterBlk
        BEQ     DontNeedScatBlk         ; If the length was a whole number fine ...

        AND     R9, R0, #CTL_TXWRITE :OR: CTL_TXREAD
        CMP     R9, #CTL_TXWRITE
        BNE     DontNeedScatBlk         ; If we arent writing fine ...
        TST     R0, #CTL_SCATTER
      [ Debug3                        ; Oh dear already using a scatter pointer ...
        BEQ     NotAppropriate1
        SWI     OS_WriteS               ; So we cant make things easy for the Driver
        DCB     "Having to save a nasty block slowly, as scatter list already used"
        SWI     OS_NewLine
NotAppropriate1
      ]
        BNE     DontNeedScatBlk

        RSB     R9, R4, R5, LSL R8      ; Get required dummy block length
        CMP     R9, #?PaddingBlock      ; Too big?
        BHS     DontNeedScatBlk         ; Let driver deal with it

      [ Debug3
        SWI     OS_WriteS
        DCB     "Tranferring to a scatter block ..."
        SWI     OS_NewLine
      ]

        Push    "R5"
        MOV     R9, #1                             ; Note that the scatterblock is required
        STR     R9, ScatterFlag
        ORR     R0, R0, #CTL_SCATTER               ; Save the transfer to a Scatter pointer
        ADR     R9, WriteScatBlk
        STR     R3, [R9, #0]
        STR     R4, [R9, #4]
        ADR     LR, PaddingBlock
        STR     LR, [R9, #8]

        ;Take   the rounded up no. of blocks
        MOV     R5, R5, LSL R8                     ; Convert this into bytes
        STR     R5, ScatterData                    ; Save this new Total Length
        SUB     R5, R5, R4                         ; And take off the original length
        STR     R5, [R9, #12]                      ; This is the length of the dummy block
        Pull    "R5"

DontNeedScatBlk
    ]

    [ CheckXferLen
;
; Block count fields in CDB's are 16 bit, ie max 65535.
;
; If the byte count given by filecore, converted to a block count exceeds this,
; put 65535 in the CDB.
;
; XferLenMax255 switch introduced for test purposes .. some devices found
;   that won't do more than 256 blocks at a time safely (JB 20050131)
;
; Now renamed & repurposed for limiting the max length to 64K, as that seems
; to be the limit which most OS's use for USB mass storage transfers (and
; potentially all other I/O transfers, but my main interest is finding a fix
; for all the shoddy USB devices) (JL 20101214)
      [ XferLenMax64K
        MOV     R3,#65536
        MOV     R3,R3,LSR R8
        CMP     R3,R5
        MOVLO   R5,R3
        CMP     R5,#65536               ; Just in case we have a device with a block size of 1
        SUBEQ   R5,R5,#1
      |
        CMP     R5,#&10000
        MOVHS   R5,#&FF00
        ORRHS   R5,R5,#&00FF
      ]
    ]

        MOV     R3,R5,LSR #8
        MOV     R3,R3,LSL #24                      ; MSB in CDB[7] {R3 bits 24..31}
        AND     R6,R5,#&FF                         ; LSB in CDB[8] {R4 bits  0..7 }

        ORR     R1,R1,R7,LSL #16                   ; Add sex changed block address
        ORR     R3,R3,R7,LSR #16                   ; to CDB

        STMIA   R2,{R1,R3,R6}                    ; Copy 12bytes of CDB onto stack

;R0,R1
;R2,R3,R4,R9 stacked
;R5 must be remade 0

        MOV     R1,#10
        Pull    "R2,R3,R4,R9"

      [ UseSpecialScatterBlk
        LDR     R6, ScatterFlag                    ; Are we using the special ScatterBlock ?
        CMP     R6, #0
        ADRNE   R3, WriteScatBlk                 ; Yes ! Better make RAMptr point to it,
        LDRNE   R4, ScatterData                  ; and use the rounded up total length
      ]

; If we're transferring less than what was requested, clamp R4 so that
; SCSI drivers won't get confused by the CDB saying one thing but R4 saying
; something else
; But remember how much we clamped by, so that we can return the correct unread
; amount to FileCore
        SUBS    R6,R4,R5,LSL R8
        SUBGT   R4,R4,R6
        MOVLT   R6,#0
        Push    "R6,R8"

        MOV     R5, #0

        TSTS    R0, #CTL_BACKGROUND
        LDRNE   R6, WinnieCallAfter     ; Callback address
        LDRNE   R7, FileCorePrivate     ; & wsptr (if needed)

        MOV     R8, R12                 ; AccessKey

    [ RetryIfBusy
        MOV     R9,#60                  ; Retry on timeout for 60 seconds
30
        Push    "R0-R4"                 ; Stack incase we need to retry
        DoSCSIOp
        BLVS    CheckRecovered          ; If error is 'Recovered error' clear
        ; V and pretend it never happened
        BVC     %FT35                   ; OK, so ditch stacked R0-R4 and continue
        BL      CheckRetry
        Pull    "R0-R4",VC              ; Retry with stacked R0-R4
        BVC     %BT30
35
        LDR     LR, FCFeatures
        ADD     SP,SP,#5*4              ; Ditch stacked R0-R4 before reporting error
    |
        DoSCSIOp
        BLVS    CheckRecovered          ; If error is 'Recovered error' clear
        ; V and pretend it never happened
        LDR     LR, FCFeatures
    ]

; Adjust R4 by our stacked R6
        Pull    "R6"
        ADD     R4,R4,R6

        TST     LR, #FC_NewErrors
        ORREQ   R0, R0, #ExternalErrorBit
        MOVVC   R0, #0
        LDR     R8, [SP], #16           ; Pull blocksize & discard stacked CBD
        LDMIB   SP, {R1-R2,R6-R7}

      [ UseSpecialScatterBlk
; If we used the temp scatter block then we need to restore the correct R3
; value and 'forget' the extra bytes that we transferred
        LDR     R9, ScatterFlag
        TEQ     R9, #0
        BEQ     %FT10
        LDR     R9, ScatterData         ; Get adjusted length
        SUB     R9, R9, R7              ; Calculate the amount of padding we used
        SUB     R4, R4, R9              ; Forget the padding
        TEQ     R4, #0                  ; Preserve V
        MOVMI   R4, #0                  ; Clamp R4 if we managed to transfer everything we were originally asked to
        SUB     R3, R7, R4              ; Get amount transferred
        ADD     R3, R6, R3              ; And calculate the correct value of R3 to return
10
      ]

        SUB     R7, R7, R4                      ; Amount Xfered
        TST     LR, #FC_Big
        ADDNE   R2, R2, R7, LSR R8              ; Inc. disc address by amount transfered
        ADDEQ   R2, R2, R7                      ; Inc. disc address by amount transfered
        STMIA   SP, {R0,R1-R2,R3-R4}

    [ XferLenMax64K
;
; For file read/write operations, FileCore will automatically call us again to
; transfer any data which we didn't transfer due to our 64K xfer limit.
;
; HOWEVER this behaviour seems to simply be a side-effect of the implementation,
; not something that we should be relying upon. Plus if you call SCSIFS_DiscOp
; (or FileCore_DiscOp) directly then the FileCore won't automatically repeat
; the operation, leading to confusion when calls complete without error but
; without all the data having been transferred.
;
; So implement our own loop to make sure that the user really does get all the
; data that he was after.
;
        BVS     %FT10
        TEQ     R4,#0
        BEQ     %FT10
        AND     LR, R1, #OpMask
        TEQ     LR, #1                  ; Read sectors
        TEQNE   LR, #2                  ; Write sectors
      [ Debug3
        mess    EQ,"Looping back round to transfer rest of data",NL
      ]
        ADDEQ   R5,R13,#5*4
        LDMEQIA R5,{R5-R6}              ; Recover disc record, defect list
        BEQ     LowLevelOp2
10
    ]

LowError
        STRVS   R0, [SP]
      [ Debug3
        wrhex   R0,VS
        mess    VC,"         "
        wrhex   R1
        wrhex   R2
        wrhex   R3
        wrhex   R4
        mess    ,"*<LowLevel",NL,NL
      ]
DoNOP
        Pull    "R0-R8,R9,PC"

BadParameters
        MOV     R0, #BadParmsErr
        BL      SetV
        B       LowError

BadDrive
        MOV     R0, #BadDriveErr
        BL      SetV
        B       LowError

;
; CheckRecovered
; ==============
; On entry
;   R0 -> error & VS
;
; Clear V_bit if the error is RecoveredError
;
CheckRecovered
        Push    "R0,R1"
        LDR     R0,[R0]                         ; Error number
        LDR     R1,=ErrorNumber_SCSI_RecoveredError
        TEQ     R0,R1                           ; If RecoveredError (V preserved)
        Pull    "R0,R1"
        CMPEQ   R0,#0                           ; pretend it never happened (clear V)
        MOV     PC,LR                           ; else keep the error

;
; CheckRetry
; ==========
;
; On entry
;   R0 -> error
;
; On exit
;   VC means retry
;   VS means report error
;
CheckRetry
        Push    "R0,R1,LR"
        SETV
        LDR     R0,[R0]                         ; Error number
        LDR     R1,=ErrorNumber_SCSI_Busy
        TEQ     R0,R1                           ; If NOT("Target status - busy" or
        LDRNE   R1,=ErrorNumber_SCSI_CC_NotReady
        TEQNE   R0,R1                           ;       "Target error - Not ready"
        LDRNE   R1,=ErrorNumber_SCSI_Timeout
        TEQNE   R0,R1                           ;       "Timeout"
        LDRNE   R1,=ErrorNumber_SCSI_Died
        TEQNE   R0,R1                           ;       "Unexpected disconnection"
                                                ;       )
        Pull    "R0,R1,PC",NE                   ; return indicating report error

        TEQ     R9,#0                           ; Retry counter hit zero
        Pull    "R0,R1,PC",EQ                   ; return indicating report error
;
; Counter is non-zero
;
        SUB     R9,R9,#1
        BL      WaitOneSecond
        CMP     R0,#0
        Pull    "R0,R1,PC",AL                   ; Return indicating do-a-retry


;
; WaitOneSecond (give or take a bit)
; =============
;
WaitOneSecond
        Push    "R0,R1,LR"
        SWI     XOS_ReadMonotonicTime
        ADD     R1,R0,#100
Wait_10
        SWI     XPortable_Idle
        SWI     XOS_ReadMonotonicTime
        CMP     R0,R1
        BLO     Wait_10
        Pull    "R0,R1,PC",AL


; =============
; DoPollChanged
; =============

;entry
; R1 drive 0-3
; R2 sequence number

;exit
; R2 new sequence number
; R3 status bits
;   b0 not changed
;   b1 maybe changed
;   b2 changed
;   b3 empty
;   b6 empty works
;   b7 changed works

DoPollChanged ROUT
        Push    "R0,R1,R4-R9,LR"
      [ Debug4
        mess    ,"*Enter PollChanged",NL
      ]
; MOV    R3, #Drv_NotChanged
; Pull   "R0,R1,R4-R9,PC"

; address drive record

        DrvRecPtr R4,R1
        LDRB    R5, [R4,#DrvFlags]
        LDR     R6, [R4,#DrvSequenceNum]

        BIC     R0, R5, #LostUnitAtn
        STRB    R0, [R4,#DrvFlags]

        TST     R1, #4
        BEQ     %FT05
        ; Hard drive case - only for hotplugging, really.
        mapDrvToDevLo LR, R1, R4
        CMP     LR, #-1
        MOVEQ   R3, #MiscOp_PollChanged_Empty_Flag
        BEQ     %FT04
        TSTS    R5, #LostUnitAtn
        TEQEQS  R2, R6
        MOVEQ   R3, #MiscOp_PollChanged_NotChanged_Flag
        MOVNE   R3, #MiscOp_PollChanged_Changed_Flag
04
        TST     R3, #MiscOp_PollChanged_NotChanged_Flag
        ADDEQ   R2, R6, #1
        STREQ   R2, [R4,#DrvSequenceNum]
        ORR     R3, R3, #MiscOp_PollChanged_ChangedWorks_Flag :OR: MiscOp_PollChanged_EmptyWorks_Flag
        Pull    "R0,R1,R4-R9,PC"


05
        ; Floppy drive case
        BL      TestUnitReady
        BLNE    TestUnitReady           ; try once more to be sure
; ORRNE  R0, R5, #LostUnitAtn
; STRNEB R0, [R4,#DrvFlags]
        TSTEQS  R5, #LostUnitAtn
        BNE     %FT10

08
;here if not changed
        MOV     R3, #MiscOp_PollChanged_Changed_Flag+MiscOp_PollChanged_Ready_Flag
        TEQS    R2, R6
        MOVEQ   R3, #MiscOp_PollChanged_NotChanged_Flag+MiscOp_PollChanged_Ready_Flag
        MOV     R2, R6                   ; Returned as the new drive sequence number
        B       %FT20

;
10
        ADD     R2, R6, #1
        STR     R2, [R4,#DrvSequenceNum]
        BL      TestUnitReady
        BLNE    TestUnitReady           ; try once more to be sure
        ORRNE   R0, R5, #LostUnitAtn
        STRNEB  R0, [R4,#DrvFlags]

;18
        MOVEQ   R3, #MiscOp_PollChanged_Changed_Flag+MiscOp_PollChanged_Ready_Flag
        MOVNE   R3, #MiscOp_PollChanged_Empty_Flag
20

      [ Debug4
        ASSERT  Drv_Empty   > Drv_Changed
        ASSERT  Drv_Changed > Drv_NotChanged
        AND     R0, R3, #Drv_Empty+Drv_Changed+Drv_NotChanged
        CMP     R0, #Drv_Changed
        mess    LT,"Not Changed",NL
        mess    EQ,"*Changed"   ,NL
        mess    GT,"*Empty"     ,NL
      ]
        ORR     R3, R3, #MiscOp_PollChanged_ChangedWorks_Flag :OR: MiscOp_PollChanged_EmptyWorks_Flag :OR: MiscOp_PollChanged_ReadyWorks_Flag
        Pull    "R0,R1,R4-R9,PC"

;
; This issues ControlErrors, if device not reserved or reserved by us, this
; should succeed. If reserved by another user it will fail.
; It is left in the state that floppies require.
;
TestUnitReady
95
        Push    "R0-R6,R8,LR"
        SavePSR R6
      [ Debug4
        mess    , "TestUnitReady"
      ]
        mapDrvToDevLo R1, R1
        MOV     R0, #4                  ; RC for ControlErrors
        MOV     R2, #1                  ; Auto Request-Sense, Return Unit-Attention
        MOV     R8, R12
        SWI     XSCSI_Control

        MOVVC   R0, R1
        ADRVC   R2, CDB_TestUnitReady

        MOVVC   R1, #6                  ; CDB len
        MOVVC   R3, #0                  ; 
        MOVVC   R4, #0
        MOVVC   R5, #0
        MOVVC   R8, R12                 ; AccessKey
        SWIVC   XSCSI_Op
      [ Debug4
        mess    VC,"*Ready",NL
        mess    VS,"*Not Ready ("
        LDRVS   LR,[R0]
        wrhex   LR,VS
        mess    VS,")",NL
      ]
        ORRVC   R6, R6, #Z_bit
        BICVS   R6, R6, #Z_bit
        RestPSR R6,,f
        Pull    "R0-R6,R8,PC"

CDB_TestUnitReady
        DCB     &00                     ; TEST UNIT READY
        DCB     0                       ; LUN & reserved
        DCB     0                       ; Reserved
        DCB     0                       ; Reserved
        DCB     0                       ; Reserved
        DCB     0                       ; Vendor Unique=0 (b7..b6),Reserved=0 (b5..b2),
        ALIGN

;
; SCSIOp_HandlingAtn
; ==================
;
; On entry
;   R0-R8 as for SWI XSCSI_Op
;
; On exit
;   As SWI XSCSI_Op
;
SCSIOp_HandlingAtn
        Push    "R0-R4,R9,LR"           ; Remember entry registers corrupted by SCSI_Op
        SWI     XSCSI_Op                ; Issue the operation
        ADDVC   SP,SP,#5*4      ; If no error, then return
        Pull    "R9,PC",VC
        LDR     LR,[R0]                 ; Was it a "Unit attention" error?
        LDR     R9,=ErrorNumber_SCSI_CC_UnitAttention
        TEQ     LR,R9
        ADDNE   SP,SP,#5*4      ; If not, return the error
        Pull    "R9,PC",NE

        LDRB    R2,[SP,#0]              ; R2 = SCSI device of operation
        MOV     R1,#MaxDriveNo-1        ; loop over 8 drives
05
        mapDrvToDevLo R0,R1
        TEQ     R0,R2                   ; were we talking to this drive's device?
        BNE     %FT10
        DrvRecPtr R4,R1                 ; if so, set the drive's LostUnitAtn flag
        LDRB    LR, [R4,#DrvFlags]
        ORR     LR, LR, #LostUnitAtn
        STRB    LR, [R4,#DrvFlags]
10
        SUBS    R1, R1, #1              ; keep going down to drive 0
        BPL     %BT05
        Pull    "R0-R4"                 ; restore entry registers
        SWI     XSCSI_Op                ; retry the operation
        Pull    "R9,PC"

        LNK     ScsiFs20.s
