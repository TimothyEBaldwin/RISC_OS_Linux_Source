; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;>ScsiFs20

        MACRO
        Misc    $str
        ASSERT  MiscOp_$str=(.-MiscTable) :SHR: 2
        B       Do$str
        MEND


; =========
; MiscEntry
; =========

MiscEntry ROUT
      [ Debug4
        wrhex   R0
        wrhex   R1
        wrhex   R2
        wrhex   R3
        wrhex   R4
        wrhex   R5
        mess    ,"*>Misc",NL
      ]
        Push    "LR"
        getSB
        CMPS    R0, #MiscOp_DriveStatus
        BHI     %FT95

        MOV     LR, PC
        ADD     PC, PC, R0, LSL #2
        B       %FT90
MiscTable
        Misc    Mount
        Misc    PollChanged
        Misc    LockDrive
        Misc    UnlockDrive
        Misc    PollPeriod
        Misc    Eject
        B       %FT95
        Misc    DriveStatus
90
      [ Debug4
        wrhex   R0
        wrhex   R1
        wrhex   R2
        wrhex   R3
        wrhex   R4
        wrhex   R5
        mess    ,"*<Misc"
        mess    ,NL
      ]
        Pull    "PC"

95
        MOV     R0, #BadParmsErr
        BL      SetV
        B       %BT90

; =======
; DoMount
; =======

;entry
; R1 drive
; R2 disc address
; R3 -> buffer
; R4 length
; ( R5 -> disc rec to fill in for floppies )

;exit R0,V internal error

DoMount ROUT
        Push    "R1-R4,R6-R8,LR"
        BL      Misc_CheckDrivePresent
        Pull    "R1-R4,R6-R8,PC",VS
        MOV     R8, #0
        LDR     LR, FCFeatures
        TST     LR, #FC_Big
        BEQ     %FT05
; fudgery - Mount gets passed a byte address; we need to convert to the
; sector/block address that LowLevelOp expects.
        sbaddr  R7, BlockSizes          ; if not examined drive yet

        LDRB    R8, [R7,R2, LSR #(32-3)]
        CMPS    R8, #0                  ; V=0
        BLEQ    Examine                 ; (R2,R7->R0,R8,V) then do it now
        Pull    "R1-R4,R6-R8,PC",VS
05
      [ AllowPartitions
        Push    "R0, R1,R2,R3"
        MOV     R2, R2, LSR #(32-3)     ; disc number requested
        DrvRecPtr R1, R2
        LDRB    R3, [R1, #:INDEX:DevNo]
        LDR     R1, =Service_SCSIMounting
        SWI     OS_ServiceCall          ; announce our intentions
        Pull    "R0,R1,R2,R3"
      ]
        TSTS    R2, #bit31
        BEQ     %FT20                   ; Floppy
;read in the boot block as requested, will probably trigger Examine
10
        AND     LR, R2, #DiscBits
        BIC     R2, R2, #DiscBits
        ORR     R2, LR, R2, LSR R8
        MOV     R6, #NIL                ; no defect list - empty for SCSI discs anyway
        MOV     R1, #DiscOp_ReadSecs :OR: DiscOp_Op_IgnoreEscape_Flag
        CMP     R4, #0
        BLNE    LowLevelOp              ; (R1-R6->R0-R4,V)
      [ Debug4
        mess    ,"Mount, finished"
        mess    ,NL
      ]
95
        Pull    "R1-R4,R6-R8,PC"


20
;Read from DefectListDiscAdd &C00, size SzDefectList &200
;
;
      [ Debug4
        mess    ,"Mount-floppy, reading bootblock"
        mess    ,NL
      ]
        AND     R2, R2, #DiscBits
        MOV     R4, #DefectListDiscAdd
        ORR     R2, R2, R4, LSR R8
        MOV     R3, #ScratchSpace
        MOV     R4, #SzDefectList
        MOV     R1, #DiscOp_ReadSecs :OR: DiscOp_Op_IgnoreEscape_Flag
        MOV     R6, #NIL                ; no defect list - empty for SCSI discs anyway
        BL      LowLevelOp              ; (R1-R6->R0-R4,V)
        BVS     %BT95
        LDRB    R4, [R7,R2, LSR #(32-3)]
;check defect list
        MOV     R0, #ScratchSpace
        MOV     R1, #SzDefectList
        BL      CheckSum                ; (R0,R1->R0-R2,V)
        ;MOVVS  R0,#BadDefectListErr
        ;B      %FT30
        BVS     %FT30

;read disc structure from defect list
        MOV     R2, #ScratchSpace
        ADD     R2, R2, #SzDefectList-MaxStruc

        LDRB    LR, [r2, #3]            ; check possible other type of disc image
        CMP     LR, #8
        BHI     %ft30
        LDRB    LR, [r2, #2]
        TEQ     LR, #0
        BEQ     %ft30
        LDRB    LR, [r2, #1]
        TEQ     LR, #8
        BEQ     %ft30
        LDRB    LR, [r2, #0]
        TEQ     LR, R8
        BNE     %ft30

        LDR     R0, [SP]                ; recover drive number
        LDMIA   R2,{R1-R4,LR}
        ASSERT  RootDir=3*4
        ORR     R4, R4, R0, LSL #(32-3)
        STMIA   R5, {R1-R4,LR}
        B       %FT40
30
        ; Not a FileCore boot block - create a semi-fake disc record to get
        ; us off the ground for non-FileCore discs
        STRB    R8, [R5, #SectorSize]
        MOV     R0, #1
        STRB    R0, [R5, #Heads]
        STRB    R0, [R5, #SecsPerTrk]
        STRB    R0, [R5, #LowSector]
        MOV     R0, #0
        STRB    R0, [R5, #Density]
        ;STR    R0, [R5, #DiscSize2]
        MOV     R0, #640*1024
        STR     R0, [R5, #DiscSize]

40

      [ Debug4
        mess    ,"Mount-floppy, reading map"
        mess    ,NL
      ]

        LDMFD   SP, {R1-R4}
        B       %BT10

Misc_CheckDrivePresent
        Push    "LR"
        mapDrvToDevLo LR,R1
        CMP     LR,#-1
        Pull    "PC",NE
        MOV     R0, #BadDriveErr
        BL      SetV
        Pull    "PC"

; ========
; CheckSum
; ========

; Check consistency of block under checksum, last byte of block is check byte

; entry: R0=start, R1=block length
; exit:  R0,R1 preserved, R2=checksum, V=0 <=> good

CheckSum ROUT
        Push    "R1,R3,LR"
        ADDS    LR, R0, R1              ; ->end+1      C=0
        SUB     R1, LR, #1              ; ->check byte
        MOV     R2, #0
        B       %FT20
10
        LDRB    R3, [R1,#-1] !          ; get next byte
        ADC     R2, R2, R3              ; add into checksum
        MOVS    R2, R2, LSL #24         ; bit 8 = carry
        MOV     R2, R2, LSR #24
20
        TEQS    R0, R1
        BNE     %BT10                   ; loop until done
        LDRB    R3, [LR,#-1]

        CMPS    R2, R3                  ; check sum = old check byte ?
        BLNE    SetV
        Pull    "R1,R3,PC"

; =======
; Examine
; =======

;do any initialisation required for drive including reading block size

;entry
; R2 FileCore drive number in top 3 bits
; R7 -> BlockSizes

;exit
; R0, V set if error
; R8 log to base 2 of block size, also stored in correct entry in BlockSizes

Examine
        Push    "R0,R9,LR"
;
; Issue SCSI_Initialise (R0=DetermineDevice) which returns device type
; and block size.
;
; The device type must be 0 (Direct-access) and the block size 256,512 or 1024.
; FileCore can handle block sizes of 256,512 or 1024 bytes.
;
      [ Debug3
        mess    , "Examine - doing DetermineDevice", NL
      ]

        Push    "R1,R2"
        SUB     SP,SP,#16
        MOV     R0,#2                           ; DetermineDevice
        MOV     R1,R2,LSR #(32-3)
        mapDrvToDevLo R1,R1                    ; map drive onto devices
      [ Ejecting
        BL      LoadMedia
      ]
        MOV     R2,SP
      [ RetryIfBusy
        MOV     R9,#60                  ; Retry on timeout for 60 seconds
78
        Push    "R0-R2"                 ; Stack incase we retry
        SWI     XSCSI_Initialise
        ADDVC   SP,SP,#12               ; Command OK, ditch stacked R0-R2
        BVC     %FT79
        BL      CheckRetry
        Pull    "R0-R2",VC              ; Retry with stacked R0-R2
        BVC     %BT78
        ADDVS   SP,SP,#28               ; Balance stack, before
        Pull    "R1,R2",VS
        BVS     %FT95                   ; reporting error
79
      |
        SWI     XSCSI_Initialise
        ADDVS   SP,SP,#16               ; Balance stack, before
        Pull    "R1,R2",VS
        BVS     %FT95                   ; reporting error
      ]
        LDRB    R0,[SP,#0]              ; Device type
        CMP     R0,#DevDirectAccess     ;
        CMPNE   R0,#DevOpticalMemory
        BNE     %FT96                   ; Error - 'Not a winnie'

        LDR     R0,[SP,#12]             ; Block (sector) size
        CMP     R0,#256
        CMPNE   R0,#512
        CMPNE   R0,#1024
        BNE     %FT97                   ; Error - Not 256/512/1024 byte sectors

        ADD     SP,SP,#16               ; Finished with stacked data

        LDR     R2,[SP,#4]              ; Get drive number back
        Push    "R0,R3"
        MOV     R2,R2,LSR #(32-3)
        BL      ReserveDevice
        STRVS   R0, [SP]
        Pull    "R0,R3"
        Pull    "R1,R2"

        BVS     %FT95


;assuming R0 has block size = 2^n , the following code will convert it to the
;log to base two ie n as needed elsewhere

        MOV     R8, #0
80
        MOVS    R0, R0, LSR #1
        ADDNE   R8, R8, #1
        BNE     %BT80
        STRB    R8, [R7, R2, LSR #(32-3)]       ; save result in BlockSize table

95                                      ;error exit
        LDRVS   LR, FCFeatures
        TSTVS   LR, #FC_NewErrors
        ORREQ   R0, R0, #ExternalErrorBit
        STRVS   R0, [SP]
        Pull    "R0,R9,PC"


96                              ;Return error - NotWinnie
        ADD     SP,SP,#16
        Pull    "R1,R2"
        ADRL    R0, ErrorBlock_SCSIFSBadSCSIType
        BL      CopyError
        B       %BT95

97                              ;Return error - BadBlockSize
        ADD     SP,SP,#16
        Pull    "R1,R2"
        ADRL    R0, ErrorBlock_SCSIFSSectorSizeNotSupported
        BL      CopyError
        B       %BT95


; ===========
; DoLockDrive
; ===========

;entry
; R1 drive 0..3
;
; Only called for a floppy drive

DoLockDrive
        Push    "R1,R2,LR"
        SavePSR R2
        mapDrvToDevLo R1, R1
        BL      LockMedia
        RestPSR R2,,f
        Pull    "R1,R2,PC"


; =============
; DoUnlockDrive
; =============

;entry
; R1 drive 0..3
;
; Only called for a floppy drive

DoUnlockDrive
        Push    "R1,R2,LR"
        SavePSR R2
        mapDrvToDevLo R1, R1
        BL      UnLockMedia
        RestPSR R2,,f
        Pull    "R1,R2,PC"


DiscString
        DCB     "disc",0
        ALIGN


; ============
; DoPollPeriod
; ============

;entry
; R1 drive (floppy only??)

;exit
; R5 minimum polling period (in centi-seconds), or -1 if disc changed doesn't work
; R6 -> media type string eg 'disc'

DoPollPeriod
        Push    "LR"
        MOV     R5, #PollPeriod
        ADR     R6, DiscString
        Pull    "PC"

; =======
; DoEject
; =======

;entry
; R1 drive 0..7, or bit 31 set indicating our choice (searching for media)
; R4 = iteration count (if bit 31)
; R5 = timeout period (if bit 31)
;

DoEject
      [ Ejecting
        Push    "R1,R2,LR"
        SavePSR R2
        TST     R1, #bit31
        BEQ     %FT10
; media search case
        TEQ     R4, #0                  ; only eject on iteration 0
        BNE     %FT95
        MOV     R1, #0                  ; if not specified, eject SCSI::0
10
        mapDrvToDevLo R1, R1
        BL      EjectMedia
95
        RestPSR R2,,f
        Pull    "R1,R2,PC"
      |
        MOV     PC, LR
      ]

; =============
; DoDriveStatus
; =============

;entry
; R1 drive 0..7
DoDriveStatus
        CMP     R1, #MaxDriveNo
        MOVHS   R2, #2
        MOVHS   PC, LR
        mapDrvToDevLo R2, R1
        CMP     R2, #-1
        MOVEQ   R2, #2
        MOVNE   R2, #0
        MOV     PC, LR

;
; On entry
;   R1 = Scsi deviceID
;
DoDismount
        Push    "R0-R5,LR"

        ASSERT  (Dismount_end - Dismount_start) = 16
        ADR     R2, Dismount_start
        LDMIA   R2, {R2,R3,R4,R5}       ; Copy string onto stack (known to be 16 bytes)
        STMDB   SP!,{R2,R3,R4,R5}       ; 
        mapDevToDrvLo R1, R1            ; map device onto drives
        ADD     R0, R1, #"0"
        STRB    R0, [SP,#14]            ; position pf 'x' in dismount string below
        MOV     R0, SP
        SWI     XOS_CLI

      [ AllowPartitions
        MOV     R2, #0
        ADD     LR, SB, R1, LSL #2      ; disc number to word address
        STR     R2, [LR, #:INDEX:PartitionBaseAdd-:INDEX:SCSIFSWorkBase] ; reset
      ]

        ADD     SP, SP, #(Dismount_end - Dismount_start)

        sbaddr  R2, BlockSizes          ; Zero the BlockSize table entry, to force
        MOVVC   R0, #0                  ; an Examine and re-initialise of the
        STRVCB  R0, [R2,R1]             ; ScsiDriver on next ScsiFs access
        mapDrvToDevLo R1, R1            ; map drive onto device
        MOVVC   R0, #2                  ; RC for ReleaseDevice
        MOVVC   R8, R12                 ; Access Key
        SWIVC   XSCSI_Reserve

        STRVS   R0, [SP]
        Pull    "R0-R5,PC"


Dismount_start
        DCB     "SCSI:DISMOUNT x", 0
Dismount_end



;
; On entry
;   R1 = Scsi deviceID
;   R2 = Drive number
;
ReserveDevice ROUT
        Push    "R2,LR"
        MOV     R0, #0                  ; RC for ClaimDevice
        ADR     R2, DoDismount          ; Adr } Used by ScsiDriver to request us to
        MOV     R3, R12                 ; R12 } Dismount & ReleaseDevice
        MOV     R8, R12                 ; Good value for the access key
        SWI     XSCSI_Reserve
        BVS     %FT95

        LDR     R3, [SP]
        MOV     R0, #4                  ; RC for ControlErrors
        TST     R3, #4
        MOVEQ   R2, #1                  ; Floppy - Auto Request-Sense, Return Unit-Attention
        MOVNE   R2, #2                  ; Winnie - Auto Request-Sense, Ignore Unit-Attention
        SWI     XSCSI_Control
        TST     R3, #4
        BLNE    LockMedia               ; Lock winnies in place
95
        Pull    "R2,PC"


;
; On entry
;   R1 = Scsi deviceID
;
LockMedia
      [ Debug3
        mess    , "Locking media", NL
      ]
        Push    "R0-R6,R8,LR"
        SavePSR R6
        ADR     R2, CDB_LockMedia
LocMed_10
        AND     R0, R1, #&FF            ; DeviceID
        MOV     R1, #6                  ; CDB length
        MOV     R3, #0
        MOV     R4, #0
        MOV     R5, #0
        MOV     R8, R12                 ; AccessKey
        DoSCSIOp
        BIC     R6, R6, #V_bit
        RestPSR R6,,f
        Pull    "R0-R6,R8,PC"


;
; On entry
;   R1 = Scsi deviceID
;
UnLockMedia
      [ Debug3
        mess    , "Unlocking media", NL
      ]
        Push    "R0-R6,R8,LR"
        SavePSR R6
        ADR     R2, CDB_UnLockMedia
UnLocMed_10
        mapDevToDrvLo R1, R1
        sbaddr  R5, BlockSizes
        MOV     R4, #0
        STRB    R4, [R5,R1]

        B       LocMed_10

      [ Ejecting
;
; On entry
;   R1 = Scsi deviceID
;
EjectMedia
      [ Debug3
        mess    , "Ejecting media", NL
      ]
        Push    "R0-R6,R8,LR"
        SavePSR R6
        ADR     R2, CDB_EjectMedia
        B       UnLocMed_10

;
; On entry
;   R1 = Scsi deviceID
;
LoadMedia
      [ Debug3
        mess    , "Loading media", NL
      ]
        Push    "R0-R6,R8,LR"
        SavePSR R6
        ADR     R2, CDB_LoadMedia
        B       UnLocMed_10
      ]

CDB_LockMedia
        DCB     &1E                     ; PREVENT/ALLOW MEDIA REMOVAL
        DCB     0                       ; LUN & reserved
        DCB     0
        DCB     0
        DCB     1                       ; Reserved=0 (b7..b1), Prevent removal=1 (b0)
        DCB     0                       ; Vendor Unique=0 (b7..b6),Reserved=0 (b5..b2),
        ALIGN                           ; Flag=0 (b1),Link=0 (b0)

CDB_UnLockMedia
        DCB     &1E                     ; PREVENT/ALLOW MEDIA REMOVAL
        DCB     0                       ; LUN & reserved
        DCB     0
        DCB     0
        DCB     0                       ; Reserved=0 (b7..b1), Prevent removal=0 (b0)
        DCB     0                       ; Vendor Unique=0 (b7..b6),Reserved=0 (b5..b2),
        ALIGN                           ; Flag=0 (b1),Link=0 (b0)

      [ Ejecting
CDB_LoadMedia
        DCB     &1B                     ; START/STOP UNIT
        DCB     0                       ; LUN & immediate
        DCB     0
        DCB     0
        DCB     3                       ; Power=0 (b7..b4), Reserved=0 (b3..b2), Load/Eject=1 (b1),
        DCB     0                       ; Start=1 (b0)
        ALIGN

CDB_EjectMedia
        DCB     &1B                     ; START/STOP UNIT
        DCB     1                       ; LUN & immediate
        DCB     0
        DCB     0
        DCB     2                       ; Power=0 (b7..b4), Reserved=0 (b3..b2), Load/Eject=1 (b1),
        DCB     0                       ; Start=0 (b0)
        ALIGN
      ]

        LNK     TokHelpSrc.s
