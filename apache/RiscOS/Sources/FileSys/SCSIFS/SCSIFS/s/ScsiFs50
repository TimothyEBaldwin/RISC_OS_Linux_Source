; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; >ScsiFs50

        TTL     "Initialisation and FS star commands"

FSCreateBlock
        DCB     EjectBit+CreateFlag_FixedDiscPollChangeSupport
        DCB     (CreateFlag_DriveStatusWorks:SHR:8)+(BigBit:SHR:8)+(NewErrorBit:SHR:8)
        DCB     0,fsnumber_SCSIFS
        DCD     CreateTitle    - Module_BaseAddr
        DCD     ScsiFsBootText - Module_BaseAddr
        DCD     LowLevelEntry  - Module_BaseAddr
        DCD     MiscEntry      - Module_BaseAddr

; >>>>>>>>>
; InitEntry
; >>>>>>>>>

InitEntry ROUT                           ; NO REENTRANCY CHECK NEEDED
      [ Debug1
        mess    ,"InitEntry - In",NL
      ]
        Push    "R7-R11,SB,LR"
        MOV     R10,#1                  ; init flag
        MOV     R11,R12

        MOV     R0, #ModHandReason_Claim
        MOV     R3, #:INDEX: WorkSize
        SWI     XOS_Module              ; claim workspace
        BVS     %FT95

        MOV     SB, R2
        STR     SB, [R11]

      [ UseSpecialScatterBlk
        ADR     R0, PaddingBlock
        MOV     R1, #0
        MOV     R2, #?PaddingBlock
05
        SUBS    R2, R2, #4
        STR     R1, [R0], #4
        BNE     %BT05
      ]

        sbaddr  R0, DrvRecs
        MOV     R1, #(-1:SHL:8)
        MOV     R2, #0
        STRB    R2, AttCBCount
        STRB    R2, DetCBCount
        STRB    R2, Dieing
        ASSERT  MaxDriveNo = 8
      [ AllowPartitions
        STR     R2, PartitionBaseAdd+0
        STR     R2, PartitionBaseAdd+4
        STR     R2, PartitionBaseAdd+8
        STR     R2, PartitionBaseAdd+12
        STR     R2, PartitionBaseAdd+16
        STR     R2, PartitionBaseAdd+20
        STR     R2, PartitionBaseAdd+24
        STR     R2, PartitionBaseAdd+28
      ]
        STMIA   R0!, {R1,R2}            ; Drive rec for ScsiFs::0
        STMIA   R0!, {R1,R2}            ;                      1
        STMIA   R0!, {R1,R2}            ;                      2
        STMIA   R0!, {R1,R2}            ;                      3
        STMIA   R0!, {R1,R2}            ;                      4
        STMIA   R0!, {R1,R2}            ;                      5
        STMIA   R0!, {R1,R2}            ;                      6
        STMIA   R0!, {R1,R2}            ;                      7

        MOV     R2, #0
        ASSERT  MaxDriveNo = 8
        STR     R2, BlockSizes          ; >>>>Init floppy BlockSizes
        STR     R2, BlockSizes+4        ; Block (sector size) for ScsiFs::4..7

        ASSERT  DrvFlags = 0
        ASSERT  DevNo = 1
        ASSERT  DrvSequenceNum = 4
        ASSERT  SzDrvRec = 8
        sbaddr  R0, DrvRecs
        MOV     R1, #LostUnitAtn :OR: (-1:SHL:8)
        MOV     R2, #0
        STMIA   R0!, {R1,R2}            ; Drive rec for ScsiFs::0
        STMIA   R0!, {R1,R2}            ;                      1
        STMIA   R0!, {R1,R2}            ;                      2
        STMIA   R0!, {R1,R2}            ;                      3

        MOV     R0, #0
        STR     R0, MsgFileOpen         ; No it isn't
      [ standalone
        BL      MsgFileReg
        BVS     %FT85
      ]
        BL      ReadCMOSflags           ; (->R0,R2,R3,R4,R5,V) read #floppies & winnies
        BL      ScanDrives
        MOV     R3, #4
        MOV     R4, #4
        STRB    R3, Floppies
        STRB    R4, Winnies

;Assume for the moment that user wants to start up with dir set as SCSI drives
;remain attached, unlike floppies.


        ORR     R3, R3, R5, LSL #16     ; Combine floppy count and default drive
        ORR     R3, R3, R4, LSL #8      ; OR in number of drives
        BL      ReadDirCMOS             ; (->R0,R4,V)
        BL      ReadFileCMOS            ; (->R0,R5,V)

      [ DoBuffering
        ASSERT  F,"Read ahead/write behind NOT supported"
      |
        MOV     R5,#0                   ; NO background Xfers
      ]

        MVN     R6, #0
        ADR     R0, FSCreateBlock
        ADRL    R1, Module_BaseAddr
        MOV     R2, R11
        SWI     XFileCore_Create        ; (R0-R6->R0-R2,V)
        BVS     %FT85
        STR     R0, FileCorePrivate
        STR     R2, WinnieCallAfter
        MOV     R8, R0
        MOV     R0, #6
        SWI     XFileCore_MiscOp        ; (R0,R8->R0,V)
        MOVVC   R7, #FC_Big
        MOVVS   R7, #0
        SWI     XFileCore_Features
        SUBVSS  R0, R0, R0              ; R0=0, V cleared
        TST     R0, #Feature_NewErrors
        ORRNE   R7, R7, #FC_NewErrors
        STR     R7, FCFeatures
      [ Debug1
        mess    ,"InitEntry - Out",NL
      ]
        Pull    "R7-R11,SB,PC"

        LTORG



; >>>>>>>>
; DieEntry
; >>>>>>>>

DieEntry
      [ Debug1
        mess    ,"DieEntry",NL
      ]
        Push    "R7-R11,SB,LR"
        getSB

        MOV     R0, #1
        STRB    R0, Dieing                      ; prevent any further callbacks
        LDRB    R0, AttCBCount
        TEQ     R0, #0
        LDREQB  R0, DetCBCount
        TEQ     R0, #0
        BEQ     %ft1                            ; none awaited
; need to let CBs in **********
1


        MOV     R10,#0                          ; Delete "FileCore%SCSI"
        MOV     R0, #ModHandReason_Delete       ; filecore will close down all
        ADRL    R1, DieTitle                    ; open files before dying
        SWI     XOS_Module                      ; 
        MOVVS   R9, R0

        MOV     R5, #0
        sbaddr  R7, BlockSizes
01
        MOV     R0, #UpCall_DriveRemoved
        MOV     R1, R5
        BL      UpcallDrive
        LDRB    R4, [R7, R5]
        TST     R4, #&FF
        BEQ     %FT02
        mapDrvToDevLo R1, R5
        BL      UnLockMedia
        MOV     R0, #2                  ; RC for ReleaseDevice
        MOV     R8, R12                 ; Access key
        SWI     XSCSI_Reserve
02
        MOVVS   R9, R0
        ADD     R5, R5, #1              ; Next drive
        CMP     R5, #MaxDriveNo
        BNE     %BT01                   ; Loop until no more drives

85
        MOVVS   R9, R0                  ; Keep any earlier error

        LDR     R0, MsgFileOpen
        TEQ     R0, #0
        ADRNE   R0, MsgFileBlock        ; Close the messages if open
        SWINE   XMessageTrans_CloseFile 
        MOV     R0, #0
        STR     R0, MsgFileOpen

      [ standalone
        BL      MsgFileDereg            ; Remove ResourceFS messages
      ]

        MOV     R0, #ModHandReason_Free
        MOV     R2, SB
        SWI     XOS_Module              ; Free workspace
      [ Dev
        wrhex   R0, VS
        mess    VS,"Heap error",NL
      ]
95
        MOVVS   R9, R0
      [ Dev
        wrhex   R10,VS
        wrhex   R0, VS
        mess    VS,"Error",NL
      ]

        ADDS    R0, R9, #0
        ADDNES  R10,R10,#0              ; only error on init (R10=0/1 for Die/Init)
        Pull    "R7-R11,SB,PC",EQ

        BL      SetV
      [ Dev
        wrhex   R0
        mess    ,"ScsiFs Init Error",NL
      ]
        Pull    "R7-R11,SB,PC"

        LTORG

ServiceTable
        ASSERT  Service_Reset < Service_ResourceFSStarting
        ASSERT  Service_ResourceFSStarting < Service_SCSIAttached
        ASSERT  Service_SCSIAttached < Service_SCSIDetached
        ASSERT  Service_SCSIDetached < Service_BootChoicesVarsSet
        DCD     0
        DCD     ServiceEntry2 - Module_BaseAddr
        DCD     Service_Reset
      [ standalone
        DCD     Service_ResourceFSStarting
      ]
        DCD     Service_SCSIAttached
        DCD     Service_SCSIDetached
        DCD     Service_BootChoicesVarsSet
      [ PartitionManager
        DCD     Service_MorePartitions
      ]  
        DCD     0

;>>>>>>>>>>>>
;ServiceEntry
;>>>>>>>>>>>>

        DCD     ServiceTable - Module_BaseAddr
ServiceEntry
        MOV     R0, R0
        CMP     R1, #Service_Reset
      [ standalone
        CMPNE   R1, #Service_ResourceFSStarting
      ]
        Push    "LR"
        ASSERT  Service_SCSIAttached >= 0x10000
        LDRNE   LR,=Service_SCSIAttached
        CMPNE   R1,LR
        ADDNE   LR,LR,#Service_SCSIDetached - Service_SCSIAttached
        CMPNE   R1,LR
        LDRNE   LR,=Service_BootChoicesVarsSet
        CMPNE   R1,LR
      [ PartitionManager
        LDRNE   LR,=Service_MorePartitions
        CMPNE   R1,LR
      ]
        Pull    "PC",NE
        Pull    "LR"

ServiceEntry2
      [ standalone
        TEQ     R1, #Service_ResourceFSStarting
        BEQ     ServiceResFS
      ]
        Push    "LR"
        LDR     LR, =Service_BootChoicesVarsSet
        TEQ     R1, LR
        BEQ     ServiceChoicesVarsSet
      [ PartitionManager
        LDR     LR, =Service_MorePartitions
        TEQ     R1, LR
        Pull    "LR",EQ
        BEQ     ServiceMorePartitions
      ]
        TEQ     R1, #Service_Reset
        Pull    "LR",NE
        BNE     ServiceAttachDetach
      [ Debug1
        mess    ,"ServiceEntry, Service_Reset",NL
      ]
        Push    "R0-R5,R8"
        getSB
;
;Reservations are lost over reset, so Re-reserve each device/drive with
; a non-zero BlockSize entry
;
        MOV     R5, #0
        sbaddr  R4, BlockSizes
101
        LDRB    LR, [R4, R5]
        TST     LR, #&FF
        BEQ     %FT102
        mapDrvToDevLo R1, R5
        BL      ReserveDevice
102
        ADD     R5, R5, #1              ; Next drive
        CMP     R5, #MaxDriveNo
        BNE     %BT101                  ; Loop until no more drives

        Pull    "R0-R5,R8,PC"

      [ standalone
ServiceResFS
        Push    "R0,LR"
        ADRL    R0, MsgFileResArea
        MOV     LR, PC
        MOV     PC, R2                  ; Re-register with ResourceFS
        Pull    "R0,PC"
      ]

ServiceChoicesVarsSet ROUT
        Push    "R0-R5"                 ; LR already stacked
        ; Check all attached devices against the Hide file
        getSB
        MOV     R3, #MaxDriveNo-1
        sbaddr  R4, BlockSizes
10
        LDRB    LR, [R4, R3]
        TEQ     LR, #&FF
        BEQ     %FT90
        DrvRecPtr R5,R3
        mapDrvToDevLo R1,R3,R5
        BL      CheckDriveAllowed
        BNE     %FT90
        BL      DoDismount
        BLVC    DetachDevice
90
        SUBS    R3, R3, #1
        BGE     %BT10
        Pull    "R0-R5,PC"

DoSwiRetryDiscOp ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_DiscOp
        Pull    "R8,PC"


DoNulSwi
        MOV     PC,LR


DoSwiDrives ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_Drives
        Pull    "R8,PC"


DoSwiFreeSpace ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_FreeSpace
        Pull    "R8,PC"

DoSwiMiscOp ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_MiscOp
        Pull    "R8,PC"

DoSwiSectorDiscOp ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_SectorDiscOp
        Pull    "R8,PC"

DoSwiDiscOp64 ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_DiscOp64
        Pull    "R8,PC"

DoSwiFreeSpace64 ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_FreeSpace64
        Pull    "R8,PC"

;Retry word = (Retry word BIC R0) EOR (R1 AND R0)
;exit R1 = R1 AND R0
; R2 old value, R3 new value

DoSwiDescribeDisc ROUT
        Push    "R8,LR"
        LDR     R8, FileCorePrivate
        SWI     XFileCore_DescribeDisc
        Pull    "R8,PC"


;
; DoSwiTestReady
; ==============
;
; On entry
;   R1 = Drive number (4..7)
;
; On exit
;   R0 = 0 - Drive NOT present
;   R1 = 1 - Don't know
;   R2 = 2 - Drive present & ready
;
DoSwiTestReady
        Push    "R1,R2,LR"
        SUB     SP,SP,#16
        MOV     R0,#2                           ; DetermineDevice
        mapDrvToDevLo R1, R1                   ; map drive onto device
        MOV     R2,SP
        SWI     XSCSI_Initialise
        ADD     SP,SP,#16

        MOVVS   R0,#1
        MOVVC   R0,#2
        CLRV
        Pull    "R1,R2,PC"

      [ AllowPartitions
; On entry
;   R0 = reason code
;   Reason code:  0 = Sector offset for drive 1 in R1 is in R2
;   R1 = Drive number (0..7)
;   R2 = sector offset to base of image
;        This offset is zero by default. It may be changed by this swi
;        at any time, but changing this value after mounting the disc
;        is usually not wise!
;
;   Reason code:  1 = report SCSIFS disc number
; returns
;   R2 = SCSIFS disc icon (0..7), or -1 if none
;
DoSWIPartitions ROUT
        Push    "R1,R2,LR"
        TEQ     R0, #0                  ; set partition base
        BNE     %ft1
        CMP     R1, #MaxDriveNo-1
        Pull    "R1,R2,PC", HI
        ADD     LR, SB, R1, LSL #2      ; word offset
        STR     R2, [LR, #:INDEX:PartitionBaseAdd-:INDEX:SCSIFSWorkBase] ; remember
        Pull    "R1,R2,PC"
1
        TEQ     R0, #1                 ; report icon used
        BNE     %FT6                   ; report ID and offset for icon

        Push    "R3,R4,R5"
        MOV     R2, #MaxDriveNo-1
        LDR     R5, [SP, #4*4]         ; retrieve R2 off stack
2
        DrvRecPtr R4, R2
        LDRB    R3, [R4, #:INDEX:DevNo]
        TEQ     R3, R1
        BNE     %ft4
        ADD     LR, SB, R2, LSL #2      ; word offset
        LDR     LR, [LR, #:INDEX:PartitionBaseAdd-:INDEX:SCSIFSWorkBase] ;
        TEQ     LR, R5
        BEQ     %ft3                    ; same start sector too
4
        SUBS    R2,R2,#1
        BGE     %bt2
3
        Pull    "R3,R4,R5"
        STR     R2,[R13, #4]            ; update R2 return value
        Pull    "R1,R2,PC"

6
        TEQ     R0, #2                 ; report id & offset for icon
        Pull    "R1,R2,PC",NE

        Push    "R3,R4,R5"

        DrvRecPtr R4, R1
        ADD     LR, SB, R1, LSL #2      ; word offset
        LDRB    R1, [R4, #:INDEX:DevNo]
        LDR     R2, [LR, #:INDEX:PartitionBaseAdd-:INDEX:SCSIFSWorkBase] ;

        Pull    "R3,R4,R5"
        ADD     sp, sp, #4*2            ; forget stored r1 and r2
        Pull    "PC"
      ]

; >>>>>>>>
; SwiEntry
; >>>>>>>>

SwiEntry ROUT
        Push    "SB,LR"
        getSB
        CMPS    R11,#FirstUnusedSwi
        ADRLO   LR, %FT10
        ADDLO   PC, PC, R11,LSL #2
        B       UnknownSwi
05
        B       DoSwiRetryDiscOp
        B       DoNulSwi
        B       DoSwiDrives
        B       DoSwiFreeSpace

        B       DoNulSwi
        B       DoSwiDescribeDisc
        B       DoSwiTestReady
        B       UnknownSwi

        B       UnknownSwi
        B       UnknownSwi
        B       UnknownSwi
        B       UnknownSwi

        B       DoSwiMiscOp
        B       DoSwiSectorDiscOp
        B       UnknownSwi
        B       UnknownSwi

        B       UnknownSwi
        B       UnknownSwi
        B       DoSwiFreeSpace64
        B       UnknownSwi

        B       DoSwiDiscOp64
      [ AllowPartitions
        B       DoSWIPartitions
      |
        B       UnknownSwi
      ]


FirstUnusedSwi  * (.-%BT05)/4

UnknownSwi
        ; SWI out of range: set R0 -> international error block
        Push    "R1"
        ADR     R0,ErrorBlock_ModuleBadSWI
        ADR     R1,SwiNames             ; R1 -> name of this module
        BL      CopyErrorOne
        Pull    "R1"
        ; Fall through

10      TEQ     PC,PC
        Pull    "SB,PC",EQ
        Pull    "SB,LR"
        MOVVCS  PC,LR
        ORRVSS  PC,LR,#V_bit

SwiNames ROUT
        DCB     "SCSIFS",0
        DCB     "DiscOp",0
        DCB     "NOP",0
        DCB     "Drives",0
        DCB     "FreeSpace",0
        DCB     "NOP",0
        DCB     "DescribeDisc",0
        DCB     "TestReady",0
        DCB     "7",0
        DCB     "8",0
        DCB     "9",0
        DCB     "10",0
        DCB     "11",0
        DCB     "MiscOp",0
        DCB     "SectorDiscOp",0
        DCB     "14",0
        DCB     "15",0
        DCB     "16",0
        DCB     "17",0
        DCB     "FreeSpace64",0
        DCB     "19",0
        DCB     "DiscOp64",0
        DCB     "Partitions",0
        DCB     0
        ALIGN

        MakeInternatErrorBlock ModuleBadSWI,,BadSWI

      [ international_help
ihflag  *       International_Help
      |
ihflag  *       0
      ]

ComTab
        Command SCSI,           0, 0, ihflag
      [ DoBuffering
        Command SCSIFSBuffers,  0, 0, ihflag :OR: Status_Keyword_Flag
      ]
        Command SCSIFSDirCache, 0, 0, ihflag :OR: Status_Keyword_Flag
        Command SCSIFSDrive,    0, 0, ihflag :OR: Status_Keyword_Flag
        DCB     0
        ALIGN


; >>>>>>>>>
; SCSI_Code
; >>>>>>>>>

SCSI_Code
        Push    "LR"                    ; NO REENTRANCY CHECK NEEDED
        MOV     R0, #FSControl_SelectFS
        ADRL    R1, CreateTitle         ; >>>RCM was ScsiFsTitle
        SWI     XOS_FSControl
        Pull    "PC"


; CONFIGURE/STATUS HANDLERS

      [ DoBuffering
; >>>>>>>>>>>>>>>>>>
; SCSIFSBuffers_Code
; >>>>>>>>>>>>>>>>>>

ShortConSynSCSIFSBuffers
        DCB     "SCSIFSBuffers <D>",0
        ALIGN

SCSIFSBuffers_Code ROUT
        Push    "R0-R6,SB,LR"
        getSB
        MOVS    R1, R0
        BNE     %FT05

; print syntax message
        ADR     R0, ShortConSynSCSIFSBuffers
        SWI     XOS_Write0
        SWIVC   XOS_NewLine
        B       ConfigReturn

05
        TEQS    R1, #1
        BNE     %FT15

; print status message
        BL      ReadFileCMOS            ; (->R0,R5,V)
        BVS     ConfigReturn
        SWI     XOS_WriteS
        DCB     "SCSIFSBuffers", 0
        ALIGN
        BVS     ConfigReturn
10
        MOV     R0, R5
        BL      WrDec                   ; (R0->R0,V)
        SWIVC   XOS_NewLine             ; (->R0,V)
        B       ConfigReturn

; parse configure
15
        MOV     R0, #10 :OR: bit31 :OR: bit30
        SWI     XOS_ReadUnsigned        ; (R0-R2->R0-R2,V)
        BVS     ConfigReturn
        MOV     R1, #FileCMOS
        B       ConWrite
      ]


; >>>>>>>>>>>>>>>>>>>
; SCSIFSDirCache_Code
; >>>>>>>>>>>>>>>>>>>

ShortConSynSCSIFSDirCache
        DCB     "SCSIFSDirCache <D>[K]",0
        ALIGN

SCSIFSDirCache_Code ROUT
        Push    "R0-R6,SB,LR"
        getSB
        MOVS    R1, R0
        BNE     %FT05

; print syntax message
        ADR     R0, ShortConSynSCSIFSDirCache
        SWI     XOS_Write0
        SWIVC   XOS_NewLine
        B       ConfigReturn

05
        TEQS    R1, #1
        BNE     %FT15

; print status message
        BL      ReadDirCMOS             ; (->R0,R4,V)
        BVS     ConfigReturn
        SWI     XOS_WriteS
        DCB     "SCSIFSDirCache", 0
        ALIGN
        BVS     ConfigReturn
10
        MOV     R0, R4, LSR #10
        BL      WrDec                   ; (R0->R0,V)
        MOVVC   R0, #"K"
        SWIVC   XOS_WriteC
        SWIVC   XOS_NewLine             ; (->R0,V)
        B       ConfigReturn

; parse configure
15
        MOV     R0, #10 :OR: bit30
        SWI     XOS_ReadUnsigned        ; (R0-R2->R0-R2,V)
        BVS     ConfigReturn
        MOV     R1, #DirCMOS
        B       ConWrite

; >>>>>>>>>>>>>>>>
; SCSIFSDrive_Code
; >>>>>>>>>>>>>>>>

ShortConSynSCSIFSDrive
        DCB     "SCSIFSDrive <D>",0
        ALIGN

SCSIFSDrive_Code ROUT
        Push    "R0-R6,SB,LR"
        getSB
        MOVS    R1, R0
        BNE     %FT05

; print syntax message
        ADR     R0, ShortConSynSCSIFSDrive
        SWI     XOS_Write0
        SWIVC   XOS_NewLine
        B       ConfigReturn

05
        BL      ReadCMOSflags           ; (->R0,R2,R4,R5,V)
        BVS     ConfigReturn
        TEQS    R1, #1
        BNE     %FT15

; print status message
        SWI     XOS_WriteS
        DCB     "SCSIFSDrive", 0
        ALIGN
        BVS     ConfigReturn
10
        MOV     R0, R5
        BL      WrDec                   ; (R0->R0,V)
        SWIVC   XOS_NewLine             ; (->R0,V)
        B       ConfigReturn

; parse configure
15
        BIC     R2, R2, #2_00111000                     ; clear old bits
        BL      ParseAnyDrive           ; (R1->R0,R1,V)
        BVS     ConfigReturn
        ORR     R2, R2, R0, LSL #3      ; form new byte
        MOV     R1, #CMOSflags
        B       ConWrite

ConWrite
        MOV     R0, #OsByte_WriteCMOS
        SWI     XOS_Byte                ; (R0-R2->R0-R2,V)
ConfigReturn
        STRVS   R0, [SP]
        Pull    "R0-R6,SB,PC"

        END
