; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; -> FileMan

;***************************************************************************

;***************************************************************************
;        This handles all of the file manipulating parts
;***************************************************************************

;***************************************************************************

; procedures in here:

; CloseFile
; ControlOpenFile
; FSOperations
; GetByte
; MultiOperations
; OpenFile
; CDFS_Code  (for *CDFS)
; WholeFile

;***************************************************************************
CDFS_Code ROUT
;        Selects filing system when *CDFS is typed
;***************************************************************************

        ;R0 - R6 corruptible
        ;R7 - R11 must be preserved
        
        ; R12 = pointer to latest version of module
        ; R13 = pointer to full descending stack
        ; R14 = return address

        log_on

        Push    "R14"
        
        MOV     R0, #FSControl_SelectFS
        ADR     R1, FilingSystemName    ; Select CDFS
        SWI     XOS_FSControl
        
        Pull    "PC"

StartUpText
        DCB     "Next Technology "      ; let it run on !
title                                   ; identical strings
FilingSystemName
        DCB     "CDFS", 0               ;
        ALIGN

;*************************************************************************
OpenFile ROUT      ;  see page 973
;*************************************************************************

; on entry:
; R0 = 0 for read, 1 for create and update, 2 for update
; R1 -> filename
; R3 = file switch handle for the file
; R6 -> special field, else 0

; on exit:
; R0 = file information word ( see page 974 )
; R1 = file handle used by me
; R2 = buffer size for FS to use
; R3 = file extent ( buffered files only )
; R4 = space currently allocated to file ( buffered files )

; If a directory is opened, THEN it will NOT have bytes requested from it !
; this means that the object type is not needed

; R8 -> disc buffer
        log_on
        
        
        PushAllWithReturnFrame

;**************
; Trying to write to CDROM ?
;**************

        TEQ     R0, #fsopen_ReadOnly    ; Open for read only
        TEQNE   R0, #fsopen_Update      ; Or updating, but no actual writing

        MOVNE   r0, #ERROR_WRITETOCD
        BNE     ErrorExit


        MOV     R0, R1

;*********************************
; Dir: ( pathname$, RETURN pointer to block of object info, 0 if not found,
;                   RETURN R2 = 1 if a file, 2 if a directory )
;                   RETURN R3 -> start of disc buffer
;                   RETURN R4 = drive number
        MOV     R1, #2
        BL      Dir
;*********************************

        TEQ     R2, #object_nothing     ; File not found
        PullAllFromFrame EQ
        SUBEQS  R1, R1, R1              ; R1 = 0, also clear V
        MOVEQ   PC, R14
        
        MOV     R8, R3
        
        MOV     r9, r1
        
                                        ; see 'Open' for below proc
        BL      NumberOfNextFreeFileHandle  ; Returns R0 = FileHandle ( 1 - 256 )

        STR     R0, verytemporary       ;


        MOV     R0, #ModHandReason_Claim
        MOV     R3, #SizeOfOpenBuffer   ; 45 bytes claimed
        SWI     XOS_Module

        BVC     %FT01
        PullAllFromFrame                ; if cannot get it, then cry to FS
        LDR     R0, temp1               ;
        SUBS    R1, R1, R1              ; R1 = 0, clear V
        MOV     PC, R14                 ;
01

;***************
; Put details into my small buffer
;***************

        Push    "R1"                    ; add drive number to details
        MOV     R0, R4                  ;
        Push    "R2"
        
        BL      PreGetUniqueNumber      ; R0=drive number, RETURNS R1, R2
        Pull    "R2"
        STR     R1, [ R2, #DISCNUMBEROPEN ]
        
        ADD     R1, R2, #DISCNAMEOPENOFFSET ; add disc name to details
        Push    "R2"
        MOV     R2, R8
        BL      GetDiscName             ; R0 = drive, R1 -> where to put
        MOV     R0, R1                  ; R2 -> disc buffer
        BL      CutSpace                ;
        Pull    "R2"
        Pull    "R1"
                
        STRB    R4, [ R2, #DRIVENUMBEROPEN ]; add drive number to details
                
        LDRB    R3, [ R8, #DiscBuff_DiscMode ]  ; disc mode details

        ;------------------------------------
        ; Added in version 2.23 30-Aug-94
        ; Read mode 2 form 2 files properly
        ;------------------------------------

        ; r3 =  mode of disc
        ; r9 -> Object information

        MACRO__XA_WHAT_DATA_MODE r3, r9, r0, r1

        STRB    R3, [ R2, #DISCMODEOPEN ]
        
        LDR     R3, [ R8, #DiscBuff_BlockSize ] ; add size of block to details
        STR     R3, [ R2, #SIZEOFBLOCKOPEN ]


        ; Work out actual LBA start

        MOV     R7, #myblocksize
        
        DivRem  R8, R7, R3, R14, norem
        
        LDR     R3, [ R1, #LBASTARTOFFSET ] ; add start block to details
        MOV     R3, R3, LSR #8
        
        DivRem  R7, R3, R8, R14, norem
        
        STR     R7, [ R2, #STARTLBAOPEN ]

        ;         ************
        
        LDR     R3, [ R1, #LENGTHOFFSET ] ; add length of file to details
        STR     R3, [ R2, #SIZEOFFILEOPEN ]
        STR     R3, temp1               ; tell FS the size of the file
        
        ;*****************
        
        
        ADRL    R4, OpenFileList - 4       ; Put pointer to claimed memory in list
        LDR     R0, verytemporary          ;
        STR     R2, [ R4, R0, ASL #2 ]     ; R2 -> my small buffer
        
        
        MOV     R3, #0                  ; obj is a dir, so set info word for FS
        
        LDRB    R2, [ R1, #OBJECTTYPEOFFSET ]
        TEQ     R2, #object_directory           
        ORREQ   R3, R3, #1:SHL:29        
        
        
        LDRB    R2, [ R1, #FILEATTRIBUTESOFFSET ] ; read is permitted from this entry
        TST     R2, #2_10001      
        ORRNE   R3, R3, #1:SHL:30 
        STR     R3, temp2         
        
        PullAllFromFrame
        
        LDR     R0, temp2               ; info word for FileSwitch about entry
                
        LDR     R1, verytemporary       ; file handle
                
        MOV     R2, #FSBUFFERSIZE       ; size of buffer that FileSwitch should use
                
        LDR     R3, temp1               ; file extent
        
        ADDS    R4, R3, #0              ; space currently allocated to file; clear V
        
        MOV     PC, R14


;***************************************************************************
GetByte ROUT      ; see page 975
;
; on entry:
;          r1 = my file handle
;          r2 = memory address to put data
;          r3 = number of bytes to read
;          r4 = file offset to get data from
; on exit:
;          preserve all registers except for usual error blocks in r0
;***************************************************************************

; on exit:
;         nothing

; temp1 = disc number
; bufferedblockdiscnumber
; bufferedblocknumber

; R7 = blocksize, R10 = blocksize ( in loop )
; R8 = block
; R11 = memory address to put data at    SEE IN LOOP FOR MORE FIXS !


        PushAllWithReturnFrame
        
        Debug   fs,"Get byte"

        MOV     r11, r2
        
        ADRL    r5, OpenFileList - 4    ; R6 -> my little buffer of entry details
        LDR     r6, [ r5, r1, LSL #2 ]

;******************
; Make sure that correct disc is in drive
;******************

        LDR     r5, [ r6, # DISCNUMBEROPEN ]
        LDRB    r0, [ r6, # DRIVENUMBEROPEN ]
        MOV     r2, # 1
        BL      GetUniqueNumber         ; R0 = drive number, RETURNS R1 = number
                                        ; R2 = 1 no error
        STR     r1, temp1
                                        ; R2 = 0 if error, else RETURNS R1=0
        TEQ     r1, r5
        
        MOV     r1, r0
        ADDNE   r0, r6, # DISCNAMEOPEN
        BLNE    PromptForDisc           ; R0 -> disc name, R1 = drive number


;******************


        LDR     r5, [ r6, # STARTLBAOPEN ] ; R4 = start LBA of load
        LDRB    r0, [ r6, # DISCMODEOPEN ]
        ADR     r7, sparecontrolblock
        
        TEQ     r0, # 2
        BNE     GB_Ordinary

        BL      ChangeDiscMode          ; R0 = mode, R7 -> control block


        ;----------------------------------
        ; Where is the byte of user data ?
        ;----------------------------------
        MOV     r1, r5
        MOV     r0, #LBAFormat
        MOV     r2, r4
        
        SWI     XCD_SeekUserData
        BVS     ErrorExit
        
        MOV     r4, r2

; R0  =
; R1  = start block
; R2  =
; R3  = number of bytes to load
; R4  = start byte
; R5  =
; R6  =
; R7  -> controlblock
; R8  =
; R9  =
; R10 =
; R11 -> where to put data
; R14 =

        MOV     r0, #LBAFormat
        MOV     r2, r3
        MOV     r3, r11
        SWI     XCD_ReadUserData
        BVS     ErrorExit
        
        PullAllFromFrameAndExit

;----------------------------------------------------------------------------------------------
; This loads data in for normal mode 1 discs
;----------------------------------------------------------------------------------------------

GB_Ordinary

        BL      ChangeDiscMode          ; R0 = mode, R7 -> control block


        MOV     R0, #1
                
                
        LDR     R10, [ R6, #SIZEOFBLOCKOPEN ]

                                        ; work out where to start from:
                                        ; R4 = offset to get data from

        ADD     R6, R4, R3              ; R9 = file offset + number of bytes to read
        
        DivRem  R0, R4, R10, R14        ; R0 = block to read from
        ADD     R0, R0, R5              ; R4 = bytes offset in a block to read from
        
        
        DivRem  R2, R6, R10, R14        ; R2 = block to end at
        ADD     R2, R2, R5              ; R6 = bytes offset in a block to end at
        
        ADR     R7, sparecontrolblock

; R0 = start block
; R1 = drive number
; R2 = end block
; R3
; R4 = start byte
; R5 = number to load
; R6 = endbyte
; R7 -> controlblock
; R8 =
; R9 =
; R10 = blocksize
; R11 -> where to put data
; R14 =

;          . If the byte to start from is not 0 THEN
;          .                                    {
;          .                                     load a block into my buffer
;          .                                     copy out whats needed
;          .                                     add 1 to start block
;          .                                    }

        TEQ     R4, #0
        BEQ     %FT01
        
        Push    "R0-R3"
        
        MOV     R3, R1
        
        MOV     R2, #1
        
        BL      PreLoadBlockFromDrive   ; r1->buffer

        ; This assumes that I am not asked for 0 bytes
        ;
        ; if (end byte==0) THEN
        ;     Copy from r1+start byte, r11, length (blocksize-start byte)
        ; else
        ;     IF (end block==start block)
        ;         Copy from r1+start byte, r11, length (end byte-start byte)
        ;     else
        ;         Copy from r1+start byte, r11, length (blocksize-start byte)

        ADD     R1, R1, R4
        MOV     R2, R11
        
        TEQ     R6, #0
        SUBEQ   R3, R10, R4
        BEQ     %FT11
        
        TEQ     R2, R0
        SUBNE   R3, R10, R4
        SUBEQ   R3, R6, R4

11
        ADD     R11, R11, R3            ; Move to further along the buffer
                                        
        CD_ByteCopy                     
                                        
        Pull    "R0-R3"                   
                                        
        ADD     R0, R0, #1              ; start block++
                                        
        MOV     R4, #0                  ; start byte=0

01

;          . if (start block < end block)
;          .    {

        CMP     R0, R2
        BGE     %FT02

;          .     if (end byte ==0 (or blocksize ?) )
;          .        number_to_load=end block-start block

        SUB     R5, R2, R0

;          .    else
;          .        number_to_load=(end block-start block)-1
;          .
;          .     if (number_to_load > 0)

        CMP     R5, #0

;          .         load number_to_load blocks into users area


        BLE     %FT02
                
        Push    "R1-R3"
                
        MOV     R3, R1
        MOV     R1, R11
        MOV     R2, R5
                
        BL      LoadBlockFromDrive
                
        Pull    "R1-R3"
                
        ADD     R0, R0, R5

      [ myblocksize<>2048
        ! 0, "AAAAAAAAa see fileman"
      ]
        ADD     R11, R11, R5, LSL #11

;          .    }

02

;          . if (end_byte !=0 )

        TEQ     R6, #0
        BEQ     %FT04

;          .    {
;          .     load a block into my buffer

        MOV     R2, #1
        MOV     R3, R1
        BL      PreLoadBlockFromDrive

;          .     copy out whatevers needed
;          .    }

        MOV     R2, R11
        MOV     R3, R6
        CD_ByteCopy

04

        CLRV
        PullAllFromFrameAndExit

;***************************************************************************
ControlOpenFile ROUT   ; see page 979 ( FSEntry_Args )
;***************************************************************************

        PushAllWithReturnFrame
        log_on
        
        CMP     R0, #(EndOfJumpTableForFSEntry_Args-StartOfJumpTableForFSEntry_Args)/4
        
        PullAllFromFrameAndExit HS
        
        
        LDR     r14, [ pc, r0, LSL #2 ]
        ADD     pc, pc, r14

StartOfJumpTableForFSEntry_Args

        ; 0. Read sequential pointer for unbuffered file
        DCD     Nothing - StartOfJumpTableForFSEntry_Args - 4
        
        ; 1. Write sequential pointer
        DCD     SaveFile - StartOfJumpTableForFSEntry_Args - 4
        
        ; 2. Reads the size of the file of unbuffered
        DCD     Nothing - StartOfJumpTableForFSEntry_Args - 4
        
        ; 3. Writes the size of the file
        DCD     SaveFile - StartOfJumpTableForFSEntry_Args - 4
        
        ; 4. Reads the size of the file (all types)
        DCD     ReadSizeAllocatedToFile - StartOfJumpTableForFSEntry_Args - 4
        
        ; 5. EOF check for unbuffered files
        DCD     Nothing - StartOfJumpTableForFSEntry_Args - 4
        
        ; 6. Flush buffers, if bit 27 was set
        DCD     Nothing - StartOfJumpTableForFSEntry_Args - 4
        
        ; 7. Ensure the size of the file (all types)
        DCD     ReadSizeAllocatedToFile - StartOfJumpTableForFSEntry_Args - 4
        
        ; 8. Write zeros to a file
        DCD     SaveFile - StartOfJumpTableForFSEntry_Args - 4
        
        ; 9. Read the date stamp of the file
        DCD     ReadFileDateStamp - StartOfJumpTableForFSEntry_Args - 4

EndOfJumpTableForFSEntry_Args


;----------------------------------------------------------------------------------------------
CloseFile ROUT
;
; see page 983   DONE
; on entry:
;          R1 = my file handle
;          R2 = new load address
;          R3 = new execution address
;----------------------------------------------------------------------------------------------

        PushAllWithReturnFrame

;******************
; Free the claimed buffer used for the open file details
;******************

        ADRL    R3, OpenFileList - 4
        LDR     R2, [ r3, r1, LSL #2 ]!
                
        MOV     R0, #ModHandReason_Free
        SWI     XOS_Module

;******************
; Remove the buffer from the list
;******************

        SUBS    R0, R0, R0  ; clears V
        STR     R0, [ R3 ]

        PullAllFromFrameAndExit

;----------------------------------------------------------------------------------------------
WholeFile ROUT
; on entry:
;          r0 = reason code
; on exit:
;
;
; see PRM2 page 983 ( FSEntry_File )  see 'EntryFile'
;----------------------------------------------------------------------------------------------

        log_on

        PushAllWithReturnFrame
        
        TEQ     R0, #255                ; MUST BE HERE BEFORE ENDOFTABLECHECK !!!!!
        BEQ     LoadFile                ; DONE

        ; Make sure that the reason code is supported by me

        CMP     R0, #( EndOfJumpTableForFSEntry_File - JumpTableForFSEntry_File ) / 4
        PullAllFromFrameAndExit HS
        
        LDR     r14, [ pc, r0, LSL #2 ]
        ADD     pc, pc, r14

JumpTableForFSEntry_File

        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 0  n / a       DONE
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 1  n / a       DONE
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 2  n / a       DONE
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 3  n / a       DONE
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 4  n / a       DONE
        DCD     ReadCatalogue - JumpTableForFSEntry_File - 4 ; 5  used to find an object
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 6  n / a       DONE
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 7  n / a       DONE
        DCD     SaveFile - JumpTableForFSEntry_File - 4 ; 8  n / a       DONE
        DCD     Nothing - JumpTableForFSEntry_File - 4  ; 9  only called if bit 26 set ( not supported )
        DCD     ReadBlockSize - JumpTableForFSEntry_File - 4 ; 10        DONE natural block size

EndOfJumpTableForFSEntry_File

;----------------------------------------------------------------------------------------------
FSOperations ROUT
;
; on entry:
;          r0 = reason code
;          r1 - r6 depends on reason code
; on exit:
;
;  see page 988 ( FSEntry_Func )
;
;      These are to be found in 'Filer'
;----------------------------------------------------------------------------------------------

        log_on
        
        PushAllWithReturnFrame

        ; Make sure that the reason code is supported by me
        CMP     R0, #( EndOfJumpTableForFSEntry_Func - JumpTableForFSEntry_Func ) / 4
        PullAllFromFrameAndExit HS
        
        LDR     r14, [ pc, r0, LSL #2 ]
        ADD     pc, pc, r14

JumpTableForFSEntry_Func

        ; Under RISC OS 3 this is done for me
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 0  *DIR
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 1  *LIB
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 2  *CAT
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 3  *EX
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 4  *LCAT
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 5  *LEX
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 6  *INFO
                                                                 
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 7  *OPT
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 8  *RENAME
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 9  *ACCESS
      [ BootFromCD                                               
        DCD BootFromCDFS - JumpTableForFSEntry_Func - 4          ; 10  attempt to boot from CD - yay, lets do this!
      |                                                          
        DCD Nothing - JumpTableForFSEntry_Func - 4               ; 10  attempt to boot from CD - ignore
      ]

        ; Under RISC OS 3 this is done for me
        DCD     ReadBoot_OS3 - JumpTableForFSEntry_Func - 4      ; 11 Get disc name
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 12 Read dir name
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 13 Read lib name
                
        DCD     CurrentDirObjects - JumpTableForFSEntry_Func - 4 ; 14
        DCD     ObjectInfo - JumpTableForFSEntry_Func - 4        ; 15
        DCD     Shutdown - JumpTableForFSEntry_Func - 4          ; 16 *SHUTDOWN
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 17 not used -see 'Information Block'
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 18 SET DIR CONTEXTS not used (bit 24)
        DCD     ReadEntriesAndLength - JumpTableForFSEntry_Func - 4; 19 READ DIR ENTRIES
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 20 *FileInfo not used (bit 25)
                
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 21 notification of new image (RISC OS3)
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 22 image about to be closed (RISC OS3)
        DCD     CanonicaliseName - JumpTableForFSEntry_Func - 4  ; 23 Canoncalise name (RISC OS3)
        DCD     ResolveWildcard - JumpTableForFSEntry_Func - 4   ; 24 Get a complete file name
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 25 Read defect list
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 26 Add a defect to the list
      [ BootFromCD
        DCD     ReadBoot2 - JumpTableForFSEntry_Func - 4         ; 27 Read boot option 2
      |
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 27 Read boot option 2
      ]
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 28 Write boot option
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 29 Read used space map
        DCD     ReadFreeSpace - JumpTableForFSEntry_Func - 4     ; 30 Read free space
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 31 Name image
        DCD     SaveFile - JumpTableForFSEntry_Func - 4          ; 32 Stamp image
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 33 Get usage of offset
        DCD     Nothing - JumpTableForFSEntry_Func - 4           ; 34 Notification of changed directory
        DCD     ReadFreeSpace64 - JumpTableForFSEntry_Func - 4   ; 35 Read 64-bit free space

EndOfJumpTableForFSEntry_Func


;************* End of jump table

;****************
;MultiOperations    ; see page 998 ( FSEntry_GBPB ) not used ( bit 28 in Open)
;****************

        LTORG

        END
