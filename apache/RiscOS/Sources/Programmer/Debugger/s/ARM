; Copyright 2016 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; File:    Thumb.s
; Purpose: Disassembly of the (base) ARM instruction set


; In    r0 = instruction to disassemble
;       r9 = where it is

; Out   r1 -> core containing string
;       r2 = length of string including 0
;       r10,r11 corrupt

Instruction Entry "r0, r3-r9"

        SUB     R4, R9, #4
        LDR     R5, OldAddress
        TEQS    R4, R5
        MOV     R4, #0
        STRNE   R4, Mistake         ;not going in sequence, so forget context
        STR     R9, OldAddress      ;save for next time

        MOV     r4, r0

        ADR     r0, StringBuffer        ; Always build into temp buffer

 [ debug
        MOV     r14, #0                 ; zap buffer
        MOV     r3, #?StringBuffer-4
00      STR     r14, [r0, r3]
        SUBS    r3, r3, #4
        BPL     %BT00
 ]

        ADDS    r14, r4, #1:SHL:28      ;  C := unconditional space
        MOV     r3, r4, LSR #24         ; r3 := primary instruction nibble
        AND     r3, r3, #2_00001111     ; remove condition codes
        BCS     %FT10

        ; arrive here with cccc xxxx xxxx xxxx xxxx xxxx xxxx xxxx
        ; (cccc != 1111)
        TEQS    r3, #&0F
        BEQ     Swi                     ; [software interrupt = 0F]

        CMP     r3, #&0C
        BHS     Coprocessor_Conditional ; [coproc 0C..0E]

        CMP     r3, #&0A
        BHS     Branch                  ; [branch = 0A..0B]

        CMP     r3, #&08
        BHS     LdmStm                  ; [load/store multiple = 08,09]

        CMP     r3, #&04
        BHS     LdrStr                  ; [load/store register = 04..07]

        B       DataProcessing          ; [data processing = 00..03]
10
        ; arrive here with 1111 xxxx xxxx xxxx xxxx xxxx xxxx xxxx
        TEQS    r3, #&0F
        BEQ     Undefined               ; [undefined = 0F]

        CMP     r3, #&0C
        BHS     Coprocessor_Unconditional ; [coproc 0C..0E]
 
        CMP     r3, #&0A
        BHS     BLX_offset              ; [BLX = 0A,0B]

        CMP     r3, #&08                
        BHS     Srs_Or_Rfe              ; [SRS or RFE = 08,09]

        CMP     r3, #&04
        BHS     Preload                 ; [preload = 04..07]

        CMP     r3, #&02
      [ UseCVFPNEON
        BHS     ASIMD                   ; [ASIMD data processing = 02,03]
      |
        BHS     Undefined
      ]

        CMP     r3, #&01
        BEQ     ControlExtension_uncond ; [CPS & SETEND = 01]
        ; Fall through...

; .............................................................................

Undefined
        MOV     r14, #0                 ; Undefined can't have a mistake :)
        STR     r14, Mistake

        ADR     r0, StringBuffer        ; Reset pointer
        ADR     r10, Unknown
        BL      lookup_r10

; .............................................................................
; In    r0 -> buffer to be terminated. registers stacked as for entry
;       r10 -> string to be appended first

SaveStringEnd

        BL      SaveString

; .............................................................................
; In    r0 -> buffer to be terminated. registers stacked as for entry

; Out   r1 -> completed buffer
;       r2 = length of buffer

InstructionEnd

        BL      TestMistakes

        MOV     r14, #0
        STRB    r14, [r0]

        ADR     r1, StringBuffer
        SUBS    r2, r0, r1              ; Clears V flag

        EXIT


; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Software interrupt

        ; arrive here with cccc 1111 xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        cccc 1111 nnnn nnnn nnnn nnnn nnnn nnnn
        ;
        ; SWI{cond} <number|name>
        ;
        ; where cccc = condition
        ;       nnnn = number

Swi     LDR     R10, Mistake
        CMP     R10, #Potential_SWICDP
        MOVEQ   R10, #Mistake_SWICDP
        STREQ   R10, Mistake

        AddStr  Swi_Instruction,,conds

        BL      Tab

        MOV     r1, r0
        ADR     r2, StringBuffer+?StringBuffer
        SUB     r2, r2, r1
        BIC     r0, r4, #&FF000000      ; Mask out SWI<cc>
        SWI     XOS_SWINumberToString   ; This may give error (eg. Buffer over)
        BVS     InstructionEnd
        MOV     R3, #24
        BL      unknown_swis
        ADD     r0, r1, r2
        B       InstructionEnd

; Compare the string against 'User' and 'OS_Unknown' with or without the leading 'X'. If
; it matches, dump the SWI number as a string with a preceding '&' into the buffer instead.
;
; Entry...
;   R0 - SWI number
;   R1 - pointer to string
;   R2 - offset to last char of string
;   R3 - number of bits to print in SWI number
; Exit...
;   R0 - corrupted
;   R1 - preserved
;   R2 - offset to last char of string (may be different)
;   R3 - preserved
;   R8 - corrupted
;
unknown_swis
        Entry
        ; Ensure all bytes after the SWI name string up to the next word boundary are zero
        MOV     R14, #0
        ADD     R8, R1, R2
unk_swi_align
        TST     R8, #2_11
        STRNEB  R14, [R8], #1
        BNE     unk_swi_align
        ; Compare this SWI name string against some pre-defined unknown SWI name strings
        ADR     R8, str_user
        BL      strcmp
        ADRNE   R8, str_xuser
        BLNE    strcmp
        ADRNE   R8, str_os_undefined
        BLNE    strcmp
        ADRNE   R8, str_xos_undefined
        BLNE    strcmp
        BEQ     unk_swi_replace
        SUB     R2,R2,#1                ; Adjust to avoid taking the zero byte
        EXIT
        ; Replace the SWI name string with the SWI number decoded as hexadecimal
unk_swi_replace
        MOV     R8, R0
        MOV     R0, R1
        SUB     R2, R3, #4
        BL      StoreHex
        SUB     R2, R0, R1              ; Convert end pointer back to offset
        EXIT

; Compare two word-aligned strings. They *must* be padded with zero or more null bytes after
; the terminator (which must also be null) up to the word boundary.
;
; Entry...
;   R1    - pointer to first string
;   R8    - pointer to second string
; Exit...
;   R1    - preserved
;   R8    - corrupted
;   flags - EQ=match, NE=don't match
strcmp
        Entry   "R0, R1"
strcmp_loop
        LDR     R0, [R1], #4
        LDR     R14, [R8], #4
        TEQ     R0, R14
        BNE     strcmp_exit             ; Conditional Pull is bad on newer ARMs, branch instead
        TST     R0, #&FF000000
        BNE     strcmp_loop
strcmp_exit
        EXIT

        ; Some SWI names indicating that a SWI number was not recognised (in length order)
str_user
        DCB     "User", 0
        ALIGN
str_xuser
        DCB     "XUser", 0
        ALIGN
str_os_undefined
        DCB     "OS_Undefined", 0
        ALIGN
str_xos_undefined
        DCB     "XOS_Undefined", 0
        ALIGN


; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Branch, Branch and Link

        ; arrive here with cccc 101x xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        cccc 101l oooo oooo oooo oooo oooo oooo
        ;
        ; B{L}{cond} address
        ;
        ; where cccc = condition
        ;          l = Link
        ;          o = 24-bit signed offset (in words)

Branch ROUT

        AddChar "B"
        TestBit 24,"L"

        BL      Conditions

        BL      Tab

        MOV     r4, r4, ASL #8          ; shift offset up to top
BranchCommon
        LDRB    r2, SysIs32bit

        ADD     r8, r9, r4, ASR #6
        ADD     r8, r8, #8              ; pc + 8

        TST     r9, #ARM_Addr_Mask      ; if in bottom 64M, and system is 26 bit
        TEQEQ   r2, #0                  ; then...
        BICEQ   r8, r8, #ARM_Addr_Mask  ; Wrap in 64MByte space for 26-bit
        BEQ     BranchLdrStrCommon

; Check no wrap for 32-bit. Wrap detected by one address being < &10000000
; and one being >= &80000000.

        TST     r8, #&F0000000
        TSTNE   r9, #&F0000000
        BNE     BranchLdrStrCommon      ; EQ if one of the addresses is < &10000000

        ORRS    r2, r8, r9              ; MI if one of the addresses is >= &80000000

        MOVMI   r2, #Mistake_Unpred     ; must have wrapped
        STRMI   r2, Mistake

BranchLdrStrCommon
        MOV     r2, #32-4                 ; (keeping byte offsets)
        BL      StoreHex

        B       InstructionEnd

Unknown DCB     "M00", 0                ; "Undefined instruction"
Swi_Instruction
        DCB     "SWI", 0
Ldm     DCB     "LDM", 0
Stm     DCB     "STM", 0
W_Back  DCB     "!,{", 0
        ALIGN


; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Load and store multiple

        ; arrive here with cccc 100x xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        cccc 100p uswl nnnn rrrr rrrr rrrr rrrr
        ;
        ; <LDM|STM>{cond}<I|D><A|B> Rn{!},register list{^}
        ;
        ; where cccc = condition
        ;          p = Pre-indexing/~Post-indexing
        ;          u = Up/~Down
        ;          s = the ^ bit (transfer user registers, or restore PSR)
        ;          w = Writeback
        ;          l = Load/~Store
        ;       nnnn = Rn
        ;       rrrr = register list
LdmStm ROUT

        ANDS    R5, R4, #&7F00
        BLNE    checkreg                ; error if R8-R14 in list

        ;test for silly conditions - ldm/stm ! and ^ with R15 not in list is bad,
        ;and LDM ^ with R15 not in the list is a deferred bank error

        TestBit 15
        BNE     notbad                  ; R15 in list

        TestBit 22
        BEQ     notbad                  ; not forcing user

        ;case 1 - ! too
        TestBit 21
        MOVNE   r10, #Mistake_PlingHat
        STRNE   r10, Mistake
        BNE     notbad2                 ; don't bother with next test

        ;case 2 - LDM
        TestBit 20
        MOVNE   r10,#Potential_Banked_Next
        STRNE   r10,Mistake
        BNE     notbad2                 ; don't bother with next test

notbad
        ;case 3 - Rn in list with writeback
        TestBit 21
        BEQ     notbad3
        MOV     r10, r4, LSR #16
        AND     r10, r10, #2_1111
        MOV     r14, #1
        MOV     r14, r14, LSL r10
        TSTS    r4, r14
        BEQ     notbad3
        TestBit 20                      ; If it's an STM
        SUBEQ   r14, r14, #1
        TSTEQS  r4, r14                 ; and Rn is lowest in list, then it's okay
        MOVNE   r10,#Mistake_Rninlist
        STRNE   r10,Mistake
        BNE     notbad2

notbad3
        ;case 4 - storing PC
        AND     r14, r4, #1 :SHL: 20 :OR: 1 :SHL: 15
        TEQ     r14, #1 :SHL: 15
        BNE     notbad2
        ; okay, we're storing PC, but complaining about APCS would be boring.
        ; Therefore don't bother whinging about STMFD R13!,{[any R0-R9],R11,R12,R14,PC}
        AND     r14, r4, #2_1111 :SHL: 21
        TEQ     r14, #2_1001 :SHL: 21           ; Check FD, !, and no ^
        ANDEQ   r14, r4, #2_1111 :SHL: 16
        TEQEQ   r14, #13 :SHL: 16               ; Check R13
        ANDEQ   r14, r4, #2_1111110000000000
        TEQEQ   r14,     #2_1101100000000000
        MOVNE   r10,#Mistake_StorePC
        STRNE   r10,Mistake

notbad2
        TestStr 20,Ldm,Stm,conds        ; Load/~Store bit

        TestBit 23,"I","D"              ; Up/~Down bit

        TestBit 24,"B","A"              ; Pre/~Post bit

        MOV     r5, r4, LSR #16         ; Rn
        AND     r5, r5, #2_1111
        TEQS    r5, #15
        MOVEQ   r14, #Mistake_R15
        STREQ   r14, Mistake
        BL      Tab_Dis_Register

        TST     r4, #1 :SHL: 21         ; Writeback bit
        ADR     r10, W_Back
        ADDEQ   r10, r10, #1            ; just do ',{'
        BL      SaveString

; Register list

        MOV     r5, #0          ; Current register
 [ WarnSArev2
        MOV     r7, #0          ; Total registers
 ]
        MOV     r8, #0          ; Registers in current group

RegList ADD     r2, r5, #1
        TST     r2, r4, LSR r2
        BCC     Next

        TEQ     r8, #0
        BLNE    AddComma

        BL      Dis_Register
        ADD     r5, r5, #1
 [ WarnSArev2
        ADD     r7, r7, #1
 ]
        TEQS    r5, #16
        BEQ     Xxm_End

        ADD     r2, r5, #1
        TSTS    r2, r4, LSR r2
        MOV     r8, #1
        BCC     Next                    ; If next reg not done, loop

Count   ADD     r8, r8, #1
        ADD     r5, r5, #1              ; Count registers
        TEQS    r5, #16
        BEQ     Group

        ADD     r2, r5, #1
        TSTS    r2, r4, LSR r2
        BCS     Count

; Group or single register

Group   MOV     r6, #","
        CMPS    r8, #3
        MOVHS   r6, #"-"
        STRB    r6, [r0], #1

        SUB     r5, r5, #1
        BL      Dis_Register
 [ WarnSArev2
        ADD     r7, r7, r8
        SUB     r7, r7, #1
 ]

Next    ADD     r5, r5, #1
        TEQS    r5, #16
        BNE     RegList

Xxm_End
        AddChar "}"                     ; end register list

        TestBit 22,"^"                  ; '^' bit

        [ WarnSArev2
        BEQ     InstructionEnd          ; It's not ^
        TestBit 20
        BNE     InstructionEnd          ; It's not a store

        ; If we have more than 1 register in a STM user bank,
        ; then it may fail on early StrongARMs
        CMPS    r7, #1
        MOVHI   r10, #Mistake_STMHat
        STRHI   r10, Mistake
        ]

        B       InstructionEnd

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Load or store register

LdrStr  ROUT
        ; Load and Store Word or Byte
        ; arrive here with cccc 01xx xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        cccc 010p ubwl nnnn dddd iiii iiii iiii
        ;        or        cccc 011p ubwl nnnn dddd rrrr rtt0 mmmm
        ;
        ; <LDR|STR>{cond}{B}    Rd,[Rn,#offset]{!}
        ;                       Rd,[Rn,{+|-}Rm{,shift}]{!}
        ; <LDR|STR>{cond}{B}{T} Rd,[Rn],#offset
        ;                       Rd,[Rn],{+|-}Rm{,shift}
        ; where cccc = condition
        ;          p = Pre-indexed/~Post-indexed
        ;          u = Up/~Down
        ;          b = Byte/~Word
        ;          w = Writeback (or T bit if post-indexed)
        ;          l = Load/~Store
        ;       nnnn = Rn
        ;       dddd = Rd
        ;      rrrrr = shift amount
        ;         tt = shift type (LSL, LSR, ASR or ROR)
        ;       mmmm = Rm
        ;       iiii = 12-bit unsigned immediate offset

        ; Not totally clear which order these tests should be in, but it
        ; doesn't matter for the time being - this restriction applies to
        ; both LDR and PLD.
        TST     r4, #1 :SHL: 25         ; If Rm and shift and trying Rs
        TSTNE   r4, #1 :SHL: 4          ; then that was xxR Rd,[Rn,Rm,SHF Rs]
        BNE     UndefinedExtension      ; but ARM2 doesn't do that anymore

        TestStr 20,Ldr,Str,conds        ; Load/~Store bit

        MOV     r5, r4, LSR #12         ; Rd
        AND     r5, r5, #2_1111

        TST     r4, #1 :SHL: 22         ; Byte/~Word bit
        BEQ     %FT01

        AddChar "B"
        TEQS    r5, #15
        MOVEQ   r10, #Mistake_BytePC
        STREQ   r10, Mistake
        BEQ     %FT03

1       TEQS    r5, #15
        TSTEQS  r4, #1 :SHL: 20
        MOVEQ   r10, #Mistake_StorePC
        STREQ   r10, Mistake

3       AND     r10, r4, #2_1001 :SHL: 21
        TEQS    r10,     #2_0001 :SHL: 21 ; postincrement with writeback ?
        AddChar "T",EQ                    ; -> T(ranslate)

        BL      Tab_Dis_Register        ; uses r5 from above

        ; Check for writeback LDR/STR with Rd=Rn
        AND     r10, r4, #2_1001 :SHL: 21
        TEQS    r10, #2_1000 :SHL: 21
        BEQ     %FT10                   ; if not writeback, okay

      [ {FALSE}                         ; ARM ARM says nothing about zero
        TestBit 25                      ; offset OK, and it isn't entirely
        BNE     %FT05                   ; logical - KJB
        MOVS    r14, r4, LSL #32-12
        BEQ     %FT10                   ; zero offset, so okay
      ]

5       AND     r14, r4, #2_1111 :SHL: 12
        AND     r10, r4, #2_1111 :SHL: 16
        TEQS    r10, r14, LSL #4
        MOVEQ   r10, #Mistake_RdRn
        STREQ   r10, Mistake

        ; Check for post-indexed LDR/STR with Rm=Rn
10      AND     r14, r4, #2_11000 :SHL: 21
        TEQS    r14, #2_10000 :SHL: 21
        BNE     %FT20
        AND     r14, r4, #2_1111 :SHL: 16
        AND     r10, r4, #2_1111
        TEQS    r10, r14, LSR #16
        MOVEQ   r10, #Mistake_RmRn
        STREQ   r10, Mistake

20
DataTransfer_Common ; for CPDT - address part similar to LDR/STR (no reg index)
        AddChar ","

DataTransfer_Common_NoComma

        ;from 1.23, show resultant LDR/STR Rx,[R15,#nnn] address directly

        MOV     R5,R4,LSR #16
        AND     R5,R5,#15
        TEQ     R5,#15
        BNE     not_rel

        TestBit 21                      ; Writeback bit set?
        MOVNE   r10, #Mistake_PCwriteback
        STRNE   r10, Mistake
        TST     r4, #2_1001 :SHL: 24    ; or post-indexed LDR/STR? (can have post-indexed CPDT without writeback)
        MOVEQ   r10, #Mistake_PCwriteback
        STREQ   r10, Mistake

        ; if immediate offset, pre-indexed, no writeback then it's PC-relative
        AND     r14,R4,#2_11001 :SHL: 21
        TEQS    r14,#2_01000 :SHL: 21
        BNE     not_rel

        MOV     R8,R4,LSL #32-12
        MOV     R8,R8,LSR #32-12

; offset in LDC/STC instructions is a word offset

        TestBit 27                      ; if a coproc data transfer
        MOVNE   r8, r8, LSL #2          ; then multiply offset by 4

        TestBit 23                      ; Up/~Down bit
        SUBEQ   r8, r9, r8              ; Form offset from required address
        ADDNE   r8, r9, r8

        ADD     r8, r8, #8
        B       BranchLdrStrCommon

not_rel
        AddChar "["

        MOV     r5, r4, LSR #16         ; Rn
        BL      Dis_Register

        TestStr 24,Close_B+1,Close_B    ; Pre/~Post bit - just ',' else '],'

        TestBit 25                      ; Shift/~Immediate bit
        BNE     %FT90

; Immediate offset

        AND     r14, r4, #2_1111001 :SHL: 21 ; post-indexed CPDT, no writeback?
        TEQ     r14, #2_1100000 :SHL: 21
        BEQ     CPDT_info

        AddChar "#"

        TestBit 23,,"-"                 ; Up/~Down bit

        MOV     r8, r4, LSL #32-12
        MOV     r8, r8, LSR #32-12      ; r4 & 0x0FFF
        TestBit 27
        MOVNE   r8, r8, LSL #2          ; Word offset if coprocessor
        BL      StoreDecimal

80
        TestBit 24,"]"                 ; Pre/~Post bit
                                       ; [preincrement, so close]

        AND     r10, r4, #2_1001 :SHL: 21
        TEQS    r10,     #2_1001 :SHL: 21 ; preincrement with writeback ?
        AddChar "!",EQ

        B       InstructionEnd

; CPDT info form
CPDT_info
        AddChar "{"
        AND     r8, r4, #&FF
        BL      StoreDecimal
        AddChar "}"
        B       InstructionEnd


; Register offset
90
        TestBit 23,,"-"                 ; Up/~Down bit

        MOV     r5, r4                  ; Rm
        BL      Dis_Register

        BL      ShiftField

        B       %BT80

Ldr     DCB     "LDR", 0
Str     DCB     "STR", 0
Open_B  DCB     ",[", 0
Close_B DCB     "],", 0
        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Mull    DCB     "MULL",0
        ALIGN
Mlal    DCB     "MLAL",0
        ALIGN
Mul     DCB     "MUL", 0
Mla     DCB     "MLA", 0
aswp    DCB     "SWP", 0

UndefinedDataProcessing ROUT
        ; Arrive here with cccc 000x xxxx xxxx xxxx xxxx 1xx1 xxxx

        TSTS    R4,#2_11:SHL:5
        TSTEQS  R4,#2_1:SHL:24
        BNE     LoadStoreExtension

ArithmeticExtension
        ; Arrive here with cccc 0000 oooo xxxx xxxx xxxx 1001 xxxx
        AND     R5,R4,#2_1111:SHL:20
        CMP     R5,#2_1000:SHL:20
        BHS     mul_long                ; opcode 8-15 = long multiply

        CMP     R5,#2_0110:SHL:20
        BHI     Undefined               ; opcode 7 = undefined
        BEQ     mul_sub                 ; opcode 6 = multiply with subtract

        CMP     R5,#2_0100:SHL:20
        BHI     Undefined               ; opcode 5 = undefined
        BEQ     maal                    ; opcode 4 = long multiply accumulate accumulate

        ; Multiply + Multiply-with-Accumulate
        ; Arrive here with cccc 0000 00xx xxxx xxxx xxxx 1001 xxxx
        ; Format is        cccc 0000 00as dddd nnnn ssss 1001 mmmm
        ;
        ; MUL{cond}{S} Rd,Rm,Rs
        ; MLA{cond}{S} Rd,Rm,Rs,Rn
        ;
        ; where cccc = condition
        ;          a = Accumulate
        ;          s = Set flags
        ;       dddd = Rd
        ;       nnnn = Rn
        ;       ssss = Rs
        ;       mmmm = Rm

        TestStr 21,Mla,Mul,conds        ; Accumulate bit

        TestBit 20,"S"                  ; Update flags bit

        MOV     r5, r4, LSR #16         ; Rd
        BL      Tab_Dis_Register

        AND     R5,R4,#15               ; Rm
        MOV     R10, R4, LSR #16
        AND     R10,R10,#15
        MOV     R8, R4, LSR #12
        AND     R8, R8, #15
        MOV     R9, R4, LSR #8
        AND     R9, R9, #15

        TEQS    R5, R10
        MOVEQ   R14, #Mistake_RdRm
        STREQ   R14, Mistake

        TEQS    R10, #15
        TEQNES  R8, #15
        TEQNES  R9, #15
        TEQNES  R5, #15
        MOVEQ   R14, #Mistake_R15
        STREQ   R14, Mistake

        BL      Comma_Dis_Register

        MOV     r5, r4, LSR #8          ; Rs
        BL      Comma_Dis_Register

        TestBit 21                      ; MLA has extra operand
        MOVNE   r5, r4, LSR #12         ; Rn
        BLNE    Comma_Dis_Register
        BNE     InstructionEnd

        TST     r4, #2_1111:SHL:12      ; Check Rn=R0 for MUL
        BNE     Undefined

        B       InstructionEnd

mul_long
        ; Multiply long + Multiply-with-Accumulate long
        ; Arrive here with cccc 0000 1xxx xxxx xxxx xxxx 1001 xxxx
        ; Format is        cccc 0000 1uas hhhh llll ssss 1001 mmmm
        ;
        ; <U|S><MULL|MLAL>{cond}{S} RdLo,RdHi,Rm,Rs
        ;
        ; where cccc = condition
        ;          u = Signed/~Unsigned
        ;          a = Accumulate
        ;          s = Set flags
        ;       hhhh = RdHi
        ;       llll = RdLo
        ;       ssss = Rs
        ;       mmmm = Rm

        ;deal with signed/unsigned
        TestBit 22,"S","U"

        ;note: the accumulate works differently here to MUL/MLA
        ;on an accumulating multiply you get Rd+=Rm * Rs, otherwise
        ;you get Rd==Rm * Rs, so wheras MLA uses one more register
        ;than MUL both MULL/MLAL use four registers.

        TestStr 21,Mlal,Mull,conds      ; Accumulate bit

        TestBit 20,"S"                  ; Update flags bit

        ; This stupid opcode can be too long. Will hardly ever happen
        ; so don't increase the tab width (might break some programs).
        ; Instead, just push the registers over one space.
        BL      TabOrPushOver

        MOV     r5, r4, LSR #12         ; RdLo
        AND     r6, r5, #2_1111
        BL      Dis_Register

        MOV     r5, r4, LSR #16         ; RdHi
        AND     r7, r5, #2_1111
        BL      Comma_Dis_Register

        MOV     r5, r4                  ; Rm
        AND     r8, r5, #2_1111
        BL      Comma_Dis_Register

        MOV     r5, r4, LSR #8          ; Rs
        AND     r9, r5, #2_1111
        BL      Comma_Dis_Register

        ; Can't use R15 as any register, unpredictable
        ; if RdLo=RdHi or RdLo=Rm or RdHi=Rm
        LDR     r14, Mistake
        TEQS    r7, r8
        MOVEQ   r14, #Mistake_RdHiRm
        TEQS    r6, r8
        MOVEQ   r14, #Mistake_RdLoRm
        TEQS    r6, r7
        MOVEQ   r14, #Mistake_RdLoRdHi
        TEQS    r6, #15
        TEQNES  r7, #15
        TEQNES  r8, #15
        TEQNES  r9, #15
        MOVEQ   r14, #Mistake_R15
        STR     r14, Mistake

        B       InstructionEnd

swp_type
        ; arrive here with cccc 0001 xxxx xxxx xxxx xxxx 1001 xxxx
        TestBit 23
        BNE     LdrexStrex

swp
        ; Swap register/memory
        ; arrive here with cccc 0001 0xxx xxxx xxxx xxxx 1001 xxxx
        ; format is        cccc 0001 0b00 nnnn dddd 0000 1001 mmmm
        ;
        ; SWP{cond}{B} Rd,Rm,[Rn]
        ;
        ; where cccc = condition
        ;          b = Byte/~Word
        ;       nnnn = Rn
        ;       dddd = Rd
        ;       mmmm = Rm

        TSTS    R4,#2_1111 :SHL: 8
        BNE     Undefined

        TST     R4,#2_11 :SHL: 20
        BNE     Undefined

        AddStr  aswp,,conds
        TestBit 22,"B"

        MOV     r9,r4,LSR #16
        AND     r9,r9,#2_1111           ; Rn

        MOV     R5,R4,LSR #12
        AND     r5,r5,#2_1111           ; Rd
        ; Rd=Rn -> unpredictable
        TEQS    r5,r9
        MOVEQ   r14,#Mistake_RdRn
        STREQ   r14,Mistake
        ; Rd/Rn=R15 -> unpredictable
        TEQS    r5,#15
        TEQNES  r9,#15
        MOVEQ   r14,#Mistake_R15
        STREQ   r14,Mistake
        BL      Tab_Dis_Register

        AND     R5,R4,#2_1111           ; Rm
        ; Rm=Rn -> unpredictable
        TEQS    r5,r9
        MOVEQ   r14,#Mistake_RmRn
        STREQ   r14,Mistake
SwpCommon1
        ; Rm=R15 -> unpredictable
        TEQS    r5,#15
        MOVEQ   r14,#Mistake_R15
        STREQ   r14,Mistake
        BL      Comma_Dis_Register
SwpCommon2
        AddStr  Open_B
        MOV     r5,r9                   ; Rn
        BL      Dis_Register
        AddChar "]"
        B       InstructionEnd

LoadStoreExtension
        ; Arrive here with cccc 000p xxxx xxxx xxxx xxxx 1oo1 xxxx
        ; (poo != 000)
        TSTS    r4, #2_11:SHL:5
        BEQ     swp_type

LdrStrH ; Load and Store Halfword/Doubleword or Load Signed Byte
        ; arrive here with cccc 000x xxxx xxxx xxxx xxxx 1nn1 xxxx
        ; (nn != 00)
        ; format is        cccc 000p uiwl nnnn dddd aaaa 1sh1 bbbb
        ;
        ; LDR{cond}<H|SH|SB|D> Rd,[Rn,<#offset|Rm>]{!}
        ;                      Rd,[Rn],<#offset|Rm>
        ; STR{cond}<H|D>       Rd,[Rn,<#offset|Rm>]{!}
        ;                      Rd,[Rn],<#offset|Rm>
        ; where cccc = condition
        ;          p = Pre-indexed/~Post-indexed
        ;          u = Up/~Down
        ;          i = Immediate/~Register offset
        ;          w = Writeback (must be zero if post-indexed)
        ;          l = Load/~Store
        ;       nnnn = Rn
        ;       dddd = Rd
        ;       aaaa = immediate offset high nibble (if i==1), else 0
        ;          s = Signed/~Unsigned halfword
        ;          h = Halfword/~Signed byte
        ;       bbbb = immediate offset low nibble (if i==1), else Rm

        ; cccc 000x x0xx xxxx xxxx nnnn 1xx1 xxxx is undefined if any of
        ; nnnn are 1.
        TSTS    r4, #1:SHL:22           ; Immediate/~Register offset
        BNE     %F5
        TSTS    r4, #&00000F00
        BNE     Undefined
5
        TSTS    r4, #1:SHL:20           ; Load/~Store
        BNE     %F20
        AND     r5, r4, #&000000F0
        TEQS    r5, #&000000B0
        BEQ     %F20

        ; STRSB or STRSH - actually LDRD+STRD
        TestStr 5,Str,Ldr,conds
        AddChar "D"
        TestBit 12                      ; Must be even-numbered register
        BNE     Undefined

        [ WarnARMv5E
        MOV     r14, #Mistake_ARMv5E
        STR     r14, Mistake
        ]

        AND     r10, r4, #15:SHL:12     ; r10 = Rd << 12

        ; Check for Rd/Rd+1=Rn with writeback
        AND     r14, r4, #2_10010:SHL:20
        TEQS    r14, #2_10000:SHL:20
        BEQ     %F15

        AND     r14, r4, #14:SHL:16
        TEQ     r14, r10, LSL #4
        MOVEQ   r14, #Mistake_RdRn
        STREQ   r14, Mistake

15      ; Check for LDRD with Rd/Rd+1=Rm
        TSTS    r4, #1:SHL:5            ; H clear => LDRD
        TSTEQS  r4, #1:SHL:22           ; I clear => [Rd,Rm]
        ANDEQ   r14, r4, #14
        TEQEQS  r14, r10, LSR #12
        MOVEQ   r14, #Mistake_RdRm
        STREQ   r14, Mistake

        ; Check for LDRD/STRD of R14+PC
        TEQ     r10, #14:SHL:12
        MOVEQ   r14, #Mistake_R15
        STREQ   r14, Mistake

        B       %F22

20
        TestStr 20,Ldr,Str,conds
        TestBit 6,"S"
        TestBit 5,"H","B"
        AND     r5, r4, #1:SHL:24 :OR: 1:SHL:21
        TEQS    r5, #1:SHL:21           ; Post-indexed with W set => translated
        BNE     %FT22
        [ WarnARMv6T2
        MOV     r14, #Mistake_ARMv6T2
        STR     r14, Mistake            ; No warnings evaluated in this code path yet
        ]
        AddChar "T"                     ; LDRHT/LDRSBT/LDRSHT/STRHT
        
22      MOV     r5, r4, LSR #12
        BL      TabOrPushOver           ; LDRccSBT & LDRccSHT spill over
        BL      Dis_Register

        ; Check we're not storing/loading PC
        TEQS    r5, #15
        MOVEQ   r10, #Mistake_R15
        STREQ   r10, Mistake
        BEQ     %FT30

        ;
        ; Check for Rd=Rn with writeback
        ;
        AND     r10, r4, #2_10010:SHL:20        ; If pre-indexed and no writeback, then OK
        TEQS    r10, #2_10000:SHL:20
        BEQ     %FT30
        AND     r10, r4, #&F:SHL:16             ; If Rd <> Rn then OK
        TEQS    r5, r10, LSR #16
        BNE     %FT30
      [ {FALSE}
        TSTS    r4, #1:SHL:22                   ; If a register offset, then fail
        BEQ     %FT25
        TSTS    r4, #&F
        TSTEQS  r4, #&F00
        BEQ     %FT30                           ; If an immediate offset of 0, then OK
      ]

25      MOV     r10, #Mistake_RdRn
        STR     r10, Mistake

30      MOV     r5, r4, LSR #16
        AND     r5, r5, #&F
        TEQS    r5, #15
        BNE     not_ldrh_rel

        ;
        ; Check for PC-writeback
        ;
        AND     r10, r4, #2_10010:SHL:20
        TEQS    r10, #2_10000:SHL:20
        MOVNE   r10, #Mistake_PCwriteback
        STRNE   r10, Mistake

        ANDEQ   r10, r4, #2_10110:SHL:20        ; Check P,I and W bits
        TEQEQS  r10, #2_10100:SHL:20            ; Must be pre-indexed,immediate,no writeback
        BNE     not_ldrh_rel

        ; show resultant [LD|ST]R[H|SH|SB] Rd,[PC,#nn] address directly

        AddChar ","
        AND     r8,  r4, #&0000000F
        AND     r10, r4, #&00000F00
        ORR     r8, r8, r10, LSR #4
        TSTS    r4, #1:SHL:23           ; Up/~Down bit
        SUBEQ   r8, r9, r8
        ADDNE   r8, r9, r8
        ADD     r8, r8, #8
        B       BranchLdrStrCommon


not_ldrh_rel

        AddStr  Open_B                  ; ",["

        BL      Dis_Register

        TSTS    r4, #1:SHL:24           ; Pre/~Post bit
        ADRL    r10, Close_B
        ADDNE   r10, r10, #1            ; just ',' else '],'
        BL      SaveString
        TSTS    r4, #1:SHL:22           ; Immediate/~Register bit
        BEQ     ldrstrh_reg

        AddChar "#"
        TestBit 23,,"-"                 ; Up/~Down bit

        AND     r8,  r4, #&0000000F
        AND     r10, r4, #&00000F00
        ORR     r8, r8, r10, LSR #4
        BL      StoreDecimal
        B       %F50

ldrstrh_reg
        TestBit 23,,"-"                 ; Up/~Down bit
        AND     r5, r4, #&F
        BL      Dis_Register

        ; Check for Rm=Rn in register post-indexed case
        AND     r6, r4, #&F :SHL: 16
        TEQS    r5, r6, LSR #16
        TSTEQS  r4, #1:SHL:24
        MOVEQ   r6, #Mistake_RmRn
        STREQ   r6, Mistake

50      TSTS    r4, #1:SHL:24           ; Pre/~Post bit
        BEQ     InstructionEnd

        AddChar "]"

        TestBit 21,"!"                  ; Writeback bit
        B       InstructionEnd

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Preload ROUT
        ; Preload
        ; arrive here with 1111 01xx xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        1111 010h uw01 nnnn 1111 iiii iiii iiii
        ;        or        1111 011h uw01 nnnn 1111 rrrr rtt0 mmmm
        ;
        ; PL<D|I><W> [Rn,#offset]
        ;            [Rn,{+|-}Rm{,shift}]
        ;
        ; where    h = PLD/~PLI hint
        ;          u = Up/~Down
        ;          w = Read/~Write intent
        ;       nnnn = Rn
        ;      rrrrr = shift amount
        ;         tt = shift type (LSL, LSR, ASR or ROR)
        ;       mmmm = Rm
        ;       iiii = 12-bit unsigned immediate offset

      [ UseCVFPNEON
        TestBit 20
        BEQ     ASIMD                   ; ASIMD element or structure load/store
      ]

        LDR     r14, =&F57FF0           ; Bits 8-31 of a group of 16 unconditionals
        TEQ     r14, r4, LSR #8
        BEQ     Maintenance_uncond

        TST     r4, #1:SHL:25
        TSTNE   r4, #1:SHL:4            ; Register form needs bit 4 clear
        BNE     Undefined

        TestBit 24
        [ WarnARMv5E
        MOVNE   r14, #Mistake_ARMv5E
        ]
        [ WarnARMv7
        MOVEQ   r14, #Mistake_ARMv7
        ]
        [ WarnARMv5E :LOR: WarnARMv7
        STR     r14, Mistake
        ]

        AddStr  Pload
        TestBit 24, "D", "I"
        TestBit 22
        AddChar "W",EQ
        [ WarnARMv7MP
        MOVEQ   r14, #Mistake_ARMv7MP
        STREQ   r14, Mistake
        ]
        BL      Tab      ; huw01
        AND     r5, r4, #2_10011:SHL:20
        TEQ     r5,     #2_10001:SHL:20 ; PLD
        TEQNE   r5,     #2_00001:SHL:20 ; PLI
        ORREQ   r4, r4, #1:SHL:24       ; Sidestep PC writeback warning
        ANDEQ   r5, r4, #2_1111:SHL:12
        TEQEQ   r5, #2_1111:SHL:12
        BEQ     DataTransfer_Common_NoComma
        B       Undefined

Pload   DCB     "PL", 0
        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Data processing opcodes

        ; Arrive here with cccc 00xx xxxx xxxx xxxx xxxx xxxx xxxx
        ; Format is        cccc 000o ooos nnnn dddd rrrr rtt0 mmmm
        ;                  cccc 000o ooos nnnn dddd ssss 0tt1 mmmm
        ;                  cccc 001o ooos nnnn dddd rrrr iiii iiii
        ;
        ; <data op>{cond}{S} Rd,Rn,op2    (0-7,12,14: AND,EOR,SUB,RSB,ADD,ADC,SBC,RSC,ORR,BIC)
        ; <data op>{cond}{S} Rd,op2       (13,15: MOV,MVN)
        ; <compare>{cond}{P} Rn,op2       (8-11: TST,TEQ,CMP,CMN)
        ;
        ;        op2 = #imm
        ;              Rm{,<LSL|LSR|ASR|ROR> <#imm|Rs>}
        ;              Rm,RRX
        ;
        ; where cccc = condition
        ;       oooo = operation
        ;          s = Set flags (required for comparisons)
        ;       nnnn = Rn (0 if not used, 15 signals P flag on compares)
        ;       dddd = Rd (0 if not used)
        ;      rrrrr = rotate/shift amount
        ;         tt = shift type (LSL,LSR,ASR,ROR) (RRX encoded by ROR #0)
        ;   iiiiiiii = immediate
        ;       mmmm = Rm
DataProcessing ROUT

        LDR     r3, =&02000090          ; Data processing op with bit 4 set
        AND     r3, r4, r3              ; -> shift by Rs, but bit 7 set, so
        TEQS    r3, #&90                ; is not defined
        BEQ     UndefinedDataProcessing ; MUL/MLA in here too (and SWP from 1.23)
                                        ; (and MULL/MLAL from 1.44)
                                        ; (and LDRH etc from 1.48)
                                        ; (and LDA/STL from 1.98) 

        MOV     r6, r4, LSR #21         ; r6 := dp opcode
        AND     r6, r6, #2_1111
;Version 1.25 bug fix - only apply ADR to immediate operands
        ;If ADD or SUB, and Rn=15 then call it ADR instead
        TEQS    R6,#2
        TEQNES  R6,#4
        BNE     notADR ;not ADD/SUB

        MOV     R5,R4,LSR #16
        AND     R5,R5,#15
        TEQS    R5,#15
        BNE     notADR ;Rn not R15

        TST     r4, #1 :SHL: 20
        BNE     notADR ;S bit set - not ADR

        TestBit 25                      ; I bit
        BNE     isADR
notADR
        AND     r5, r4, #2_11001 :SHL: 20
        TEQS    r5, #2_10000 :SHL: 20           ; Check for opcodes 8-11 with S clear
        BEQ     ControlExtension

        ADR     r10, OpcTAB
        ADD     r10, r10, r6, LSL #2

        BL      SaveStringConditions

        MOV     r5, r4, LSR #12         ; Rd
        CMP     r6, #&08                ; TEQ,TST,CMP,CMN
        RSBHSS  r14, r6, #&0B
        BLO     %FT10

        ANDS    r5, r5, #2_1111         ; If Rd = pc then xxxP
        BEQ     %FT05
        TEQ     r5, #15
        BNE     Undefined               ; Otherwise check Rd=R0
        AddChar "P"
        MOV     R10, #Potential_Banked_Next
        STR     R10, Mistake            ; potential bank access error

5       BL      Tab
        B       %FT20

10
        TestBit 20,"S"                  ; Update flags bit

        BL      Tab_Dis_Register

        BL      AddComma

        TEQ     r6, #13                 ; MOV
        TEQNE   r6, #15                 ; MVN
        BNE     %FT20
        TSTS    r4, #2_1111 :SHL: 16    ; Check that Rn=R0
        BNE     Undefined
        B       Operand_2

20      MOV     r5, r4, LSR #16         ; Rn
        BL      Dis_Register
        BL      AddComma

Operand_2
        TestBit 25                      ; Immediate bit
        BNE     Immediate

        MOV     r5, r4                  ; Rm
        BL      Dis_Register

        BL      ShiftField
        B       InstructionEnd


Immediate
        AddChar "#"

        MOV     r2, r4, LSR #7
        AND     r2, r2, #2_11110        ; Yes, I really do mean 30, see the book
        AND     r8, r4, #2_11111111
        MOV     r8, r8, ROR r2

        MOVS    r14, r8, LSR #8         ; Does value fit in LSB
        BNE     %FT10
        TEQ     r2, #0                  ; but have a non-zero shift ?
        BNE     WackyShift
        B       %FT20                   ; Unshifted 0-255 
10
;Testing for weird shifts. There are 1023 duplicate immediates in total, of which 846 are >= 256.
;
;Since <data> is 8 bit, and <shift> is encoded divided by 2, there are at most 4 alternate encodings
;for any immediate before the <data> value encodes some completely different number.
;eg.     <data << 0><lowest_shift+0>   1  ROR 24 = 256
;        <data << 2><lowest_shift+1>   4  ROR 26 = 256
;        <data << 4><lowest_shift+2>   16 ROR 28 = 256
;        <data << 6><lowest_shift+3>   64 ROR 30 = 256
;
;The <lowest_shift> is the preferred one per DDI 0406C, A5.2.4, so that's the one
;whose <data> has at least one of bits 0-2 set. 
        TST     r4, #3
        BEQ     WackyShift
20
        CMPS    r8, #10
        BLO     Rem_Number              ; If really simple, just display number
                                        ; ie. 0..9 unambiguous

        MOV     r2, #8-4                ; default is byte
        CMPS    r8, #&100
        MOVHS   r2, #16-4               ; then halfword
        CMPS    r8, #&10000
        MOVHS   r2, #32-4               ; then fullword
        BL      StoreHex

        CMPS    r8, #8192               ; If really big, don't comment on it
        BHS     InstructionEnd          ; May have VSet from CMP!

        BL      Tab                     ; Will also insert '; '

        AddChar "="


Rem_Character
        CMPS    r8, #delete
        CMPNES  r8, #space-1
        BLS     Rem_Number

        CMPS    r8, #&100
        BHS     Rem_Number

        MOV     r10, #quote
        STRB    r10, [r0], #1
        STRB    r8,  [r0], #1
        STRB    r10, [r0], #1
        B       InstructionEnd


Rem_Number
        BL      StoreDecimal
        B       InstructionEnd

;ADR pseudo-op - display address being created instead of the ADD/SUB

isADR
        AddStr  AdrTAB,,conds
        MOV     R5, R4, LSR #12
        BL      Tab_Dis_Register
        BL      AddComma
        MOV     R2, R4, LSR #7
        AND     R2, R2, #2_11110
        AND     R8, R4, #2_11111111
        MOV     r8, r8, ROR r2
        TEQS    R6, #2
        SUBEQ   R8, R9, R8
        ADDNE   R8, R8, R9
        ADD     R8, R8, #8
        MOV     R2, #32-4
        BL      StoreHex
        B       InstructionEnd

CPSR_or_SPSR
        TestBit 22
        ADREQ   R10,cpsr_tab
        ADRNE   R10,spsr_tab
        B       SaveString

ControlExtension
        ; arrive here with cccc 00i1 0xx0 xxxx xxxx xxxx rxxs xxxx
        ; (irs != 011)
        ;
        ; This was simple when it was just MRS and MSR, but
        ; ARMv5 and 5E have made it much harder. The approach we
        ; take is to first split between register and immediate,
        ; then split the register form up by bits 4:7.

        TestBit 25
        BNE     ControlExtension_immediate

        ANDS    r5, r4, #2_1111 :SHL: 4         ; 0 = MRS/MSR
        BEQ     MRS_MSR_reg

        CMP     r5, #2_0011 :SHL: 4             ; 1,2,3 = CLZ/BX
        BLS     CLZ_BX_type

        TEQ     r5, #2_0100 :SHL: 4             ; 4 = CRC
        BEQ     is_CRC

        TEQ     r5, #2_0101 :SHL: 4             ; 5 = QADD etc
        BEQ     SaturatingArithmetic

        TEQ     r5, #2_0110 :SHL: 4             ; 6 = ERET
        BEQ     is_ERET

        TEQ     r5, #2_0111 :SHL: 4             ; 7 = BKPT, HVC, SMI
        BEQ     MonitorCalls

        TST     r5, #2_0001 :SHL: 4             ; 8,10,12,14 = SMUL etc
        BEQ     Mul16

        B       Undefined                       ; 9,11,13,15 undefined

ControlExtension_immediate
        ; arrive here with cccc 0011 0xx0 xxxx xxxx xxxx xxxx xxxx
        TST     r4, #2_1011:SHL:20
        BEQ     Move_imm16

        LDR     r3, =1:SHL:21 + 2_1111:SHL:12
        AND     r5, r4, r3
        TEQ     r5, r3
        BNE     Undefined
        TST     r4, #2_01001111:SHL:16
        BNE     MSR_immediate
        B       Hints

MRS_MSR_reg
        ; arrive here with cccc 0001 0xx0 xxxx xxxx xxxx 0000 xxxx
        TestBit 21
        BNE     MSR_reg

is_MRS  ; arrive here with cccc 0001 0x00 xxxx xxxx xxxx 0000 xxxx
        ;
        ; format is        cccc 0001 0r00 1111 dddd 0000 0000 0000
        ;
        ; MRS{cond} Rd, <CPSR|SPSR>
        ; where cccc = condition
        ;          r = 1 for SPSR, 0 for CPSR
        MOVS    r5, r4, LSL #20            ; bottom 12 bits should be 0
        BEQ     %FT10
        TEQ     r5, #2_10:SHL:(8+20)
        TEQNE   r5, #2_11:SHL:(8+20)
        BEQ     MRS_banked
        B       Undefined
10
        AND     r14, r4, #2_1111 :SHL: 16  ; bits 16-19 should be 1
        TEQS    r14, #2_1111 :SHL: 16
        BNE     Undefined

        AddStr  MrsTAB,,conds
        MOV     r5, r4, LSR #12
        BL      Tab_Dis_Register           ; Rd
        BL      AddComma
        BL      CPSR_or_SPSR
        B       InstructionEnd

OpcTAB  DCB     "AND",0
        DCB     "EOR",0
SubTAB  DCB     "SUB",0
        DCB     "RSB",0
AddTAB  DCB     "ADD",0
        DCB     "ADC",0
        DCB     "SBC",0
        DCB     "RSC",0
        DCB     "TST",0
        DCB     "TEQ",0
        DCB     "CMP",0
        DCB     "CMN",0
        DCB     "ORR",0
        DCB     "MOV",0
        DCB     "BIC",0
        DCB     "MVN",0

cpsr_tab DCB    "CPSR",0
spsr_tab DCB    "SPSR",0
BxTAB   DCB     "BX", 0
BxjTAB  DCB     "BXJ",0
        ALIGN

AdrTAB  DCB     "ADR",0
        ALIGN

MSR_reg ; arrive here with cccc 0001 0x10 xxxx xxxx xxxx 0000 xxxx
        TST     r4, #2_11111111:SHL:4 ; if register, bits 4-11 must be 0
        BEQ     %FT01
        AND     r5, r4, #2_11111110:SHL:8
        TEQ     r5, #2_11110010:SHL:8
        BEQ     MSR_banked
        B       Undefined

MSR_immediate
01      ; arrive here with cccc 0001 0x10 xxxx xxxx 0000 0000 xxxx
        ;               or cccc 0011 0x10 xxxx 1111 xxxx xxxx xxxx
        ;
        ; format is        cccc 0001 0r10 fsxc 1111 0000 0000 mmmm
        ;        or        cccc 0011 0r10 fsxc 1111 rrrr iiii iiii
        ; MSR{cond} <CPSR|SPSR>_<fields>, Rm
        ; MSR{cond} <CPSR|SPSR>_<fields>, #imm
        ;
        ; where cccc = condition
        ;          r = 1 for SPSR, 0 for CPSR
        ;       fsxc = field mask bits
        ;       rrrr = rotate amount (/2)
        ;       iiii = immediate
        ;       mmmm = Rm
05      ADR     r10, MsrTAB
        BL      SaveStringConditions
        BL      Tab
        BL      CPSR_or_SPSR
        AddChar "_"
        TestBit 16,"c"
        TestBit 17,"x"
        TestBit 18,"s"
        TestBit 19,"f"
        BL      AddComma
        B       Operand_2

CLZ_BX_type
        ; arrive here with cccc 0001 0xx0 xxxx xxxx xxxx 00xx xxxx
        TestBit 22
        BNE     is_CLZ

is_BX   ; arrive here with cccc 0001 00x0 xxxx xxxx xxxx 00la xxxx  (la != 00)
        ;
        ; format is        cccc 0001 0010 1111 1111 1111 00l1 mmmm
        ;
        ; B{L}X{cond} Rm
        ;
        ; where cccc = condition
        ;          l = Link
        ;       mmmm = Rm
        LDR     r3, =&002FFF00
        AND     r5, r4, r3
        TEQ     r5, r3
        BNE     Undefined

        TestBit 4
        BEQ     is_BXJ

        TestStr 5,BlxTAB,BxTAB,conds
        AND     R5,R4,#2_1111
        BL      Tab_Dis_Register

        [ WarnARMv5
        TestBit 5
        MOVNE   r10, #Mistake_ARMv5
        STRNE   r10, Mistake
        ]

        B       InstructionEnd

is_BXJ  ; arrive here with cccc 0001 0010 1111 1111 1111 0010 xxxx
        ; format is        cccc 0001 0010 1111 1111 1111 0010 mmmm
        ;
        ; BXJ{cond}   Rm
        ;
        ; where cccc = condition
        ;       mmmm = Rm
        AddStr  BxjTAB,,conds
        AND     R5,R4,#2_1111
        BL      Tab_Dis_Register
        B       InstructionEnd

BLX_offset
        ; arrive here with 1111 101x xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        1111 101h oooo oooo oooo oooo oooo oooo
        ;
        ; BLX address
        ;
        ; where    h = bit 1 of offset
        ;          o = signed offset (bits 25-2)
        [ WarnARMv5
        MOV     r10, #Mistake_ARMv5
        STR     r10, Mistake
        ]

        ADR     r10, BlxTAB
        BL      SaveString
        BL      Tab

        MOVS    r4, r4, ASL #8          ; shift offset up to top (bit 1 into C)
        ADDCS   r4, r4, #1 :SHL: 7      ; put bit 1 underneath
        B       BranchCommon


is_CLZ  ; arrive here with cccc 0001 01x0 xxxx xxxx xxxx 00xx xxxx
        ;
        ; format is        cccc 0001 0110 1111 dddd 1111 0001 mmmm
        ;
        ; CLZ{cond} Rd, Rm
        ;
        ; where cccc = condition
        ;       dddd = Rd
        ;       mmmm = Rm
        LDR     r3, =&002F0F10
        TestBit 5
        BNE     Undefined
        AND     r5, r4, r3
        TEQ     r5, r3
        BNE     Undefined

        [ WarnARMv5
        MOV     r10, #Mistake_ARMv5
        STR     r10, Mistake
        ]

        ADR     r10, ClzTAB
        BL      SaveStringConditions
        MOV     r5, r4, LSR #12
        BL      Tab_Dis_Register
        MOV     r5, r4
        BL      Comma_Dis_Register
        B       InstructionEnd

MonitorCalls
        ; arrive here with cccc 0001 0xx0 xxxx xxxx xxxx 0111 xxxx
        ;
        ; format is        cccc 0001 0oo0 xxxx xxxx xxxx 0111 xxxx
        ;
        ; where oo = op per DDI 0487A, F4.2.12
        AND     r5, r4, #2_11:SHL:21
        TEQ     r5, #2_11:SHL:21
        BEQ     is_SMC                  ; 11 (SMC)
        ; Fall through                  ; 00 (HLT), 01 (BKPT) and 10 (HVC)

BKPT_or_HVC_or_HLT
        ; arrive here with cccc 0001 0qq0 xxxx xxxx xxxx 0111 xxxx
        ; (qq != 11)
        ;
        ; format is        1110 0001 0qq0 nnnn nnnn nnnn 0111 nnnn
        ;
        ; BKPT <number>
        ; HVC  <number>
        ; HLT  <number>
        ;
        ; where nnnn = number
        ;         qq = qualifier (00 = HLT, 01 = BKPT, 10 = HVC)
        AND     r8, r4, #2_1111:SHL:28
        TEQ     r8, #2_1110:SHL:28
        BNE     Undefined               ; Note conditional is unpredictable

        CMP     r5, #2_01:SHL:21
        [ WarnARMv5
        MOVEQ   r14, #Mistake_ARMv5
        ]
        ADREQ   r10, BkptTAB
        [ WarnARMv7VE
        MOVHI   r14, #Mistake_ARMv7VE
        ]
        ADRHI   r10, HvcTAB
        [ WarnARMv8
        MOVCC   r14, #Mistake_ARMv8
        ]
        ADRCC   r10, HltTAB
        [ WarnARMv5 :LOR: WarnARMv7VE :LOR: WarnARMv8
        STR     r14, Mistake
        ]
        BL      SaveString
        BL      Tab

        MOV     r8, r4, LSR #8
        MOV     r8, r8, LSL #20
        AND     r5, r4, #2_1111
        ORR     r8, r5, r8, LSR #16
        MOV     r2, #16-4
        BL      StoreHex
        B       InstructionEnd

BkptTAB DCB     "BKPT",0
HvcTAB  DCB     "HVC",0
HltTAB  DCB     "HLT",0
        ALIGN
        
SaturatingArithmetic
        ; arrive here with cccc 0001 0xx0 xxxx xxxx xxxx 0101 xxxx
        ; format is        cccc 0001 0ds0 nnnn dddd 0000 0101 mmmm
        ;
        ; Q{D}<ADD|SUB>{cond} Rd, Rm, Rn
        ;
        ; where cccc = condition
        ;          d = Double
        ;          s = Subtract/~Add
        ;       nnnn = Rn
        ;       dddd = Rd
        ;       mmmm = Rm

        TST     r4, #2_1111:SHL:8
        BNE     Undefined

        [ WarnARMv5E
        MOV     r14, #Mistake_ARMv5E
        STR     r14, Mistake
        ]

        AND     r14, r4, #15:SHL:16
        TEQ     r14, #15:SHL:16
        ANDNE   r14, r4, #15:SHL:12
        TEQNE   r14, #15:SHL:12
        ANDNE   r14, r4, #15
        TEQNE   r14, #15
        MOVEQ   r14, #Mistake_R15
        STREQ   r14, Mistake

        AddChar "Q"
        TestBit 22,"D"
        TestStr 21,SubTAB,AddTAB,conds
        MOV     r5, r4, LSR #12
        BL      Tab_Dis_Register
        MOV     r5, r4
        BL      Comma_Dis_Register
        MOV     r5, r4, LSR #16
        BL      Comma_Dis_Register
        B       InstructionEnd

Mul16   ROUT
        ; arrive here with cccc 0001 0xx0 xxxx xxxx xxxx 1xx0 xxxx
        ; format is        cccc 0001 0oo0 dddd nnnn ssss 1yx0 mmmm
        ;
        ; SMLA<B|T><B|T>{cond}  Rd, Rm, Rs, Rn       op = 0
        ; SMLAW<B|T>{cond}      Rd, Rm, Rs, Rn       op = 1, x = 0
        ; SMULW<B|T>{cond}      Rd, Rm, Rs           op = 1, x = 1
        ; SMLAL<B|T><B|T>{cond} RdLo, RdHi, Rm, Rs   op = 2
        ; SMUL<B|T><B|T>{cond}  Rd, Rm, Rs           op = 3
        ;
        ; where cccc = condition
        ;         oo = operation
        ;       dddd = Rd or RdHi
        ;       nnnn = Rn or RdLo
        ;       ssss = Rs
        ;          y = Top/~Bottom of Rs
        ;          x = Top/~Bottom of Rm
        ;       mmmm = Rm

        AND     r7, r4, #2_11:SHL:21    ; r7 = oo (<< 21)
        TestBit 5
        ORRNE   r6, r7, #1:SHL:20       ; r6 = oox (<< 20)
        MOVEQ   r6, r7
        ADR     r10, Mul16TAB
        ADD     r10, r10, r6, LSR #17
        SUB     r10, r10, r6, LSR #20   ; r10 = Mul16TAB + 7 * oox
        BL      SaveString
        TestBit 6,"T","B"
        BL      Conditions

        TEQ     r6, #2_011:SHL:20
        TEQNE   r7, #2_11 :SHL:21
        BNE     %FT10
        TST     r4, #2_1111:SHL:12      ; Check Rn field is 0 for SMUL
        BNE     Undefined
10
        [ WarnARMv5E
        MOV     r14, #Mistake_ARMv5E
        STR     r14, Mistake
        ]

        BL      TabOrPushOver

        TEQ     r7, #2_10 :SHL:21       ; EQ if SMLAL
        BNE     %FT15

        MOV     r5, r4, LSR #12
        AND     r8, r5, #15
        BL      Dis_Register            ; RdLo
        MOV     r5, r4, LSR #16
        AND     r9, r5, #15
        BL      Comma_Dis_Register      ; RdHi

        TEQ     r8, r9
        MOVEQ   r14, #Mistake_RdLoRdHi
        STREQ   r14, Mistake
        B       %FT17

15      MOV     r5, r4, LSR #16
        BL      Dis_Register            ; Rd

17      MOV     r5, r4
        BL      Comma_Dis_Register      ; Rm
        MOV     r5, r4, LSR #8
        BL      Comma_Dis_Register      ; Rs

        CMP     r6, #2_010:SHL:20
        MOVLS   r5, r4, LSR #12
        BLLS    Comma_Dis_Register      ; Rn

        AND     r14, r4, #15:SHL:16
        TEQ     r14, #15:SHL:16
        ANDNE   r14, r4, #15:SHL:12
        TEQNE   r14, #15:SHL:12
        ANDNE   r14, r4, #15:SHL:8
        TEQNE   r14, #15:SHL:8
        ANDNE   r14, r4, #15
        TEQNE   r14, #15
        MOVEQ   r14, #Mistake_R15
        STREQ   r14, Mistake

        B       InstructionEnd


MrsTAB  DCB     "MRS",0
MsrTAB  DCB     "MSR",0
BlxTAB  DCB     "BLX",0
ClzTAB  DCB     "CLZ",0
Mul16TAB
        DCB     "SMLAB",0,0
        DCB     "SMLAT",0,0
        DCB     "SMLAW",0,0
        DCB     "SMULW",0,0
        DCB     "SMLALB",0
        DCB     "SMLALT",0
        DCB     "SMULB",0,0
        DCB     "SMULT",0
        ALIGN

Ldc     DCB     "LDC", 0
Stc     DCB     "STC", 0
Cdp     DCB     "CDP", 0
Mcr     DCB     "MCR", 0
Mrc     DCB     "MRC", 0
Cp      DCB     "CP", 0
Mcrr    DCB     "MCRR", 0
Mrrc    DCB     "MRRC", 0

        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Coprocessor_Unconditional
        ; Coprocessor operations
        ; arrive here with 1111 11nn xxxx xxxx xxxx xxxx xxxx xxxx
        ; with nn <> 11

        ; So far, ARMv8 VFP are the only known instructions that fit here
      [ UseCVFPNEON
        AND     r2, r4, #2_1111 :SHL: 8 ; r2 := CP# << 8
        TEQ     r2, #10 :SHL: 8
        TEQNE   r2, #11 :SHL: 8
        BEQ     VFP
      ]
        B       Coprocessor_NotFP

Coprocessor_Conditional
        ; Coprocessor operations
        ; arrive here with cccc 11nn xxxx xxxx xxxx xxxx xxxx xxxx
        ; with nn <> 11, cccc <> 1111

        ANDS    r2, r4, #2_1111 :SHL: 8 ; r2 := CP# << 8

 [ XScaleDSP
        BEQ     XScaleDSPInstruction    ; Coprocessor 0
 ]

        TEQS    r2, #1 :SHL: 8
        BEQ     Floating_Point          ; [Floating Point coprocessor]

        TEQS    r2, #2 :SHL: 8
        BEQ     New_FPA                 ; New FPA

        TEQS    r2, #10 :SHL: 8
        TEQNES  r2, #11 :SHL: 8
        BEQ     VFP

 [ CirrusDSP
        CMPS    r2, #4 :SHL: 8
        BLO     Coprocessor_NotFP
        CMPS    r2, #6 :SHL: 8
        BLS     CirrusDSPInstruction    ; Coprocessor 4-6
 ]

Coprocessor_NotFP

; Currently unknown coprocessor

        TEQS    r3, #&0E
        BEQ     Co_Operations

Co_Transfer

        ; arrive here with cccc 110x xxxx xxxx xxxx xxxx xxxx xxxx
        ; format is        cccc 110p unwl nnnn dddd #### iiii iiii
        ;
        ; <LDC|STC>{cond}{L} CP#,Cd,[Rn,#imm]{!}
        ;                    CP#,Cd,[Rn],#imm
        ;                    CP#,Cd,[Rn],{info}
        ; <LDC|STC>2{L}      ...
        ;
        ; where cccc = condition
        ;          p = Pre-indexed/~Post-indexed
        ;          u = Up/~Down
        ;          n = Transfer length
        ;          w = Writeback
        ;          l = Load/~Store
        ;       nnnn = Rn
        ;       dddd = Cd
        ;       #### = CP#
        ;       iiii = immediate offset (in words), or info (puw = 010)

; Coprocessor Data Transfer (CPDT)

        TST     r4, #2_1101 :SHL: 21
        BEQ     CoprocessorExtension    ; Post-indexed, down, no writeback

        TestStr 20,Ldc,Stc,conds,2      ; Load/~Store bit

        TestBit 22,"L"                  ; Length bit

        BL      Tab_CPN                 ; CP#

        MOV     r5, r4, LSR #12         ; CRd
        BL      Dis_C_Register

CPDT_Common                             ; FP entry point from below

        BIC     r3, r3, #2_1000         ; Make like a normal data transfer
                                        ; 0C,0D -> 04,05 (STC,LDC -> STR,LDR)
        BIC     r4, r4, #&00000F00      ; Clear CP# field
        B       DataTransfer_Common

CoprocessorExtension
        ; arrive here with cccc 1100 0x0x xxxx xxxx xxxx xxxx xxxx
        TestBit 22
        BEQ     Undefined

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Coprocessor Double Register Transfer (CPRRT)

CPRRT
        ; arrive here with cccc 1100 010x xxxx xxxx xxxx xxxx xxxx
        ; format is        cccc 1100 010l nnnn dddd #### oooo mmmm
        ;
        ; <MCRR|MRRC>{cond} CP#,op,Rd,Rn,Cm
        ; <MCRR|MRRC>2      CP#,op,Rd,Rn,Cm
        ;
        ; where cccc = cond
        ;          l = Load/~Store (ie load from coprocessor)
        ;       nnnn = Rn
        ;       dddd = Rd
        ;       #### = CP#
        ;       oooo = operation
        ;       mmmm = Cm

        [ WarnARMv5E
        MOV     r14, #Mistake_ARMv5E
        STR     r14, Mistake
        ]
        TestStr 20,Mrrc,Mcrr,conds,2
        CMP     r4, #2_1111:SHL:28
        [ WarnARMv6
        MOVCS   r14, #Mistake_ARMv6
        |
        MOVCS   r14, #0                 ; Splat the default ARMv5 '2' warning
        ]
        STRCS   r14, Mistake
        BL      Tab_CPN
        MOV     r8, r4, LSR #4
        AND     r8, r8, #2_1111
        BL      StoreDecimal
        BL      AddComma
        BL      Dis_RdRn
        BL      AddComma
        MOV     r5, r4
        BL      Dis_C_Register
        B       InstructionEnd

; Disassemble Rd and Rn for CPRRT instructions
Dis_RdRn Entry
50      MOV     r5, r4, LSR #12
        AND     r6, r5, #2_1111
        BL      Dis_Register
        MOV     r5, r4, LSR #16
        AND     r7, r5, #2_1111
        BL      Comma_Dis_Register
        TestBit 20
        BEQ     %FT10
        TEQ     r6, r7
        MOVEQ   r14, #Mistake_RdRn
        STREQ   r14, Mistake
10      TEQ     r6, #15
        TEQNE   r7, #15
        MOVEQ   r14, #Mistake_R15
        STREQ   r14, Mistake
        EXIT

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


Co_Operations

        ; arrive here with cccc 1110 xxxx xxxx xxxx xxxx xxxx xxxx

        TestBit 4               ; Transfer/~Operation bit
        BNE     Co_Transfers

; Coprocessor Data Operation (CPDO)
        ; arrive here with cccc 1110 xxxx xxxx xxxx xxxx xxx0 xxxx
        ; format is        cccc 1110 oooo nnnn dddd #### iii0 mmmm
        ;
        ; CDP{cond} CP#,op,Cd,Cn,Cm{,info}
        ; CDP2      CP#,op,Cd,Cn,Cm{,info}
        ;
        ; where cccc = cond
        ;       oooo = operation
        ;       nnnn = Cn
        ;       dddd = Cd
        ;       #### = CP#
        ;        iii = info
        ;       mmmm = Cm

        MOV     R10, #Potential_SWICDP_Next ;deferred warning in case SWI next
        STR     R10, Mistake

        AddStr  Cdp,,conds,2

        BL      Tab_CPN                 ; CP#

        MOV     r8, r4, LSR #20         ; CPOpc
        AND     r8, r8, #2_1111         ; All of nibble for CPDO
        BL      StoreDecimal_Comma

        MOV     r5, r4, LSR #12         ; CRd
        BL      Dis_C_Register_Comma

CPRT_CPDO_Common

        MOV     r5, r4, LSR #16         ; CRn
        BL      Dis_C_Register_Comma

        MOV     r5, r4                  ; CRm
        BL      Dis_C_Register

        MOV     r8, r4, LSR #5          ; CP
        ANDS    r8, r8, #2_111          ; non-zero ?
        BLNE    AddComma
        BLNE    StoreDecimal

        ;if MRC/MCR check for system control coprocessor and comment accordingly

        TestBit 4                       ; Transfer/~Operation bit
        BEQ     InstructionEnd

        AND     R5,R4,#&F00
        TEQS    R5,#&F00
        BNE     InstructionEnd          ; Not CP15

        AND     R5,R4,#&F0000
        TEQS    R5,#&40000
        BEQ     InstructionEnd
        CMPS    R5,#&80000
        BHI     InstructionEnd

        BL      Tab

        ADR     R10,msgtable
        ADD     R10,R10,R5,LSR #14
        BL      lookup_r10
        B       SaveStringEnd


; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Coprocessor Register Transfer (CPRT)

Co_Transfers
        ; arrive here with cccc 1110 xxxx xxxx xxxx xxxx xxx1 xxxx
        ; format is        cccc 1110 oool nnnn dddd #### iii1 mmmm
        ;
        ; <MCR|MRC>{cond} CP#,op,Rd,Cn,Cm{,info}
        ; <MCR|MRC>2      CP#,op,Rd,Cn,Cm{,info}
        ;
        ; where cccc = cond
        ;        ooo = operation
        ;          l = Load/~Store (ie load from coprocessor)
        ;       nnnn = Cn
        ;       dddd = Rd
        ;       #### = CP#
        ;        iii = info
        ;       mmmm = Cm

        TestStr 20,Mrc,Mcr,conds,2      ; Load/~Store bit

        BL      Tab_CPN                 ; CP#

        MOV     r8, r4, LSR #21         ; CPOpc
        AND     r8, r8, #2_111          ; Only three bits for CPRT
        BL      StoreDecimal_Comma

        MOV     r5, r4, LSR #12         ; Rd (ARM register)
        BL      Dis_Register
        BL      AddComma

        B       CPRT_CPDO_Common

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Tab_CPN Entry

        BL      Tab

        AddStr  Cp

        MOV     r8, r4, LSR #8
        AND     r8, r8, #2_1111
        BL      StoreDecimal_Comma      ; CP#
        EXIT

        LTORG

msgtable
msg0    = "M02",0       ; CP15,C0 : ARMv4 ID
msg1    = "M03",0       ; CP15,C1 : ARMv4 Control
msg2    = "M04",0       ; CP15,C2 : ARMv4 Translation Table
msg3    = "M05",0       ; CP15,C3 : ARMv4 Domain Access Control
msg4    = "M08",0       ; CP15,C4 : ARMv4 - Bad register
msg5    = "M06",0       ; CP15,C5 : ARMv4 Fault Status
msg6    = "M07",0       ; CP15,C6 : ARMv4 Fault Address
msg7    = "M51",0       ; CP15,C7 : ARMv4 Cache Operations
msg8    = "M52",0       ; CP15,C8 : ARMv4 TLB Operations


; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

WackyShift
        AND     r8, r4, #&FF            ; Get unrotated byte back
        MOV     r2, #8-4                ; Display unrotated byte,decimal rotate
        BL      StoreHex

        AddChar ","

        MOV     r8, r4, LSR #7          ; Extract shift part again
        AND     r8, r8, #30
        B       Rem_Number

ShiftField ROUT

        MOV     r2, r4, LSR #4
        ANDS    r2, r2, #&FF            ; LSL #0 -> no shift at all
        MOVEQ   pc, lr

        Entry

        TEQ     r2, #2_00000110         ; ROR #0 -> RRX
        BEQ     %FT80

        AND     r1, r2, #2_00000110     ; Each is 6 chars long
        ADR     r10, ShiftTypes
        ADD     r10, r10, r1, LSL #1
        ADD     r10, r10, r1
        BL      SaveString

        TST     r2, #2_00000001
        BNE     %FT50                   ; [, SHF Rs]

; Shift number

        AddChar "#"

        MOVS    r8, r2, LSR #3          ; LSR #0 -> LSR #32
        MOVEQ   r8, #32                 ; ASR #0 -> ASR #32
        BL      StoreDecimal
        EXIT

50 ; Shift register

        MOV     r5, r2, LSR #4          ; Rs

        AND     r5, r5, #2_1111
        TEQS    r5, #15
        MOVEQ   r14, #Mistake_R15shift
        STREQ   r14, Mistake

        BL      Dis_Register
        EXIT


80      ADR     r10, %FT90              ; RRX
        BL      SaveString
        EXIT

90
        DCB     ",RRX", 0

ShiftTypes
        DCB     ",LSL ", 0              ; 00x
        DCB     ",LSR ", 0              ; 01x
        DCB     ",ASR ", 0              ; 02x
        DCB     ",ROR ", 0              ; 03x

        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r5 = fp register

Dis_F_Register EntryS "r2"

        LDRB    r10, DisReg_F
        AND     r5, r5, #2_111          ; Only 8 FP registers
;       MOV     r10, #"F"
        B       %FT20

; .............................................................................

Dis_C_Register ALTENTRY

        LDRB    r10, DisReg_C
;        MOV     r10, #"C"
;        B       %FT20

; .............................................................................
; In    r5 = register number
;       r10 = first character

20      STRB    r10, [r0], #1
        AND     r5, r5, #2_1111
        ADD     r2, r5, #"0"
        CMPS    r5, #10
        AddChar "1",HS
        SUBHS   r2, r2, #10
        STRB    r2, [r0], #1
        EXITS

; .............................................................................

Comma_Dis_Register ALTENTRY

        BL      AddComma
        B       %FT50

; .............................................................................

Tab_Dis_Register ALTENTRY

        BL      Tab
        B       %FT50

; .............................................................................

; In    r5 = register number (in bottom 4 bits, rest can be garbage)
; Out   r5 = register number (correctly masked)
;       r2, r10 corrupted

Dis_Register ALTENTRY

50      AND     R5, R5, #2_1111
;        TEQ     r5, #15
;        BEQ     Dis_pc

        CMP      R5,#8
        BLHS     checkreg

        ADD     R14, R12, #:INDEX: DisRegLabels
        LDR     R14, [R14, R5, LSL #2]
        LDR     R14, [R14]

;       MOV     R14, #"R"
;       ORR     R14, R14, #"1" :SHL: 8
;       ORR     R14, R14, #"4" :SHL: 16

        STRB    R14, [R0], #1
        MOV     R14, R14, LSR #8
        STRB    R14, [R0], #1
        MOVS    R14, R14, LSR #8
        STRNEB  R14, [R0], #1
        EXITS

; .............................................................................

;Dis_pc
;       AddStr  pc_Reg
;        EXITS
;
;
;pc_Reg  DCB     "PC", 0
;        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r10 -> string, 0 to copy into buffer
;       r0 -> current buffer position

; Out   r0 ++, r10++

SaveString EntryS

10      LDRB    r14, [r10], #1
        CMPS    r14, #32
        STRHSB  r14, [r0], #1
        BHS     %BT10
        EXITS

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r4 = instruction

; Out   r0++ with condition codes appended

Conditions EntryS "r1, r10"

10      MOV     r1, r4, LSR #28
        CMPS    r1, #14
        EXITS   HS                      ; 14 = AL (no code printed)
                                        ; 15 was NV, now decoded upfront
        ADRLO   r10, CondSym
        ADDLO   r1, r10, r1, LSL #1
        LDRLOB  r10, [r1], #1
        STRLOB  r10, [r0], #1
        LDRLOB  r10, [r1], #1
        STRLOB  r10, [r0], #1
        EXITS

CondSym DCB     "EQ"                    ; 0
        DCB     "NE"                    ; 1
        DCB     "CS"                    ; 2
        DCB     "CC"                    ; 3
        DCB     "MI"                    ; 4
        DCB     "PL"                    ; 5
        DCB     "VS"                    ; 6
        DCB     "VC"                    ; 7

        DCB     "HI"                    ; 8
        DCB     "LS"                    ; 9
        DCB     "GE"                    ; 10
        DCB     "LT"                    ; 11
        DCB     "GT"                    ; 12
        DCB     "LE"                    ; 13

        ALIGN

; .............................................................................

SaveStringConditions ALTENTRY

        BL      SaveString
        B       %BT10

; .............................................................................

SaveStringConditions2 ALTENTRY

        BL      SaveString
        CMP     r4, #2_1111:SHL:28      ; Condition 'NV' => '2'
        BCC     %BT10
        [ WarnARMv5
        LDR     r10, Mistake
        CMP     r10, #0
        MOVLE   r10, #Mistake_ARMv5
        STRLE   r10, Mistake
        ]
        AddChar "2"
        EXITS

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Pad with spaces to operand field or comment field

Tab Entry

10      ADR     r1, StringBuffer
        SUB     r1, r0, r1
15      MOV     r14, #space
        STRB    r14, [r0], #1
        TEQS    r1, #7
        EXIT    EQ
        CMPS    r1, #25 ;new exit position for longer comments
        BLO     %BT10

        ADR     r10, Rem
        BL      SaveString
        EXIT

TabOrPushOver
        ALTENTRY
        ADR     r1, StringBuffer
        SUB     r1, r0, r1
        CMPS    r1, #8
        BLO     %BT15
        AddChar " "
        EXIT




Rem     DCB     " ; ", 0
        ALIGN

TestMistakes
        Entry   "r5"
        LDR     R5,Mistake
        CMPS    R5,#1
        BLT     exitM

        BL      Tab
        MOV     R10,#0
        STR     R10,Mistake
        ADR     R10,Silly
        SUB     R5,R5,#1
        ADD     R10,R10,R5,LSL #2
;        LDR     R5,[R5]
;        ADD     R10,R10,R5
        BL      lookup_r10
        BL      SaveString
exitM
        EXIT

Silly
        =       "M10",0         ; ! and ^
        =       "M11",0         ; Not R8-R14
        =       "M12",0         ; SWI after CDP
        =       "M13",0         ; Rd=Rm or Rd=R15
        =       "M53",0         ; Shift by R15
        =       "M54",0         ; Not R15
        =       "M55",0         ; PC writeback
        =       "M56",0         ; Byte load/store of PC
        =       "M57",0         ; May store PC+8 or PC+12
        =       "M58",0         ; Unpredictable
        =       "M59",0         ; Rd=Rn
        =       "M60",0         ; Rm=Rn
        =       "M61",0         ; RdLo=RdHi
        =       "M62",0         ; RdLo=Rm
        =       "M63",0         ; RdHi=Rm
        =       "M64",0         ; Rn in list
        =       "M66",0         ; Rd=Rm
        =       "M67",0         ; Only 1 reg on SA-110 rev 2
        =       "A00",0         ; ARMv5 or later
        =       "A01",0         ; ARMv5E or later
        =       "A02",0         ; ARMv6 or later
        =       "A03",0         ; ARMv6K or later
        =       "A04",0         ; ARMv6T2 or later
        =       "A05",0         ; ARMv7 or later
        =       "A15",0         ; ARMv7VE or later
        =       "A16",0         ; ARMv7MP or later
        =       "M49",0         ; Odd base of pair
        =       "A17",0         ; XScale DSP
        =       "A18",0         ; ARMv8 or later
        
        ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r0 -> buffer
;       r2 = bit position to start at
;       r8 = value to store in hex

; Out   r0 ++

StoreHex Entry "r2"

        AddChar "&"

10      MOV     r14, r8, LSR r2
        AND     r14, r14, #15
        CMPS    r14, #9
        ORRLS   r14, r14, #"0"
        ADDHI   r14, r14, #"A"-10
        STRB    r14, [r0], #1
        SUBS    r2, r2, #4
        BPL     %BT10
        EXIT

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r0 -> current buffer position
;       r8 = value to store in decimal

; Out   r0++

StoreDecimal EntryS "r1-r2, r4"

        SUB     sp, sp, #16
        MOV     r4, r0

        MOV     r0, r8
        MOV     r1, sp
        MOV     r2, #16                 ; Can't get an error with this much
        SWI     XOS_BinaryToDecimal     ; r2 := length of number

        MOV     r0, r4
10      SUBS    r2, r2, #1
        LDRPLB  r14, [r1], #1
        STRPLB  r14, [r0], #1
        BNE     %BT10

        ADD     sp, sp, #16
        EXITS

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

StoreCommaHash_Decimal Entry

        BL      AddCommaHash
        BL      StoreDecimal
        EXIT

StoreDecimal_Comma Entry

        BL      StoreDecimal
        B       %FT50

;..............................................................................
; In    r0 -> output buffer
; Out   comma appended, r0++, flags preserved

AddComma ALTENTRY

50
        AddChar ","
        EXIT

;..............................................................................
; In    r0 -> output buffer
; Out   comma & hash appended, r0++, flags preserved

AddCommaHash

        AddChar ","
        AddChar "#"
        MOV     pc, lr

; .............................................................................

Dis_C_Register_Comma ALTENTRY

        BL      Dis_C_Register
        B       %BT50

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

MemoryI_Error
        DCD     ErrorNumber_Syntax
        DCB     "SDBGMMI", 0
        ALIGN


MemoryI_Code Entry "r6-r11"

        MOV     R6,#"T"
        BL      MemoryCommon
        TEQS    R6,#1
        MOVEQ   R6,#2

        ADR     r2, MemoryI_Error
        MOV     r10, #0 ; arguments can only be 32-bit
        BL      GetCommandParms
        BLVS    CopyErrorR2
        EXIT    VS

        TST     r8, #secondparm
        ADDEQ   r7, r9, r6, LSL #4      ; r7 = r9 + r6 * 24
        ADDEQ   r7, r7, r6, LSL #3

        TEQ     r6, #4

        BICEQ   r9, r9, #3              ; ensure word aligned
        BICEQ   r7, r7, #3
        BICNE   r9, r9, #1              ; ensure halfword aligned
        BICNE   r7, r7, #1

        TEQS    r9, r7                  ; If same, ensure we do one word
        ADDEQ   r7, r7, r6

        BL      SwapAllBreakpoints

10 ; Loop doing disassembly until ESCape or ended

        SWI     XOS_ReadEscapeState
        BCS     %FT95

        BL      DisplayHexWord_R9       ; Display address

        BLVC    MarkPC
        EXIT    VS

        TEQ     r6, #4
        BNE     %FT40

        ; ARM disassembly

        MOV     r0, r9
        BL      do_readW

        MOV     r4, r1
        BL      DisplayCharactersR      ; Display R6 chars contained in R4
        EXIT    VS

        SWI     XOS_WriteS
        DCB     " : ", 0
        ALIGN
        EXIT    VS

        MOV     r10, r1
        BL      DisplayHexWord
        BLVC    MarkBreakpoints
        MOVVC   r0, r1
        MOVVC   r1, r9
        SWIVC   XDebugger_Disassemble
        B       %FT55

40      ; Thumb disassembly
        ; This code will probably want updating once we can decode all the new 32bit encodings
 [ NoARMv5
        ; We may be running on a machine without LDRH, or without proper support for it (e.g. RiscPC)
        ; To avoid alignment faults on ARMv6+, load an aligned word and shift it ourselves
        BIC     r0, r9, #2
        BL      do_readW
        TST     r9, #2
        MOVNE   r1, r1, LSR #16
 |
        ; ARMv5+, safe to LDRH
        MOV     r0, r9
        BL      do_readH
 ]

        MOV     r4, r1
        BL      DisplayCharactersR      ; Display R6 chars contained in R4
        EXIT    VS

        SWI     XOS_WriteS
        DCB     " : ", 0
        ALIGN
        EXIT    VS

        MOV     r10, r1
        BL      DisplayHexHalfword        
        BLVC    MarkBreakpoints
        MOVVC   r0, r1
        MOVVC   r1, r9
        SWIVC   XDebugger_DisassembleThumb

55      MOVVC   r0, r1

        SWIVC   XOS_Write0
        SWIVC   XOS_NewLine
        EXIT    VS

        ADD     r9, r9, r6

        TEQS    r9, r7
        BNE     %BT10


90      BL      SwapAllBreakpoints
        EXIT

95      BL      AckEscape
        B       %BT90

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

InitStore_Error
        DCD     ErrorNumber_Syntax
        DCB     "SDBGINS", 0
        ALIGN


InitStore_Code Entry "r6-r11"

        LDR     wp, [r12]

        MOV     r1, r0
        ADR     r0, InitStore_Error
        MOV     r10, #0 ; arguments can only be 32-bit
        BL      ReadOneParm             ; r7 := parm, r8 state
        BLVS    CopyError
        EXIT    VS

        TST     r8, #hasparm
        LDREQ   r7, =&E7FFFFFF          ; A nice invalid instruction
        SWI     XOS_GetEnv              ; r1 -> end of user memory

        MOV     r14, #AppSpaceStart
10      TEQ     r14, r1                 ; Might have zero apl space
        STRNE   r7, [r14], #4
        BNE     %BT10

        BL      message_writes
        DCB     "M16", 0                ; "Store inititalised to &"
        ALIGN

        MOVVC   r10, r7
        BLVC    DisplayHexWord
        SWIVC   XOS_NewLine
        EXIT

        LTORG

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

ShowRegs_Code

        LDR     wp, [r12]

; .............................................................................

ShowRegs_Code_Common Entry "r6-r11"

        BL      message_writes        ; Display address of register dump
        DCB     "M17", 0              ; "Register dump (stored at &)
        ALIGN

        ADRVC   r10, Registers
        BLVC    DisplayHexWord
        EXIT    VS

        BL      message_writes
        DCB     "M18", 0              ; ") is:"
        ALIGN
        EXIT    VS

        MOV     r4, #0
        ADR     r11, Registers

10 ; Display dumped registers

        TST     r4, #3
        SWIEQ   XOS_NewLine
        EXIT    VS

        SWINE   XOS_WriteI+space

        SWIVC   XOS_WriteI+"R"

        MOVVC   r8, r4
        BLVC    DisplayDecimalNumber
        EXIT    VS

        CMPS    r4, #10
        SWILO   XOS_WriteI+space
        EXIT    VS

        SWI     XOS_WriteS
        DCB     " = ", 0
        ALIGN

        LDRVC   r10, [r11, r4, LSL #2]
        BLVC    DisplayHexWord
        EXIT    VS

        ADD     r4, r4, #1
        TEQS    r4, #16
        BNE     %BT10

        SWI     XOS_NewLine
        EXIT    VS

; Display processor mode (r10 = dumped pc at end of loop)

; Check whether we're 26 or 32 bit
        LDRB    r0, SysIs32bit
        TEQ     r0, #0
        BEQ     ShowRegs_Mode_26

        BL      message_writes
        DCB     "M19", 0                ; "Mode "
        ALIGN

        LDRVC   r10, [r11, #4*16]       ; get saved PSR

        ANDVC   r2, r10, #2_11111       ; Mask with M[4:0] bits
        ADRVC   r0, ARM32_Modes
        ADDVC   r0, r0, r2, LSL #2
        ADDVC   r0, r0, r2, LSL #1
        SWIVC   XOS_Write0
        EXIT    VS

        BL      message_writes
        DCB     "M20", 0                ; " flags set: "
        ALIGN

        MOVVC   r0, #"N"
        MOVVC   r1, #N_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"Z"
        MOVVC   r1, #Z_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"C"
        MOVVC   r1, #C_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"V"
        MOVVC   r1, #V_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"Q"
        MOVVC   r1, #Q32_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"J"
        MOVVC   r1, #J32_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"G"
        MOVVC   r1, #GE3_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"G"
        MOVVC   r1, #GE2_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"G"
        MOVVC   r1, #GE1_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"G"
        MOVVC   r1, #GE0_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"E"
        MOVVC   r1, #E32_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"A"
        MOVVC   r1, #A32_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"I"
        MOVVC   r1, #I32_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"F"
        MOVVC   r1, #F32_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"T"
        MOVVC   r1, #T32_bit
        BLVC    DoFlagBit
        EXIT    VS

        BL      message_writes
        DCB     "M14", 0                ; "   PSR = "
        BLVC    DisplayHexWord

        SWIVC   XOS_NewLine
        EXIT

ShowRegs_Mode_26

        BL      message_writes
        DCB     "M19", 0                ; "Mode "
        ALIGN

        ANDVC   r2, r10, #2_11          ; Mask with M[1:0] bits
        ADRVC   r0, ARM_Modes
        ADDVC   r0, r0, r2, LSL #2
        SWIVC   XOS_Write0
        EXIT    VS

        BL      message_writes
        DCB     "M20", 0                ; " flags set: "
        ALIGN

        MOVVC   r0, #"N"
        MOVVC   r1, #N_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"Z"
        MOVVC   r1, #Z_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"C"
        MOVVC   r1, #C_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"V"
        MOVVC   r1, #V_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"I"
        MOVVC   r1, #I_bit
        BLVC    DoFlagBit

        MOVVC   r0, #"F"
        MOVVC   r1, #F_bit
        BLVC    DoFlagBit

        SWIVC   XOS_NewLine
        EXIT


ARM_Modes
        DCB     "USR", 0
        DCB     "FIQ", 0
        DCB     "IRQ", 0
        DCB     "SVC", 0

ARM32_Modes
        DCB     "USR26", 0
        DCB     "FIQ26", 0
        DCB     "IRQ26", 0
        DCB     "SVC26", 0
        DCB     "00100", 0
        DCB     "00101", 0
        DCB     "00110", 0
        DCB     "00111", 0
        DCB     "01000", 0
        DCB     "01001", 0
        DCB     "01010", 0
        DCB     "01011", 0
        DCB     "01100", 0
        DCB     "01101", 0
        DCB     "01110", 0
        DCB     "01111", 0
        DCB     "USR32", 0
        DCB     "FIQ32", 0
        DCB     "IRQ32", 0
        DCB     "SVC32", 0
        DCB     "10100", 0
        DCB     "10101", 0
        DCB     "MON32", 0
        DCB     "ABT32", 0
        DCB     "11000", 0
        DCB     "11001", 0
        DCB     "11010", 0
        DCB     "UND32", 0
        DCB     "11100", 0
        DCB     "11101", 0
        DCB     "11110", 0
        DCB     "SYS32", 0
        ALIGN

        LTORG

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r1 = flag bit to test
;       r10 = psr
;       r0 = uppercase char to print if set

; Out   char printed uppercase or lowercase, r0 corrupt

DoFlagBit Entry

        TST     r1, r10
        ORREQ   r0, r0, #&20            ; Cheap lowercase, known values input
        SWI     XOS_WriteC
        EXIT

checkreg
        Entry
        LDR     R14,Mistake
        CMP     R14,#Potential_Banked
        MOVEQ   R14,#Mistake_Banked
        STR     R14,Mistake
        EXIT

        END
        