; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > Sources.ROMFonts

; *********************************
; ***    C h a n g e   L i s t  ***
; *********************************

; Date       Description
; ----       -----------
; 19-Feb-90     0.01    File created.
; 26-Feb-90     0.02    Fixed bug: Font$Path was set to ResourceFS:, not Resources:
; 22-Jun-90     0.03    Forgot to include Sidney font!
; 20-Jul-90     0.04    Saved 24k from IntMetrics by omitting bboxes and rearranging char map
;                       Replaced NewHall.Bold by Sidney (Symbol)
; 07-Jan-91     0.05    Saved 96k by deriving Homerton and Corpus Oblique fonts from normal ones
;                       Regenerated all IntMetrics files from the AFM files
; 15-Apr-91     0.06    Introduced Encodings./Base0, Latin1, Latin2, Latin3 and Latin4
;                       Replaced Homerton.Medium, Trinity.Medium and Trinity.Medium.Italic with /Base0 versions
;                       Regenerated all other IntMetrics files so they're version 2 (new kern format)
; 17-May-91     0.07    (WS) Replaced Corpus.Medium, Corpus.Bold, Trinity.Bold.Italic with /Base0 versions
;                       Trinity.Bold kept back, the Aring kills the font manager!
; 21-May-91     0.08    (WS) all 12 major fonts now in /Base0 form.
; 25-Jun-91     0.10    (WS) Selwyn removed - now on the disc.
; (Log information missed)
; 01-Apr-92     0.14    (SAH) Fixed position of tilde accents in the bold fonts
; 08-Feb-93             BCockburn; changed GETs to use Hdr: rather than &.Hdr.
; 19-Jul-93     0.15    (ECN) Fixed slant of German quote characters in Corpus.Medium
; 31-May-95     0.17    (JRH) Added handset font
; 10-Jul-96     0.18    (SMC) Removed handset font for NCs.
; 08-Apr-98     0.20    (KJB) Added Latin0,Latin5,Latin6,Welsh encodings.
;                             Changed 0xF0 in Latin2 from eth to eth1. Added Eng/eng to Latin4.
;                             Added Wgrave,Wdieresis,wgrave,wdieresis,Ygrave,ygrave,Eng,eng,euro to
;                             /Base0.
;                             Added Wgrave,Wdieresis,wgrave,wdieresis,Ygrave,ygrave glyphs to Corpus,
;                             Homerton and Trinity.
;                             Added euro symbol to Sidney.
;                             Still need euro (and Eng/eng) for Corpus, Homerton, Trinity.
; 10-Aug-98     0.22   (BJGA) Renamed Latin0 to Latin9.
;                             Added Messages1 file.
; 17-Aug-98     0.23   (BJGA) Added /Euro to Latin1-Latin9 and Welsh encodings (already in Base0 and Sidney)
; 07-Oct-98     0.24    (KJB) Merged all the branches.
;               0.25    (KJB) Switched to using ObjAsm. Added Japanese encoding files. Removed Lucida.
;


        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:Services
        GET     Hdr:Proc
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors
        GET     Hdr:Variables
        GET     Hdr:ResourceFS
        GET     Hdr:ImageSize.<ImageSize>
        GET     VersionASM

        GET     Hdr:NDRDebug

        GBLL    debug
        GBLL    debugxx
        GBLL    hostvdu

debug   SETL    false
debugxx SETL    true :LAND: debug
hostvdu SETL    true

        GBLL    DoLucida
DoLucida SETL   {FALSE}         ; No license, except for RCA...

        GBLL    StandAlone
StandAlone SETL {TRUE}          ; Always used to be stand-alone. Is now again.

        AREA    |!!!Module|, CODE, READONLY

; **************** Module code starts here **********************

Module_BaseAddr

        DCD     0 ; ROMFonts_Start    - Module_BaseAddr
        DCD     ROMFonts_Init     - Module_BaseAddr
  [ StandAlone
        DCD     ROMFonts_Die      - Module_BaseAddr
        DCD     ROMFonts_Service  - Module_BaseAddr
  |
        DCD     0
        DCD     0
  ]
        DCD     ROMFonts_Title    - Module_BaseAddr
        DCD     ROMFonts_HelpStr  - Module_BaseAddr
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     ROMFonts_Flags    - Module_BaseAddr

ROMFonts_Title     DCB     "ROMFonts", 0
ROMFonts_HelpStr   DCB     "ROM Fonts", 9, "$Module_MajorVersion ($Module_Date)", 0
                ALIGN
ROMFonts_Flags     DCD     ModuleFlag_32bit

; *****************************************************************************
;
;       ROMFonts_Init - Initialisation entry
;

stk_buffsize    *       256

ROMFonts_Init Entry

  [ StandAlone
        ADR     R0, ROMFonts
        SWI     XResourceFS_RegisterFiles   ; ignore errors (starts on Service_ResourceFSStarting)
  ]

        SUB     sp, sp, #stk_buffsize

        ADR     R0, FontPath
        MOV     R1, sp
        MOV     R2, #stk_buffsize
        MOV     R3, #0
        MOV     R4, #VarType_Macro      ; don't expand it
        SWI     XOS_ReadVarVal          ; error => variable does not exist
        BVS     %FT12                   ; if error, set up the new one

        CMP     R4, #VarType_Macro      ; check for *SetMacro Font$Path <Font$Prefix>.
        BNE     %FT02

        MOV     R14, #0                 ; put in terminator
        STRB    R14, [R1, R2]

        ADR     R0, FontPrefix2         ; R0 -> "<Font$Prefix>."
01      LDRB    R2, [R0], #1
        LDRB    R3, [R1], #1
        CMP     R2, R3
        BNE     %FT02
        CMP     R2, #0
        BNE     %BT01

12      ADR     R0, SetFontPath         ; R0 -> "SetMacro Font$Path ..."
        SWI     XOS_CLI
        BVS     %FT02

        ADR     R0, FontPrefix          ; if Font$Prefix not defined,
        MOV     R2, #-1                 ; set it to " " to pre-empt !Fonts
        MOV     R3, #0
        MOV     R4, #VarType_Expanded
        SWI     XOS_ReadVarVal          ; returns R2=0 if doesn't exist
        CMP     R2, #0                  ; clears V as well!

        ADREQ   R0, SetFontPrefix
        SWIEQ   XOS_CLI
02
        ADD     sp, sp, #stk_buffsize

        EXIT

FontPath        DCB     "Font$$Path", 0
FontPrefix      DCB     "Font$$Prefix", 0
SetFontPath     DCB     "SetMacro Font$$Path <Font$$Prefix>.,Resources:$$.Fonts.", 0
FontPrefix2     DCB     "<Font$$Prefix>.", 0
SetFontPrefix   DCB     "Set Font$$Prefix "" """, 0
                ALIGN

  [ StandAlone

; *****************************************************************************
;
;       ROMFonts_Die - Die entry
;

ROMFonts_Die Entry

        ADR     R0, ROMFonts
        SWI     XResourceFS_DeregisterFiles

        ADDS    r0, r0, #0      ; ignore errors (ResourceFS might be dead)
        EXIT

; *****************************************************************************
;
;       ROMFonts_Service - Main entry point for services
;
; in:   R1 = service reason code
;       R2 = sub reason code
;       R3-R5 parameters
;
; out:  R1 = 0 if we claimed it
;       R2 preserved
;       R3-R5 = ???
;

;Ursula format
;
ROMFonts_UServTab
        DCD     0
        DCD     ROMFonts_UService - Module_BaseAddr
        DCD     Service_ResourceFSStarting
        DCD     0
        DCD     ROMFonts_UServTab - Module_BaseAddr
ROMFonts_Service ROUT

        Debug   xx,"ROMFonts_Service:",R1

        MOV     r0,r0   ;Ursula magic instruction
        TEQ     R1, #Service_ResourceFSStarting
        MOVNE   PC, LR
ROMFonts_UService

   ;;   LDR     R12, [R12]     ;; not actually required in this case

; In    R2 -> address inside ResourceFS module to call
;       R3 = workspace pointer for module
; Out   R2 called with R0 -> files, R3 -> workspace

svc_desktopstarting Entry "R0-R3"

        ADR     R0, ROMFonts
        MOV     LR, PC                          ; get return address
        MOV     PC, R2                          ; R2 = address to call

        EXIT

; *****************************************************************************

        MACRO
$lab    MyDesk  $name
        LCLS src
        LCLS temp
temp    SETS "$name"
        WHILE (:LEN:"$temp" > 0)
      [ "$temp":LEFT:1 = "/"
        ; There's a slash in the output name, swap it for a calming underscore
src     SETS "$src" :CC: "_"
      |
        ; Verbatim
src     SETS "$src" :CC: ("$temp":LEFT:1)
      ]
temp    SETS "$temp":RIGHT:(:LEN:"$temp" - 1)
        WEND
$lab    ResourceFile $src, $name
        MEND

ROMFonts

        GET     <UserIF>_Fonts.s

        DCD     0

      ] ; StandAlone

      [ debug
        InsertNDRDebugRoutines
      ]

        END

