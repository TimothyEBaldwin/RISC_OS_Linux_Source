; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r4 -> dirviewer block
;       r5 -> fileinfo block
;
; Try converting Net directory datestamps
; If object is a directory with leafname !Applic, mark as dft_appl else exit
; If !Applic.!Boot exists, Run it
; else IconSprites !Applic.!Sprites
; If !Applic.!Help exists, mark application help available
; Read datestamp of !Applic.!RunImage (as !Run unlikely to change often)
; If any of the above return an error then the processing is aborted for
; that application

; No error comes out of this ...


star_run_prefix DCB     "WimpTask Run ", 0

plingboot       DCB     "!Boot", 0

star_iconsprites_prefix DCB     "IconSprites ", 0

plingsprites    DCB     "!Sprites", 0
plinghelp       DCB     "!Help", 0
plingrunimage   DCB     "!RunImage", 0

                ALIGN

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Adds an application to the application list
;
; In
; r0    0 => not !Booted, 1 => !Booted
; r6    -> Applic name
; r7    -> name of dir applic is in
;
; Out
; Regs preserved, applic added memory permitting (no error if failed)

AddApplicToApplicList Entry "r0,r1,r2,r3,r8,r9"

; Add !Applic to applic list now its been successfully booted
        MOV     r8, r0                  ; store the was booted flag somewhere handy
        MOV     r1, r6
        BL      strlen
        ADD     r3, r3, #a_headersize + 1 ; for the terminator
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        BVS     %FT60
        STRB    r8, [r2, #a_wasbooted]
        MOV     r8, r2
        ADD     r1, r8, #a_leafname
        MOV     r2, r6
        BL      strcpy

10      ; Hunt for the new directory in which the application resides
        LDR     r9, ApplicDirList
        MOV     r2, r7
        B       %FT30
20
        ADD     r1, r9, #ad_name
        BL      strcmp_excludingspaces
        BEQ     %FT40
        LDR     r9, [r9, #ad_link]
30
        CMP     r9, #Nowt
        BNE     %BT20

        ; Dir name not found in dir list - get a new block
        MOV     r1, r7
        BL      strlen_excludingspaces
        ADD     r3, r3, #ad_headersize + 1      ; for the terminator
        MOV     r0, #ModHandReason_Claim
        SWI     XOS_Module
        BVS     %FT50
        MOV     r9, r2

        ; Fill in the fields
        ADD     r1, r9, #ad_headersize
        MOV     r2, r7
        BL      strcpy_excludingspaces
        MOV     r2, #0
        STR     r2, [r9, #ad_usage]

        ; Link the new directory onto the directory chain
        LDR     r2, ApplicDirList
        STR     r2, [r9, #ad_link]
        STR     r9, ApplicDirList

40      ; Found/created new application - address in r8
        ; Found/created new directory entry - address in r9

        ; Add application onto end of application list
        LDR     r0, ApplicListEndPointer
        TEQ     r0, #Nowt
        ADREQ   r0, ApplicList
        STR     r8, [r0]
        MOV     r0, #Nowt
        STR     r0, [r8, #a_link]
        ADD     r0, r8, #a_link
        STR     r0, ApplicListEndPointer

        ; Increase the directory's usage
        LDR     r1, [r9, #ad_usage]
        ADD     r1, r1, #1
        STR     r1, [r9, #ad_usage]

        ; Point the Applic entry at the dir entry
        STR     r9, [r8, #a_dir]
        CLRV
        EXIT

50      ; Failed to allocate a directory block
        MOV     r0, #ModHandReason_Free
        MOV     r2, r8
        SWI     XOS_Module
60      ; Failed to allocate an application block
        CLRV
        EXIT

SussPlingApplic Entry "r0"
        BL      SussPlingApplic_ReturnError
        BLVS    LocalReportError
        CLRV
        EXIT

SussPlingApplic_ReturnError Entry "r0-r8"

        LDRB    r14, [r5, #df_type]
        CMP     r14, #dft_dir
        BEQ     %FT05
        MOV     r14, #-1
        STR     r14, [r5, #df_helptype]
        CLRV
        EXIT                            ; [file anyway] VClear in entry lr
05
        LDR     r14, [r5, #df_load]     ; Date stamped directory ?
        CMN     r14, #&00100000
        BCS     %FT10                   ; [yes]

        LDR     r14, [r5, #df_attr]     ; if unstamped, try interpreting attr
        MOV     r14, r14, LSR #8
        STR     r14, [sp, #-8]!
        MOV     r0, sp
        ADD     r1, sp, #4
        SWI     XNetFS_ConvertDate
        LDRVC   r14, [sp, #4]           ; exec
        STRVC   r14, [r5, #df_exec]
        LDRVCB  r14, [sp, #8]           ; load
        ORRVC   r14, r14, #&FF000000
        ORRVC   r14, r14, #&00FF0000
        ORRVC   r14, r14, #&0000FF00
        STRVC   r14, [r5, #df_load]
        ADD     sp, sp, #8

10      LDR     r7, [r4, #d_filenames] ; r7 -> dirname
        LDR     r14, [r5, #df_fileptr]
 [ not_16bit_offsets
        ADD     r6, r7, r14             ; r6 -> leafname
 |
        ADD     r6, r7, r14, LSR #16    ; r6 -> leafname
 ]

        CLRV
        LDRB    r14, [r6, #0]
        TEQ     r14, #"!"
;        TEQNE   r14,#160                ; alt_space
        EXIT    NE                      ; [not !Applic] VClear


        MOV     r14, #dft_applic        ; mark as dft_appl
        STRB    r14, [r5, #df_type]

        SUB     sp, sp, #256            ; make temp buffer on stack

      [ version >= 130                  ; Don't boot if CTRL was pressed
        LDR     R14,ctrl_pressed
        CMP     R14,#0
        BNE     %FT80
      ]

        LDR     r8, ApplicList          ; Search applic list for same leafname
        MOV     r1, r6                  ; r1 -> test leafname

        B       %FT21

20      ; Start of loop finding !applic
        ADD     r2, r8, #a_leafname     ; r2 -> stored leafname
        BL      strcmp
        BEQ     %FT71                   ; Found applic in list
        LDR     r8, [r8, #a_link]

21      ; Entry point of loop to find !Applic
        CMP     r8, #Nowt
        BNE     %BT20                   ; [no match]

50 ; Run !Applic.!Boot (if it's a file)

        ; Construct star command in userdata
        ADR     r1, userdata
        addr    r2, star_run_prefix
        BL      strcpy_advance          ; Note perviness; leave r1 ~= sp
        MOV     r2, r7                  ; r2 -> dirname
        BL      strcpy_excludingspaces
        MOV     r2, r6                  ; r2 -> !Applic
        BL      AppendLeafnameToDirname ; +leafname
        addr    r2, plingboot
        BL      AppendLeafnameToDirname ; +!Boot

        ; Check that !Boot exists, is a file and isn't dead
        MOV     r0, #OSFile_ReadNoPath  ; Have a quick peek ourselves
        SWI     XOS_File
        BVS     %FT95
        TEQ     r0, #object_file
        BNE     %FT60                   ; [no !Boot file]
        LDR     r14, =&deaddead
        TEQ     r2, r14
        TEQEQ   r3, r14
        BEQ     %FT71                   ; !Boot is dead

        ; Add it to list, we get very little information if it fails to boot
        MOV     r0, #1                  ; was booted
        BL      AddApplicToApplicList

        ADR     r0, userdata            ; *WimpTask Run !Applic.!Boot
        SWI     XOS_CLI
        BVS     %FT95
        B       %FT70

60 ; IconSprites !Applic.!Sprites (if it's a file and !Applic.!Boot not found)

        MOV     r1, sp
        addr    r2, star_iconsprites_prefix
        BL      strcpy_advance          ; Note perviness; leave r1 ~= sp
        MOV     r2, r7                  ; r2 -> dirname
        BL      strcpy_excludingspaces
        MOV     r2, r6                  ; r2 -> !Applic
        BL      AppendLeafnameToDirname ; +leafname
        addr    r2, plingsprites
        BL      AppendLeafnameToDirname ; +!Sprites

        MOV     r0, #OSFile_ReadNoPath  ; Have a quick peek ourselves
        SWI     XOS_File
        BVS     %FT95

        TEQ     r0, #object_file        ; VClear
        BNE     %FT70                   ; !Sprites is not a file - register this app in the list

        LDR     r14, =&deaddead
        TEQ     r2, r14
        TEQEQ   r3, r14
        BEQ     %FT71                   ; !Sprites is dead

        ; Add applic to list - as 'not !Booted'
        MOV     r0, #0
        BL      AddApplicToApplicList

        MOV     r0, sp                  ; !Sprites is a live file
        SWI     XOS_CLI

        BVS     %FT95

70 ; !Boot or !Sprites worked

71 ; Didn't fail when doing !Boot/!Sprites but didn't successfully do it either

 ; See if !Applic.!Help exists

        MOV     r1, sp
        MOV     r2, r7                  ; r2 -> dirname
        BL      strcpy_excludingspaces
        MOV     r2, r6                  ; r2 -> !Applic
        BL      AppendLeafnameToDirname ; +leafname
        addr    r2, plinghelp
        BL      AppendLeafnameToDirname ; +!Help

        MOV     r0, #OSFile_ReadNoPath  ; Have a quick peek ourselves
        SWI     XOS_File
        BVS     %FT95

        LDR     r5, [sp, #256 + 5*4]    ; r5in - remember what we have seen

        CMP     r0, #object_nothing     ; VClear
        MOVEQ   r2, #-1

        CMP     r0, #object_file
        MOVEQ   r2, r2, LSL #12
        MOVEQ   r2, r2, LSR #20

        CMP     r0, #object_directory
        MOVEQ   r2, #filetype_application ; !Help dir must be an application!

        STR     r2, [r5, #df_helptype]

80 ; Look up date (or even load/exec) of !RunImage; this is more pertinent that
   ; that of !Applic itself

        MOV     r1, sp
        MOV     r2, r7                  ; r2 -> dirname
        BL      strcpy_excludingspaces
        MOV     r2, r6                  ; r2 -> !Applic
        BL      AppendLeafnameToDirname ; +leafname
        addr    r2, plingrunimage
        BL      AppendLeafnameToDirname ; +!RunImage

        MOV     r0, #OSFile_ReadNoPath  ; Have a quick peek ourselves
        SWI     XOS_File
        BVS     %FT95

        CMP     r0, #object_file
        LDREQ   r5, [sp, #256 + 5*4]    ; r5in
        STREQ   r2, [r5, #df_load]
        STREQ   r3, [r5, #df_exec]

95      ADD     sp, sp, #256            ; kill temp frame
        STRVS   r0, [sp]
        EXIT

        LTORG

; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; SussPlingApplic_GivenName
;
; In    r1 -> applic name
;
; Out   Applic sussed, regs preserved, userdata used
;
SussPlingApplic_GivenName       Entry "r0-r5"
        MOV     r0, #OSFile_ReadNoPath
        SWI     XOS_File
        STRVS   r0, [sp, #0*4]
        EXIT    VS
      [ (version >= 133)  :LAND: (version < 143)
        TST     r0,#2                          ; File & directory? -> make directory.
        MOVNE   r0,#2
      ]
        STRB    r0, userdata + df_type  +512
        STR     r2, userdata + df_load  +512
        STR     r5, userdata + df_attr  +512

        ; Set up pointer to structs being made in userdata:
        ; a df_thing
        ; a d_thing
        ; a nul
        ; dirname
        ; leafname
        ADR     r5, userdata + 512
        ADD     r4, r5, #df_size
        MOV     r2, r1
        ADD     r1, r4, #d_headersize
        MOV     r0, #0
        STRB    r0, [r1], #1

        BL      strcpy
        MOV     r3, r2
        BL      FindLeafname

        CMP     r2, r1
        BGT     %FT10

        ; no dir before leaf
        SUB     r1, r2, #1      ; points at the nul before the string destination
        B       %FT20

10      ; dir present before leaf
        ; nul terminate at the . or after the :
        LDRB    r0, [r2, #-1]
        TEQ     r0, #":"
        ADDEQ   r2, r2, #1
        MOV     r0, #0
        STRB    r0, [r2, #-1]

        ; copy the leaf to after the nul termination
        MOV     r0, r1
        MOV     r1, r3
        MOV     r3, r2
        BL      FindLeafname
        MOV     r1, r3
        BL      strcpy

        ; Shuffle the regs to leave leaf^ in r2 and dir^ in r1
        MOV     r2, r1
        MOV     r1, r0

20
        ; Set up filenames and offset to leaf
        SUB     r2, r2, r1
 [ not_16bit_offsets
	STR	r2,[r5,#df_fileptr]
 |
        STRB    r2, [r5, #df_fileptr+2]
        MOV     r2, r2, LSR #8
        STRB    r2, [r5, #df_fileptr+3]
 ]
        STR     r1, [r4, #d_filenames]

        ; Suss the applic
        BL      SussPlingApplic_ReturnError

        STRVS   r0, [sp]
        EXIT

        END
