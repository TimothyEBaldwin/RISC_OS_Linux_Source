; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; In    r1 -> dirty dirviewer block^
;       r4 -> dirty dirviewer block

; Out   r4 -> new dirviewer block, linked into ViewerList

RecacheDir Entry "r0-r3, r5-r11"

; Set up state needed to read same dir back

 [ version < 139
        LDR     r14, [r4, #d_filesystem]
        STR     r14, setplusfilesystem
 ]
        InvSmiWidth     r4

        LDR     r1, [r4, #d_dirnamestore] ; Read same dir back
        LDRB    r3, [r4, #d_viewmode]
        BL      CacheDir                ; r7 -> dirblock in RMA block
        BVS     %FT99

        LDR     r14, [r7, #d_nfiles]    ; If number of files differs
        LDR     r0,  [r4, #d_nfiles]    ; then we need new window
        TEQ     r0, r14
 [ debugrecache
 BEQ %FT00
 DLINE "Dissimilar number of files -> replace"
00
 ]
        BNE     %FT50

        LDR     r14, [r7, #d_size]      ; If size differs then need new window
        LDR     r0,  [r4, #d_size]
        TEQ     r0, r14
 [ debugrecache
 BEQ %FT00
 DLINE "Dissimilar sizes -> replace"
00
 ]
        BNE     %FT50

; Compare the two blocks (just file blocks and strings)

10      SUB     r0, r0, #1
        LDRB    r1, [r4, r0]
        LDRB    r2, [r7, r0]
        CMP     r1, r2
        BNE     %FT50
        CMP     r0, #d_headersize
        BNE     %BT10

 [ debugrecache
 DLINE "dirviewer marked as dirty has not been updated; what a waste!"
 ]
        MOV     r14, #0                 ; Dir not dirty
        STRB    r14, [r4, #d_unsure]

40      MOV     r2, r7                  ; Free the new copy of the dir
        BL      SFreeArea

99      STRVS   r0, [sp]
        EXIT


50 ; Need a new dirviewer; can now keep same whandle !!!

 [ debugrecache
 DLINE "Need to replace dirviewer"
 ]
        LDR     r14, sel_dir            ; Are we replacing the selection dir?
        TEQ     r14, r4
        STREQ   r7, sel_dir
 [ debugrecache
 BNE %FT00
 DLINE "Replaced sel_dir"
00
 ]

; Copy over any info not created or updated by CacheDir

        LDR     r14, [r4, #d_link]      ; Install where the one we replaced was
        STR     r14, [r7, #d_link]
        LDR     r14, [r4, #d_handle]
        STR     r14, [r7, #d_handle]
        LDR     r14, [r4, #d_dirnamestore]
        STR     r14, [r7, #d_dirnamestore]
        LDRB    r14, [r4, #d_nchildren]
        STRB    r14, [r7, #d_nchildren]

 [ centralwrap
        MOV     r14, #db_fpr_reshape    ; Ensure we get reshaped
 |
        MOV     r14, #0                 ; Ensure we get reshaped
 ]
        STRB    r14, [r7, #d_filesperrow]

 [ retainsel
        BL      CopySelectionStateAcross
 ]


        LDR     r1, [sp, #4*1]          ; r1in -> previous dirviewer in list
 [ debugrecache
 DREG r7, "Adding new dirviewer ",cc
 DREG r1, " to list at "
 ]
        STR     r7, [r1, #d_link]       ; Install where the one we replaced was

        MOV     r2, r4                  ; Deallocate old block
        BL      SFreeArea               ; Nowt much we can do if he complains

        MOV     r4, r7


; Fake an open request at the same position to get fully redrawn/resized
; having set filesperrow to 0 to ensure the SetExtent/ForceRedraw

        ADR     r1, userdata
        LDR     r0, [r4, #d_handle]
        STR     r0, [r1, #u_handle]
        SWI     XWimp_GetWindowInfo
 [ version >= 114
        BVS     %BT99

        ; r0 = display area height
        LDR     r0, [r1, #u_way0]
        LDR     r14, [r1, #u_way1]
        SUB     r0, r14, r0

        ; Don't mess with windows whose height is GE initwindowheight
        LDR     r14, initwindowheight
        CMP     r0, r14
        BGE     %FT60

        ; subtract from r0 work area height
        LDR     r14, [r1, #u_wax0 + w_wey0]
        ADD     r0, r0, r14
        LDR     r14, [r1, #u_wax0 + w_wey1]
        SUBS    r0, r0, r14

        ; if not equal then don't expand window vertically
        BNE     %FT60

        ; Expand window up to initwindowheight
        LDR     r0, [r1, #u_way1]
        LDR     r14, initwindowheight
        SUB     r0, r0, r14
        STR     r0, [r1, #u_way0]

        ; Don't zap the width of non full size windows
        LDR     r0, [r1, #u_wflags]
        TST     r0, #ws_toggled
        BEQ     %FT60

        ; Don't zap the width of full size windows wider than initwindowwidth
        LDR     r0, [r1, #u_wax0]
        LDR     r14, [r1, #u_wax1]
        SUB     r0, r14, r0
        LDR     r14, initwindowwidth
        CMP     r0, r14
        BGE     %FT60

        ; Zap the width
      [ centralwrap
        MOV     r0, #db_fpr_invalid
      |
        MOV     r0, #0
      ]
        STRB    r0, [r4, #d_filesperrow]
        LDR     r0, [r1, #u_wax0]
        LDR     r14, initwindowwidth
        ADD     r0, r0, r14
        STR     r0, [r1, #u_wax1]
60
 ]
 [ debugrecache
 LDR r14, [r1, #24+4]
 DREG r14, "handle to open behind "
 ]
        BL      event_open_window

        LDRVC   r0, [r4, #d_handle]
        MOVVC   r1, #-bignum            ; All of this window needs repainting
        MOVVC   r2, #-bignum
        MOVVC   r3, #bignum
        MOVVC   r4, #bignum
        SWIVC   XWimp_ForceRedraw
        BVS     %BT99

        EXIT
        END
