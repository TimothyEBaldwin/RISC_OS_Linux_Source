; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
 [ ShowOpenDirs

;
; Redraw parent directory icon, if any.
;
; In  R11 -> directory just opened
;     R0  =  set state to opened/closed
;
; Previous versions of this code used "ms_writeable_dirname" as a temporary buffer,
; setting it to the null string before returning. This meant that the "New directory"
; writable menu item was cleared if a directory display was updated while the
; item was being typed into (yuch!!). The code has now been changed so that the
; file name is built into "stackbot". This might be canonicalised INTO THE SAME SPACE
; by Xstrcmp_excludingspaces but this is safe as OS_FSControl 37 can cope with
; the source and destination being in the same place.
;
RedrawParentIcon
        STMFD   R13!,{R0-R6,R14}

        wsaddr  R1,stackbot
        ADD     R4,R1,#1
        MOV     R2,R11
10      LDRB    r14, [r2], #1           ; Copy from *r2++
        CMP     r14, #delete
        CMPNE   r14, #space-1
        MOVLS   r14, #0                 ; Terminate dst with 0
        STRB    r14, [r1], #1           ; Copy to *r1++
        BHI     %BT10

        SUB     R3,R1,R4
        SUB     R1,R4,#1
        BL      TryLeafStrip
        LDMEQFD R13!,{R0-R6,PC}        ; no leafname stripable

        ADD     R6,R1,R3               ; -> final '.' char
        MOV     R14,#0
        STRB    R14,[R6],#1            ; terminate dir prefix, R6 -> leaf too

; scan for parent viewer r1

        ADR     r4, ViewerList - d_link
01      LDR     r4, [r4, #d_link]
        CMP     r4, #Nowt
        BNE     %FT05
        CLRV
        LDMFD   R13!,{R0-R6,PC}         ; not existing dirviewer
05
        LDR     r14, [r4, #d_handle]
        CMP     r14, #-1                ; don't deal in icons ???????
        BEQ     %BT01
        LDR     r2, [r4, #d_dirnamestore] ; r2 -> dirname
        BL      Xstrcmp_excludingspaces  ; May compare 'net:&','net:& (a500)'
        BNE     %BT01                   ; [no match, try next dirviewer]

; found parent's viewer (r4), now scan for leaf (R6)

        LDR     r3, [r4, #d_nfiles]
        CMP     r3, #0
        LDMEQFD R13!,{R0-R6,PC}         ; not found

        ADD     r5, r4, #d_headersize   ; r5 -> first file
        MOV     R1,R6                   ; r1 -> leafname
        LDR     R6, [R4,#d_filenames]   ; used in loop below
02      LDR     R2,[R5,#df_fileptr]
 [ not_16bit_offsets
        ADD     R2,R6,R2
 |
        ADD     R2,R6,R2,LSR #16
 ]
        BL      strcmp
        BEQ     %FT03                   ; [found file]
        ADD     r5, r5, #df_size
        SUBS    r3, r3, #1
        BNE     %BT02

        CLRV
        LDMFD   R13!,{R0-R6,PC}

; found icon, force a redraw on it

03      LDRB    R14,[R5,#df_state]      ; set new state, opened/closed
        TST     R0,#refresh_open
        BEQ     %FT97                   ; closed it
        TST     R14,#dfs_opened
        ORREQ   R14,R14,#dfs_opened
        BEQ     %FT98

        CLRV
        LDMFD   R13!,{R0-R6,PC}         ; already that

97
        CLRV
        TST     R14,#dfs_opened
        BICNE   R14,R14,#dfs_opened

        LDMEQFD R13!,{R0-R6,PC}         ; already that
98
        STRB    R14,[R5,#df_state]
        BL      GetFileBox              ; get relative to work area origin
        LDR     R0,[R4,#d_handle]
        ADR     r14, filebox1
        LDMIA   r14, {R1-R4}
        SWI     XWimp_ForceRedraw       ; refresh icon!
99
        CLRV
        LDMFD   R13!,{R0-R6,PC}


;
; If file is a directory and it is open, then set its dfs_open bit.
;
; In  R3 =  file count (descending)
;     R4 -> directory viewer
;     R5 -> current file (must be a directory!)
;
; Previous versions of this code used "ms_writeable_dirname" as a temporary buffer,
; setting it to the null string before returning. This meant that the "New directory"
; writable menu item was cleared if a directory display was updated while the
; item was being typed into (yuch!!). The code has now been changed so that the
; file name is built into "stackbot". This might be canonicalised INTO THE SAME SPACE
; by Xstrcmp_excludingspaces but this is safe as OS_FSControl 37 can cope with
; the source and destination being in the same place.
;
SetOpenState
        STMFD   R13!,{R1-R2,R4,R14}

        wsaddr  R1,stackbot
        LDR     R2,[R4,#d_dirname]
        BL      strcpy_excludingspaces  ; copy dirname
        LDR     R14, [R4,#d_filenames]
        LDR     R2,[R5,#df_fileptr]
 [ not_16bit_offsets
        ADD     R2,R14,R2
 |
        ADD     R2,R14,R2,LSR #16
 ]
        BL      AppendLeafnameToDirname ; append leaf

; R1 -> buffer containing full dirname of this directory, scan for it

        ADR     r4, ViewerList - d_link
01      LDR     r4, [r4, #d_link]
        CMP     r4, #Nowt
        LDMEQFD R13!,{R1-R2,R4,PC}      ; not existing dirviewer
02
        LDR     r14, [r4, #d_handle]
        CMP     r14, #-1                ; don't deal in icons ???????
        BEQ     %BT01
        LDR     r2, [r4, #d_dirnamestore] ; r2 -> dirname
        BL      Xstrcmp_excludingspaces  ; May compare 'net:&','net:& (a500)'
        BNE     %BT01                   ; [no match, try next dirviewer]

; found it, so set its dfs_open flag

        LDRB    R14,[R5,#df_state]
        ORR     R14,R14,#dfs_opened
        STRB    R14,[R5,#df_state]

        LDMFD   R13!,{R1-R2,R4,PC}

 ]

Xstrcmp_excludingspaces
; In:   r1->string 1 (might be canonicalised into stackbot)
;       r2->string 2 (might be canonicalised into CanonicalSpace)
; Out:  EQ or NE
;
        Push    "R0-R5,lr"
        [ {TRUE}
01
        LDRB    R14,[R1],#1
        CMP     R14,#32
        LDRLT   R1,[SP,#4]
        BLT     %FT03
        TEQ     R14,#"."
        LDREQ   R1,[sp,#4]
        BEQ     %FT03
        TEQ     R14,#":"
        BNE     %BT01

; found ':' so assume already canonicalised, now try R2...
02
        LDRB    R14,[R2],#1
        CMP     R14,#32
        LDRLT   R2,[SP,#4]
        BLT     %FT03
        TEQ     R14,#"."
        LDREQ   R2,[sp,#4]
        BEQ     %FT03
        TEQ     R14,#":"
        BNE     %BT02

        LDMIA   sp,{R0-R2}
        B       %FT05
03
        wsaddr  R2,stackbot
        MOV     R0,#37
        MOV     R3,#0
        MOV     R4,#0
        MOV     R5,#256
        SWI     XOS_FSControl
; assume disc has gone away if we get an error
        BVC     %FT04
        CLRV
        TEQ     R0,#0                                   ; sets NE
        B       %FT09
04
        LDR     R1,[SP,#8]
        wsaddr  R2,CanonicalSpace
        MOV     R5,#256
        SWI     XOS_FSControl
        wsaddr  R1,stackbot
        BVC     %FT05
        CLRV
        TEQ     R0,#0                                   ; sets NE
        B       %FT09

05
        ]
        BL      strcmp_excludingspaces
09
        Pull    "R0-R5,PC"

        END
