; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;


; s.Drag
;
; Comes here when a drag ends.



; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; drag_end
;
; Called upon receipt of User_Drag_Box event meaning a drag has ended.
;
; In: r1 -> Wimp_Poll block
;
; Out: registers may be corrupted. Returning to wimp_poll.

drag_end
        Push    "LR"

        Debug   sa,"Drag end"

        BL	Restore_Focus

        SWI     XDragASprite_Stop       ; Clear any DragASprite operations

        LDR     r0,DragType
        TEQ     r0,#DragType_SelectDrag
        BEQ     SelectDrag_end
        TEQ     r0,#DragType_AdjustDrag
        BEQ     AdjustDrag_end

        TEQ     r0,#DragType_SelectDragIcon
        BEQ     SelectDragIcon_end
        TEQ     r0,#DragType_AdjustDragIcon
        BEQ     SelectDragIcon_end

        TEQ     r0,#DragType_Iconized
        BEQ     SelectDragIcon_end
        ;BEQ     Iconized_DragEnd

        TEQ     r0,#DragType_Save
        BEQ     Save_DragEnd

        Pull    "PC"


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; AdjustDrag_end
;
; Called when user has finished dragging a selection with the right mouse button.

AdjustDrag_end                          ; Invert state of icons
        MOV     r3,#is_selected
        MOV     r4,#0
        B       %FT11


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; SelectDrag_end
;
; Called when the user has finished dragging out a selection, this function will attempt
; to select any icons within the bounding box.

SelectDrag_end

        MOV     r3,#is_selected         ; Select icons.
        MOV     r4,#is_selected

11
        MOV     r0,#DragType_NoDrag
        STR     r0,DragType
        ADR     r1,dataarea             ; make sure that x0<x1 and y0<y1
        LDMIA   r1,{r6,r7,r8,r9}        ; x0,y0,x1,y1
        CMP     r6,r8
        MOVGT   r14,r6
        MOVGT   r6,r8
        MOVGT   r8,r14
        CMP     r7,r9
        MOVGT   r14,r7
        MOVGT   r7,r9
        MOVGT   r9,r14                  ; We now know x0<x1 and y0<y1

        LDR     r0,backdrop_handle
        STR     r0,[r1]
        LDR     r5,Icon_list            ; Load first icon in list
01
        CMP     r5,#0                   ; Have we reached end of list?
        ;Pull    "PC",EQ                 ; If so, then exit
        BEQ     %FT20

        LDR     r0,[r5,#ic_icon]
        STR     r0,[r1,#4]
        SWI     XWimp_GetIconState      ; Get the state of this icon
        Pull    "PC",VS

        ; Check Icon's bounds
        LDR     r14,[r1,#8]             ; Icon's x0
        CMP     r14,r8
        BGT     %FT10                   ; > x1 !
        LDR     r14,[r1,#16]            ; Icon's x1
        CMP     r14,r6
        BLT     %FT10                   ; < x0 !
        LDR     r14,[r1,#12]            ; Icon's y0
        CMP     r14,r9
        BGT     %FT10                   ; > y1 !
        LDR     r14,[r1,#20]            ; Icon's y1
        CMP     r14,r7
        BLT     %FT10                   ; < y0 !

        LDR     r0,Pinboard_Selected
        LDR     r14,[r1,#24]            ; Get icon flags
        TST     r14,#is_selected
        ADDEQ   r0,r0,#1                ; If it wasn't selected it will be
        CMPNE   r4,#is_selected         ; If it was is this adjust ?
        SUBNE   r0,r0,#1                ; If it is, it won't be.
        STR     r0,Pinboard_Selected
        Debug   pi,"Selected icons = ",r0

        STR     r3,[r1,#8]              ; Now set new state
        STR     r4,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

10
        LDR     r5,[r5,#ic_next]        ; Load next icon in list
        B       %BT01

20
        BL      Iconized_SelectDrag_end
        Pull    "pc"


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Iconized_SelectDrag_end
;
; Called by SelectDrag_end, which is called after a selection has been dragged out on the
; pinboard, it attempts to select any iconized window icons within the bounding box.
;
; In: r6, r7, r8, r9 = Bounding box
;     r3, r4 = Clear and EOR states for SetIconState
;     r1 -> dataarea, of which r1+4 = window handle
;
; Out:

Iconized_SelectDrag_end Entry
        MOV     r0,#DragType_NoDrag
        STR     r0,DragType

        LDR     r5, iconized_ptr
01
        CMP     r5, #0
        EXIT    EQ

        LDR     r0, [r5, #w_icon_handle]
        STR     r0, [r1, #4]
        SWI     XWimp_GetIconState
        EXIT    VS

        ; Check Icon's bounds
        LDR     r14, [r1, #8]           ; Icon's x0
        CMP     r14, r8
        BGT     %FT10                   ; > x1 !
        LDR     r14, [r1, #16]          ; Icon's x1
        CMP     r14, r6
        BLT     %FT10                   ; < x0 !
        LDR     r14, [r1, #12]          ; Icon's y0
        CMP     r14, r9
        BGT     %FT10                   ; > y1 !
        LDR     r14, [r1, #20]          ; Icon's y1
        CMP     r14, r7
        BLT     %FT10                   ; < y0 !

        LDR     r0, Windows_Selected
        LDR     r14, [r1,#24]           ; Get icon flags
        TST     r14, #is_selected
        ADDEQ   r0, r0, #1              ; If it wasn't selected it will be
        CMPNE   r4, #is_selected        ; If it was is this adjust ?
        SUBNE   r0, r0, #1              ; If it is, it won't be.
        STR     r0, Windows_Selected

        STR     r3,[r1,#8]              ; Now set new state
        STR     r4,[r1,#12]
        SWI     XWimp_SetIconState
        EXIT    VS

10
        LDR     r5,[r5,#w_next_ptr]     ; Load next icon in list
        B       %BT01


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; SelectDragIcon_end
;
; Called after the user has finished dragging a selection of icons.
;
; In: r1 -> Wimp_Poll block
;
; Out: registers may be corrupted - returning to Poll loop.

SelectDragIcon_end
        MOV     r0,#DragType_NoDrag
        STR     r0,DragType

        ; Find out where it ended
        ADR     r1, PointerInfo
        SWI     XWimp_GetPointerInfo
        LDR     r2, [r1, #12]          ; Window
        LDR     r3, [r1, #16]          ; Icon

        CMP     r2, #-2
        CMPLE   r3, #-1
        BEQ     Run_Selection         ; Dragged to empty part of icon bar, run it !

        LDR     r0, backdrop_handle
        CMP     r2, r0                 ; If destination wasn't the backdrop, call
        BNE     DragIconsToApp         ; appropriate function

        LDR     r5, drag_window
        CMP     r5, r2                 ; If source wasn't the backdrop
        BNE     DragIconsToApp

        ; Work out relative movement
        LDMIA   r1, {r5, r6}           ; get x and y of destination
        ADR     r0, drag_start
        LDMIA   r0, {r8, r9}           ; load x and y co-ordinate of start of drag
        SUB     r8,r5,r8
        SUB     r9,r6,r9               ; Get relative movement

        ; If grid lock is on, make the relative movement a multiple of the spacing
        LDR     r0, Pinboard_options
        TST     r0, #PinboardOption_Grid
        BLNE    lock_movement_to_grid

; Now we have to loop through all icons and windows, test if they are selected
; and if so, move them.
; At this point, r8 = relative x movement
;                r9 = relative y movement
01
        ADR     r6, dataarea
        LDR     r0, backdrop_handle
        STR     r0, [r6]
        LDR     r7, Icon_list
        MOV     r11, #0                ; Says we're on Icon_list
02
        CMP     r7, #0                 ; Is this the last icon?
        BEQ     %FT10

        ; Check icon is on backdrop
        LDR     r0, backdrop_handle
        LDR     r1, [r7, #ic_window]
        CMP     r1, r0
        BNE     %FT04

        ; Get icon's state
        LDR     r0, backdrop_handle
        STR     r0, [r6]
        LDR     r0, [r7, #ic_icon]
        STR     r0, [r6, #4]
        MOV     r1, r6
        SWI     XWimp_GetIconState
        Pull    "pc", VS

        ; Check if icon is selected
        LDR     r0, [r6, #24]
        TST     r0, #is_selected
        BEQ     %FT04

        ; Force redraw of old icon position
        Push    "r0-r4"
        ADD     r0, r1, #8
        LDMIA   r0, {r1-r4}
        LDR     r0, backdrop_handle
        SWI     XWimp_ForceRedraw
        Pull    "r0-r4"

        ; Work out new position
        ADD     r0, r6, #8
        LDMIA   r0, {r2-r5}
        ADD     r2, r2, r8
        ADD     r3, r3, r9
        ADD     r4, r4, r8
        ADD     r5, r5, r9

        ; Move (resize) icon
        LDR     r0, backdrop_handle
        LDR     r1, [r7, #ic_icon]
        SWI     XWimp_ResizeIcon
        Pull    "pc", VS

        ; Update x and y in linked list
        CMP     r11, #0
        STREQ   r2, [r7, #ic_x]
        STREQ   r3, [r7, #ic_y]

        ; Change it's state so it is no longer selected
        ; (this will also redraw icon at new position)
        MOV     r0, #is_selected
        STR     r0, [r6, #8]
        MOV     r0, #0
        STR     r0, [r6, #12]
        MOV     r1, r6
        SWI     XWimp_SetIconState
        Pull    "pc", VS

        ; Update number of icons selected
        CMP     r11, #0
        LDREQ   r0, Pinboard_Selected
        LDRNE   r0, Windows_Selected
        SUB     r0, r0, #1
        CMP     r11, #0
        STREQ   r0, Pinboard_Selected
        STRNE   r0, Windows_Selected
04
        LDR     r7, [r7, #ic_next]
        B       %BT02

10 ; finishedfiles
        CMP     r11, #1
        BEQ     %FT20
        LDR     r7, iconized_ptr
        MOV     r11, #1
        B       %BT02

20 ; finishedall
        Pull    "pc"


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; lock_movement_to_grid
;
; In: r0 - pinboard_options
;     r5, r6 - x co-ordinate of destination
;     drag_icon - icon where drag started
;
; Out: r8, r9 - relative movement x and y
;      All other regs preserved.

lock_movement_to_grid Entry "r0-r7, r10-r11"

        MOV     r4, r0

   ; Work out the x co-ord of the nearest grid position
        TST     r4, #PinboardOption_TidyToCornerLR
        BNE     %FT02
        ; If tidy to left
        MOV     r10, #grid_x_spacing
        DivRem  r1, r5, r10, r2, norem
        MUL     r8, r1, r10                  ; x = (mouse_x DIV x_spacing) * x_spacing
        B       %FT04
02      ; If tidy to right
        MOV     r10, #grid_x_spacing
        LDR     r11, Screen_x1
        ADD     r1, r11, #grid_x_spacing
        SUB     r2, r1, r5
        DivRem  r1, r2, r10, r0, norem
        MUL     r2, r1, r10
        SUB     r8, r11, r2

04 ; Work out the y co-ord of the nearest grid position
        TST     r4, #PinboardOption_TidyToCornerTB
        BNE     %FT06
        ; If tidy to top
        MOV     r10, #grid_y_spacing
        LDR     r11, Screen_y1
        ADD     r1, r11, #grid_y_spacing
        SUB     r2, r1, r6
        DivRem  r1, r2, r10, r0, norem
        MUL     r2, r1, r10
        SUB     r9, r11, r2
        B       %FT08
06      ; If tidy to bottom
        LDR     r0, icon_bar_height
        SUB     r6, r6, r0                   ; grid starts above icon bar, so subtract
        CMP     r6, #0                       ; it's height.
        MOVLT   r6, #0
        MOV     r10, #grid_y_spacing
        DivRem  r1, r6, r10, r2, norem
        MLA     r9, r1, r10, r0              ; y = (mouse_y DIV y_spacing) * y_spacing) + icon_bar_height

08 ; Get the state of the icon where the drag was started
        ADR     r1, dataarea
        LDR     r0, drag_window
        STR     r0, [r1]
        LDR     r0, drag_icon
        STR     r0, [r1, #4]
        SWI     XWimp_GetIconState
        EXIT    VS

        LDR     r5, [r1, #8]                 ; min x
        LDR     r6, [r1, #16]                ; max x
        MOV     r7, #grid_x_spacing
        SUB     r0, r6, r5                   ; icon width
        SUBS    r0, r7, r0
        MOVMI   r0, #0
        ADD     r8, r8, r0, LSR #1           ; x = x + 0.5(grid_x_spacing - icon_width)
        SUB     r8, r8, r5

        LDR     r5, [r1, #12]                ; min y
        LDR     r6, [r1, #20]                ; max y
        MOV     r7, #grid_y_spacing
        SUB     r0, r6, r5                   ; icon height
        SUBS    r0, r7, r0
        MOVMI   r0, #0
        ADD     r9, r9, r0, LSR #1           ; y = y + 0.5(grid_y_spacing - icon_height)
        SUB     r9, r9, r5

        EXIT


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; DragIconsToApp
;
; Result of drag end was that icons ended up on another application. Send FilerSelection
; and then DataLoad messages.
;
; In: r2 = window handle of destination
;     r3 = icon handle of destination
;
; Out: Registers may be corrupted

DragIconsToApp

        Push    "r2-r3"                  ; Push window and icon handle

         ; Adjust bbox returned by Wimp_Poll to same size as original (for DragASprite).
        ADR     r1,DragBBOX
        LDMIA   r1,{r6,r7,r8,r9}
        SUB     r2,r8,r6
        SUB     r3,r9,r7
        ADR     r1,dataarea
        LDMIA   r1,{r6,r7,r8,r9}
        ADD     r8,r6,r2
        SUB     r7,r9,r3

        ; Setup message block
        ADR     r1,dataarea
        ADD     r14,r1,#ms_data
        STMIA   r14!,{r6,r7,r8,r9}      ; Drag BBOX
        ADR     r0,DragBBOX
        LDMIA   r0,{r6,r7,r8,r9}        ; Get original box.

        MOV     r4,#grid_x_spacing
        STR     r4,[r14],#4             ; width of each selected item
        MOV     r5,#grid_y_spacing
        ADD     r7,r7,r5
        SUB     r7,r7,#1
        STR     r5,[r14],#4             ; height of each selected item

        MOV     r0,#0
        STR     r0,[r14],#4             ; Display mode is always 0 (large icons sort by name)

        ; Left column = bbox_left / grid_x_spacing
        DivRem  r10,r6,r4,r0,norem
        STR     r10,[r14],#4

        ; Top row = (screen_y - bbox_top) / grid_y_spacing
        LDR     r10,Screen_y1
        SUB     r9,r10,r9
        DivRem  r10,r9,r5,r0,norem
        STR     r10,[r14],#4

        ; Right column = bbox_right / grid_x_spacing
        DivRem  r10,r8,r4,r0,norem
        STR     r10,[r14],#4

        ; Bottom row = (screen_y - bbox_bottom) / grid_y_spacing
        LDR     r10,Screen_y1
        SUB     r7,r10,r7
        DivRem  r10,r7,r5,r0,norem
        STR     r10,[r14],#4

        MOV     r0,#0
        STR     r0,[r1,#ms_myref]       ; my ref
        STR     r0,[r1,#ms_yourref]     ; your ref
        MOV     r0,#ms_data+11*4
        STR     r0,[r1,#ms_size]        ; message size
        LDR     r0,=Message_FilerSelection
        STR     r0,[r1,#ms_action]      ; message action
        MOV     r0,#17
        Pull    "r2-r3"                 ; Pull window and icon handle
        SWI     XWimp_SendMessage
        Pull    "pc", VS

        ; Check if it was the filer
        LDR     r14,Filer_taskid
        TEQ     r2,r14
        BEQ     SendToFiler

        ; This isn't the filer, simply send data load messages for each icon.
        ; And deselect it.
        LDR     r10, Icon_list
01
        CMP     r10, #0                  ; last icon?
        Pull    "pc", EQ

        LDR     r3, DragWindow
        ADR     r1, dataarea
        LDR     r0, [r10, #ic_window]
        CMP     r0, r3                   ; Check if icon is in window drag originated from
        BNE     %FT10

        STR     r0, [r1]
        LDR     r0, [r10, #ic_icon]
        STR     r0, [r1, #4]
        SWI     XWimp_GetIconState       ; Get icon state
        Pull    "pc", VS

        LDR     r0, [r1, #24]
        TST     r0, #selected
        BEQ     %FT10                    ; Not selected

        MOV     r0, #0
        STR     r0, [r1, #8]
        CMP     r3,#0
        MOVGT   r0,#is_selected
        MOVLT   r0,#selected
        STR     r0, [r1, #12]
        SWI     XWimp_SetIconState       ; Deselect icon

        CMP     r3, #0
        LDRGT   r0, Pinboard_Selected
        LDRLT   r0, TinyDirs_Selected
        SUB     r0, r0, #1
        STRGT   r0, Pinboard_Selected
        STRLT   r0, TinyDirs_Selected   ; Update number of icons selected.

        ADR     r0, PointerInfo
        LDMIA   r0, {r4, r5, r6, r7, r8} ; x,y,b,w,i
        ADD     r0, r1, #ms_data
        STMIA   r0!, {r7, r8}            ; w,i
        STMIA   r0!, {r4, r5}
        MOV     r4, #1
        STR     r4, [r0], #4            ; estimated size.
        LDR     r4, [r10, #ic_filetype]
        STR     r4, [r0], #4
        MOV     r1, r0
        ADD     r0, r10, #ic_path
        BL      Copy_r0r1
        ADD     r1, r1, #3+1            ; Include null.
        BIC     r1, r1, #3
        MOV     r0, #-1
        STR     r0, [r1], #4
        STR     r0, [r1], #4            ; Column, Row

        MOV     r0, r1
        ADR     r1, dataarea
        SUB     r0, r0, r1
        STR     r0, [r1, #ms_size]
        MOV     r0, #Message_DataLoad
        STR     r0, [r1, #ms_action]
        MOV     r0, #0
        STR     r0, [r1, #ms_myref]
        STR     r0, [r1, #ms_yourref]
        MOV     r0, #18
        LDR     r2, [r1, #ms_data]
        LDR     r3, [r1, #ms_data+4]
        SWI     XWimp_SendMessage
        Pull    "pc", VS
10
        LDR     r10, [r10, #ic_next]
        B       %BT01


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; SendToFiler
;
; The destination of a drag was a Filer window.
; We want to save files to the filer, send
; DataSave to find out the directory.

SendToFiler     ROUT

        Debug   pi,"Send to filer"

        ADR     r1,dataarea
        ADR     r14,PointerInfo
        MOV     r0,#0
        STR     r0,[r1,#ms_myref]
        STR     r0,[r1,#ms_yourref]
        MOV     r0,#Message_DataSave
        STR     r0,[r1,#ms_action]
        LDR     r0,[r14,#12]             ; Window
        STR     r0,[r1,#ms_data]
        LDR     r0,[r14,#16]             ; Icon
        STR     r0,[r1,#ms_data+4]
        LDR     r0,[r14]                 ; X
        STR     r0,[r1,#ms_data+8]
        LDR     r0,[r14,#4]              ; Y
        STR     r0,[r1,#ms_data+12]
        MOV     r0,#1                    ; Estimated size.
        STR     r0,[r1,#ms_data+16]
        MOV     r0,#0                    ; File type
        STR     r0,[r1,#ms_data+20]
        MOV     r0,#65
        STR     r0,[r1,#ms_data+44]      ; Proposed leaf name ("A").

        MOV     r0,#18
        SWI     XWimp_SendMessage

        Pull    "PC"


; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; Run_Selection
;
; The destination of a drag of icons was a bare piece of icon bar. So, we Filer_Run
; any selected files.

Run_Selection   ROUT

        LDR     r10,Icon_list
01
        CMP     r10,#0
        Pull    "PC",EQ

        LDR     r14,[r10,#ic_window]
        LDR     r3,DragWindow
        TEQ     r14,r3
        BNE     %FT10

        ADR     r1,dataarea
        LDR     r0,[r10,#ic_icon]
        STR     r3,[r1]
        STR     r0,[r1,#4]
        SWI     XWimp_GetIconState
        Pull    "PC",VS

        LDR     r0,[r1,#24]
        TST     r0,#selected
        BEQ     %FT10                   ; Not selected

        MOV     r0,#0
        STR     r0,[r1,#8]
        CMP     r3,#0
        MOVGT   r0,#is_selected
        MOVLT   r0,#selected
        STR     r0,[r1,#12]
        SWI     XWimp_SetIconState      ; Deselect

        CMP     r3,#0
        LDRGE   r0,Pinboard_Selected
        LDRLE   r0,TinyDirs_Selected
        SUB     r0,r0,#1
        STRGE   r0,Pinboard_Selected
        STRLE   r0,TinyDirs_Selected
        Debug   pi,"Selected icons = ",r0

        ADR     r1,dataarea
        ADRL    r0,FilerRunCom
        BL      Copy_r0r1
        ADD     r0,r10,#ic_path
        BL      Copy_r0r1
        ADR     r0,dataarea
        SWI     XWimp_StartTask
        Pull    "PC",VS

10
        LDR     r10,[r10,#ic_next]
        B       %BT01

        LTORG

        LNK     Menu.s
