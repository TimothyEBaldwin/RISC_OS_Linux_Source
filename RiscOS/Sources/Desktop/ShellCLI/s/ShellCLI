; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > Sources.ShellCLI

;;----------------------------------------------------------------------------
;; *********************
;; ***  CHANGE LIST  ***
;; *********************
;;
;; 20-May-88    0.13    Change list added
;; 20-May-88            OS_ReadDefaultHandler used for boring handlers
;; 20-May-88            Keyboard setup left up to Wimp
;; 20-May-88    0.14    Wimp_CommandWindow used so *cat :foo works properly
;; 23-May-88    0.15    VDU 4 before printing error messages / prompt
;;              0.15    ==== Risc OS 2.00 Release version ====
;;
;; 26-May-88    0.16    Use Service_WimpCloseDown to object to wimp tasks
;; 20-Oct-88            'Make' procedures reorganised, Version file included
;; 20-Oct-88            Conditional assembly for versions 0.15 and 0.16
;;  5-May-89    0.17    Improve error recovery on startup
;;  4-Sep-89    0.18    Improve error handling in readline routine
;; 11-Sep-89    0.19    Only claim application memory while in startup sequence
;;  9-Dec-89    0.20    Check for R0 = -64*&100000 in Service_Memory
;;  3-Sep-90    0.21    Changed New Line printing to VDU 31 sequence to
;;                      move the cursor to bottom of screen.
;;  3-Apr-91    0.22    Internationalised
;; 10-Dec-91    0.25    Shortened message tokens.
;; 20-Apr-00    0.31    Made 32-bit compatible.
;;----------------------------------------------------------------------------

        AREA      |ShellCLI$Code|, CODE, READONLY, PIC

Module_BaseAddr

        GET       Hdr:ListOpts
        GET       Hdr:Macros
        GET       Hdr:System
        GET       Hdr:ModHand
        GET       Hdr:Services
        GET       Hdr:Wimp
        GET       Hdr:EnvNumbers
        GET       Hdr:FSNumbers
        GET       Hdr:NewErrors
        GET       Hdr:MsgTrans
        GET       Hdr:OsBytes
        GET       Hdr:Proc
        GET       Hdr:Variables
        GET       hdr.Shell
        GET       VersionASM

 [ :LNOT: :DEF: international_help
        GBLL      international_help
international_help SETL {TRUE}          ; Default to RISC OS 3.60+ internationalisation
 ]

MessageBlockSize  *  16
IsAnybodyThere    *  -64*&100000        ; magic number used by the Wimp
                                        ; for checking whether an application wants its memory

;=========================================================================
;
; Provides a module to support Shelling a CLI interface from a application.
;
; The module responds to the command Wimp_StartTask(ShellCLI).
;
; Shell will only work under the Wimp, and relies on the appropriate values
; for eg. function and cursor keys being set up by the Wimp beforehand.
;
; It issues the "*" prompt and processes commands, setting up the default
; environment for each of them, with a new quit handler.  It continues to do
; this until a null line is input. This terminates the Shell session.
;
; On exit the Shell will close the command window quietly, and call the
; original exit handler.
;
; The keyboard and screen are reset by the Wimp on exit.
;
;=========================================================================

;=========================================================================
;
; The RM header
;
; As laid down by the founding fathers
;
;=========================================================================

ModuleHeader
        DCD     StartCode                       - Module_BaseAddr
        DCD     InitialisationCode              - Module_BaseAddr
        DCD     FinalisationCode                - Module_BaseAddr
        DCD     ServiceCall                     - Module_BaseAddr
        DCD     TitleString                     - Module_BaseAddr
        DCD     HelpString                      - Module_BaseAddr
        DCD     HelpAndCommandKeywordTable      - Module_BaseAddr
        DCD     ShellSWI_Base
        DCD     SWIHandlerCode                  - Module_BaseAddr
        DCD     SWIDecodingTable                - Module_BaseAddr
        DCD     0                 ; SWIDecodingCode
 [ international_help
        DCD     message_filename                - Module_BaseAddr
 |
        DCD     0
 ]
 [ :LNOT: No32bitCode
        DCD     ModuleFlags                     - Module_BaseAddr
 ]


;=========================================================================


HelpString
;=========
        DCB     "ShellCLI", 9, "$Module_HelpVersion", 0
        ALIGN
 [ :LNOT: No32bitCode
ModuleFlags
        DCD     ModuleFlag_32bit
 ]

InitialisationCode ROUT
;=================
; Must preserve r7-r11 and r13
        MOV     r6, lr
        ; Try to open the messages file
        SUB     sp, sp, #MessageBlockSize + 256
        MOV     r0, sp
        ADR     r1, message_filename
        MOV     r2, #0
        SWI     XMessageTrans_OpenFile
        BVS     %FT10
        ; Look-up the greeting string
        MOV     r0, sp
        ADR     r1, greeting_tok
        ADD     r2, sp, #MessageBlockSize
        MOV     r3, #256
        SWI     XMessageTrans_Lookup
        ; Keep it in a variable
        ADRVC   r0, greeting_var
        MOVVC   r1, r2
        MOVVC   r2, r3
        MOVVC   r3, #0
        MOVVC   r4, #VarType_String
        SWIVC   XOS_SetVarVal
        MOV     r0, sp
        SWI     XMessageTrans_CloseFile
10
        ; Free the buffer and return
        ADD     sp, sp, #MessageBlockSize + 256
        CLRV                            ; don't fail to start if no greeting
        MOV     pc, r6

greeting_tok
        DCB     "Greeting", 0
        ALIGN
greeting_var
        DCB     "CLI$$Greeting", 0
        ALIGN


FinalisationCode ROUT
;===============
; Must preserve r7-r11 and r13
; Only die if we have no instances of Shell's to support
        MOV     r6, lr
        LDR     r12, [r12]
        CMP     r12, #0                 ; V clear
        ADRNE   r12, ErrorBlock_ShellRemoval
        BNE     CopyError
        MOV     pc, r6

        LTORG

        MakeInternatErrorBlock ShellRemoval,,"SActive"

;
ServiceTable
;===========
; Ursula format service table
        ASSERT Service_Memory  <  Service_Reset
        ASSERT Service_Reset   <  Service_WimpCloseDown
        DCD    0                                ; flags
        DCD    UrsServiceCall - Module_BaseAddr
        DCD    Service_Memory
        DCD    Service_Reset
        DCD    Service_WimpCloseDown
        DCD    0                                ; terminator
        DCD    ServiceTable - Module_BaseAddr   ; table anchor
;
ServiceCall ROUT
        MOV     r0, r0                          ; magic instruction for Ursula format
        TEQ     r1, #Service_Memory
        TEQNE   r1, #Service_Reset
        TEQNE   r1, #Service_WimpCloseDown
        MOVNE   PC, LR
;
UrsServiceCall
        CMP     r1, #Service_Reset
        BEQ     svc_reset
        CMP     r1, #Service_WimpCloseDown
        BEQ     svc_wimpclos
;else fall through to svc_memory (must be Service_Memory)
svc_memory
        Push    "lr"
        addr    r14, ModuleHeader
        CMP     r14, r2
        Pull    "pc", NE
        TEQ     pc,pc
        MOVNE   r14, #IsAnybodyThere
        MOVEQ   r14,#1:SHL:31
        CMP     r0, r14               ; only claim if this is the Wimp!
        MOVEQ   r1, #0
        Pull    "pc"

svc_reset
; on reset, free all memory
        STMDB   sp!, {r0-r2, r9, lr}
        LDR     r9, [r12]      ; get workspace
        CMP     r9, #0         ; if zero, then no shell
        LDMEQIA sp!, {r0-r2, r9, pc}
        MOV     r0, #0
        STR     r0, [r12]
        MOV     r0, #ModHandReason_Free
        LDR     r2, [r9, #:INDEX:MyStack]
        SWI     XOS_Module
        MOV     r0, #ModHandReason_Free
        MOV     r2, r9
        SWI     XOS_Module
        LDMIA   sp!, {r0-r2, r9, pc}

svc_wimpclos
        CMP     R0,#0           ; not allowed to object if this was Wimp_Close
        LDRNE   r12, [r12]
        CMPNE   r12, #0         ; not active unless we have a block,
        MOVEQ   pc, lr          ; and this is our task handle
        LDR     r12, mytaskhandle
        CMP     r2, r12                      ; if we are currently active,
        ADREQ   r0, ErrorBlock_WimpCantKill  ; tell him to get stuffed!
        BEQ     CopyError
        MOV     pc, lr

        MakeInternatErrorBlock WimpCantKill,,WActive


TitleString
HelpAndCommandKeywordTable
;=========================
; We do all the real work in the *command *ShellCLI.
; The expected client is a Wimp application doing a Wimp_StartTask("ShellCLI")
;

 [ international_help
        Command "ShellCLI", 0, 0, International_Help
        DCB     0              ; terminate my *cmd list

ShellCLI_Syntax
        DCB     "SSCLSCL", 0
ShellCLI_Help
        DCB     "HSCLSCL", 0
 |
        Command "ShellCLI", 0, 0, 0
        DCB     0              ; terminate my *cmd list

ShellCLI_Syntax
        DCB     "Syntax: *ShellCLI", 0
ShellCLI_Help
        DCB     "ShellCLI - used by a Wimp Program to create a CLI shell", 0
 ]
        ALIGN

ShellCLI_Code ROUT
        LDR     r12, [r12]
        CMP     r12, #0
        ADRNE   r0, ErrorBlock_ShellCreation
        BNE     CopyError

        MOV     r0, #ModHandReason_Enter
        ADR     r1, TitleString
        MOV     r2, #0
        SWI     OS_Module
        SWI     OS_Exit

taskid  DCB     "TASK"

        MakeInternatErrorBlock ShellCreation,,NoSpawn

;========================================================================
;
StartCode ROUT
;
; The real work goes on here. The Wimp application will have done a
; Wimp_StartTask(*ShellCLI) to get here. Our aim is to create a CLI shell
; which allows the user to execute CLI commands, then exit back to
; the Wimp.
;
; The Wimp will restore the state it requires when we OS_Exit to it, so
; we don't need to maintain the state, and indeed we can't as applications
; may change for you.
;
; On entry we reset the pallette, turn off the mouse etc.
; We also reset the keyboard to do function key expansion, cursor editing,
; and enable Escape.
;
;========================================================================

        SWI     Shell_Create         ; try and create a Shell instance

        LDR     r12, [r12]           ; get workspace pointer of new instance
        LDR     sp, MyStack
        ADD     sp, sp, #MyStackSize

        MOV     R0,#200
        LDR     R1,taskid
        ADR     R2,TitleString
        SWI     Wimp_Initialise      ; goes to our error handler
        STR     R1,mytaskhandle

        BL      ScreenSetup
        MOVVC   r0, #1               ; tell the Wimp that we're using VDU 4
        SWIVC   XWimp_CommandWindow
        BVS     SetupError

        MOV     R0,#CAOPointer       ; pretend we're not the current application
        ADR     R1,Do_ExitHandler
        MOV     R2,#0
        MOV     R3,#0
        SWI     XOS_ChangeEnvironment
        BVS     SetupError

        ADR     R0, greeting_var
        ADR     R1, ErrorBufferMsg
        MOV     R2, #?ErrorBufferMsg - 1
        MOV     R3, #0
        MOV     R4, #VarType_Expanded
        SWI     XOS_ReadVarVal
        SWIVC   XOS_WriteI+4         ; just in case
        ADRVC   R0, ErrorBufferMsg
        MOVVC   R1, #0
        STRVCB  R1, [R0, R2]         ; terminator for pretty print
        SWIVC   XOS_PrettyPrint
        SWIVC   XOS_NewLine

CmdPrompt
;========
;
; The inner loop of the Cli.
;
; LOOP
;   WriteF("Shell*")
;   command := READLINE();
;   IF command = " " THEN EXIT; END;
;   OSCLI(command);
; END;
;
        BL      DisplayPrompt
        ADRVC   r0, CommandBuffer    ; Read a command line
        MOVVC   r1, #?CommandBuffer
        MOVVC   r2, #" "
        MOVVC   r3, #255
        SWIVC   XOS_ReadLine
        BVS     internalerror        ; give it a chance to recover
        BCS     EscapeAck            ; CS indicated an Escape key was pressed
        CMP     r1, #0               ; zero length string terminates CLI
        BEQ     CmdPromptExit

        ADR     r0, CommandBuffer
        SWI     XOS_CLI
        BVC     CmdPrompt            ; no error, repeat

internalerror
        ADD     r0, r0, #4           ; error - skip number
        SWI     XOS_Write0           ; and print the error message
        SWIVC   XOS_NewLine
        BVC     CmdPrompt            ; drop through if error

SetupError
        Push    "R0"
        MOV     r0, #-1              ; ensure error is reported in a box
        SWI     XWimp_CommandWindow  ; ignore errors from this
        Pull    "R0"
        MOV     R1, #6               ; cancel box highlighted
        ADRL    R2, TitleString
        SWI     Wimp_ReportError

CmdPromptExit ROUT
        SWI     Shell_Destroy        ; lose block (we are now inactive)
        SWI     Wimp_CloseDown
        MOV     r0, #-1              ; close down quietly
        SWI     Wimp_CommandWindow   ; (screen will be redrawn because of this)
        SWI     OS_Exit              ; calls Wimp_CloseDown automatically

esc_msg DCB     "Escape", 0
        ALIGN

EscapeAck ROUT
;========
;
; Also called if Readline terminates with carry set, indication escape.
;
        MOV     r0, #OsByte_AcknowledgeEscape
        SWI     XOS_Byte               ; Acknowledge the Escape
        SWI     XOS_NewLine
        MOVVC   r0, #0
        ADRVC   r1, esc_msg
        MOVVC   r2, #0
        MOVVC   r4, #0
        MOVVC   r5, #0
        MOVVC   r6, #0
        MOVVC   r7, #0
        SWIVC   XMessageTrans_Lookup
        MOVVC   r0, r2
        MOVVC   r1, r3
        SWIVC   XOS_WriteN
        SWIVC   XOS_NewLine
        B       CmdPrompt


Do_ExitHandler ROUT
;=============
;
; The application has died, so return to the Cli prompt
; but I'm paranoid - reset all my handlers first
;
        LDR     sp, MyStack
        ADD     sp, sp, #MyStackSize

        BL      SetHandlers

        B       CmdPrompt


ScreenSetup ROUT
;==========
        Push    r14
        MOV     r0, #OsByte_RW_VDUQueue
        MOV     r1, #0
        MOV     r2, #0
        SWI     XOS_Byte               ; flush VDU Q.
        ADR     r0, %f1
        ADR     r1, %f2
        SUB     r1, r1, r0
        SWI     XOS_WriteN             ; Write out a VDU String to reset things
        B       %f3
1
        =       6                      ; Enable VDU
        =       4                      ; VDU 4 mode
        =       23,1,1,0,0,0,0,0,0,0   ; enable text cursor
        =       26                     ; text and graphics windows to default
        =       30                     ; text cursor homed
        =       29, 0, 0, 0, 0         ; graphics origin to (0,0)
        =       25, 4, 0, 0, 0, 0      ; graphics cursor to (0,0)
        =       15                     ; Paged mode off
;        =       17, 7                  ; text foreground to colour 7
        =       18, 0, 7               ; graphics foreground to colour 7
;        =       17, 128                ; text background to colour 0
        =       18, 0, 128             ; graphics background to colour 0
2
        ALIGN
3
        MOV     r0, #7
        SWI     XWimp_TextColour       ; set text foreground
        MOV     r0, #&80
        SWI     XWimp_TextColour       ; set text background
        MOV     r0, #OsByte_VDUVariable
        MOV     r1, #9                 ; read bottom row
        SWI     XOS_Byte
        MOV     r0,#31                 ;goto
        SWI     XOS_WriteC
        MOV     r0,#0                  ;Left column
        SWI     XOS_WriteC
        MOV     r0,r1                  ;Bottom row.
        SWI     XOS_WriteC
        MOV     r0,#10                 ;One extra line.
        SWI     XOS_WriteC

        MOV     r0, #OsByte_SelectPointer
        MOV     r1, #0                 ; turn it off
        SWI     XOS_Byte

        Pull    pc


DisplayPrompt ROUT
;============
        Push    "lr"
        SWI     XOS_WriteI+4            ; just in case
        Pull    "pc",VS
        ADR     r0, CliDPrompt
        ADR     r1, CommandBuffer
        MOV     r2, #?CommandBuffer
        MOV     r3, #0
        MOV     r4, #VarType_Expanded
        SWI     XOS_ReadVarVal
        ADRVS   r0, DefaultPrompt
        MOVVS   r2, #1
        MOVVC   r0, r1
        ADDS    r1, r2, #0              ; R1 := R2, clear V, set Z if no chars to write
        SWIGT   XOS_WriteN              ; >0, not negative
        Pull    "pc"

CliDPrompt
        DCB     "CLI$$Prompt", 0
DefaultPrompt
        DCB     "*"
message_filename
        DCB     "Resources:$.Resources.ShellCLI.Messages", 0
        ALIGN

Do_ErrorHandler ROUT
;==============
;
; An error has occurred, print error msg and exit
;
        MOV     r12, r0                ; get Workspace pointer in correct reg
        LDR     r13, MyStack
        ADD     r13, r13, #MyStackSize

        SWI     XOS_WriteI+4           ; just in case
        ADR     r0, ErrorBufferMsg
        SWI     XOS_Write0             ; print the error message
        SWI     XOS_NewLine
        SWI     XOS_Exit               ; exit to ShellCLI's exit handler


;===================================================================
;
; The SWI handlers.
; We have Create and Destroy, used to create and destroy instances
; of the Shell.
;
;===================================================================
SWIDecodingTable
;===============
        DCB     "Shell", 0
        DCB     "Create", 0
        DCB     "Destroy", 0
        DCB     0
        ALIGN


SWIHandlerCode ROUT
;==================
        CMP     r11, #1
        BEQ     SWI_Destroy
        BCC     SWI_Create

        ADR     r0, ErrorNumber_ModuleBadSWI
        B       CopyError

        MakeInternatErrorBlock ModuleBadSWI,,BadSWI

;===================================================================
;
SWI_Create ROUT
;
; We create an instance of a shell. We allocate some workspace in the
; RMA and link it in at the head of the instance chain. We reset the
; handlers, saving the old values for restoration by destroy.
;
;===================================================================
; Can corrupt r10-r12

        Push    "r0-r9, lr"

        MOV     r0, #ModHandReason_Claim
        LDR     r3, =(LastWord - FirstWord)
        SWI     XOS_Module
        BVS     SWI_CreateExit

        MOV     r9, r2
        MOV     r3, #MyStackSize
        SWI     XOS_Module
        BVS     SWI_CreateError

        STR     r9, [r12]
        MOV     r12, r9                 ; point to our workspace
        STR     r2, MyStack

        BL      SetHandlers             ; on exit r1-r3 = old Exit handler pars
                                        ; assume no errors from this
        ADRVC   r0, OldExitHandler
        STMVCIA r0,{r1-r3}

SWI_CreateExit
        STRVS   r0, [sp]
        Pull    "r0-r9, pc"

SWI_CreateError
        STR     r0, [sp]

        MOV     r0, #ModHandReason_Free
        MOV     r2, r9
        SWI     XOS_Module

        SETV                            ; return with error
        Pull    "r0-r9, pc"

;
; set up default environment handlers (ensure none left in yukky state)
;  0 in table ==> leave alone (MemoryLimit,ApplicationSpaceSize)
;  1 in table ==> use default handler
; >1 in table ==> use values in table
;

maxenv  *       MaxEnvNumber   ; this keeps changing!

SetHandlers ROUT
        Push    "R1-R6,LR"              ; R1-R3 pushed later
;
        ADR     R4,defaulthandlers      ; R4 --> data
        MOV     R6,#maxenv
01
        LDR     R1,[R4],#4              ; if zero, it's not an address offset
        TEQ     R1,#0
        RSBEQ   R0,R6,#maxenv           ; R1=0 ==> use default handler
        SWIEQ   XOS_ReadDefaultHandler
        Pull    "R1-R6,PC",VS
        ADDNE   R1,R1,R4                ; R1 --> code address
        MOVNE   R2,R12
        ADRNEL  R3,ErrorBuffer
02
        RSB     R0,R6,#maxenv
        SWI     XOS_ChangeEnvironment
        TEQ     R6,#maxenv-ExitHandler
        STMEQIA sp,{R1-R3}              ; stash old exit handler
        SUBS    R6,R6,#1
        BNE     %BT01
;
        Pull    "R1-R6,PC"

;
; Default settings to pass to SWI XOS_ChangeEnvironment
;       R1 --> code address / buffer pointer
;       R2  =  returned in R12 (if R1 is an address) - maybe
;       R3 --> block           (if R1 is an address)
;

defaulthandlers
        DCD     0                       ; MemoryLimit

        DCD     0                       ; UndefinedHandler
        DCD     0                       ; PrefetchAbortHandler
        DCD     0                       ; DataAbortHandler
        DCD     0                       ; AddressExceptionHandler
        DCD     0                       ; OtherExceptionHandler

        DCD     Do_ErrorHandler-.-4
        DCD     0                       ; CallBackHandler
        DCD     0                       ; BreakPointHandler

        DCD     0                       ; EscapeHandler
        DCD     0                       ; EventHandler
        DCD     Do_ExitHandler-.-4
        DCD     0                       ; UnusedSWIHandler

        DCD     0                       ; ExceptionDumpArea
        DCD     0                       ; ApplicationSpaceSize
        DCD     0                       ; CAO pointer

        DCD     0                       ; UpCall handler

        ASSERT  (.-defaulthandlers)/4 = maxenv


;===================================================================
;
SWI_Destroy ROUT
;
; We dismantle this instance of shell, restoring all the old handlers
; and freeing the workspace in the RMA, delinking us from the
; instance chain.
;
;===================================================================
; can corrupt only r10-r12

        Push    "r0-r9, lr"

        MOV     r10, r12             ; save private word
        LDR     r12, [r12]           ; get workspace
        CMP     r12, #0              ; if zero, then no shell
        BEQ     SWI_Destroy_Exit     ; note: Z is set, V is clear

        MOV     r0, #0
        STR     r0, [r10]            ; unchain ourselves - no going back now !

        MOV     r0, #ExitHandler
        ADR     r1, OldExitHandler   ; restore old Exit handler
        LDMIA   r1, {r1-r3}
        SWI     XOS_ChangeEnvironment

        MOV     r0, #ModHandReason_Free
        LDR     r2, MyStack
        SWI     XOS_Module           ; ignore errors

        MOV     r0, #ModHandReason_Free
        MOV     r2, r12
        SWI     XOS_Module           ; ignore errors
        SUBS    R0, R0, R0           ; clears V, sets Z for next instruction

SWI_Destroy_Exit
        TEQ     pc, pc               ; in 32-bit mode?
        Pull    "r0-r9, pc",NE,^     ; no - 26-bit exit
        Pull    "r0-r9, pc"          ; yes - 32-bit exit (PSR trashed)

;===================================================================


CopyError
        STMDB   sp!, {r0-r7, lr}
        SUB     sp, sp, #MessageBlockSize
        MOV     r0, sp
        ADR     r1, message_filename
        MOV     r2, #0
        SWI     XMessageTrans_OpenFile
        BVS     CopyError1
        LDR     r0, [sp, #MessageBlockSize]
        MOV     r1, sp
        ADRL    r4, TitleString
        MOV     r5, #0
        MOV     r6, #0
        MOV     r7, #0
        SWI     XMessageTrans_ErrorLookup
        STR     r0, [sp, #MessageBlockSize]
        MOV     r0, sp
        SWI     XMessageTrans_CloseFile
        SETV
CopyError1
        ADD     sp, sp, #MessageBlockSize

        LDMIA   sp!, {r0-r7, lr}
        TEQ     pc, pc               ; in 32-bit mode?  (set Z if so)
        ORRNES  pc, lr, #V_bit       ; 26-bit exit - NZC preserved
        MOV     pc, lr               ; 32-bit exit - NZC trashed


;===================================================================
;
; Instance Data - kept in RMA Heap blocks
;
;===================================================================
        ^ 0, r12
FirstWord              Word 0  ; unused - a placeholder for allocating space

OldExitHandler         Word 3
mytaskhandle           Word
ErrorBuffer            Word 0
ErrorBufferPC          Word
ErrorBufferNumber      Word
ErrorBufferMsg         Word 64
CommandBuffer          Word 256  ;mjs changed for Ursula (was 64)
CallbackBuffer         Word 16
MyStack                Word

MyStackSize            * &400

LastWord               Word 0  ; unused - a placeholder for allocating space

        END
