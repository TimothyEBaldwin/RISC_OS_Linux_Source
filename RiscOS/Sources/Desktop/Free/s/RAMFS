; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;-------------------------------------------------------------------------
;
; RamFS_entry
;
; Entry point for RamFS discs.
RamFS_entry

        TEQ     r0,#FreeReason_GetName
        BEQ     RamFS_GetName
        TEQ     r0,#FreeReason_GetSpace
        BEQ     RamFS_GetSpace
        TEQ     r0,#FreeReason_ComparePath
        BEQ     RamFS_ComparePath


        Pull    "PC"

;-----------------------------------------------------------------------
; RamFS_GetName
;
; Put the name of the drive specified in r3 into the buffer at r2
;
RamFS_GetName

        Push    "r1-r3"

        Debug   xx,"Get name"

        MOV     r0,r3
        ADR     r1,disc_desc
        SWI     XRamFS_DescribeDisc
        Pull    "r1-r3,PC",VS

        ADD     r4,r1,#22
        MOV     r0,r2
        MOV     r3,#10
        BL      copy_r0r4r3_space     ; Copy name to buffer

        MOV     r0,#11             ; Name is 10 chars + terminator.

        Pull    "r1-r3,PC"
;-----------------------------------------------------------------------
; RamFS_GetSpace
;
; Put the free space on the drive specified in r3 into the buffer at r2
;
RamFS_GetSpace

        Push    "r1-r3"

        Debug   xx,"Get space"


        MOV     r0,r3
        ADR     r1,disc_desc
        SWI     XRamFS_DescribeDisc
        Pull    "r1-r3,PC",VS

        LDR     r0,[r1,#16]        ; Store disc size in buffer.
        STR     r0,[r2]

        MOV     r0,r3
        SWI     XRamFS_FreeSpace
        Pull    "r1-r3,PC",VS

        STR     r0,[r2,#4]         ; Free space
        LDR     r1,[r2]
        SUB     r1,r1,r0
        STR     r1,[r2,#8]         ; used space

        Pull    "r1-r3,PC"

;-------------------------------------------------------------------------
; RamFS_ComparePath
;
;

RamFS_ComparePath

        Debug  xx,"RAMFS compare"

        CMP    r0,r0

        Pull   "PC"

        LNK     s.SCSI
