; Copyright 2013 Timothy Baldwin
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

        IMPORT  doMoveMemory
        IMPORT  do_IMB
        IMPORT  MakeTempFile
        IMPORT  do_AMBControlSWI
        IMPORT  do_DynArea_AplSpaceLimit
        IMPORT  do_DynArea_AlterFile
        IMPORT  __HAL_hwcap

        EXPORT  __rt_udiv
        EXPORT  CheckAppSpace
        EXPORT  ErrorBlock_BadParameters
        EXPORT  ErrorBlock_OverlappingAreas
        EXPORT  VariformInternal

        PVOpsImports

; Unimplemnted paged memory SWIs
ReadMemMapEntries_Code  *       NoSuchSWI
SetMemMapEntries_Code   *       NoSuchSWI
FindMemMapEntries_Code  *       NoSuchSWI
MMUControlSWI           *       NoSuchSWI

; More stubs
RISCOS_AccessPhysicalAddress
        MOV     a1, a2
RISCOS_ReleasePhysicalAddress
RISCOS_MapInIO
RISCOS_MapInIO64
RISCOS_InitARM
RISCOS_AddRAM
RISCOS_LogToPhys
LookForHALCacheController
NullOp
        MOV     pc, lr

DAbPreVeneer
        SUB     sp, sp, #4
        Push    "r0-r3, lr"
        MOV     r1, lr
        BL      __PVirt_GetVCPU
        LDR     r2, [r0, #vcpu_spsr_abt]
        LDR     r3, [r0, #vcpu_DFAR]
        LDR     r0, =ZeroPage + Abort32_dumparea
        STMIA   r0, {r1, r2, r3}
        LDR     r1, [r0, #DAbHan - Abort32_dumparea]
        STR     r1, [sp, #5 * 4]
        Pull    "r0-r3, lr"
        Pull    "pc"


        IMPORT  Init_MapInRAM_Linux
Init_MapIn_Linux
        MOV     a1, a2
        MOV     a2, a3
        MOV     a3, a4
Init_MapInRAM_Clear_Linux
Init_MapInRAM_DMA_Linux
        B       Init_MapInRAM_Linux


SyncCodeAreasSWI
        Push    "lr"
        BL      SyncCodeAreas
        Pull    "lr"                    ; no error return possible
        B       SLVK

SyncCodeAreas
        TST     r0, #1                  ; range variant of SWI?
        BEQ     SyncCodeAreasFull

SyncCodeAreasRange
        Push    "a1-a4, ip, lr"
        ADD     a3, a3, #4
        MOV     lr, pc
        LDR     pc, =do_IMB
        Pull    "a1-a4, ip, pc"

do_IMB_Range
        Push    "a1-a4, ip, lr"
        MOV     lr, pc
        LDR     pc, =do_IMB
        Pull    "a1-a4, ip, pc"

SyncCodeAreasFull
do_IMB_Full
        Push    "a1-a4, ip, lr"
        MOV     a2, #0
        MOV     a3, #0xFFFFFFFC
        MOV     lr, pc
        LDR     pc, =do_IMB
        Pull    "a1-a4, ip, pc"

UND_Op
        DebugTX "Bad ARMOp"
        UND

ARM_Analyse
        ; Load hwcap that was saved from the ELF auxiliary vector by the HAL.
        LDR     a2, =__HAL_hwcap
        LDR     a2, [a2]

        LDR     a1, =CPUFlag_SynchroniseCodeAreas | CPUFlag_BaseRestored | CPUFlag_SplitCache | CPUFlag_32bitOS | CPUFlag_NoDCacheDisable |CPUFlag_NoWBDrain | CPUFlag_HiProcVecs | CPUFlag_ExtraReasonCodesFixed | CPUFlag_NoPhysicalPages
        ; FIXME Cache?

        TST     a2, #ix_HWCAP_26BIT
        ORR     a1, a1, #CPUFlag_No26bitMode ; FIXME Support 26 bit?

        TST     a2, #ix_HWCAP_THUMB
        ORRNE   a1, a1, #CPUFlag_Thumb

        TST     a2, #ix_HWCAP_EDSP
        ORRNE   a1, a1, #CPUFlag_DSP

        TST     a2, #ix_HWCAP_SWP
        ORREQ   a1, a1, #CPUFlag_NoSWP

        ; Check store of PC
30      STR     pc, [sp, #-4]!
        ADR     a3, %BT30 + 8
        LDR     a4, [sp], #4
        TEQ     a4, a3
        ORREQ   a1, a1, #CPUFlag_StorePCplus8

        ; THe HAL will skip the next instruction if in an undefined instruction is encountered.

        UMULL   a3, a4, a1, a2
        ORR     a1, a1, #CPUFlag_LongMul

        LDREX   a3, [sp]
        ORR     a1, a1, #CPUFlag_LoadStoreEx

        LDREXB  a3, [sp]
        ORR     a1, a1, #CPUFlag_LoadStoreClearExSizes

        LDR     a2, =ZeroPage
        STR     a1, [a2, #ProcessorFlags]

        ADR     a1, NullOp
        STR     a1, [a2, #Proc_Cache_CleanAll]
        STR     a1, [a2, #Proc_Cache_CleanInvalidateAll]
        STR     a1, [a2, #Proc_Cache_InvalidateAll]
        STR     a1, [a2, #Proc_Cache_RangeThreshold]
        STR     a1, [a2, #Proc_TLB_InvalidateAll]
        STR     a1, [a2, #Proc_TLB_InvalidateEntry]
        STR     a1, [a2, #Proc_DSB_Write]

        ADR     a1, UND_Op
        STR     a1, [a2, #MMU_PCBTrans]
        STR     a1, [a2, #Proc_MMU_ChangingEntries]
        STR     a1, [a2, #Proc_MMU_ChangingEntry]
        STR     a1, [a2, #Proc_MMU_ChangingUncachedEntries]
        STR     a1, [a2, #Proc_MMU_ChangingUncachedEntry]
        STR     a1, [a2, #Proc_MMU_ChangingUncached]
        STR     a1, [a2, #Proc_MMU_Changing]

        ADR     a1, do_IMB_Range
        STR     a1, [a2, #Proc_IMB_Range]

        ADR     a1, do_IMB_Full
        STR     a1, [a2, #Proc_IMB_Full]

        MOV     a1, #0x1000
        STR     a1, [a2, #Page_Size]

        MOV     a1, #0x40000
        STR     a1, [a2, #RAMLIMIT]

        MOV     a1, #0x1000000
        STR     a1, [a2, #VideoSizeFlags]

        ADR     a1, PPLAccess
        STR     a1, [a2, #MMU_PPLAccess]

        ; Point HAL towards to processor the vectors
        MOV     ip, lr
        ADD     a3, a2, #ProcVec_UndInst
        BL      __PVirt_GetVCPU
        STR     a3, [a1, #vcpu_vectors]
        MOV     lr, ip
        myBX    lr

PPLAccess            ; EL1EL0
                     ; RWXRWX
        GenPPLAccess 2_111111
        GenPPLAccess 2_111111
        GenPPLAccess 2_111111
        GenPPLAccess 2_101101
        GenPPLAccess 2_110110
        GenPPLAccess 2_110110
        DCD     -1


AMBControl_Init
        Push    "lr"
        LDR     r3, =MaxInterrupts * 4
        BL      ClaimSysHeapNode
        LDR     r0, =ZeroPage
        STR     r2, [r0, #AMBControl_ws]
        Pull    "pc"

ARM_PrintProcessorType
        MOV     pc, lr

DynArea_PMP_PhysOp
        DebugTX "DynArea_PMP_PhysOp"
        UND
DynArea_PMP_LogOp
        DebugTX "DynArea_PMP_LogOp"
        UND
DynArea_PMP_Resize
        DebugTX "DynArea_PMP_Resize"
        UND
DynArea_PMP_GetPages
        DebugTX "DynArea_PMP_GetPages"
        UND

DynArea_AlterFile
        Push    "r2, r6-r8, lr"
        BL      QCheckAreaNumber        ; check area exists
        BCC     UnknownDyn              ; [it doesn't]
        MOV     r3, r10
        BL      do_DynArea_AlterFile
        Pull    "r2, r6-r8, pc"

DynArea_AplSpaceLimit
        Push    "r2, r3, lr"
        BL      do_DynArea_AplSpaceLimit
        CMP     r0, #DAReason_AplSpaceLimit
        Pull    "r2, r3, pc", EQ
 [ International
        BL      TranslateError
 |
        SETV
 ]
        Pull    "r2, r3, pc"


PageShifts
        =       12, 13, 0, 14           ; 1 2 3 4
        =       0,  0,  0, 15           ; 5 6 7 8

        GBLL    DebugAborts
DebugAborts SETL {FALSE}

AMBControlSWI
        Push    "r4, ip, lr"
        BL      do_AMBControlSWI
        Pull    "r4, ip, lr"
        BIC     lr, lr, #V_bit
        B       SLVK

LinuxReadTime
        Push    "r1, r3, r7, lr"
        SUB     sp, sp, #8
        MOV     r7, #__NR_gettimeofday
        MOV     r0, sp
        MOV     r1, #0
        BL      __PVirt_LinuxSyscall

        LDR     r3, =-776530087
        LDR     r0, [sp, #4]
        MOV     r2, #0
        UMULL   r1, r0, r3, r0
        MOV     r3, #100
        LDR     r7, [sp], #8
        MOV     r0, r0, lsr #13
        UMLAL   r0, r2, r3, r7
        LDR     r1, =&6E996A00
        ADDS    r0, r0, r1
        ADC     r2, r2, #&33
        Pull    "r1, r3, r7, pc"

        END
