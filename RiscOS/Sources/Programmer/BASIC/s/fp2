; Copyright 2001 Pace Micro Technology plc
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;> fp2

 [ FPOINT=0
COSOP   STR     R14,[SP,#-4]!
        B       COS1
 ]
COS     STR     R14,[SP,#-4]!
        BL      FACTOR
        BLPL    FLOATQ
 [ FPOINT=0
COS1    CMP     FACCX,#&98
        BCS     FRNGQQ
        MOV     R10,#0
        MOV     FSIGN,#0
        FPUSH                          ;stack |X|
        ADR     TYPE,HALFPI
        BL      FADD
        BL      FRANGE
        BLMI    FCLR
        BL      INTRND
        TST     FACC,#1
        EORNE   R10,R10,#&80000000
        BL      IFLT
        MOV     FWSIGN,#&80000000
        MOV     FWACC,#&80000000
        MOV     FWACCX,#&80
        BL      FADDW
        B       SINCOMMON
 ELIF FPOINT=1
        COSD    FACC,FACC
        MOVS    TYPE,#TFP
        LDR     PC,[SP],#4
 ELIF FPOINT=2
        FPAop   COSD
        MOVS    TYPE,#TFP
        LDR     PC,[SP],#4
 |
        ! 1, "Unknown FPOINT setting"
 ]
 [ FPOINT=0
SINOP   STR     R14,[SP,#-4]!
        B       SIN1
 ]
SIN     ROUT
        STR     R14,[SP,#-4]!
        BL      FACTOR
        BLPL    FLOATQ
 [ FPOINT=0
SIN1    CMP     FACCX,#&98
        BCS     FRNGQQ
        MOV     R10,FSIGN
        MOV     FSIGN,#0
        FPUSH                          ;stack |X|
        BL      FRANGE
        BLMI    FCLR
        BL      INTRND
        TST     FACC,#1
        EORNE   R10,R10,#&80000000
        TEQ     FACC,#0
        BEQ     %20
        BL      IFLT
;
; STMFD SP!,{FACC,FSIGN,FACCX}
; ADR TYPE,HPIHI
; BL FMUL
; ADD TYPE,SP,#3*4+4 ;input value
; BL FADD
; FSTA TYPE
; LDMFD SP!,{FACC,FSIGN,FACCX}
; ADR TYPE,HPILO
; BL FMUL
; ADD TYPE,SP,#4 ;input value
; BL FADD
;
; Here follows DJS's disgusting code to multiply FACC by pi
;  = (2^0) + (2^-1) + (2^-4) + (2^-7) + (2^-11) - (2^-18) - (2^-20)
;    + (2^-22) + (2^-24) + (2^-26) + (2^-30) + (2^-34) + (2^-38)
;    - (2^-40) - (2^-42) + (2^-44) - (2^-49)
SINCOMMON
        MOV     FGRD,FACC,LSR #2
        ADD     FGRD,FGRD,FACC,LSR #6
        SUB     FGRD,FGRD,FACC,LSR #8
        SUB     FGRD,FGRD,FACC,LSR #10
        ADD     FGRD,FGRD,FACC,LSR #12
        SUB     FGRD,FGRD,FACC,LSR #17
        MOV     FWACC,#0
        ADDS    FGRD,FGRD,FACC,LSL #31
        ADC     FWACC,FWACC,FACC,LSR #1
        ADDS    FGRD,FGRD,FACC,LSL #28
        ADC     FWACC,FWACC,FACC,LSR #4
        ADDS    FGRD,FGRD,FACC,LSL #25
        ADC     FWACC,FWACC,FACC,LSR #7
        ADDS    FGRD,FGRD,FACC,LSL #21
        ADC     FWACC,FWACC,FACC,LSR #11
        SUBS    FGRD,FGRD,FACC,LSL #14
        SBC     FWACC,FWACC,FACC,LSR #18
        SUBS    FGRD,FGRD,FACC,LSL #12
        SBC     FWACC,FWACC,FACC,LSR #20
        ADDS    FGRD,FGRD,FACC,LSL #10
        ADC     FWACC,FWACC,FACC,LSR #22
        ADDS    FGRD,FGRD,FACC,LSL #8
        ADC     FWACC,FWACC,FACC,LSR #24
        ADDS    FGRD,FGRD,FACC,LSL #6
        ADC     FWACC,FWACC,FACC,LSR #26
        ADDS    FGRD,FGRD,FACC,LSL #2
        ADC     FWACC,FWACC,FACC,LSR #30
        ADDS    FACC,FACC,FWACC
        ADDCS   FACCX,FACCX,#1         ;Re-normalise
        MOVCS   FGRD,FGRD,LSR #1
        ORRCS   FGRD,FGRD,FACC,LSL #31
        MOVCS   FACC,FACC,RRX
        ADD     FACCX,FACCX,#1
;
; No overflow/underflow possible. Rounding not wanted because both
; parts of result are going to be subtracted from value on stack
;
        EOR     FSIGN,FSIGN,#&80000000 ;Negate to do subtraction (not 0-0 situation)
        MOV     TYPE,SP                ;input value
        STMFD   SP!,{FGRD,FSIGN,FACCX}
        BL      FADD
        FSTA    TYPE
        LDMFD   SP!,{FGRD,FSIGN,FACCX} ;Recover guard word
        SUB     FACCX,FACCX,#32
        MOVS    FACC,FGRD
        BEQ     %20
10      SUBPL   FACCX,FACCX,#1         ;Re-normalise - NB expected to be faster than
        MOVPLS  FACC,FACC,LSL #1       ;binary chop method in this environment
        BPL     %10
        TEQ     FACCX,#0
        BMI     %20
        MOV     TYPE,SP
        BL      FADD                   ;result of first addition
        FSTA    TYPE
        B       %30
20
        FLDA    SP                     ;input value
30      CMP     FACCX,#&71
        BCC     ENDSIN
        BL      FSQR
        FPUSH                          ;stack g, f
        ADR     TYPE,SINR5
        BL      FMUL
        ADR     TYPE,SINR4
        BL      FADD
        MOV     TYPE,SP
        BL      FMUL
        ADR     TYPE,SINR3
        BL      FADD
        MOV     TYPE,SP
        BL      FMUL
        ADR     TYPE,SINR2
        BL      FADD
        MOV     TYPE,SP
        BL      FMUL
        ADR     TYPE,SINR1
        BL      FADD
        MOV     TYPE,SP
        BL      FMUL
        ADD     SP,SP,#8
        MOV     TYPE,SP
        BL      FMUL
        BL      FADD
ENDSIN  ADD     SP,SP,#8
        EOR     FSIGN,FSIGN,R10
 ELIF FPOINT=1
        SIND    FACC,FACC
 ELIF FPOINT=2
        FPAop   SIND
 |
        ! 1, "Unknown FPOINT setting"
 ]
        MOVS    TYPE,#TFP
        LDR     PC,[SP],#4
 [ FPOINT=0
SINR5   DCD     &CD070C7F
        =       &67,0,0,&80            ;-.2386834640601E-7
SINR4   DCD     &B8B5D27E
        =       &6E,0,0,0              ;.2752397106775E-5
SINR3   DCD     &D00BD47F
        =       &74,0,0,&80            ;-.1984083282313E-3
SINR2   DCD     &888885BA
        =       &7A,0,0,0              ;.8333330720556E-2
SINR1   DCD     &AAAAAAA1
        =       &7E,0,0,&80            ;-.16666666608836
 ]
ATN     STR     R14,[SP,#-4]!
        BL      FACTOR
        BLPL    FLOATQ
 [ FPOINT=0
        STR     FSIGN,[SP,#-4]!            ;stack sign
        MOV     FSIGN,#0               ;force +ve
        MOV     R10,#0
        CMP     FACCX,#&81
        CMPEQ   FACC,#&80000001
        BCC     FATNA
        BL      FRECIP
        MOV     R10,#2
FATNA   CMP     FACCX,#&7F             ;test if greater than .2679491924
        CMPEQ   FACC,#&89000000
        CMPEQ   FACC,#&00300000
        CMPEQ   FACC,#&0000A200
        CMPEQ   FACC,#&000000F5
        BCC     FATNB
        FPUSH                          ;stack f
        ADR     TYPE,ATNCONST1
        BL      FMUL
        MOV     FWACC,#&80000000
        MOV     FWACCX,#&81
        MOV     FWSIGN,#&80000000
        BL      FADDW
        FPUSH                          ;stack top, f
        ADD     TYPE,SP,#8
        FLDA    TYPE
        ADR     TYPE,ATNCONST1
        BL      FADD
        MOV     TYPE,SP
        BL      FXDIV
        ADD     SP,SP,#16
        ADD     R10,R10,#1
FATNB   CMP     FACCX,#&71
        BCC     FATNC                  ;very small number
        FPUSH                          ;stack f, sign, link
        BL      FSQR
        FPUSH                          ;stack g, f etc.
        ADR     TYPE,ATNP1
        BL      FMUL
        ADR     TYPE,ATNP0
        BL      FADD
        MOV     TYPE,SP
        BL      FMUL
        FPUSH                          ;stack q*P(q), g, f etc.
        ADD     TYPE,SP,#8
        FLDA    TYPE
        ADR     TYPE,ATNQ1
        BL      FADD
        ADD     TYPE,SP,#8
        BL      FMUL
        ADR     TYPE,ATNQ0
        BL      FADD
        MOV     TYPE,SP
        BL      FXDIV
        ADD     TYPE,SP,#16
        BL      FMUL
        BL      FADD
        ADD     SP,SP,#24
FATNC   CMP     R10,#1
        EORHI   FSIGN,FSIGN,#&80000000
        BCC     FATND
        CMP     R10,#2
        ADRLT   TYPE,SIXTHPI
        ADREQ   TYPE,HALFPI
        ADRHI   TYPE,THIRDPI
        BL      FADD
FATND   LDMFD   SP!,{R4,R14}
        CMP     R4,#0
        EORNE   FSIGN,FSIGN,#&80000000
        TEQ     FACC,#0
        MOVEQ   FSIGN,#0
        MOVS    TYPE,#TFP
        MOV     PC,R14
 ELIF FPOINT=1
        ATND    FACC,FACC
        B       FSINSTK
 ELIF FPOINT=2
        FPAop   ATND
        B       FSINSTK
 |
        ! 1, "Unknown FPOINT setting"
 ]
 [ FPOINT=0
ATNCONST1
        DCD     &DDB3D743
        =       &81,0,0,0              ;SQR3=1.732050807568877
ATNP1   DCD     &B853ADF8
        =       &80,0,0,&80            ;-.720026848898
ATNP0   DCD     &B854A78A
        =       &81,0,0,&80            ;-1.44008344874
ATNQ1   DCD     &98123BF0
        =       &83,0,0,0              ;4.75222584599
ATNQ0   DCD     &8A3F7DBF
        =       &83,0,0,0              ;4.32025038919
; Here follows DJS's disgusting code to multiply FACC by 1/pi
;  = A2F9836E hex      (exponent 7F)
;  = (2^-1) + (2^-3) + (2^-6) - (2^-8) - (2^-13) + (2^-15) - (2^-17)
;    + (2^-22) - (2^-25) - (2^-28) - (2^-31)
FRANGE  MOV     FWGRD,FACC,LSL #30
        MOV     FWACC,FACC,LSR #2
        ADDS    FWGRD,FWGRD,FACC,LSL #27
        ADC     FWACC,FWACC,FACC,LSR #5
        SUBS    FWGRD,FWGRD,FACC,LSL #25
        SBC     FWACC,FWACC,FACC,LSR #7
        SUBS    FWGRD,FWGRD,FACC,LSL #20
        SBC     FWACC,FWACC,FACC,LSR #12
        ADDS    FWGRD,FWGRD,FACC,LSL #18
        ADC     FWACC,FWACC,FACC,LSR #14
        SUBS    FWGRD,FWGRD,FACC,LSL #16
        SBC     FWACC,FWACC,FACC,LSR #16
        ADDS    FWGRD,FWGRD,FACC,LSL #11
        ADC     FWACC,FWACC,FACC,LSR #21
        SUBS    FWGRD,FWGRD,FACC,LSL #8
        SBC     FWACC,FWACC,FACC,LSR #24
        SUBS    FWGRD,FWGRD,FACC,LSL #5
        SBC     FWACC,FWACC,FACC,LSR #27
        SUBS    FWGRD,FWGRD,FACC,LSL #2
        SBC     FWACC,FWACC,FACC,LSR #30
        ADDS    FACC,FACC,FWACC
        SUBCC   FACCX,FACCX,#1         ;Adjust exponent or re-normalise
        MOVCS   FWGRD,FWGRD,LSR #1
        ORRCS   FWGRD,FWGRD,FACC,LSL #31
        MOVCS   FACC,FACC,RRX
        CMP     FWGRD,#&80000000       ;Round correctly
        BICEQ   FACC,FACC,#1
        ADDCSS  FACC,FACC,#1
        MOVCS   FACC,FACC,RRX
        ADDCS   FACCX,FACCX,#1
        SUB     FACCX,FACCX,#1
        TEQ     FACCX,#0               ;Handle underflow (overflow is impossible)
        MOV     PC,R14
 ]
;convert fp acc to a string in stracc
;using format given by word in R4, returns with TYPE 1 after last char
;
; IEEE 754-2008 gives a handy reference for the maximum number of significant
; decimal digits that are required for displaying a binary floating point
; number at sufficient precision to allow it to be accurately read back in:
;   Pmin(bf) = 1+ceiling(p*log10(2))
; where p is the number of significant bits (i.e. p == number of bits used for
; storing mantissa + 1)
; This gives base values of 11 and 17, however BASIC64 has historically used 18
; so we'll stick with that for now.
 [ FPOINT=0
MAXDIGS                         *       11
 |
MAXDIGS                         *       18
 ]
;format 0: General format nnnn.nnn fixes maximum number of digits [or use 1]
;format 1: Exponent format n.nnEnn fixes number of digits
;format 2: Fixed format nnnnnn.nnn fixes number of digits after . [or use 1]
FMAT    RN      R5                     ;Format number
FDIGS   RN      R4                     ;(no harm the way currently written)
FPRTDX  RN      R7                     ;Decimal exponent
FPRTWN  RN      R6                     ;Work register
FCONFP  TEQ     R5,#0
        BNE     FCONHX
        MOV     FMAT,R4,LSR #16
        AND     FMAT,FMAT,#&7F
        TST     R4,#&800000
        MOV     FDIGS,R4,LSR #8
        AND     FDIGS,FDIGS,#255
        ORREQ   FDIGS,FDIGS,#&2e000000
        ORRNE   FDIGS,FDIGS,#&2c000000 ;put in . or ,
        CMP     FMAT,#3
        MOVCS   FMAT,#0                ;if unknown format use 0
        AND     R7,FDIGS,#255
        CMP     R7,#MAXDIGS+1
        BICCS   FDIGS,FDIGS,#255
        ORRCS   FDIGS,FDIGS,#MAXDIGS   ;if too high number of digs use 10/18
        TST     FDIGS,#255
        BNE     FCONA
        TEQ     FMAT,#2
        ORRNE   FDIGS,FDIGS,#MAXDIGS   ;if not in format 2 use maximum digits instead of 0
FCONA   STMFD   SP!,{FDIGS,R14}        ;May need fdigs again
        BL      FLOATY
     [ FPOINT=2
        FACCtoFPA
        STFP    F0,[SP,#-12]!
        LDMFD   SP!,{R0,R1,R2}
     ELIF FPOINT=1
        STFP    FACC,[SP,#-12]!
        LDMFD   SP!,{R0,R1,R2}
     ELIF FPOINT=0
        BL      FACCtoBCD
     |
        ! 1, "Unknown FPOINT setting"
     ]
        ADD     TYPE,ARGP,#STRACC
        TEQ     R0,#0
        MOVMI   R6,#"-"
        STRMIB  R6,[TYPE],#1

        ; Get the exponent into FPRTDX
        AND     FPRTDX,R0,#&F:SHL:20
        AND     LR,R0,#&F:SHL:16
        ADD     FPRTDX,FPRTDX,FPRTDX,LSR #2
        ADD     FPRTDX,LR,FPRTDX,LSR #1
        AND     LR,R0,#&F:SHL:12
        ADD     FPRTDX,FPRTDX,FPRTDX,LSR #2
        ADD     FPRTDX,LR,FPRTDX,LSR #1
        TST     R0,#1:SHL:30
        MOV     FPRTDX,FPRTDX,LSR #12
        RSBNE   FPRTDX,FPRTDX,#0

        MOV     R0,R0,LSL #20
        MOVS    R0,R0,LSR #20
        ORREQS  LR,R2,R1
        BNE     FPRTA
        TEQ     FMAT,#0
        MOVEQ   R6,#"0"
        STREQB  R6,[TYPE],#1
        LDMEQFD SP!,{R6,PC}            ;discard fdigs and return for 0 in G format
        TEQ     FMAT,#1
        BEQ     FPRTH
        B       FPRTZR
FPRTA
FPRTF   LDR     FDIGS,[SP]             ;stacked fdigs
        CMP     FMAT,#2
        BNE     FPRTFH
        AND     R6,FDIGS,#255
        ADCS    R6,R6,FPRTDX           ;fix up precision by adding exponent digit count
        BMI     FPRTZR
        CMP     R6,#MAXDIGS+1          ;how many digits?
        MOVCS   R6,#MAXDIGS
        MOVCS   FMAT,#0                ;treat as G10 format if unreasonable
        BIC     FDIGS,FDIGS,#255
        ORR     FDIGS,FDIGS,R6
FPRTFH  Push    "r0"
        Push    "r1"
        Push    "r2"
        ; Round to R1 sig figures
        AND     R1,FDIGS,#255
        MOV     R1,R1,LSL #2
        RSB     R1,R1,#18*4
        MOV     R2,#0
12
        LDRB    R0,[SP,R2,LSR #3]
        MOV     R0,R0,ROR #4
        TST     R2,#4
        MOVNE   R0,R0,ROR #4
        CMP     R1,R2
        BLO     %FT15
        CMPEQ   R0,#5:SHL:28
        BIC     R0,R0,#&F:SHL:28
        B       %FT20
15
        CMP     R0,#9:SHL:28
        BICCS   R0,R0,#&F:SHL:28
        ADDCC   R0,R0,#1:SHL:28
20
        MOV     R0,R0,ROR #28
        TST     R2,#4
        MOVNE   R0,R0,ROR #28
        STRB    R0,[SP,R2,LSR #3]
        BCC     %FT30
        ; Move on to next digit
        CMP     R2,#18*4
        ADDNE   R2,R2,#4
        BNE     %BT12
        ; Ran out of digits!
        MOV     R0,#1
        STRB    R0,[SP,R2,LSR #3]
        ADD     FPRTDX,FPRTDX,#1
        Pull    "r2"
        Pull    "r1"
        Pull    "r0"
        B       FPRTF
30
        Pull    "r2"
        Pull    "r1"
        Pull    "r0"
        TST     FDIGS,#255
        BNE     FPRTH
FPRTZR  LDR     FDIGS,[SP]
        ADD     FDIGS,FDIGS,#1
        MOV     R0,#0
        MOV     R1,#0
        MOV     R2,#0
        MOV     FPRTDX,#0
FPRTH   MOV     FPRTWN,#1
        TEQ     FMAT,#1
        BEQ     FPRTK
        TEQ     FPRTDX,#0
        BMI     FPRTKK
        AND     LR,FDIGS,#255
        CMP     FPRTDX,LR
        BCS     FPRTK
        ADD     FPRTWN,FPRTDX,#1
        MOV     FPRTDX,#0
        B       FPRTK
FPRTKK  TEQ     FMAT,#2
        BEQ     FPRTKL
        CMN     FPRTDX,#1
        CMNNE   FPRTDX,#2
        BNE     FPRTK
FPRTKL  MOV     FPRTWN,FDIGS,LSR #24   ;get . or ,
        STRB    FPRTWN,[TYPE,#1]       ;store "0." in string (interestingly!)
        MOV     FPRTWN,#"0"
        STRB    FPRTWN,[TYPE],#2       ;put the zero at the beginning and then skip the .
FPRTKM  ADDS    FPRTDX,FPRTDX,#1
        STRNEB  FPRTWN,[TYPE],#1       ;put in 0 if ne 0
        BNE     FPRTKM                 ;round again
        MOV     FPRTWN,#&80
FPRTK   MOV     LR,R0,LSR #8
        ORR     LR,LR,#"0"
        STRB    LR,[TYPE],#1
        ; Shift in the next digit
        MOV     R0,R0,LSL #4
        ORR     R0,R0,R1,LSR #28
        MOV     R1,R1,LSL #4
        BIC     R0,R0,#&F000
        ORR     R1,R1,R2,LSR #28
        MOV     R2,R2,LSL #4
        SUBS    FPRTWN,FPRTWN,#1
        MOVEQ   LR,FDIGS,LSR #24
        STREQB  LR,[TYPE],#1
        SUB     FDIGS,FDIGS,#1
        TST     FDIGS,#255
        BNE     FPRTK
        TEQ     FMAT,#1
        BEQ     FPRTTX
        TEQ     FMAT,#2
        BEQ     FPRTTY
FPRTTZ  LDRB    LR,[TYPE,#-1]!
        CMP     LR,#"0"
        BEQ     FPRTTZ
        CMP     LR,FDIGS,LSR #24
        ADDNE   TYPE,TYPE,#1
FPRTTY  TEQ     FPRTDX,#0
        BEQ     FPRTX
FPRTTX  MOV     LR,#"E"
        STRB    LR,[TYPE],#1
        ADDS    FPRTWN,FPRTDX,#0
        MOVMI   LR,#"-"
        STRMIB  LR,[TYPE],#1
        RSBMI   FPRTWN,FPRTWN,#0
        MOV     LR,#"0"
 [ FPOINT<>0
IPRTB   SUBS    FPRTWN,FPRTWN,#100
        ADDCS   LR,LR,#1
        BCS     IPRTB
        TEQ     LR,#"0"
        STRNEB  LR,[TYPE],#1
        ADD     FPRTWN,FPRTWN,#100
        MOV     LR,#"0"
        ORRNE   LR,LR,#256
 ]
IPRTA   SUBS    FPRTWN,FPRTWN,#10
        ADDCS   LR,LR,#1
        BCS     IPRTA
        TEQ     LR,#"0"
        STRNEB  LR,[TYPE],#1
        ADD     FPRTWN,FPRTWN,#"0"+10
        STRB    FPRTWN,[TYPE],#1
        TEQ     FMAT,#0
        BEQ     FPRTX
        MOV     R3,#" "
        TEQ     FPRTDX,#0
        STRPLB  R3,[TYPE],#1
 [ FPOINT<>0
        TST     LR,#256
        STREQB  R3,[TYPE],#1
 ]
        TEQ     LR,#"0"
        STREQB  R3,[TYPE],#1
FPRTX   LDMFD   SP!,{FDIGS,PC}
 [ FPOINT=0
; In: FACC = 5-byte float
; Out: R0-R2 = FPA format packed decimal
;      R3,R6-R7,R9 corrupt
; Although packed decimal has room for 19 digits, we only calculate to a precision of 16, to keep things within two registers (and the accuracy of anything beyond digit 11 is questionable anyway)
; Note that no rounding is performed when the mantissa grows to over 16 decimal digits in length; the result is truncated
FACCtoBCD       ROUT
        AND     FSIGN,FSIGN,#&80000000
        STMFD   R13!,{FSIGN,FDIGS,FMAT,LR}
        MOV     R2,#0 ; Digits 0-7
        MOV     R3,#0 ; Digits 8-15
        MOVS    R4,FACC
        BEQ     %FT90
        ; Convert FACC to BCD
        ADR     R5,tens
        MOV     R7,#0
        MOV     R9,#&80000000
        LDR     R6,[R5],#4
10
        CMP     R0,R6
        ADDHS   R3,R3,R9,LSR #3
        ADDHS   R2,R2,R7
        BEQ     %FT20
        SUBHI   R0,R0,R6
        BHI     %BT10
        MOVS    R9,R9,LSR #4
        MOV     R7,R7,RRX
        MOV     R7,R7,LSR #3
        LDR     R6,[R5],#4
        B       %BT10
20
        MOV     R4,#9 ; Decimal exponent
        SUBS    R1,R1,#129+31
        LDR     R9,=&88888888
        LDRGT   R7,=&33333333
        BGT     %FT50
        BEQ     %FT90
        MOV     R7,R9,LSR #3 ; &11111111
        ; Divide BCD number by two
30
        AND     R0,R3,R7
        BIC     R3,R3,R7
        MOVS    R0,R0,LSR #1
        MOV     R3,R3,LSR #1
        ORR     R0,R0,R0,LSR #2
        ADD     R3,R3,R0,LSR #1

        AND     R0,R2,R7
        BIC     R2,R2,R7
        MOV     R0,R0,RRX
        MOV     R2,R2,LSR #1
        ORR     R0,R0,R0,LSR #2
        ADD     R2,R2,R0,LSR #1

        ; Shift left one digit to keep first digit non-zero
        TST     R3,#&F0000000
        SUBEQ   R4,R4,#1
        MOVEQ   R3,R3,LSL #4
        ORREQ   R3,R3,R2,LSR #28
        MOVEQ   R2,R2,LSL #4

        ADDS    R1,R1,#1
        BNE     %BT30
        B       %FT90

        ; Multiply BCD number by two
50
        ADD     R2,R2,R7
        MVN     R0,R2
        AND     R0,R0,R9
        MOVS    R2,R2,LSL #1
        ORR     R0,R0,R0,LSR #1
        SUB     R2,R2,R0,LSR #1

        ADD     R3,R3,R7
        MVN     R0,R3
        AND     R0,R0,R9
        ADCS    R3,R3,R3
        ORR     R0,R0,R0,LSR #1
        SUB     R3,R3,R0,LSR #1

        ; Shift right one digit if we overflowed
        MOVCS   R2,R2,LSR #4
        ORRCS   R2,R2,R3,LSL #28
        MOVCS   R3,R3,LSR #4
        ORRCS   R3,R3,#&10000000
        ADDCS   R4,R4,#1

        SUBS    R1,R1,#1
        BNE     %BT50
        ; Now convert to STFP format
90
        LDR     R0,[R13],#4
        MOV     R1,R3,LSL #12
        ORR     R0,R0,R3,LSR #32-12
        ORR     R1,R1,R2,LSR #32-12
        MOV     R2,R2,LSL #12
        CMP     R4,#0
        ORRLT   R0,R0,#1:SHL:30
        RSBLT   R4,R4,#0
        BEQ     %FT99
        ; Convert exponent to BCD, noddy approach
91
        CMP     R4,#100
        SUBHS   R4,R4,#100
        ADDHS   R0,R0,#1:SHL:20
        BHI     %BT91
92
        CMP     R4,#10
        SUBHS   R4,R4,#10
        ADDHS   R0,R0,#1:SHL:16
        BHI     %BT92
        ADD     R0,R0,R4,LSL #12
99
        LDMFD   R13!,{FDIGS,FMAT,PC}
tens
        DCD 1000000000
        DCD 100000000
        DCD 10000000
        DCD 1000000
        DCD 100000
        DCD 10000
        DCD 1000
        DCD 100
        DCD 10
        DCD 1
;40 bit FACC:=FACC*10
;uses only FWGRD
FTENFX  MOV     FWGRD,FACC,ASL #30
        ORR     FWGRD,FWGRD,FGRD,LSR #2 ;concoct new guard
        ADDS    FGRD,FGRD,FWGRD        ;add facc,fgrd>>2 to facc,fgrd
        ADCS    FACC,FACC,FACC,LSR #2  ;carry in from previous ADD, out from this ADC
        ADD     FACCX,FACCX,#3         ;increase faccx by 3
        MOVCC   PC,R14                 ;exit if no renorm
FTENFA
        STR     R14,[SP,#-4]!
        SavePSR R14
        ADD     FACCX,FACCX,#1         ;renormalise if required
        MOVS    FACC,FACC,RRX
        MOV     FGRD,FGRD,RRX
        RestPSR R14,,f                 ;restore callers state
        LDR     PC,[SP],#4
;40 bit FACC:=FACC/10
;uses only FWGRD
FTENFQ  STR     R14,[SP,#-4]!
        TSTS    FACC,FACC,LSR #1       ;set carry out from facc
        ADDS    FGRD,FGRD,FGRD,RRX     ;add facc,fgrd>>1 to facc,fgrd
        ADCS    FACC,FACC,FACC,LSR #1
        SUB     FACCX,FACCX,#4         ;decrease faccx by 4
        BLCS    FTENFA
        MOV     FWGRD,FACC,ASL #28
        ORR     FWGRD,FWGRD,FGRD,LSR #4 ;concoct new guard
        ADDS    FGRD,FGRD,FWGRD        ;add facc,fgrd>>4 to facc,fgrd
        ADCS    FACC,FACC,FACC,LSR #4  ;carry in from previous ADD, out from this ADC
        BLCS    FTENFA
        MOV     FWGRD,FACC,ASL #24
        ORR     FWGRD,FWGRD,FGRD,LSR #8
        ADDS    FGRD,FGRD,FWGRD        ;add facc,fgrd>>8 to facc,fgrd
        ADCS    FACC,FACC,FACC,LSR #8
        BLCS    FTENFA
        MOV     FWGRD,FACC,ASL #16
        ORR     FWGRD,FWGRD,FGRD,LSR #16
        ADDS    FGRD,FGRD,FWGRD        ;add facc,fgrd>>16 to facc,fgrd
        ADCS    FACC,FACC,FACC,LSR #16
        BLCS    FTENFA
        ORR     FGRD,FGRD,#1           ;perturb system slightly!
        ADDS    FGRD,FGRD,FACC         ;add facc,fgrd>>32 to facc,fgrd
        ADCS    FACC,FACC,#0
        BLCS    FTENFA
        LDR     PC,[SP],#4
 ]
;convert to hex string
FCONHX  STR     R14,[SP,#-4]!
        BL      INTEGY
        ADD     TYPE,ARGP,#STRACC
        MOV     R2,#32-4
        MOV     R3,#0                  ;lzb
FCONH1  MOV     R1,IACC,LSR R2
        AND     R1,R1,#15
        CMP     R1,#9
        ORRLS   R1,R1,#"0"
        ADDHI   R1,R1,#"A"-10
        TEQ     R2,#0                  ;last time?
        MOVEQ   R3,#1
        TEQ     R3,#0
        CMPEQ   R1,#"0"
        MOVNE   R3,#1
        STRNEB  R1,[TYPE],#1
        SUBS    R2,R2,#4
        BPL     FCONH1
        LDR     PC,[SP],#4
FRDDXX  MOVS    TYPE,#TINTEGER         ;clear carry: nothing read
        MOV     PC,R14
;read constant from aeline: exit with aeline ready for next load of character
FREAD   CMP     R10,#"."
 [ FPOINT=0
        MOV     FACC,#0
        MOV     FGRD,#0
        MOV     FSIGN,#0               ;convenient flag for decimal pt
        MOV     FWACCX,#0              ;decimal exponent
        BEQ     FRDDDD
        SUB     R10,R10,#"0"
        CMP     R10,#9
        BHI     FRDDXX                 ;if <0 or >9
        MOV     FGRD,R10,LSL #24
        LDRB    R10,[AELINE],#1
        SUBS    FACCX,R10,#"0"
        BCC     FRDDDP
        CMP     FACCX,#9
        BHI     FRDDD
        MOV     FACCX,FACCX,LSL #24
        ADD     FGRD,FGRD,FGRD,LSL #2
        ADD     FGRD,FACCX,FGRD,LSL #1
FRDDC   LDRB    R10,[AELINE],#1
FRDDDP  CMP     R10,#"."
        BEQ     FRDDDD
FRDDD   CMP     R10,#"E"
        BEQ     FRDDEX                 ;start of exponent
        SUB     R10,R10,#"0"
        CMP     R10,#9
        BHI     FRDDQ                  ;end of number
        CMP     FACC,#&18000000        ;see how large it is
        TEQ     FSIGN,#0               ;both branches need "If after ."
        BCS     FRDDE
        SUBNE   FWACCX,FWACCX,#1       ;if after "." dec exp
        ADDS    FGRD,FGRD,FGRD
        ADC     FACC,FACC,FACC
        MOV     FACCX,FGRD,LSR #30
        ORR     FACCX,FACCX,FACC,LSL #2
        ADDS    FGRD,FGRD,FGRD,LSL #2
        ADC     FACC,FACC,FACCX
        ADDS    FGRD,FGRD,R10,LSL #24
        ADC     FACC,FACC,#0
        B       FRDDC
FRDDE   ADDEQ   FWACCX,FWACCX,#1       ;ignore if after "." else increase exponent
        B       FRDDC
FRDDDD  EORS    FSIGN,FSIGN,#1         ;seen "." before ?/set seen now
        BNE     FRDDC                  ;more chars
        EOR     FSIGN,FSIGN,#1         ;reset seen
        B       FRDDQ                  ;second one is end of number
FRDDEX  MOV     FWSIGN,R14
        BL      FRDEXP
        MOV     R14,FWSIGN
        ADD     FWACCX,FWACCX,FWGRD
FRDDQ   SUB     AELINE,AELINE,#1
        ORRS    FACCX,FWACCX,FSIGN
        BEQ     FRINT                  ;no exponent, no decimal so possible integer
FRFP    MOV     FWSIGN,R14
        ORRS    FACCX,FGRD,FACC
        BEQ     FRDDZZ
        MOV     FACCX,#&A8
        TEQ     FACC,#0
        BLPL    FNRMA
        TEQ     FWACCX,#0
        BEQ     FRDDZ
        BMI     FRDDM
FRDDP   BL      FTENFX
        SUBS    FWACCX,FWACCX,#1
        BNE     FRDDP
        B       FRDDZ
FRDDM   BL      FTENFQ
        ADDS    FWACCX,FWACCX,#1
        BNE     FRDDM
FRDDZ   BL      FTIDY
FRDDZZ  MOV     TYPE,#TFP
        MOV     FSIGN,#0
        SUBS    TYPE,TYPE,#0           ;set carry and status flags
        MOV     PC,FWSIGN              ;return
FRINT   CMP     FACC,#&800000
        BCS     FRFP                   ;too large to represent as integer
        MOV     FACC,FACC,LSL #8
        ORR     FACC,FACC,FGRD,LSR #32-8
        MOV     TYPE,#TINTEGER
        SUBS    TYPE,TYPE,#0           ;set carry and status flags
        MOV     PC,R14
FRDEXP  LDRB    R10,[AELINE],#1
        CMP     R10,#"-"
        BEQ     FRDEXM
        CMP     R10,#"+"
        BNE     FRDEXA
FRDEXB  LDRB    R10,[AELINE],#1
FRDEXA  CMP     R10,#"9"
        BHI     FRDEXQ
        SUBS    R10,R10,#"0"
        BCC     FRDEXQ
        MOV     FWGRD,R10
        LDRB    R10,[AELINE],#1
        CMP     R10,#"9"
        BHI     FRDEXX
        SUBS    R10,R10,#"0"
        ADDCS   AELINE,AELINE,#1
        ADDCS   FWGRD,FWGRD,FWGRD,LSL #2
        ADDCS   FWGRD,R10,FWGRD,LSL #1
FRDEXX  MOV     PC,R14
FRDEXQ  MOV     FWGRD,#0
        MOV     PC,R14
FRDEXM  STR     R14,[SP,#-4]!
        BL      FRDEXB
        RSB     FWGRD,FWGRD,#0
        LDR     PC,[SP],#4
 |
        MOV     IACC,#0                ;32 bit mantissa low
        SUB     R3,AELINE,#1           ;remember first digit
        BEQ     FRDDOT                 ;starts with a .
        SUB     R10,R10,#"0"
        CMP     R10,#9
        BHI     FRDDXX                 ;if <0 or >9
        MOV     IACC,R10               ;first character
        LDRB    R10,[AELINE],#1
        SUBS    R1,R10,#"0"
        BCC     FRDDDP                 ;could be .
        CMP     R1,#9
        BHI     FRDDD
        ADD     IACC,IACC,IACC,LSL #2
        ADD     IACC,R1,IACC,LSL #1    ;mult by 10 and add next char
FRDDC   LDRB    R10,[AELINE],#1
FRDDDP  CMP     R10,#"."
        BEQ     FRDDOT
FRDDD   CMP     R10,#"E"
        BEQ     FRDINTEX               ;start of exponent
        SUB     R10,R10,#"0"
        CMP     R10,#9
        BHI     FRDINTQ                ;end of number
        ADD     IACC,IACC,IACC,LSL #2
        ADD     IACC,R10,IACC,LSL #1   ;mult by 10 and add next char
        CMP     IACC,#&0CC00000        ;see how large it is
        BCC     FRDDC
        B       FRDRANGE
FRDINTQ SUB     AELINE,AELINE,#1
        MOV     TYPE,#TINTEGER
        SUBS    TYPE,TYPE,#0           ;set carry and status flags
        MOV     PC,R14
FRDDOT
FRDINTEX
FRDRANGE
        ; OK, we have a floating point number to read in
        ; To ensure accuracy, we will read in the value as packed decimal and convert to float via LDFP
        ; (old routine relied on extended precision math for accuracy with FPA, but with VFP the best we have available is double precision)
        ; This routine is also a lot faster than the old one (esp. with FPEmulator), due to only executing a single FP instruction
        MOV     AELINE,R3
        MOV     IACC,#0
        MOV     R3,#0
        MOV     R5,#0
        ASSERT  IACC<R3
        Push    "IACC,R3,R5"
        MOV     R3,#8                  ;bit index into packed value
        ; R5 used as exponent
        ; IACC used as "dot seen" flag
        ; Skip leading zeros
FRDFP0
        LDRB    R10,[AELINE],#1
        SUBS    R10,R10,#"0"
        SUBEQ   R5,R5,IACC             ;decrement exponent if leading zeros after dot
        BEQ     FRDFP0
        CMP     R10,#"."-"0"
        BEQ     FRDFPDOT
FRDFPDIG
        ; Process digits
        CMP     R10,#9
        BHI     FRDFPNOTDIG
        CMP     R3,#3*32
        BHS     FRDFPEXPINC
        AND     TYPE,R3,#4
        LDRB    R7,[SP,R3,LSR #3]
        ORR     R7,R7,R10,LSL TYPE
        STRB    R7,[SP,R3,LSR #3]
        TST     R3,#31
        SUB     R3,R3,#4
        ADDEQ   R3,R3,#64
FRDFPEXPINC
        ; Increment exponent if this wasn't the first digit, and we haven't seen the dot
        CMP     R3,#4
        CMPNE   IACC,#1
        ADDNE   R5,R5,#1
FRDFPNEXTDIG
        LDRB    R10,[AELINE],#1
        SUB     R10,R10,#"0"
        B       FRDFPDIG
FRDFPNOTDIG
        CMP     R10,#"."-"0"
        BEQ     FRDFPDOT
        CMP     R10,#"E"-"0"
        BEQ     FRDFPEXP
        MOVS    R7,R5
        LDRNE   R3,[SP]
        RSBMI   R7,R7,#0
        EORMI   R3,R3,#1:SHL:30
        BNE     FRDFPEXPBIN1           ;set exponent
FRDFPDONE
      [ FPOINT=1
        LDFP    FACC,[SP],#12
        MVFD    FACC,FACC              ;Ensure result is rounded to double precision
      |
        LDFP    F0,[SP],#12
        FPAtoFACC
      ]
        MOV     TYPE,#TFP
        SUB     AELINE,AELINE,#1
        SUBS    TYPE,TYPE,#0           ;set carry and status flags
        MOV     PC,R14                 ;return
FRDFPDOT
        CMP     IACC,#0
        BNE     FRDFPDONE
        MOV     IACC,#1
        CMP     R3,#8
        MOVEQ   R5,#-1
        BEQ     FRDFP0                 ;continue with leading zeros
        B       FRDFPNEXTDIG
FRDFPEXP
        ; Read sign
        LDRB    R10,[AELINE],#1
        CMP     R10,#"-"
        LDR     R3,[SP]
        RSBEQ   R5,R5,#0
        EOREQ   R3,R3,#1:SHL:30        ;Flip exponent sign
        CMPNE   R10,#"+"
        ; Read up to three digits, including leading zeros
        LDREQB  R10,[AELINE],#1
        CMP     R5,#0
        SUB     R7,R10,#"0"
        BNE     FRDFPEXPBIN
        ; No correction to apply, so read as BCD
        CMP     R7,#9
        BHI     FRDFPDONE
        LDRLSB  R10,[AELINE],#1
        SUBLS   R10,R10,#"0"
        CMPLS   R10,#9
        ORRLS   R7,R10,R7,LSL #4       ;read as BCD
        LDRLSB  R10,[AELINE],#1
        SUBLS   R10,R10,#"0"
        CMPLS   R10,#9
        ORRLS   R7,R10,R7,LSL #4       ;read as BCD
        ADDLS   AELINE,AELINE,#1       ;+1 so -1 in FRDFPDONE is OK
        ORR     R3,R3,R7,LSL #12
        STR     R3,[SP]
        B       FRDFPDONE
FRDFPEXPBIN
        ; Read three digits and add to R5
        CMP     R7,#9
        MOVHI   R7,#0
        LDRLSB  R10,[AELINE],#1
        SUBLS   R10,R10,#"0"
        CMPLS   R10,#9
        ADDLS   R7,R7,R7,LSL #2
        ADDLS   R7,R10,R7,LSL #1       ;*10
        LDRLSB  R10,[AELINE],#1
        SUBLS   R10,R10,#"0"
        CMPLS   R10,#9
        ADDLS   R7,R7,R7,LSL #2
        ADDLS   AELINE,AELINE,#1       ;+1 so -1 in FRDFPDONE is OK
        ADDLS   R7,R10,R7,LSL #1       ;*10
        ADDS    R7,R7,R5
        RSBMI   R7,R7,#0
        EORMI   R3,R3,#1:SHL:30        ;Flip exponent sign
        ; Convert R7 to BCD, noddy approach
        ; Max exponent we can get is E999 + ~250 leading zeros
        CMP     R7,#1000
        SUBHS   R7,R7,#1000
        ADDHS   R3,R3,#1:SHL:24
FRDFPEXPBIN1
        CMP     R7,#100
        SUBHS   R7,R7,#100
        ADDHS   R3,R3,#1:SHL:20
        BHS     FRDFPEXPBIN1
FRDFPEXPBIN2
        CMP     R7,#10
        SUBHS   R7,R7,#10
        ADDHS   R3,R3,#1:SHL:16
        BHS     FRDFPEXPBIN2
        ADD     R3,R3,R7,LSL #12
        STR     R3,[SP]
        B       FRDFPDONE
 ]
        LNK     Expr.s
