; Copyright 1997 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; BigDirCode

        TTL     "Big directory support code"
 [ BigDir

; ==========
; TestBigDir
; ==========

; test for long filenames on this disc

; entry: R3=disc address

; exit  Z=1 <=> big directories, C,V preserved, LR->disc rec

TestBigDir ROUT
        Push    "R0,LR"
        BL      DiscAddToRec            ;(R3->LR)
        LDRB    R0, [LR,#DiscRecord_BigDir_DiscVersion]
        TEQS    R0, #1
 [ DebugX :LAND: {FALSE}
        BNE     %FT01
        DLINE   " big dir ",cc
        B       %FT02
01
        DLINE   " not big dir ",cc
02
 ]
        Pull    "R0,PC"

; =======================
; SanityCheckBigDirHeader
; =======================

; checks that a big directory's header is acceptable.

; used by FindDir to ensure that a directory seems ok-ish
; before going any further, to avoid nasty crash-type
; situations caused by stupid values appearing.

; checks:

; 1) that BigDirStartName is correct
; 2) that BigDirVersion is 0
; 3) that the size of the directory is a multiple of NewDirSize

; any other checks are left until the entire directory is read

; entry:

;   R5 points to the directory's header

; exit:

;   if error, R0 result, V set

SBProven
 = "SBPr"
 = "oven"


SanityCheckBigDirHeader ROUT
        Push    "R0,R1,LR"

 [ DebugX
        DLINE   ">SanityCheckBigDirHeader"
 ]

 [ DebugXd
        ADD     lr, r5, #BigDirName
        DSTRING lr, "sanity check; name: "
 ]

        LDR     r1, SBProven                    ; get "SBPr"
        LDR     lr, [r5, #BigDirStartName]      ; and get StartName from Dir
        TEQS    r1,lr                           ; is it broke?
 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at BigDirStartMasSeq"
01
 ]
        BNE     %FT95

        ASSERT  (BigDirVersion:MOD:4)=1
        LDR     lr, [r5, #BigDirVersion-1]
        BICS    lr, lr, #255

 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at BigDirVersion"
01
 ]
        BNE     %FT95

        LDR     lr, [r5, #BigDirSize]
        MOVS    r1, lr, LSL #21         ; bottom 11 bits must be clear

 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at BigDirSize not multiple of 2048"
01
 ]
        BNE     %FT95

        ASSERT  BigDirMaxSize = 1:SHL:22
        SUB     r1, lr, #1              ; can be equal to BigDirMaxSize though
        MOVS    r1, r1, LSR #22

 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at BigDirSize too large (or zero)"
01
 ]

95
        MOVNE   R0, #BrokenDirErr
        MOVEQ   R0, #0
        BL      SetVOnR0
        STRVS   R0, [SP]
 [ DebugX
        DLINE   "<SanityCheckBigDirHeader"
 ]
        Pull    "R0,R1,PC"

; ===========
; CheckBigDir
; ===========

; Check dir is well formed

; entry: R3=dir disc add, R5->dir start, R6->dir end, R10 0 -> don't check the check
; bytes, R10=1-> do check it

; exit:  IF error V set, R0 result

CheckBigDir ROUT
        Push    "R0,R1,R10,R11,LR"
 [ DebugX
        DLINE   ">CheckBigDir"
 ]

 [ DebugX
        LDR     R1, [R5, #BigDirNameLen]
        DREG    R1, "dir's name length is:"
 ]

        baddr   R1, SBProven
        LDMIA   R1, {R1, R11}

        LDR     LR, [R5, #BigDirStartName]

        TEQS    LR, R1                          ; is the StartName Ok?

 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at SBPr"
01
 ]
        LDREQB  R0, [R5,#BigDirStartMasSeq]
        LDREQB  LR, [R6,#BigDirEndMasSeq]
        TEQS    R0, LR

 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at sequences"
01
 ]
        LDREQ   LR, [R6, #BigDirEndName]
        TEQEQS  LR, R11

 [ DebugX
        BEQ     %FT01
        DLINE   "Failed at endname"
        DREG    LR, "End name read was:"
01
 ]
        MOV     R0, #0
        BNE     %FT90

        MOVS    R10,R10                 ;quick flag
        BLNE    TestDirCheckByte        ;(R3,R5,R6->LR,Z)

90
        BEQ     %FT95
 [ DebugX
        DLINE   "Big dir broken"
 ]
        MOV     R0, #BrokenDirErr
        LDR     LR, BufDir
        TEQS    LR, R3
        BLEQ    InvalidateBufDir
        BEQ     %FT95
        BL      TryCache                ;(R3->R11,V)
        BLVC    InvalidateDirCache      ;if a dir in cache is broken, scrap whole cache
95
        BL      SetVOnR0
        STRVS   R0, [SP]

 [ DebugX
        DLINE   "<CheckBigDir"
 ]
        Pull    "R0,R1,R10,R11,PC"

; ===================
; TestBigDirCheckByte
; ===================

; entry
;  R3 ind disc add of dir
;  R5 -> dir start
;  R6 -> dir end

; exit
;  LR check byte
;  Z  set if matches existing byte

TestBigDirCheckByte     ROUT
        Push    "R0-R2,R5,R7,LR"

 [ DebugX
        DLINE   ">TestBigDirCheckByte"
        DREG    R5, "dir at:"
 ]

; first, work out the size of the first area to accumulate

        MOV     R2,#0   ; zero the acc.

        LDR     R0, [R5, #BigDirNameLen]

        ADD     R0, R0, #4
        BIC     R0, R0, #3                      ; name len

        LDR     R1, [R5, #BigDirEntries]        ; number of entries

        ASSERT  BigDirEntrySize=28
        RSB     R1, R1, R1, LSL #3              ; *7
        ADD     R0, R0, R1, LSL #2              ; 7*4=28

        LDR     R1, [R5,#BigDirNamesSize]       ; size of the heap of names

        ADD     R0, R0, R1

        ADD     R0, R0, #BigDirHeaderSize

 [ DebugX
        DREG    R0, "bytes to check:"
 ]

        ADD     R0, R0, R5                      ; now R0 points to first word after names

10
        LDR     LR, [R5], #4
        EOR     R2, LR, R2, ROR #13
        TEQS    R5, R0
        BNE     %BT10

; now the tail
        ADD     R5, R6, #BigDirEndName
        LDR     LR, [R5], #4
        EOR     R2, LR, R2, ROR #13
        SUB     R0, R6, #1

        ASSERT  BigDirCheckByte=-1

20
        LDRB    LR, [R5], #1
        EOR     R2, LR, R2, ROR #13
        TEQS    R5, R0
        BNE     %BT20

        EOR     R2, R2, R2, LSR #16     ;reduce to 8 bits
        EOR     R2, R2, R2, LSR #8
        AND     R2, R2, #&FF

        LDRB    LR, [R6,#BigDirCheckByte]
        TEQS    R2, LR
 [ DebugX
        DREG    LR, "check byte"
        DREG    R2, "value:"
 ]
        MOV     LR, R2

 [ DebugX
        DLINE   "<TestBigDirCheckByte"
 ]

        Pull    "R0-R2,R5,R7,PC"

; =================
; ReadBigDirEntries
; =================

; entry:

; R3 indirect disc addr of dir
; R5 pointer to big dir
; R7 buffer ptr
; R8 number of names to transfer
; R9 offset in dir
; R10 buffer end
; R11 fsfunc reason code

; exit:

; R5=number of names transferred
; R9=new offset

; all other regs may be corrupted except R12, R13

; while running;

; R4=pointer to current entry

ReadBigDirEntries       ROUT
        Push    "R0,R1,R2,R9,lr"
 [ DebugX
        DLINE   ">ReadBigDirEntries"
 ]
        LDR     r1, [r5, #BigDirEntries]        ; get the number of entries in the dir
 [ DebugX
        DREG    r1, "dir entries:"
        DREG    r9, "index value:"
 ]
        CMP     r9, r1                  ; check if offset too large
        MOVHS   r9, #-1
        MOVHS   R5, #0
        BHS     %FT95                   ; nothing transferred

; this still to be implemented!

; common set-up code

        BL      GetDirFirstEntry        ; (R3,R5->R4)

        ASSERT  BigDirEntrySize=28
        RSB     LR, R9, R9, LSL #3      ; *7
        ADD     R4, R4, LR, LSL #2      ; 7*4=28

        TEQS    R11, #fsfunc_ReadDirEntriesInfo
        BEQ     %FT20

        TEQS    R11, #fsfunc_ReadDirEntries
        BEQ     %FT40

        TEQS    R11, #fsfunc_CatalogObjects
        BEQ     %FT60

        B       %FT95

20
        ADD     R7, R7, #3      ; word align it
        BIC     R7, R7, #3
        SUB     LR, R10, R7
        LDR     R0, [R4, #BigDirObNameLen]
 [ DebugX
        DREG    R0, "name length:"
 ]
        ADD     R0, R0, #&15+3
        BIC     R0, R0, #3
        CMPS    R0, LR                  ; space for entry?
 [ DebugX
        DREG    LR, "space in buffer:"
        DREG    R0, "space for entry:"
        BLS     %FT01
        DLINE   "Buffer overflowed"
01
 ]
        BHI     %FT25                   ; no space!

; have space for entry
        BL      ReadIntAtts     ;(R3,R4->LR)
        TSTS    LR, #DirBit
        MOVEQ   R0, #1
        MOVNE   R0, #2
        BL      ReadLoad        ;(R3,R4->LR)
        STR     LR, [R7],#4
        BL      ReadExec        ;(R4->LR)
        STR     LR, [R7],#4
        BL      ReadLen         ;(R3,R4->LR)
        STR     LR, [R7],#4
        BL      ReadExtAtts     ;(R3,R4->LR)
        STR     LR, [R7],#4
        STR     R0, [R7],#4     ;atts

        LDR     R6, [R4, #BigDirObNameLen]
        Push    "R4"
        BL      GetBigDirName   ;(R5,R6->LR)
        MOV     R4, LR
 [ DebugX :LOR: DebugXd
        DSTRING R4, "name:"
 ]
        BL      PutStringToBuf  ;write the string to the buffer
        Pull    "R4"
        BLCC    Put0ToBuf

        BCS     %FT25           ; buffer full

        ADD     R9, R9, #1
        ADD     R4, R4, #BigDirEntrySize
        BL      BigDirFinished
        BEQ     %FT30

        SUBS    R8, R8, #1
        BGT     %BT20

; finished count of entries to extract, but not finished dir

25
; here when buffer full or nothing left to do
        LDR     R8, [sp, #3*4]          ; old offset in dir
        SUB     R5, R9, R8              ; number of names transferred
        B       %FT95                   ; and exit

30
; end of dir
        LDR     R8, [sp, #3*4]          ; old offset in dir
        SUBS    R5, R9, R8              ; number of names transferred
        MOVEQ   R9, #-1                 ; finished
        B       %FT95                   ; and exit

40
        SUB     LR, R10, R7
        LDR     R0, [R4, #BigDirObNameLen]
        ADD     R0, R0, #1
        CMPS    R0, LR                  ; space for entry?
 [ DebugX
        BLS     %FT01
        DLINE   "Buffer overflowed"
01
 ]
        BHI     %FT45                   ; no space!

        LDR     R6, [R4, #BigDirObNameLen]
        Push    "R4"
        BL      GetBigDirName   ;(R5,R6->LR)
        MOV     R4, LR
 [ DebugX :LOR: DebugXd
        DSTRING R4, "name:"
 ]
        BL      PutStringToBuf  ;write the string to the buffer
        Pull    "R4"
        BLCC    Put0ToBuf

        BCS     %FT45           ; buffer full

        ADD     R9, R9, #1
        ADD     R4, R4, #BigDirEntrySize
        BL      BigDirFinished
        BEQ     %FT50

        SUBS    R8, R8, #1
        BGT     %BT40

; finished count of entries to extract, but not finished dir

45
; here when buffer full or nothing left to do
        LDR     R8, [sp, #3*4]          ; old offset in dir
        SUB     R5, R9, R8              ; number of names transferred
        B       %FT95                   ; and exit

50
; end of dir
        LDR     R8, [sp, #3*4]          ; old offset in dir
        SUBS    R5, R9, R8              ; number of names transferred
        MOVEQ   R9, #-1                 ; finished
        B       %FT95                   ; and exit


60 ; CatalogObjects
        ADD     R7, R7, #3      ; word align it
        BIC     R7, R7, #3
        SUB     LR, R10, R7
        LDR     R0, [R4, #BigDirObNameLen]
 [ DebugX
        DREG    R0, "name length:"
 ]
        ADD     R0, R0, #&1E+3
        BIC     R0, R0, #3
        CMPS    R0, LR                  ; space for entry?
 [ DebugX
        DREG    LR, "space in buffer:"
        DREG    R0, "space for entry:"
        BLS     %FT01
        DLINE   "Buffer overflowed"
01
 ]
        BHI     %FT65                   ; no space!

; have space for entry
        BL      ReadIntAtts     ;(R3,R4->LR)
        TSTS    LR, #DirBit
        MOVEQ   R0, #1
        MOVNE   R0, #2
        BL      ReadLoad        ;(R3,R4->LR)
        STR     LR, [R7],#4
        CMNS    LR, #1 :SHL: 20 ;C=1 <=> stamped
        ANDCS   R1, LR, #&FF
        MOVCC   R1, #0
        BL      ReadExec        ;(R4->LR)
        MOVCS   R2, LR
        MOVCC   R2, #0
        STR     LR, [R7],#4
        BL      ReadLen         ;(R3,R4->LR)
        STR     LR, [R7],#4
        BL      ReadExtAtts     ;(R3,R4->LR)
        STR     LR, [R7],#4
        STR     R0, [R7],#4     ;atts
        BL      ReadIndDiscAdd  ;(R3,R4->LR)
        BIC     LR, LR, #DiscBits
        STR     LR, [R7], #4
        STR     R2, [R7], #4
        STRB    R1, [R7], #1

        LDR     R6, [R4, #BigDirObNameLen]
        Push    "R4"
        BL      GetBigDirName   ;(R5,R6->LR)
        MOV     R4, LR
 [ DebugX :LOR: DebugXd
        DSTRING R4, "name:"
 ]
        BL      PutStringToBuf  ;write the string to the buffer
        Pull    "R4"
        BLCC    Put0ToBuf

        BCS     %FT65           ; buffer full

        ADD     R9, R9, #1
        ADD     R4, R4, #BigDirEntrySize
        BL      BigDirFinished
        BEQ     %FT70

        SUBS    R8, R8, #1
        BGT     %BT60

; finished count of entries to extract, but not finished dir

65
; here when buffer full or nothing left to do
        LDR     R8, [sp, #3*4]          ; old offset in dir
        SUB     R5, R9, R8              ; number of names transferred
        B       %FT95                   ; and exit

70
; end of dir
        LDR     R8, [sp, #3*4]          ; old offset in dir
        SUBS    R5, R9, R8              ; number of names transferred
        MOVEQ   R9, #-1                 ; finished
        B       %FT95                   ; and exit


95
        STR     r9, [sp,#3*4]
 [ DebugX
        DLINE   "<ReadBigDirEntries"
 ]
        Pull    "r0,r1,r2,r9,pc"


; ==============
; BigDirFinished
; ==============

; entry:

; R5=pointer to dir
; R4=dir entry ptr

; exit:

; EQ if entry is beyond end of entries, LR=entries in dir

BigDirFinished ROUT
        Push    "R4,R6,LR"
 [ DebugX :LAND: {FALSE}
        DLINE   ">BigDirFinished"
        DREG    R5, "dir at:"
        DREG    R4, "entry at:"
 ]
        LDR     R6, [R5, #BigDirNameLen]
        ADD     R6, R6, #4
        BIC     R6, R6, #3

        LDR     LR, [R5, #BigDirEntries]

        ASSERT  BigDirEntrySize=28
        ADD     R6, R6, LR, LSL #5      ; *32
        SUB     R6, R6, LR, LSL #2      ; *(32-4)=*28

        ADD     R6, R6, R5
        ADD     R6, R6, #BigDirHeaderSize       ; now points to header

 [ DebugX
        DREG    R6, "end of entries:"
 ]
        CMPS    R4, R6
        MOVHI   R4, R6
        TEQS    R4, R6

        Pull    "R4,R6,PC"

; =============
; GetBigDirName
; =============

; entry: R5=dir start, R4=pointer to entry

; exit: LR=pointer to name (NOT header)

; if entry is after end of entries, returns pointer to what would
; be name after name heap

GetBigDirName ROUT
        Push    "R5,R6,R7,R8,LR"
        SavePSR R8

        BL      BigDirFinished                  ; is it finished
        LDREQ   R7, [R5, #BigDirNamesSize]      ; size of names heap

        LDR     LR, [R5, #BigDirNameLen]
        LDR     R6, [R5, #BigDirEntries]
        ADD     LR, LR, #4
        BIC     LR, LR, #3
        ADD     R5, R5, LR
        ADD     R5, R5, #BigDirHeaderSize

; R5 now points at entries

        ASSERT  BigDirEntrySize=28
        RSB     LR, R6, R6, LSL #3
        ADD     R6, R5, LR, LSL #2

        LDRNE   R5, [R4, #BigDirObNamePtr]      ; offset into name heap
        ADDNE   LR, R5, R6
        ADDEQ   LR, R7, R6                      ; end of name heap

; [ DebugXd
;       DREG    LR, "name ptr:"
; ]

        RestPSR R8,,f
        Pull    "R5,R6,R7,R8,PC"

; =================
; ReadBigDirIntAtts
; =================

; entry: R3=disc address of dir
;        R4=index of entry in dir
;        R5=start of dir

; exit:  LR=atts

ReadBigDirIntAtts ROUT
        Push    "LR"

        LDRB    LR, [R4, #BigDirAtts]           ; read the bottom 8 bits of atts

 [ DebugX
        DREG    LR, " ", cc
        DLINE   "=Big Atts"
 ]
        TSTS    LR, #DirBit
 [ FullAtts
        ASSERT  IntAttMask = &FF
 |
        ANDEQ   LR, LR, #IntAttMask
 ]
        ANDNE   LR, LR, #IntDirAttMask

        Pull    "PC"

 [ BinaryChop
; =========
; WildCheck
; =========

; check if wild card spec term has any wild cards

; entry:
;  R1   -> term, terminated by control codes, delete, space or delimiter

; exit:  EQ -> is wild card
;        NE -> no wild card

WildCheck ROUT
 [ DebugXb
        DREG    r1, "WildCheck(wild=",cc
        DLINE   ")"
 ]

        Push    "R0, R1, LR"

        SUB     R1, R1, #1      ; get to just before term (NextChar skips one)
10
        BL      NextChar        ; get next wild character
        BCS     %FT20
        TEQS    R0, #DelimChar
        BEQ     %FT20

        TEQS    R0, #OneWildChar        ; is this a wildcard?
        TEQNES  R0, #MultiWildChar
        BNE     %BT10

; here when found a wild ard
 [ DebugXb
        DLINE   "wildcard found"
 ]
        Pull    "R0, R1, PC"

20
; here when reched end; need to exit NE
        MOVS    R0, #1

 [ DebugXb
        DLINE   "no wildcard"
 ]

        Pull    "R0, R1, PC"
 ]


; ============
; LookUpBigDir
; ============

; look up wild card spec term in dir

; entry:
;  R1   -> term, terminated by control codes ,delete ,space or delimiter
;  R2   name restriction bits, only 'must be file' and 'must be dir' apply
;  R3   disc address of dir
;  R4   -> entry in dir to start at (or following entry if C=1)
;  R5   pointer to start of dir

; exit:

; IF found R4=index of entry in dir, Z=1 <=> file, C=0
; ELSE     R4->next alphabetical entry, C=1

LookUpBigDir    ROUT
 [ DebugX
        DREG    r1,"LookUpBigDir(wild=",cc
        DREG    r2,",restrict=",cc
        DREG    r3,",dir=",cc
        DREG    r4,",index ",cc
        DREG    r5,",dir start ",cc
        DLINE   ")",cc
 ]
        Push    "r7,LR"

        SUBCC   r4,r4,#BigDirEntrySize  ; caller asked to start at this entry
        MOV     R7, #0

 [ BinaryChop
        BL      WildCheck               ; is it wildcarded?
        BNE     %FT99                   ; no!
 ]
10
        ADD     r4,r4,#BigDirEntrySize
        BL      BigDirFinished          ; (R4,R5->eq->dir finished)
        BNE     %FT11

        ; end of dir
        TEQ     r7, #0
        MOVNE   r4, r7
        SEC
        B       %FT90                   ; carry already set

11
        Push    "R4,R5,R6"
        LDR     R6, [R4, #BigDirObNameLen]
        BL      GetBigDirName           ; get the name's address (in LR)
        MOV     R4, LR
        MOV     r5, #&ff                ; mask is ff
        BL      BigLexEqv               ; (R1,R4,R5->LO/EQ/HI)
        Pull    "R4,R5,R6"

        BCC     %BT10                   ;if mismatch and name<wild try next
        BEQ     %FT15                   ;EQ=>match

12
        ;mismatch and name >wild done
        TEQ     r7, #0                  ; doesn't affect C
        MOVEQ   r7, r4
        BCS     %BT10

        ; completion in the not found case

        MOV     r4, r7
        SEC                             ; set carry (not found)
        B       %FT90

15
        ; Match, but...
        BL      ReadBigDirIntAtts       ; read the attributes

;LR=Atts
        TSTS    LR, #DirBit
        BNE     %FT20                   ; found a dir
;found a file

        TSTS    R2, #MustBeDir
        BNE     %BT10
;fall through next few instructions as EQ
20
        TSTNES  R2, #MustBeFile
        BNE     %BT10

        TSTS    LR, #DirBit :SHL: 2,2 ;restore Z=1 <=> file, C=0
90
 [ Debug6
        DREG    r4,"->(entry=",cc
        BCC     %FT01
        DLINE   ",not found)"
        B       %FT02
01
        BNE     %FT01
        DLINE   " file ",cc
01
        DLINE   ",found)"
02
 ]
95
        Pull    "R7,PC"

 [ BinaryChop
99
; name was not wildcarded; we can binary chop it!

; register allocation.

; r8 = offset of entry of interest
; r9 = number of entries in range being considered
; r4 = ptr to current entry being examined
; r5 = start of dir

        Push    "r8, r9"

        ADD     r4, r4, #BigDirEntrySize

; first, work out offset of r4 in dir
        MOV     r8, r4                          ; R4 = size of an entry
        BL      GetDirFirstEntry                ; (R3,R5->R4)

        SUB     lr, r8, r4                      ; offset in bytes between start entry and thing

        MOV     r9, #BigDirEntrySize            ; size of a dir entry

 [ DebugX
        DREG    r8, "r8 : "
        DREG    lr, "lr : "
        DREG    r9, "r9 : "
        DREG    r4, "r4 : "
 ]

        DivRem  r8, lr, r9, r4, norem           ; r8 = offset into dir

 [ DebugXb
        DREG    r8, "init offset: "
 ]

        LDR     r9, [r5, #BigDirEntries]        ; number of entries in the dir
        SUB     r9, r9, r8                      ; difference

 [ DebugXb
        DREG    r9, "init range: "
 ]

        CMPS    r9, #0
        BNE     %FT05

        ; not found

        BL      GetDirFirstEntry                ; (R3,R5->R4)
        ASSERT  BigDirEntrySize=28

        RSB     R8, R8, R8, LSL #3              ; *7
        ADD     R4, R4, R8, LSL #2              ; 7*4=28

        SEC

        Pull    "r8, r9"

        B       %BT90

05

; here we have to check the middle item in the

;       ADD     lr, r9, #1
 [ DebugXb
        DREG    r8, "start: "
        DREG    r9, "len: "
 ]
        ADD     r8, r8, r9, LSR #1              ; r8 <- offset of entry to look into

        BL      GetDirFirstEntry                ; (R3,R5->R4)
        ASSERT  BigDirEntrySize=28

        RSB     LR, R8, R8, LSL #3              ; *7
        ADD     R4, R4, LR, LSL #2              ; 7*4=28

; now R4->entry of interest

        Push    "R4,R5,R6"
        LDR     R6, [R4, #BigDirObNameLen]
        BL      GetBigDirName           ; get the name's address (in LR)
        MOV     R4, LR
        MOV     r5, #&ff                ; mask is ff
        BL      BigLexEqv               ; (R1,R4,R5->LO/EQ/HI)
        Pull    "R4,R5,R6"

        BEQ     %FT80                   ; name=wild

        BCC     %FT20                   ; name<wild

        CMPS    r9, #1
        BEQ     %FT75

; name>wild.  range becomes below r8
 [ DebugXb
        DLINE   "name>wild"
 ]
        SUB     r8, r8, r9, LSR #1
        MOV     r9, r9, LSR #1
        B       %BT05

20
 [ DebugXb
        DLINE   "name<wild"
 ]
        CMPS    r9, #1
        BEQ     %FT70
; name<wild.  range becomes above r8
        ADD     r8, r8, #1
        SUB     r9, r9, #1
        MOVS    r9, r9, LSR #1
        BNE     %BT05                   ; if it's zero then it's not found


70
        ; not found
        ADD     R4, R4, #BigDirEntrySize
75
        SEC
        Pull    "R8, R9"
        B       %BT90


80
        ; Match, but...
 [ DebugXb
        DLINE   "Match, but..."
 ]
        BL      ReadBigDirIntAtts       ; read the attributes

;LR=Atts
        TSTS    LR, #DirBit
        BNE     %FT85                   ; found a dir
;found a file

        TSTS    R2, #MustBeDir
        BNE     %BT70
;fall through next few instructions as EQ
85
        TSTNES  R2, #MustBeFile
        BNE     %BT70

        TSTS    LR, #DirBit :SHL: 2,2 ;restore Z=1 <=> file, C=0

        Pull    "R8, R9"

        B       %BT90


 ]

; ================
; GetDirFirstEntry
; ================

; entry:

; R3 = dir ind disc address
; R5 = dir start

; exit:

; R4 points to first entry

GetDirFirstEntry ROUT
        Push    "R6,LR"
        SavePSR R6

        BL      TestBigDir

        BNE     %FT01                           ; not a big dir

        LDR     LR, [R5, #BigDirNameLen]
        ADD     LR, LR, #4
        BIC     LR, LR, #3

        ADD     R4, R5, LR
        ADD     R4, R4, #BigDirHeaderSize

        RestPSR R6,,f
        Pull    "R6,PC"
01
        ADD     R4,R5,#DirFirstEntry            ; not a big dir
        RestPSR R6,,f
        Pull    "R6,PC"

; ============
; NextDirEntry
; ============

; entry:

; R3=ind disc add
; R4->dir entry

; exit: r4->next dir entry

NextDirEntry ROUT
        Push    "LR"

        BL      TestBigDir
        ADDEQ   R4, R4, #BigDirEntrySize
        ASSERT  NewDirEntrySz=OldDirEntrySz
        ADDNE   R4,R4,#NewDirEntrySz

        Pull    "PC"

; ===============
; BigDirFreeSpace
; ===============

; entry:

; R5 -> dir start

; exit:

; LR number of bytes of unused space in the directory (always a word multiple)

BigDirFreeSpace ROUT
        Push    "R0,LR"

 [ DebugX
        DLINE   ">BigDirFreeSpace"
 ]
        LDR     R0, [R5, #BigDirSize]           ; size of the directory
        SUB     R0, R0, #BigDirHeaderSize + BigDirTailSize
        LDR     LR, [R5, #BigDirEntries]

        ASSERT  BigDirEntrySize=28
  [ BigDirFullBackup
        RSB     LR, LR, LR, LSL #3              ; *7
        SUB     R0, R0, LR, LSL #3              ; *7*8=56
  |
        SUB     R0, R0, LR, LSL #5              ; *(28+4)
  ]

        LDR     LR, [R5, #BigDirNameLen]        ; length of dir name
        ADD     LR, LR, #4
        BIC     LR, LR, #3
        SUB     R0, R0, LR

        LDR     LR, [R5, #BigDirNamesSize]      ; size of the names heap
        SUB     LR, R0, LR                      ; now LR is the number of free bytes in the dir

 [ DebugX
        DREG    LR, "<BigDirFreeSpace"
 ]
        Pull    "R0, PC"

; ============
; IsBigDirFull
; ============

; entry:

; R1 -> last term of name
; R3 top 3 bits disc id
; R5 -> dir start

; exit:
;   IF full, V set, R0 result

; number of bytes free in a big dir is:

; size of dir - (header size + tail size) - (size for all entries) - size of name heap

IsBigDirFull ROUT
        Push    "R0,R1,R2,R6,LR"

        BL      BigDirFreeSpace                 ; (R5->LR)
        MOV     R6, LR
        ; now get the number of bytes needed for the new entry:

        MOV     R2, R1                          ; keep start of term

        BL      SkipTerm                        ; (R1->R0,R1)

        SUB     R0, R1, R2                      ; length of term

        ADD     R0, R0, #4
        BIC     R0, R0, #3

 [ BigDirFullBackup
        ADD     R0, R0, #BigDirEntrySize*2
 |
        ADD     R0, R0, #BigDirEntrySize
        ADD     R0, R0, #4
 ]

 [ DebugX
        DREG    R0, "size needed:"
        DREG    R6, "size available:"
 ]
        CMPS    R0, R6
        MOVHI   R0, #DirFullErr
        MOVLS   R0, #0
        BL      SetVOnR0
        STRVS   R0, [SP]

        Pull    "R0,R1,R2,R6,PC"

; ===============
; MakeBigDirSpace
; ===============

; make space for a new entry in RAM copy of a directory
; ASSUMES DIR IS NOT FULL can check this with IsDirFull

; entry:

;  R1->term
;  R3 ind disc address
;  R4 -> entry
;  R5 -> dir start
;  R6 -> dir end

MakeBigDirSpace ROUT
        Push    "R0-R11,LR"
        SavePSR R11

 [ DebugX
;       BL      DumpBigDir
 ]

        MOV     R2, R1
        BL      SkipTerm                ; (R1->R0,R1)
        SUB     R8, R1, R2              ; R8 = term length
        ADD     R6, R8, #4
        BIC     R6, R6, #3              ; R6 = extra required in name heap

; now we can find the entry in the name heap, and bump everything up

        BL      GetBigDirName           ; (R4,R5->LR)

; LR = name heap entry
        MOV     R0, LR                  ; source for block move
        ADD     R1, R0, R6
        ADD     R1, R1, #BigDirEntrySize ; dest for block move

; length for move
        LDR     R2, [R5, #BigDirNameLen];length of dir name
        ADD     R2, R2, #4+BigDirHeaderSize
        BIC     R2, R2, #3
        LDR     LR, [R5, #BigDirEntries]

        ASSERT  BigDirEntrySize=28
        RSB     LR, LR, LR, LSL #3      ; *7
        ADD     R2, R2, LR, LSL #2      ; *7*4=28

        LDR     LR, [R5, #BigDirNamesSize] ; size of the heap of names

        ADD     R2, R2, LR              ; word after name heap
        ADD     R2, R2, R5

        SUB     R2, R2, R0              ; length of move

 [ DebugX
        DLINE   "Moving upper section of names"
        DREG    R0, "source="
        DREG    R1, "  dest="
        DREG    R2, "   len="
 ]

        BL      BlockMove               ; do the block move

        ADD     R7, R0, #BigDirEntrySize

        MOV     R0, R7
        MOV     R1, R6
        BL      ZeroRam                 ; zero rest of the entry

; R7 now points to start of new name heap entry

        MOV     R0, R4
        ADD     R1, R4, #BigDirEntrySize
        SUB     R2, R7, R1              ; amount to move

 [ DebugX
        DLINE   "Moving entries and lower section of names"
        DREG    R0, "source="
        DREG    R1, "  dest="
        DREG    R2, "   len="
 ]

        BL      BlockMove

; now update dir header fields
        LDR     LR, [R5, #BigDirEntries]        ; number of entries
        ADD     LR, LR, #1
        STR     LR, [R5, #BigDirEntries]        ; new number of entries

        LDR     LR, [R5, #BigDirNamesSize]      ; size of names heap
        ADD     LR, LR, R6
        STR     LR, [R5, #BigDirNamesSize]      ; new number of entries

; finally, generate new offsets into the name heap

        ADD     R4, R4, #BigDirEntrySize

        BL      BigDirFinished                  ; (R4, R5->C)

; C=0->can use next entry
; C=1->next entry not there, so use size of heap take R6
        LDRNE   LR, [R4, #BigDirObNamePtr]
        LDREQ   LR, [R5, #BigDirNamesSize]
        SUBEQ   LR, LR, R6

        STR     LR, [R4, #BigDirObNamePtr-BigDirEntrySize]      ; copy it
;
 [ DebugX
        DREG    R8, "storing name len of:"
 ]
        STR     R8, [R4, #BigDirObNameLen-BigDirEntrySize]      ; and set the name length

; now, bump all the other name indexes up by R6

        BEQ     %FT95                                           ; if Z=1 then finished

10
        LDR     LR, [R4, #BigDirObNamePtr]
        ADD     LR, LR, R6                      ; new pointer
        STR     LR, [R4, #BigDirObNamePtr]
        ADD     R4, R4, #BigDirEntrySize
        BL      BigDirFinished                  ; (R4, R5->C)
        BNE     %BT10
 [ DebugX
;       BL      DumpBigDir
 ]
95
        RestPSR R11,,f
        Pull    "R0-R11,PC"                             ; if C=1 then finished

 [ DebugX

; r5-dir start

DumpBigDir ROUT
        Push    "R5,R11,LR"                             ; save regs
        DLINE   "Directory dump:"

        LDR     R11, [R5, #BigDirSize]                  ; size of dir
10
        LDR     LR, [R5], #4                            ; get a word
        DREG    LR, " ",cc                              ; dump it
        AND     LR, R11, #4*7                           ; is it end of line
        CMPS    LR, #4*7
        BEQ     %FT01
        DLINE   " "
01
        SUBS    R11, R11, #4
        BGT     %BT10

        Pull    "R5,R11,PC"
 ]

; ============
; WriteBigName
; ============

; copy file name string to dir entry

; entry:
;  R1 -> name
;  R4 -> entry
;  R5 -> dir start

WriteBigName ROUT
        Push    "R0-R2,R4,LR"
 [ DebugXd
        DSTRING R1, "WriteBigName : "
 ]
        LDR     R2, [R4, #BigDirObNameLen]
        ADD     R2, R2, #3
        BIC     R2, R2, #3      ; up to word boundary
        BL      GetBigDirName   ; (R4,R5->LR)
        MOV     R4, LR
10
        BL      ThisChar        ;(R1->R0,C)
        ADD     R1, R1, #1
        MOVCS   R0, #CR         ;if terminator
        STRB    R0, [R4],#1
        BCS     %FT90
        B       %BT10
90
        Pull    "R0-R2,R4,PC"


; ===============
; WriteBigDirName
; ===============

; copy dir name string into big dir

; entry:
; R1->name
; R5->dir

WriteBigDirName ROUT
        Push    "R0-R2,R4,R6,LR"
 [ DebugXd
        DSTRING R1, "WriteBigDirName : "
 ]
        MOV     R6, #0
        ADD     R4, R5, #BigDirHeaderSize
10
        BL      ThisChar        ;(R1->R0,C)
        ADD     R1, R1, #1
        MOVCS   R0, #CR         ;if terminator
        ADDCC   R6, R6, #1
        STRB    R0, [R4],#1
        BCS     %FT90
        B       %BT10
90
        STR     R6, [R5, #BigDirNameLen]
        Pull    "R0-R2,R4,R6,PC"

; ================
; WriteBigDirNames
; ================

; entry: R5->start of dir
;        R6->end

WriteBigDirNames ROUT
        Push    "R11,LR"

        ADRL    R11, SBProven
        LDMIA   R11, {R11, LR}
        STR     R11, [R5, #BigDirStartName]
        STR     LR, [R6, #BigDirEndName]

        Pull    "R11,PC"

; =============
; TryGrowDirBuf
; =============

; entry R4 required size of dir buf

; exit

; if error, VS and R0->error

TryGrowDirBuf ROUT
        Push    "R0-R8,LR"      ; stuff
 [ DebugX
        DREG    R4,"entry TryGrowDirBuf - need: "
 ]
        ; because DA size may be rounded up to a page, we read the real size here
        MOV     R0, #2                  ; read area size
        LDR     R1, DirBufferArea       ; area addr
        BL      OnlyXOS_DynamicArea     ; do the SWI call
        BVS     %FT95

        STR     R2, DirBufferSize       ; if it's already small enough then we're OK

        LDR     R4, [SP, #4*4]          ; get back the size needed

        CMP     R4, R2                  ; do we need more space
        BLS     %FT94                   ; no

        MOV     R0, R1                  ; area number to R0
        SUB     R1, R4, R2              ; amount needed
        BL      OnlyXOS_ChangeDynamicArea
        BVS     %FT95

        ADD     R2, R2, R1              ; number of bytes added to area
        STR     R2, DirBufferSize       ; etc
94
        CLRV                            ; clear the V flag
95
        STRVS   R0, [SP]                ; if V set then store the error
 [ DebugX
        DLINE   "Exit TryGrowDirBuf"
 ]
        Pull    "R0-R8,PC"      ; other stuff

; ==========
; GrowBigDir
; ==========

; entry:

; R3  =  dir ind disc address
; R5  -> dir start
; R6  -> dir end

; dir is in DirBuffer


GrowBigDir ROUT
        Push    "R0-R11,LR"
 [ DebugX :LOR: DebugXg
        DLINE   "entering GrowBigDir"
        DREG    R3, "dir "
        DREG    R5, "at  "
 ]
; first of all, ensure have enough memory for dir of required size
        SUB     R4, R6, R5              ; current dir size
        ADD     R4, R4, #BigDirMinSize  ; add a bit to it
        CMP     R4, #BigDirMaxSize      ; clear V
        MOVHI   R0, #DirFullErr
        BLHI    SetV
        BLVC    TryGrowDirBuf
        BVS     %FT99

; loop back to here when need to move more files
10
        LDR     R3, [SP, #3*4]
        LDR     R6, [SP, #6*4]
        LDR     R5, [SP, #5*4]
        SUB     R4, R6, R5
        ADD     R4, R4, #BigDirMinSize  ; size required
        BL      DiscAddToRec            ;(R3->LR) get disc record
        LDRB    R7, [LR, #DiscRecord_Log2SectorSize]   ; sector size
        LDRB    LR, [LR, #DiscRecord_BigMap_ShareSize]    ; share size
        ADD     R7, R7, LR
        MOV     R11, #1
        RSB     R11, R11,R11,LSL R7
        TSTS    R4, R11
        MOV     R9, R4, LSR R7
        ADDNE   R9, R9, #1              ; sharing blocks needed for dir

 [ DebugXg
        DREG    R9, "dir needs ",cc
        DLINE   " sharing blocks"
 ]

        BL      BeforeAlterFsMap        ; (R3->R0,V)
        BVS     %FT99

        BL      CritInitReadNewFs       ; (->R10,R11)

        BL      SortDir                 ; (R3,R5->R8)

15
        LDMIA   R8!, {R4, R5}           ; get table entry
 [ DebugXg
        DREG    R5," table entry: ",cc
        DREG    R4,""
 ]
        AND     R0, R5, #&FF    ; r5 - sharing offset
        TEQS    R5, #0
        TEQEQS  R4, #0
        BEQ     %FT25           ;end of list marker found
        MOVS    R5, R5, LSR #9  ;link bits from word 1 bits 9-31
        ADDCS   R5, R5, R3, LSR #8
        RSBCC   R5, R5, R3, LSR #8
 [ DebugXg
        DREG    R0, "File sector:",cc
        DREG    R4, ", file index:",cc
        DREG    R5, ", link bits:"
 ]

        TEQS    R4, #0          ;is it the dir itself?
        BEQ     %FT20

        TEQS    R5, R3, LSR #8  ;is it the same object as the dir
        BNE     %FT25           ;note: this is a termination case due to the sorting of the dir

        CMPS    R0, #1          ;is the sharing offset 1?
        BEQ     %FT20

        CMPS    R0, R9          ;is it in the way?
        BHI     %FT20           ; no

        ; an object is in the way
 [ DebugXg
        DREG    R0,"offset in the way:"
 ]
        SUB     LR, r0, #1
        MOV     R7, LR, LSL R7          ; dummy dir size for ClaimFreeSpace
        ; find the offending dir entry

        ASSERT  BigDirEntrySize=28
        RSB     R4, R4, R4, LSL #3      ; *7
        MOV     R4, R4, LSL #2          ; *4*7=28

        LDR     LR, DirBufferPtr
        LDR     LR, [LR, #BigDirNameLen]
        ADD     LR, LR, #4
        BIC     LR, LR, #3
        ADD     R4, LR, R4
        LDR     LR, DirBufferPtr
        ADD     LR, LR,#BigDirHeaderSize-BigDirEntrySize
        ADD     R4, R4, LR

  [ DebugXg
        BL      ReadIndDiscAdd          ; (R4->LR)
        DREG    LR, "Ind disc add of entry:"
  ]
        BL      ReadLen                 ; (R3,R4->LR)
        MOV     R10, LR                 ; length to claim
        MOV     R11,#fsfile_Save        ; reason code to ClaimFreeSpace
        LDR     R5, [SP, #5*4]          ; get the dir
        ADD     R7, R7, R10             ; increase dummy length
        STR     R7, [R5, #BigDirSize]   ; fake dir size (MUST restore later - see below)
 [ DebugXg
        DREG    R7, "dummy size:"
 ]
        BL      ClaimFreeSpace          ; claim space for object
  [ DebugXg
        BVC     %FT01
        DLINE   "ClaimFreeSpace failed"
01
  ]
        LDR     R6, [SP, #6*4]          ;
        SUB     R6, R6, R5
        STR     R6, [R5, #BigDirSize]   ; restore dir's original size MUST do this
        BVS     %FT96


; loop among Fcbs until we find that matches.  if it does, then we
; then have to adjust it

        BL      ReadIndDiscAdd

        sbaddr  r1, FirstFcb-FcbNext
        B       %FT02
01
        LDR     r8, [r1, #FcbIndDiscAdd]
 [ DebugXg
        DREG    r8, "File in chain:"
 ]

        CMP     r8, lr
        BNE     %FT02
 [ DebugXg
        DLINE   "matches file we're interested in"
 ]
        BL      Flush           ;flush the file of interest
        B       %FT03
02
        LDR     r1, [r1,#FcbNext];get next FCB
        CMPS    r1, #-1
        BNE     %BT01           ;
03

        Push    "R1,R3,R4"
        BL      ReadIndDiscAdd
        MOV     R1, LR
        MOV     R3, R10
        BL      DefaultMoveData         ; move the data
        Pull    "R1,R3,R4"
        BVS     %FT96                   ; argh

        MOV     R0, R2
        BL      WriteIndDiscAdd ; update the dir
        BL      WriteFsMapThenDir       ; write the stuff out
        BVS     %FT96           ; err, this would be BAD

        ; now if we found a matching Fcb, update its ind disc address

        CMP     R1, #-1
        STRNE   R2, [R1, #FcbIndDiscAdd]

        BL      UnlockMap       ; unlock the map
        B       %BT10           ; and try going round the loop again

20
        B       %BT15           ; round the loop again

25      ; SortDir didn't find any objects which clash.  do the grow operation, checking
        ; it won't grow beyond MinMapObj

        BIC     R1, R3, #&ff                    ; pretend to be unshared
        BL      MeasureFileAllocSize_GotMap     ; (R1,R3,R4->R0) always < 2G - dir object only
 [ DebugXg
        DREG    R0, "space allocated to dir:"
 ]

        LDR     R5, [SP, #5*4]  ; get the dir addr again
        LDR     R7, [R5, #BigDirSize] ; size of dir at present
        ADD     R7, R7, #BigDirMinSize
        CMPS    R7, R0
        BLS     %FT58

        ; here we don't have any shared objects getting in the way, but we
        ; don't have enough space allocated (how convenient).

        ; we call ClaimFreeSpace with RandomAccessExtend reason code to
        ; get things sorted out.  but we cheat.  because, y see, we don't
        ; want to have to get the parent dir in just yet, with all that
        ; copying around of data, so I'm gonna build a dummy dir entry
        ; on the stack.  cunning, eh no?

        ; we also then return most of the space anyway, cos RandomAccessExtend
        ; has a tendency to allocate far too much space

        Push    "R3,R4,R6,R7"

        SUB     SP, SP, #BigDirEntrySize        ; space for an entry
        MOV     R4, SP
        MOV     R6, R3                  ; keep addy around
        BL      ToParent                ; get parent addy
        BIC     R6, R6, #DiscBits       ; knock off DiscBits
        BIC     R6, R6, #&ff            ; and pretend unshared
        STR     R6, [R4, #BigDirIndDiscAdd]
        SUB     LR, R7, #BigDirMinSize  ; current size
        STR     LR, [R4, #BigDirLen]

        ; only need IndDiscAdd and Len to be valid in the dummy entry.

 [ {FALSE}
        ADD     R7, R7, #262144         ; 64K allocation?
        MOV     R10, R7
 |
        MOV     R10, R7                 ; size wanted
 ]
        MOV     R0, LR                  ; old extent
        MOV     R11, #RandomAccessExtend
        BL      ClaimFreeSpace          ; (R0,R3,R4,R5,R10,R11->R0,R2,R10,V)
        BVS     %FT55

        MOV     R0, R10

        Push    "R0,R2,R4,R5,R8,R10"

        MOV     R0, R7

        BL      CritInitReadNewFs       ; (->R10, R11)

        BL      RoundUpAlloc            ; (R0,R3->R0) always < 2G - dir object only

        BL      DiscAddToRec            ; (R3->LR)

        LDRB    R8, [LR, #DiscRecord_Log2bpmb]      ; convert units to map bits
        MOV     R1, R0, LSR R8          ;

 ; *********************************************************************************************

 ; 04 Dec 1997 SBP - copied fixed code to shorten object back to reasonable length from
 ;                   ShrinkBigDir routine, and made it fit :-)



 [ {TRUE}

        BL      DefFindFragment                 ; (R1, R2, R10 -> R1, R9, R11, LR)

 [ DebugXg
        DREG    R1, "map ptr : "
        DREG    R11, "frag at : "
        DREG    LR,  "remains : "
        DREG    R0, "new len : "
 ]

        ; R1  - map ptr to desired offset
        ; R11 - map ptr to fragment start
        ; LR  - number of bits left in fragment
        ; R0  - size to set length to
        ; R9  - pre gap

; first check that length to start of frag is OK

        SUBS    R1, R1, R11                     ; offset into frag
 [ DebugXg
        BNE     %FT01
        DLINE   "Frag disappearing entirely"
01
 ]
        BEQ     %FT40                           ; fragment disappearing entirely

        Push    "LR"

        BL      MinMapObj                       ; (R10->LR)

        MOV     R8, LR                          ; keep it around

        Pull    "LR"

        CMP     R1, R8                          ; is it too close to start of frag
        BGE     %FT30

 [ DebugXg
        DLINE   "frag would be too short - adjusting length"
 ]

; if fragment is going to be too short then
        SUB     R8, R8, R1                      ; need this many extra bits
        ADD     R1, R1, R11
        ADD     R1, R1, R8

        LDRB    R2, [R10, #ZoneHead+DiscRecord_Log2bpmb]    ; size of a map bit

        ADD     R0, R0, R8, LSL R2              ; new file size
        SUB     LR, LR, R8                      ; number of bits left in fragment


30 ; here when new fragment length long enough

        LDRB    R8, [R10, #ZoneHead+DiscRecord_IdLen]   ; get idlen again
        ADD     R8, R8, #1
        CMPS    LR, R8

        BGE     %FT40                           ; shortening by more than idlen+1, so safe

 [ DebugXg
        DREG    LR, "shortening by:"
 ]

        MOV     R2, LR                          ; keep number of bits around

; here when shortening by less than idlen+1.  check that object followed by a gap

        Push    "R7"
        BL      FragRdLenBits                   ; (R10->R11,R7)
        ADD     R8, R11, R7                     ; next object
        Pull    "R7"
        MOV     R11, R9
        BL      NextFree_Quick                  ; (R10,R11->R9,R11)
        BCS     %FT35                           ; exhausted, so no gap
        CMPS    R11, R8                         ; is it a gap
        BEQ     %FT40

35
 [ DebugXg
        DLINE   "can't shorten"
 ]

        LDRB    R8, [R10, #ZoneHead+DiscRecord_Log2bpmb]
        ADD     R0, R0, R2, LSL R8              ; new length

40 ; here when new length is OK
        Pull    "R1,R2,R4,R5,R8,R10"

 [ DebugXg
        DREG    R0, "new length is:"
        DREG    R1, "old length is:"
 ]

        BL      ReturnSpace                     ; return allocated space

 ; *********************************************************************************************

 |

        BL      DefFindFragment         ; (R1,R2,R10->R1,R9,R11,LR)
        SUBS    R1,R1,R11               ; R1 = offset in map bits into fragment
 [ DebugXg
        DREG    R1, "offset into fragment:"
 ]
        BEQ     %FT52                   ; no change needed as offset at frag start - removing frag entirely
        LDRB    R9, [R10, #ZoneHead+DiscRecord_IdLen]
 [ DebugXg
        DREG    R9, "IdLen:"
 ]
        ADD     R9, R9, #1
        CMP     R1, R9
        BHS     %FT52                   ; no change needed as offset is more than idlen+1 into frag

 [ DebugXg
        DREG    R7,"too short:"
 ]
        ; here if offset less than needed
        SUB     R1, R9, R1              ; offset to add to R7
        LDRB    R9, [R10, #ZoneHead+DiscRecord_Log2bpmb]
        ADD     R7, R7, R1, LSL R9      ; add it

 [ DebugXg
        DREG    R1, "amount to increase by:"
        DREG    R9, "BitSize: "
        DREG    R7, "changing to: "
 ]

52
        Pull    "R2,R4,R5,R8,R10"

        ; now try to return excess space
        MOV     R0, R7                  ; new length
        MOV     R1, R10                 ; allocated length
        BL      ReturnSpace             ; return the space

 ]


 ; *********************************************************************************************




55
        ADD     SP, SP, #BigDirEntrySize
        Pull    "R3,R4,R6,R7"

        BVS     %FT96                   ; if error

        BL      WriteFsMap              ; write out the FsMap.  extra space allocated after dir.

58
        ; here we have enough space in the object
        ; and there's no shared objects in the way
        ; so let's do it.  first alter the dir

 [ DebugXg
        DLINE   "can grow dir... doing so"
 ]

        BL      InvalidateBufDir        ; going to trash dir
        STR     R7, [R5, #BigDirSize]
        SUB     R0, R6, #BigDirTailSize
        ADD     R1, R0, #BigDirMinSize
        MOV     R2, #BigDirTailSize
        BL      BlockMove

 [ DebugXg
        DLINE   "Writing dir"
        LDR     LR, [R5, #BigDirParent]
        DREG    LR, "parent is:"
 ]
        BL      WriteDir        ;write out the dir
        BVS     %FT96

 [ DebugXg
        DLINE   "Getting parent"
        DREG    R3, "R3: "
        DREG    R5, "R5: "
 ]
        MOV     R8, R3          ;keep ind disc add around
        BL      ToParent        ;(R3,R5->R3)
 [ DebugXg
        DREG    R3, "Parent: "
 ]
        CMPS    R8, R3          ;is it the root dir
        BNE     %FT55

 [ DebugXg
        DLINE   "It's the root dir - updating the FS map"
 ]
        BL      DiscAddToRec    ;(R3->LR) get disc record

        STR     R7, [LR, #DiscRecord_BigDir_RootDirSize]          ; store the root dir size in the disc record

        LDR     r10, CritDrvRec

 [ DynamicMaps
        LDR     r10, [r10, #DrvsFsMapAddr]
 |
        LDR     r10, [r10, #DrvsFsMap]
        BIC     r10, r10, #HiFsBits
 ]

        MOV     r1, #(ZoneHead*8)+Zone0Bits
        BL      MarkZone                ;(r1,r10)

        STR     R7, [R10, #ZoneHead+DiscRecord_BigDir_RootDirSize]

        BL      WriteFsMap
        BVS     %FT96
        B       %FT80

55

        BL      GetDir          ; get the parent dir into dirbuf

        BL      GetDirFirstEntry        ; get first entry in dir

        BL      InvalidateBufDir
        MOV     R0, R7          ; length to write out
60
        BL      BigDirFinished ;(R4,R5->Z)
        BEQ     %FT70
        BL      ReadIndDiscAdd  ; get ind disc add
        CMPS    LR, R8          ; is it our object
        BEQ     %FT75
        ADD     R4, R4, #BigDirEntrySize
        B       %BT60

75
        BL      WriteLen        ;(R0,R3,R4) write the length

70
        BL      WriteDir        ; write the dir
        BVS     %FT96           ; error!

80
        MOV     R3, R8          ; get the original dir back into the dir buffer
        BL      GetDir
        STRVC   R5, [SP, #4*5]
        STRVC   R6, [SP, #4*6]
        MOVVC   R0, #0
96
        BL      UnlockMap
        BL      SetVOnR0
99
        STRVS   R0, [SP]
 [ DebugXg
        DLINE   "exiting GrowBigDir"
        BVC     %FT01
        DREG    R0, "error:"
01
 ]
        Pull    "R0-R11,PC"


; ===============
; CanBigDirShrink
; ===============

; entry:

; R5-> big dir in dir buffer
; R3   dir in disc add

CanBigDirShrink ROUT
        Push    "R0,LR"
 [ DebugX :LOR: DebugXg
        DLINE   ">CanBigDirShrink"
 ]

        BL      BigDirFreeSpace                 ; (R5->LR)
        MOV     R0, LR
 [ DebugX :LOR: DebugXg
        DREG    R0, "free space:"
 ]

        MOV     LR, #BigDirMinSize
        SUB     LR, LR, #1
        BICS    R0, R0, LR                      ; clear bottom bits
        MOVNE   R0, #-1                         ; -1 if non-0
        MVNS    R0, R0                          ; Z=1 if can shrink
 [ DebugX :LOR: DebugXg
        BNE     %FT01
        DLINE   "can shrink"
01
        DLINE   "<CanBigDirShrink"
 ]
        Pull    "R0,PC"


; ============
; ShrinkBigDir
; ============

; entry:

; R3=dir ind disc add
; R5->dir (in dir buffer)

; disc has already been BeforeAlterFsMapped

; exit: if error then R0->error block and V set

ShrinkBigDir ROUT
        Push    "R0-R11,LR"

 [ DebugX :LOR: DebugXg
        DLINE   ">ShrinkBigDir"
 ]

        BL      BigDirFreeSpace                 ; (R5->LR)
        ASSERT  BigDirMinSize=2048

        MOV     R8, LR, LSR #11
        MOV     R8, R8, LSL #11                 ; number of bytes to shrink dir by

        BL      InvalidateBufDir                ; going to alter BufDir

        LDR     R7, [R5, #BigDirSize]           ; current size of dir
        ADD     R0, R5, R7
        SUB     R0, R0, #BigDirTailSize         ; source for move
        SUB     R1, R0, R8                      ; dest for move
        MOV     R2, #BigDirTailSize             ; size of tail
        BL      BlockMove                       ; do the move

        SUB     R7, R7, R8                      ; new size of dir
        STR     R7, [R5, #BigDirSize]

        BL      BigDirHasSharedObjects          ; is it sharing with anything?
        BEQ     %FT50                           ; yes, sharing

        BL      CritInitReadNewFs               ; (->R10,R11)

        BIC     R1, R3, #&ff
        BL      MeasureFileAllocSize_GotMap     ; (R1,R3,R4->R0) always < 2G dir object only
        Push    "R0"

        BL      RoundUpAlloc                    ; (R0,R3->R0) always < 2G - dir object only

        LDRB    LR, [R10, #ZoneHead+DiscRecord_Log2bpmb]
        MOV     R1, R0, LSR LR                  ; number of map bits needed for object

        BIC     R2, R3, #&ff

        BL      DefFindFragment                 ; (R1, R2, R10 -> R1, R9, R11, LR)

        ; R1  - map ptr to desired offset
        ; R11 - map ptr to fragment start
        ; LR  - number of bits left in fragment
        ; R0  - size to set length to
        ; R9  - pre gap

; first check that length to start of frag is OK

        SUBS    R1, R1, R11                     ; offset into frag
        BEQ     %FT40                           ; fragment disappearing entirely

        Push    "LR"

        BL      MinMapObj                       ; (R10->LR)
        MOV     R8, LR

        Pull    "LR"

        CMP     R1, R8                          ; is it too close to start of frag
        BGE     %FT30

 [ DebugX :LOR: DebugXg
        DLINE   "frag would be too short - adjusting length"
 ]

; if fragment is going to be too short then
        SUB     R8, R8, R1                      ; need this many extra bits
        ADD     R1, R1, R11
        ADD     R1, R1, R8

        LDRB    R2, [R10, #ZoneHead+DiscRecord_Log2bpmb]    ; size of a map bit

        ADD     R0, R0, R8, LSL R2              ; new file size
        SUB     LR, LR, R8                      ; number of bits left in fragment


30 ; here when new fragment length long enough

        LDRB    R8, [R10, #ZoneHead+DiscRecord_IdLen]   ; get idlen again
        ADD     R8, R8, #1
        CMPS    LR, R8

        BGE     %FT40                           ; shortening by more than idlen+1, so safe

 [ DebugX :LOR: DebugXg
        DREG    LR, "shortening by:"
 ]

        MOV     R2, LR                          ; keep number of bits around

; here when shortening by less than idlen+1.  check that object followed by a gap

        Push    "R7"
        BL      FragRdLenBits                   ; (R10->R11,R7)
        ADD     R8, R11, R7                     ; next object
        Pull    "R7"
        MOV     R11, R9
        BL      NextFree_Quick                  ; (R10,R11->R9,R11,Z,C)
        BCS     %FT35
        CMPS    R11, R8                         ; is it a gap
        BEQ     %FT40

35
 [ DebugX :LOR: DebugXg
        DLINE   "can't shorten"
 ]

        LDRB    R8, [R10, #ZoneHead+DiscRecord_Log2bpmb]
        ADD     R0, R0, R2, LSL R8              ; new length

40 ; here when new length is OK
        Pull    "R1"

 [ DebugX :LOR: DebugXg
        DREG    R0, "new length is:"
        DREG    R1, "old length is:"
 ]

        BIC     R2,R3,#&ff
        BL      ReturnSpace                     ; return allocated space

        BL      WriteDirThenFsMap
        BVS     %FT95
        B       %FT60

50      ; sharing so just write the dir
        BL      WriteDir
        BVS     %FT95

60
        ; now sort out the parent
        MOV     R2, R3                          ; keep ind disc add around
        BL      ToParent                        ; (R3, R5->R3)
        CMPS    R2, R3                          ; is it the root dir
        BNE     %FT70                           ; not root dir

        ; alter the size in the disc record

        BL      DiscAddToRec    ;(R3->LR) get disc record

        STR     R7, [LR, #DiscRecord_BigDir_RootDirSize]          ; store the root dir size in the disc record

        LDR     r10, CritDrvRec
 [ DynamicMaps
        LDR     r10, [r10, #DrvsFsMapAddr]
 |
        LDR     r10, [r10, #DrvsFsMap]
        BIC     r10, r10, #HiFsBits
 ]

        MOV     r1, #(ZoneHead*8)+Zone0Bits
        BL      MarkZone                ;(r1,r10)

        STR     R7, [R10, #ZoneHead+DiscRecord_BigDir_RootDirSize]

        BL      WriteFsMap
        B       %FT95

70
        BL      GetDir
        BVS     %FT95                           ; can't get the dir!
        BL      GetDirFirstEntry                ; (R3, R5->R4)

80
        BL      BigDirFinished                  ; (R4,R5->Z)
        BEQ     %FT95
        BL      ReadIndDiscAdd                  ; (R3,R4->LR)
        TEQ     R2,LR
        BEQ     %FT85
        ADD     R4, R4, #BigDirEntrySize
        B       %BT80

85
        ; found dir entry, write updated length
        MOV     R0, R7
        BL      WriteLen                        ;(R0,R3,R4)
        BL      WriteDir

95
        STRVS   R0, [SP]
 [ DebugX :LOR: DebugXg
        DLINE   "<ShrinkBigDir"
 ]

        Pull    "R0-R11,PC"

; ======================
; BigDirHasSharedObjects
; ======================

; tests if a big dir contains shared object
;
; entry - R3 = dir ind disc add
;         R5-> dir

; exit:   Z=1<=>dir shares space with file(s)

BigDirHasSharedObjects ROUT
        Push    "R1,R4, LR"

        BL      GetDirFirstEntry        ; (R3, R5->R4)
        MOV     R1, R3, LSR #8

10
        BL      BigDirFinished          ; (R5, R4 -> Z)
        BEQ     %FT90
        BL      ReadIndDiscAdd          ; (R3, R4 -> LR)
        TEQS    R1, LR, LSR #8  ; is it in the same disc object?
 [ DebugX
        BNE     %FT01
        DLINE   "dir shares with child"
01
 ]
        Pull    "R1, R4, PC",EQ
        ADD     R4, R4, #BigDirEntrySize
        B       %BT10

90      ; run out of dir
        MOVS    R1, #1          ; Z=0
 [ DebugX
        DLINE   "dir does not share with child"
 ]

        Pull    "R1,R4, PC"


; ===================
; DoOsFunRenameBigDir
; ===================

; This function does OsFunRename for big dirs.  After initial work
; done by DoOsFunRename, this function is called.  We have a separate
; special function because there is extra complexity involved in
; doing the rename.

; entry:

; R2 -> new name
; R3 source dir ind disc add
; R4 entry in dir
; R5 -> dir start
; R6 -> first byte after dir

; exit if error, R0 = error, V set

DoOsFunRenameBigDir ROUT
        Push    "R1-R11, LR"
 [ DebugX :LOR: DebugXr
        DLINE   ">DoOsFunRenameBigDir"
        DREG    r3, "source dir ind disc add: "
 ]

; this bit same as for DoOsFunRename

        MOV     R7, R3
        SUB     R8, R4, R5
        BL      ReadIndDiscAdd  ;(R3,R4->LR)
        MOV     R9, LR
 [ DebugXr
        DREG    r9, "Ind disc add of object being moved: "
 ]
        BL      ReadIntAtts     ;(R3,R4->LR)
        MOV     R11,LR
        BL      ReadLen         ;(R3,R4->LR)
        MOV     R10,LR
        MOV     R2, R9
        BL      ReallyShared    ;(R2-R5->Z)
        ORREQ   R11,R11,#1 :SHL: 31
        BL      IncUsage        ;(R3)

; need stack for dir entry, and ptr to new final component and new final component length
        SUB     SP, SP, #BigDirEntrySize+4+4
        MOV     R0, R4
        MOV     R1, SP
        MOV     R2, #BigDirEntrySize
        BL      BlockMove       ; (R0-R2)

; now look up new name
        LDR     R1, [SP,#BigDirEntrySize+4+4+4] ;new name string
        AND     R2, R3, #DiscBits
        ORR     R2, R2, #MustBeRefDisc
        ORR     R2, R2, #DirToBuffer :OR: NotLastUp :OR: NotLastWild :OR: NotNulName
        BL      FullLookUp      ;(R1,R2->R0-R6,C,V)
        BVS     %FT05
        TEQS    R7, R3          ;if new obj already exists is it old obj
 [ DebugXr
        DREG    R3, "Dest ind dir: "
 ]
        SUB     LR, R4, R5
        TEQEQS  LR, R8
        BEQ     %FT07
        MOVNE   R0, #ExistsErr

05
        TEQS    R0, #NotFoundErr
        BNE     %FT85
        BL      ThisChar        ;(R1->R0,C)
        MOVCC   R0, #NotFoundErr
        BCC     %FT85           ;not terminator => not last term

07
        ; No need to explicitly ensure a new Id as this will be handled by WriteDir
        ; or writing the FSMap out.

        LDR     R0, [SP,#BigDirEntrySize+4+4+4];string start
        BL      TermStart       ;backtrack to start of last term (R0,R1->R1)

 [ DebugX :LOR: DebugXr
        DSTRING R1, "New final component:"
 ]

        STR     R1, [SP,#BigDirEntrySize]    ;final component new name

        ADD     R8, R8, R5      ;convert old entry offset to ptr
        TEQS    R3, R7
        BNE     %FT20           ;not in same dir

        ; if renaming a directory, then before we can do anything else, we'll
        ; need to ensure that there's space

        LDR     R2, [SP, #BigDirObNameLen]; get the old name length
 [ DebugX :LOR: DebugXr
        DREG    R2, "From allocated name length: "
 ]
        ADD     R2, R2, #4
        BIC     R2, R2, #3              ; allocated length

 [ DebugX :LOR: DebugXr
        DREG    R2, "Old allocated name length:"
 ]

        BL      SkipTerm                ;(R1->R0,R1)
        LDR     LR, [SP, #BigDirEntrySize]
 [ DebugX :LOR: DebugXr
        DSTRING LR, "and: "
 ]
        SUB     R1, R1, LR              ;new name length
        STR     R1, [SP, #BigDirEntrySize+4]    ; store new name length
        ADD     R1, R1, #4
        BIC     R1, R1, #3              ;new required allocated length
 [ DebugX :LOR: DebugXr
        DREG    R1, "New required:"
 ]

        SUBS    R1, R1, R2              ;extra required
 [ DebugX :LOR: DebugXr
        DREG    R1, "Extra required for name:"
 ]
        BLE     %FT09                   ;no extra needed

        TSTS    R11, #DirBit
        BEQ     %FT08                   ;if not moving a dir then no need to thingy

        Push    "R1,R3"
        MOV     R3, R9
        BL      MaybeGrowBigDirForName  ;(R1,R3->R0,R1,V)
        STR     R1, [SP, #BigDirLen+2*4]
        Pull    "R1,R3"
        BVS     %FT90

        BL      GetDir                  ; get the parent dir back
        BVS     %FT90                   ; if error then error it is


08
 [ DebugX :LOR: DebugXr
        DLINE   "Considering growing dir"
 ]
        BL      BigDirFreeSpace         ; (R5->LR)
        CMPS    LR, R1
        BLLO    GrowBigDir              ; (R3,R5,R6->R0, LR)
        BVS     %FT90                   ;

        ; because dir entry may have been altered, take a copy
        MOV     R0, R8
        MOV     R1, SP
        MOV     R2, #BigDirEntrySize
        BL      BlockMove               ;(R0,R1,R2)

09 ; here child and parent have grown as much as is required

; as case for big dirs is much harder to work out, we don't beat around the bush
        BL      InvalidateBufDir        ; going to write this dir out
        Push    "R4"
        MOV     R4, R8                  ; dir entry being removed
        BL      RemoveDirEntryOnly      ; remove just the dir entry, not allocated space
        Pull    "R4"
        CMPS    R4, R8
        SUBHI   R4, R4, #BigDirEntrySize        ; bump down ptr if needs it

   ; now make space for the term
        LDR     R1, [SP, #BigDirEntrySize] ; size of dir entry
        BL      MakeBigDirSpace         ; make space for entry

   ; now copy entry excluding bits we don't want
        MOV     R0, SP
        MOV     R1, R4
        MOV     R2, #BigDirObNameLen
        BL      BlockMove               ; copy entry

   ; finally, the name
        LDR     R1, [SP, #BigDirEntrySize]
        BL      WriteBigName            ; write the name
        BL      WriteDir
        BVS     %FT90

        BL      CanBigDirShrink
        BLEQ    ShrinkBigDir
        BVS     %FT90

   ; now, go back to the child, and update its name
        MOV     R1, R3
        BL      %FT10
        BVS     %FT90

        B       %FT80

10
; subroutine:
; job: write new name into child (and also parent dir if need be)

; R1  new parent dir
; R2  new name length
; R9  dir to rename
; R11 attributes
        Push    "R0,R1,R2,R3,R4,R6,R7,LR"
        TSTS    R11,#DirBit
        BEQ     %FT14
        MOV     R3, R9          ;child dir
        LDR     R4, [SP,#8*4+BigDirEntrySize] ;final component new name
        BL      GetDir          ;(R3->R0,R5,R6,V)
        BVS     %FT14
        BL      InvalidateBufDir
        MOV     R3, R1
        BL      WriteParent     ;(R3,R5,R6)
        LDR     R7, [R5, #BigDirNameLen]
        LDR     R6, [SP, #8*4+BigDirEntrySize+4]
        STR     R6, [R5, #BigDirNameLen]
 [ DebugX :LOR: DebugXr
        DREG    R6, "new dir name length is:"
        DREG    R7, "old dir name length is:"
 ]
        ADD     R6, R6, #4
        BIC     R6, R6, #3
        ADD     R7, R7, #4
        BIC     R7, R7, #3
        LDR     R2, [R5, #BigDirEntries]
        LDR     LR, [R5, #BigDirNamesSize]

        ASSERT  BigDirEntrySize=28
        RSB     R2, R2, R2, LSL #3      ; *7
        ADD     R2, LR, R2, LSL #2      ; *7*4=*28

        ADD     R0, R5, #BigDirHeaderSize
        ADD     R0, R0, R7
        ADD     R1, R5, #BigDirHeaderSize
        ADD     R1, R1, R6
 [ DebugX :LOR: DebugXr
        DREG    R0, "src:"
        DREG    R1, "dst:"
        DREG    R2, "len:"
 ]
        BL      BlockMove               ; (R0, R1, R2)
        MOV     R1, R6
        ADD     R0, R5, #BigDirName
        BL      ZeroRam                 ; (R0, R1)
 [ DebugX :LOR: DebugXr
        DREG    R0, "zer:"
        DREG    R1, "len:"
 ]
        LDR     R6, [R5, #BigDirNameLen]
        ADD     R7, R5, #BigDirName
        BL      PutMaskedString ;(R3,R4,R6,R7->R7)
        BL      WriteDir        ;(->R0,V)
        BVS     %FT14

        BL      CanBigDirShrink
        BLEQ    ShrinkBigDir
14
        STRVS   R0, [SP]
        Pull    "R0,R1,R2,R3,R4,R6,R7,PC"


15
; Subroutine:
;
; Job: Fill in and write out new parent dir
;
        Push    "R1,LR"
 [ DebugXr
        DLINE   "filling in and writing out new parent dir"
 ]
        LDR     R1, [SP,#2*4+BigDirEntrySize]  ;new name final component
        BL      WriteBigName       ;(R1,R4)
        ADD     R0, SP, #8      ;move copy of old dir entry back into new place
        MOV     R1, R4
        MOV     R2, #BigDirObNameLen
        BL      BlockMove       ;(R0-R2)
        AND     R0, R11,#IntAttMask
        BL      SetIntAtts      ;(R0,R3,R4) only needed for old format
        BL      IncObjSeqNum    ;(R3-R5)
 [ DebugXr
        LDR     lr, [r5, #BigDirEntries]
        DREG    lr, "entries: "
 ]
        BL      WriteDir        ;(->R0,V)
        Pull    "R1,PC"

20 ; here when not in same dir

 [ DebugX :LOR: DebugXr
        DLINE   "renaming across (big) dirs"
 ]

        TSTS    R11,#DirBit
        BEQ     %FT35

; If we're here then we're renaming a directory into a different directory
;
; Check not doing a duff rename

 [ DebugXr
        DLINE   "check for duff rename"
 ]

        MOV     R1, R3         ;if renaming dir walk up new path pathname
 [ DebugXr
        DREG    R3, "start from: "
        DREG    r9, "renaming: "
 ]
25
        BL      DiscAddToRec   ;(R3->LR)
        LDR     R0, [LR,#DiscRecord_Root]
        TEQS    R3, R0          ;if meet root dir
        TEQNES  R3, R7          ;or old parent dir
        BEQ     %FT30           ;then ok
 [ DebugXr
        DREG    R3, "compare ",cc
        DREG    R9, "with "
 ]
        TEQS    R3, R9          ;but if meet dir to rename
        MOVEQ   R0, #BadRenameErr;then moan
        BEQ     %FT85
        BL      ToParent        ;(R3,R6->R3)
 [ DebugXr
        DREG    R3, "parent is:"
 ]
        BL      FindDir         ;(R3->R0,R5,R6,V)
        BVS     %FT90
        B       %BT25
30
 [ DebugXr
        DLINE   "rename OK"
 ]
        MOV     R3, R1          ;retreive new parent dir
        BL      GetDir          ;(R3->R0,R5,R6,V)
 [ DebugXr
        DREG    R3, "retrieving new parent dir: "

 ]
        BVS     %FT90
35
; If we're here then we're renaming something into a different directory
; and its safe to do so (ie duff directory renames have been checked for)

        ; check to see if we need to grow the new parent

        LDR     R1, [SP, #BigDirEntrySize]
        BL      IsBigDirFull
        BLVS    GrowBigDir      ; need to grow new parent
        BVS     %FT90           ; can't grow it

; here have grown new parent dir.  may also need to grow child if it's a dir

        ; if renaming a directory, then before we can do anything else, we'll
        ; need to ensure that there's space

        LDR     R2, [SP, #BigDirObNameLen]; get the old name length
        ADD     R2, R2, #4
        BIC     R2, R2, #3              ; allocated length

 [ DebugX :LOR: DebugXr
        DREG    R2, "Old allocated name length:"
        DSTRING  R1, "Start with name:"
 ]

        BL      SkipTerm                ;(R1->R0,R1)
        LDR     LR, [SP, #BigDirEntrySize]
        SUB     R1, R1, LR              ;new name length
        STR     R1, [SP, #BigDirEntrySize+4]    ; store new name length
        ADD     R1, R1, #4
        BIC     R1, R1, #3              ;new required allocated length
 [ DebugX :LOR: DebugXr
        DREG    R1, "New required:"
 ]

        SUBS    R1, R1, R2              ;extra required
 [ DebugX :LOR: DebugXr
        DREG    R1, "Extra required for name:"
 ]
        BLE     %FT38                   ;no extra needed

        TSTS    R11, #DirBit
        BEQ     %FT38           ; not a dir, so don't bother

        Push    "R3"
        MOV     R3, R9
        BL      MaybeGrowBigDirForName  ;(R1,R3->R0,R1,V)
        Pull    "R3"
        BVS     %FT90

        BL      GetDir                  ; get the parent dir back
        BVS     %FT90                   ; if error then error it is

        ; dir's length may have changed, so update our copy of the entry
        STR     R1, [SP, #BigDirLen]

 [ DebugX :LOR: DebugXr
        DREG    R1, "Updated length is..."
 ]

38
; here, if child is a dir, then it's as big as needs be
        TSTS    R11,#1 :SHL: 31
        BEQ     %FT40

 [ DebugXr
        DLINE   "renaming of shared object"
 ]

; If we're here then object being renamed is shared with something else. This
; means we've got to claim new space in the destination directory for it and copy
; the renamee across.

        BL      BeforeAlterFsMap
        BVS     %FT90
        Push    "R3,R4,R11"
        MOV     R11,#fsfile_Create
        BL      ClaimFreeSpace  ;(R3-R6,R10,R11->R0,R2,V)
        MOV     R6, R2

        MOVVC   R1, R9
        MOVVC   R3, R10
        BLVC    DefaultMoveData ;(R1-R3->R0-R4,V)
 [ DebugXr
        DebugError "Error after DefaultMoveData: "
 ]
        Pull    "R3,R4,R11"

        LDRVC   R1, [SP, #BigDirEntrySize]      ; get term again
        BLVC    MakeBigDirSpace    ;(R3-R6)

        LDRVC   R1, [SP,#BigDirEntrySize] ;new name final component
        BLVC    WriteBigName            ;(R1,R4,R5)
        MOVVC   R0, SP                  ;move copy of old dir entry back into new place
        MOVVC   R1, R4
        MOVVC   R2, #BigDirObNameLen
        BLVC    BlockMove               ;(R0-R2)
        MOVVC   R0, R6                          ;adjust ind disc add to new value
        BLVC    WriteIndDiscAdd ;(R0,R3,R4)

      BL      InvalidateBufDir

 [ DebugXr
        DLINE   "about to write target dir"
        LDR     lr, [r5, #BigDirEntries]
        DREG    lr, "entries: "
        DREG    r3, "In disc add in R3:"
        BVC     %FT01
        DLINE   "V set before WriteFsMapThenDir!"
01
 ]

        BLVC    WriteFsMapThenDir       ;(->R0,V)
 [ DebugXr
        DebugError "error when writing fsmap then dir: "
 ]
        BL      UnlockMap
        B       %FT45

40
 [ DebugXr
        DLINE   "renaming of unshared object"
 ]
        LDR     R1, [SP, #BigDirEntrySize]      ; get term again
        BL      MakeBigDirSpace    ;(R3-R6)
        BL      %BT15
45
; Renaming object between directories and its already been placed in the
; destination.

 [ DebugXr
        DLINE   "object now in destination dir"
 ]

        MOVVC   R1,R3
        MOVVC   R3, R7          ;old parent dir
        BLVC    GetDir          ;(R3->R0,R5,R6,V)
        MOVVC   R4, R8
        BLVC    RemoveDirEntryOnly      ;(R3-R6->R0,V) always succeeds
        BLVC    WriteDir        ;(->R0,V)
        BVS     %FT90
        BL      CanBigDirShrink
        BLEQ    ShrinkBigDir
        BLVC    %BT10
        B       %FT90


80
        MOV     R0, #0
85
        BL      SetVOnR0
90
        ADD     SP, SP, #BigDirEntrySize+4+4   ;return dir entry buffer
        MOVVC   R0, #0
        STRVC   R0, [SP,#0]     ;zero return R1 to indicate success
        MOV     R3, R7
        BL      DecUsage        ;(R3)

 [ DebugX :LOR: DebugXr
        DLINE   "<DoOsFunRenameBigDir"
 ]
        Pull    "R1-R11,PC"

; ======================
; MaybeGrowBigDirForName
; ======================

; entry:

; R1 number of extra bytes required (word multiple)
; R3 ind disc add of dir to grow (isn't in dir buffer)

; exit:

;   either:

;     directory has been grown, R1 = new size of directory

;   or:

;     directory grow failed, R0 -> error block, V set

MaybeGrowBigDirForName ROUT
        Push    "R0,R2-R11, LR"

 [ DebugX
        DLINE   ">MaybeGrowBigDirForName"
 ]

        BL      GetDir          ; (R3->R5, R6->R0, V)
        BVS     %FT95

        BL      BigDirFreeSpace ; (R5->LR)
        CMP     R1, LR
        BLO     %FT90

        BL      GrowBigDir      ; (R3, R5, R6->R0, V)
        BVS     %FT95

90
        MOV     R0, #0
95
        BL      SetVOnR0
        STRVS   R0, [SP]
        LDRVC   R1, [R5, #BigDirSize]   ; return new size of dir
 [ DebugX
        BVC     %FT01
        DREG    R1, "New dir size: "
01
        DebugError      "Error from MaybeGrowBigDirForName: "
        DLINE   "<MaybeGrowBigDirForName"
 ]
        Pull    "R0,R2-R11, PC"


; =====================
; ReallySharedNotFudged
; =====================

;entry
; R2 ind disc add
; R3 dir
; R4 -> dir entry
; R5 -> dir start, MUST NOT BE FUDGED
; R6 -> dir entry to start at

;exit EQ <=> file is in shared frag and at least one other obj is sharing frag

;CAN'T READ IN DISC ADD FROM DIR ENTRY AS CLOSE FILE WILL HAVE ALREADY MODIFIED
;THIS IF FILE IS ZERO LENGTH

ReallySharedNotFudged ROUT
        Push    "R0,R1,R4,LR"
 [ DebugE
        DREG    R2, "ReallySharedNotFudged(disc add:",cc
        DREG    R3, ", dir:",cc
        DREG    R4, ", entry:",cc
        DREG    R5, ", dir start:",cc
        DREG    R6, ", start entry:"
 ]
        MOV     R1, R2, LSR #8
        TSTS    R2, #&FF        ;If not in shared fragment
        TOGPSR  Z_bit,LR
        BNE     %FT95
        TEQS    R1, R3, LSR #8
        BEQ     %FT95           ;sharing with parent dir
        MOV     R0, R4

        SUB     R4, R6, #BigDirEntrySize          ;get first dir entry to start at
        B       %FT10
05
        TEQS    R4, R0
        BEQ     %FT10
        BL      ReadIndDiscAdd  ;(R3,R4->LR)
        TEQS    R1, LR, LSR #8
        BEQ     %FT95           ;sharing with file
10
        ADD     R4, R4, #BigDirEntrySize
        BL      BigDirFinished
        BNE     %BT05           ;loop while more dir entries
        TOGPSR  Z_bit,LR
                        ;NE if no sharers
95
 [ DebugE
        BEQ     %FT01
        DLINE   "<-ReallyShared(not really sharing)"
        B       %FT02
01
        DLINE   "<-ReallyShared(really sharing)"
02
 ]
        Pull    "R0,R1,R4,PC"

; =========================
; ReturnWholeSpaceNotFudged
; =========================

; As ReturnSpaceNotFudged but new length=0

ReturnWholeSpaceNotFudged
        MOV     R0, #0          ; fall through to ReturnSpaceNotFudged

; ====================
; ReturnSpaceNotFudged
; ====================

; this function acts like ReturnSpace, but avoids the nasty fudge
; of the start of directory to ReallyShared (which is a bit nasty)

; it can only be used for Big dirs.  it must be

; entry:
;  R0 new file length
;  R1 old file length
;  R2 Ind Disc address of file
;  R3 dir ind disc add
;  R4 -> dir entry
;  R5 -> dir start, MUST NOT BE FUDGED
;  r6 -> dir entry to start search at for ReallySharedNotFudged

; exit: IF error V set, R0 result


ReturnSpaceNotFudged ROUT
 [ Debug5 :LOR: DebugX
        DLINE   "new len :old len :ind disc:dir     :DirEntry:DirStart enter ReturnSpaceNotFudged"
        DREG   R0," ",cc
        DREG   R1," ",cc
        DREG   R2," ",cc
        DREG   R3," ",cc
        DREG   R4," ",cc
        DREG   R5," ",cc
        DREG   R6," "
 ]
        Push    "R0-R4,R6-R11,LR"
        BL      RoundUpAlloc    ;(R0,R3->R0)
        MOV     R6, R0
        MOV     R0, R1
        BL      RoundUpAlloc    ;(R0,R3->R0)
      [ BigFiles
        TEQ     R6, #RoundedTo4G
        MOVEQ   R7, #&FFFFFFFF
        MOVNE   R7, R6
        TEQ     R0, #RoundedTo4G
        MOVEQ   R1, #&FFFFFFFF
        MOVNE   R1, R0

        SUBS    LR, R1, R7
      |
        SUBS    R1, R0, R6      ;length of returned space
      ]
        BLS     %FT90           ;nothing to return

        TSTS    R2, #&FF
        BEQ     %FT15           ;not shared obj

        TEQS    R6, #0
        BNE     %FT90           ;IF not returning whole obj then done

        Push    "R6"
        LDR     R6, [SP, #6*4]           ;recover entry R4
        BL      ReallySharedNotFudged    ;(R2-R6->Z)
        Pull    "R6"
        BEQ     %FT90           ;IF frag shared then done

        BIC     R2, R2, #&FF
15
        BL      CritInitReadNewFs   ;(->R10,R11)
        LDRB    LR, [R10,#ZoneHead+DiscRecord_Log2bpmb]
      [ BigFiles
        TEQ     R6, #RoundedTo4G
        ASSERT  RoundedTo4G = 1
        MOVEQ   R6, R6, ROR LR
        MOVNE   R6, R6, LSR LR  ;new length in map bits
        TEQ     R0, #RoundedTo4G
        MOVEQ   R7, R0, ROR LR
        MOVNE   R7, R0, LSR LR  ;old length in map bits
      |
        MOV     R6, R6, LSR LR  ;new length in map bits
        MOV     R7, R0, LSR LR  ;old length in map bits
      ]
        MOV     R9, #-1         ;don't know about pre gaps yet
20
        MOV     R1, R6
        BL      FindFragment    ;(R1,R2,R9,R10->R1,R9,R11,LR)
        SUB     R0, R1, R11     ;new length
        ADD     R1, R0, LR      ;old length = new length + length returning
        Push    "R2"
        MOV     R2, R11
        SUB     R7, R7, LR      ;decrement length left, may go -ve
        MOV     R3, R9
        BL      ShortenFrag     ;(R0-R3,R10)
        Pull    "R2"
        CMPS    R6, R7          ;loop until whole file returned
        BLT     %BT20           ;LT rather than LO as R7 may be -ve
        BL      InvalidateFragCache
90
        MOV     R0, #0
95
        BL      SetVOnR0
 [ Debug5 :LOR: DebugX
        BVC     %FT96
        DREG   R0," ",cc
96
        DLINE "leave ReturnSpaceNotFudged"
 ]
        STRVS   R0, [SP]
        Pull    "R0-R4,R6-R11,PC"


 [ :LNOT: BigDirFullBackup

; ================
; BigDirBackupMove
; ================

; construct the backup entries

; entry:

; r0 - source addr
; r1 - dest addr
; r2 - number of bytes (word multiple)

BigDirBackupMove ROUT
        Push    "R0-R2,LR"

        CMP     R2, #0
        BEQ     %FT95

        ADD     R0, R0, #BigDirIndDiscAdd

10
        LDR     LR, [R0], #BigDirEntrySize
        SUBS    R2, R2, #4
        STR     LR, [R1], #4
        BNE     %BT10
95
        Pull    "R0-R2,PC"
 ]

 ] ; BigDir

        LTORG
        END
