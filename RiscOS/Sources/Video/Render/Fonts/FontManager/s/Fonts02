; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > Sources.Fonts02

; *********************************
; ***  CHANGE LIST AND HISTORY  ***
; *********************************
;
; 16-Jun-94   AMcC  Replaced ScratchSpaceSize with ?ScratchSpace
;

;;----------------------------------------------------------------------------
;; Decode incoming characters
;;----------------------------------------------------------------------------

; offsets into oswrch block

vdu23length     *       9

; VDU 23,26

fontnumber      *       1
pointsize       *       2
overrideX       *       3
overrideY       *       4
xscaling        *       5
yscaling        *       6

scbits          *       4               ; shift factor for scaling
maxfactor       *       8000            ; avoid overflow when scaling pixels

; VDU 23,25,&80,...

pal_background  *       1               ; &80 + background
pal_foreground  *       2               ; foreground
pal_backRGB     *       3               ; background RGB
pal_foreRGB     *       6               ; foreground RGB

; VDU 23,25,0-3,...

vdu25thresh     *       2               ; VDU 23,25,0-3,<thresholds>

; VDU 25,&D0-&D7

vdu25length     *       5

plotcode        *       0
xcoordinate     *       1
ycoordinate     *       3

; for Font_Paint

paint_justify   *       1 :SHL: 0       ; if 1, do justification
paint_rubout    *       1 :SHL: 1       ; if 1, do rubout box
paint_osunits   *       1 :SHL: 4       ; if 0, then use 1/72000"
paint_coordblk  *       1 :SHL: 5       ; if 1, then use coordinate block
paint_matrix    *       1 :SHL: 6       ; if 1, R6 -> matrix
paint_length    *       1 :SHL: 7       ; if 1, R7 = length of string
paint_font      *       1 :SHL: 8       ; if 1, R0 = initial font handle
paint_kern      *       1 :SHL: 9       ; if 1, do kerning
paint_rightleft *       1 :SHL: 10      ; if 1, paint from right to left
paint_blended	*	1 :SHL: 11	; if 1, paint blending to background present
paint_16bit     *       1 :SHL: 12      ; if 1, string consists of 16-bit shorts
paint_32bit     *       1 :SHL: 13      ; if 1, string consists of 32-bit longs (can't have both bits set)
paint_notyetimp *       0
paint_blendsupr *       1 :SHL: 14      ; if 1, extend blending to handle supremacy
paint_res1      EqBits  15,31           ; bits 15..31 (inclusive)
paint_reserved  *       paint_res1 :OR: paint_notyetimp

; for Font_Caret

caret_osunits   *       1 :SHL: 4       ; if 0, then use 1/72000"
caret_double    *       1 :SHL: 5       ; if 1, make caret double-thickness

bignum          *       &20000000

bigtime         *       &10000000       ; bigtime * 2 is used in cachebitmaps

; impossible value of aliascolours => set from RGB values

invalid_flag    *       &80

; values to put in setoutputdataflag

setout_invalid  *       0
setout_valid    *       1

setout_badblending * 	&80		; if 1, then blending data is not valid

; escape codes within a string to be printed

; null          *          &00
MoveX           *       &09             ;  9, n,n,n (move in x-direction)
; lf            *          &0A
MoveY           *       &0B             ; 11, n,n,n (move in y-direction)
; cr            *          &0D
Colour          *       &11             ; 17, bc/fc
Colour3         *       &12             ; 18, bc,fc,steps
ColourRGB       *       &13             ; 19, bg_R, bg_G, bg_B, fg_R, fg_G, fg_B, maxoffset
Comment         *       &15             ; 21, <ignore until ctrl-char>
Underline       *       &19             ; 25, <n/256 posn>, <n/256 size>
Fontchar        *       &1A             ; 26, <font handle>
Matrix00        *       &1B             ; 27, <align>, <4-element matrix>
Matrix          *       &1C             ; 28, <align>, <6-element matrix>

;;----------------------------------------------------------------------------
;; oswrch entry point
;;----------------------------------------------------------------------------

fx3code *       &EC
fx3bit  *       &20

setfx3  Entry ""

        STRB    R0,wrchvflag
        STR     R1,wrchblkptr
        MOV     R0,#0
        STRB    R0,wrchindex
        STRB    R0,wrchflag

        MOV     R0,#fx3code
        MOV     R1,#fx3bit
        MOV     R2,#fx3bit:EOR:&FF
        SWI     XOS_Byte

        MOV     R0,#-1
        STR     R0,fx3flag

        Claim   VDUXV,VDUXVentry

        EXIT

;.............................................................................

clrfx3  Entry "R0-R2"

        MOV     R0,#fx3code
        MOV     R1,#0
        MOV     R2,#fx3bit:EOR:&FF
        SWI     XOS_Byte

        MOV     R0,#0
        STR     R0,fx3flag

        Release VDUXV,VDUXVentry

        EXIT

;;----------------------------------------------------------------------------
;; entry points via UKVDU23V, UKPLOTV, VDUXV
;; Entry:  R0 = character
;;         R1 --> data collected by VDU handler
;;         R12 --> workspace (set up when vector claimed)
;;----------------------------------------------------------------------------

errunrec
        MOV     PC,LR                           ; pass it on!

UKVDU23Ventry   ROUT

        CMP     R0,#25
        BEQ     isvdu2325
        CMP     R0,#26
        BNE     errunrec

        Push    "R0-R2,wsptr"

        MOV     R0,#23
        BL      setfx3                          ; collect the string following
        Pull    "R0-R2,wsptr,PC"                ; exit

isvdu2325
        Push    "R0-R12"

        MOV     wrchbase,R1
        LDRB    R1,[wrchbase,#pal_background]
        CMP     R1,#&80
        BCS     isvdu2325setpal
        ADD     wrchbase,wrchbase,#vdu25thresh
        TEQ     R1,#4                           ; if 4 bits per pixel,
        ADREQL  wrchbase,thresholds+th16        ; use default settings
        MOV     R14,#1
        MOV     aliascols,R14,ASL R1            ; compute no of colours (=2^bpp)
        SUB     aliascols,aliascols,#2          ; offset from 1st to last colour
        BL      settransfer                     ; VDU 23,25,bits,t1,t2,t3,t4,t5,t6,t7

vdu2325exit
        PullS   "R0-R12"                        ; ignore next vector user

isvdu2325setpal
        LDRB    R1,[wrchbase,#pal_background]
        AND     R1,R1,#&7F                      ; R1 = background colour (7 bits)
        LDRB    R2,[wrchbase,#pal_foreground]   ; R2 = foreground colour (8 bits)
        STRB    R1,backcolour
        STRB    R2,forecolour
        MOV     R14,#setout_invalid             ; must always do this cos of Font_SetColourTable
        STRB    R14,setoutputdataflag

        LDRB    R3,aliascolours                 ; R3 = aliascolours (between -14 and 14)
        MOV     R3,R3,LSL #24
        MOV     R3,R3,ASR #24                   ; (sign-extended)

        LDRB    R4,[wrchbase,#pal_backRGB]
        MOV     R4,R4,LSL #8
        LDRB    R14,[wrchbase,#pal_backRGB+1]
        ORR     R4,R4,R14,LSL #16
        LDRB    R14,[wrchbase,#pal_backRGB+2]
        ORR     R4,R4,R14,LSL #24               ; R4 = background RGB

        LDRB    R5,[wrchbase,#pal_foreRGB]
        MOV     R5,R5,LSL #8
        LDRB    R14,[wrchbase,#pal_foreRGB+1]
        ORR     R5,R5,R14,LSL #16
        LDRB    R14,[wrchbase,#pal_foreRGB+2]
        ORR     R5,R5,R14,LSL #24               ; R5 = foreground RGB

        LDR     R14,=&F0F0F000                  ; compatibility: only believe top 12 bits of palette
        AND     R4,R4,R14
        AND     R5,R5,R14
        ORR     R4,R4,R4,LSR #4
        ORR     R5,R5,R5,LSR #4                 ; make bottom nibbles a copy of the top ones

        BL      setpalette                      ; doesn't preserve flags

        B       vdu2325exit

UKPLOTVentry    ROUT

        Push    "R0"
        SUB     R0,R0,#&D0              ; decode plot type
        CMP     R0,#&08
        Pull    "R0"
        BCS     errunrec                ; not for me!

        Push    "R0-R2,wsptr"

        AND     R0,R0,#7                ; only lowest 3 bits needed
        STR     R0,plottype             ; used later

        BL      checkblend

        BL      setmodedata             ; do this now (for currentpoint etc.)
        MOVVC   R0,#25
        BLVC    setfx3

        STRVS   R0,[sp]                 ; bodge stack if error
        Pull    "R0-R2,wsptr,PC"

VDUXVentry      ROUT

        Push    "R0-R3,wrchbase,wsptr"

        ADR     wrchbase,wrchblk
        LDRB    R2,wrchindex

        ORRS    R3,R2,R0                ; strip leading zeros
        BEQ     ordwrchexit

        LDRB    R3,wrchflag             ; if wrchflag = &FF,
        CMP     R3,#&FF                 ; we are in a VDU 21 sequence
        BNE     %FT01

        CMP     R0,#32                  ; terminate sequence o
        MOVCC   R14,#0
        STRCCB  R14,wrchflag
        B       putchar

01      SUBS    R3,R3,#1
        STRGEB  R3,wrchflag             ; update if >= 0
        BGE     putchar                 ; still in a sequence

        TEQ     R0,#Colour              ; set up wrchflag if escape code found
        TEQNE   R0,#Fontchar
        MOVEQ   R3,#1
        STREQB  R3,wrchflag             ; absorb next byte (colour/font)
        BEQ     putchar

        TEQ     R0,#Underline
        MOVEQ   R3,#2
        STREQB  R3,wrchflag             ; absorb posn/size
        BEQ     putchar

        TEQ     R0,#ColourRGB           ; 19, r,g,b,R,G,B,maxoffset
        MOVEQ   R3,#7
        BEQ     %FT11

        TEQ     R0,#Colour3             ; 18, bg, fg, offset
        TEQNE   R0,#MoveX               ;  9, x0, x1, x2
        TEQNE   R0,#MoveY               ; 11, y0, y1, y2
        MOVEQ   R3,#3

11      STREQB  R3,wrchflag             ; absorb next 3 bytes
        BEQ     putchar

        TEQ     R0,#Comment
        MOVEQ   R3,#&FF                 ; set to &FF to ignore chars till ctrl
        STREQB  R3,wrchflag
        BEQ     putchar

        CMP     R0,#32                  ; buffer char unless terminator
        BCS     putchar

        STRB    R0,[wrchbase,R2]        ; put byte into block

        BL      clrfx3                  ; don't forget to stop intercepting!!!

        Push    "R4-R10"                ; save missing registers

        LDR     wrchbase,wrchblkptr     ; original oswrch block
        LDRB    R1,wrchvflag
        CMP     R1,#25                  ; plot code
        BEQ     isvdu25paintchars

; set up parameters for DefineFont
; calculate x,y scaling

        LDRB    R0,[wrchbase,#pointsize]
        LDRB    R1,[wrchbase,#xscaling]
        CMP     R1,#0
        MOVEQ   R1,#1:SHL:scbits         ; scaling is shifted by 4 bits
        MUL     R3,R0,R1
        STR     R3,xscale

        LDRB    R0,[wrchbase,#pointsize]
        LDRB    R1,[wrchbase,#yscaling]
        CMP     R1,#0
        MOVEQ   R1,#1:SHL:scbits        ; scaling is shifted by 4 bits
        MUL     R3,R0,R1
        STR     R3,yscale

        LDRB    R0,[wrchbase,#fontnumber]
        ADR     R1,wrchblk              ; pointer to name
        LDR     R2,xscale
        LDR     R3,yscale
        LDRB    R4,[wrchbase,#overrideX]
        LDRB    R5,[wrchbase,#overrideY]
        BL      setmodedata
        BLVC    DefineFont
        B       wrchexit

; either put character into buffer or forget it

putchar
        STRB    R0,[wrchbase,R2]        ; put byte into block
        ADD     R2,R2,#1                ; increment index
        CMP     R2,#256                 ; check within a page
        BCS     errtoolong
        STRB    R2,wrchindex            ; and store

ordwrchexit
        CLRPSR  C_bit,R14               ; don't send to printer !!!
        STRVS   R0,[sp]                 ; bodge stack if error
        Pull    "R0-R3,wrchbase,wsptr,PC"

isvdu25paintchars
        LDR     xcoord,x_new            ; convert OS coords to 1/72000ths inch
        LDR     ycoord,y_new
        BL      scaleyco
        MOV     R4,R3                   ; R4 = y coordinate
        BL      scalexco                ; R3 = x coordinate
        ADR     R1,wrchblk
        LDR     R2,plottype

        SWI     XOS_RemoveCursors
        BLVC    paintchars              ; ... drop through

        BL      restoreoutput           ; in case outline chars were cached

        SavePSR R4
        SWI     XOS_RestoreCursors
        RestPSR R4,VC,f

wrchexit
        CLRPSR  C_bit,R14               ; don't send to printer !!!
        Pull    "R4-R10"
        STRVS   R0,[sp]                 ; bodge stack if error
        Pull    "R0-R3,wrchbase,wsptr,PC"

errtoolong
        BL      clrfx3
        BL      xerr_buffoverflow
        B       ordwrchexit


;;----------------------------------------------------------------------------
;; setpalette
;; In   R1 = background colour
;;      R2 = foreground colour
;;      R3 = colour offset (abs(R3) = number of intermediate colours)
;;      R4 = background RGB (true 24-bit palette)
;;      R5 = foreground RGB (true 24-bit palette)
;; Out  In non-256 colour modes, palette value for given logical colours are set up
;;      In 256 colour modes, pseudo-palette entry R2 set to given values
;;      VC => R0 corrupt
;;      VS => R0 -> error block
;;----------------------------------------------------------------------------

                ^       0
psb_aliascolours   #    4                       ; 0 => monochrome, else anti-aliased
psb_backRGB     #       4
psb_foreRGB     #       4
psb_table       #       4 * 16                  ; outputdata table to be used
psb_size        #       0

setpalette PEntry Paint_Palette, "R1-R9"

        Debug   rgb,"setpalette:",R1,R2,R3,R4,R5

        BL      setmodedata

        LDRB    R14,bitsperpixel
        CMP     R14,#8
        BLT     gosetrealpalette

; pseudo-palette entry: first obtain a block for the appropriate entry

        CMP     R2,#maxpal                      ; max no of pseudo-colours
        BLCS    errpseudopal
        PExit   VS

        ADR     R1,pseudopalettes
        LDR     R2,[R1,R2,LSL #2]!              ; R1 -> anchor, R2 -> existing block, or 0

        TEQ     R2,#0
        MOVEQ   R0,#ModHandReason_Claim
        MOVEQ   R3,#psb_size
        SWIEQ   XOS_Module
        PExit   VS

        STR     R2,[R1]                         ; link into table

        ASSERT  psb_aliascolours = 0
        ASSERT  psb_backRGB = 4
        ASSERT  psb_foreRGB = 8
        LDR     R3,[sp,#2*4]                    ; recover R3 (aliascolours) from stack
        CMP     R3,#0
        RSBMI   R3,R3,#0                        ; R3 = number of intermediate colours (sign is meaningless)
        STMIA   R2!,{R3,R4,R5}                  ; remember number of colours and RGB values

; now separate into R<<16,G<<16,B<<16 and Rstep,Gstep,Bstep

        ADRL    R9,threshoffsets
        LDRB    R14,[R9,R3]
        ADR     R9,thresholds
        ADD     R9,R9,R14                       ; R9 -> threshold table for this number of colours (for storeoutput)

        MOV     R0,R4                           ; store background colour first,
        BL      getoutputfromRGB                ; R0 := value to store in table
        MOVVC   R1,#0
        BLVC    storeoutput                     ; store as offset R1 out of a total of R3+2 entries

        MOVVC   R0,R5                           ; store foreground colour at end of table
        BLVC    getoutputfromRGB
        ADDVC   R1,R3,#1
        BLVC    storeoutput                     ; store as offset R1 out of a total of R3+2 entries
        PExit   VS

        SUBS    R1,R1,#1                        ; finished if there were only 2 colours
        PExit   EQ

        BL      getstepvalues                   ; R3,R4,R5 => R3,R4,R5,R6,R7,R8 (red,green,blue << 16)

01      ADD     R3,R3,R6                        ; steps go from foreground to background
        ADD     R4,R4,R7
        ADD     R5,R5,R8
        BL      getRGBfromR3R4R5                ; R0 = RGB equivalent of R3<<8,R4<<16,R5<<24
        BL      getoutputfromRGB
        BLVC    storeoutput                     ; store as offset R1 out of a total of R3+2 entries
        PExit   VS
        SUBS    R1,R1,#1
        BNE     %BT01

        PExit

; In:   R1 = background logical colour
;       R2 = first foreground logical colour
;       R3 = offset to last foreground colour
;       R4 = background RGB to set
;       R5 = foreground RGB to set

gosetrealpalette
        MOV     R0,R4
        BL      setrealpalette          ; R0 = RGB, R1 = logical colour
        PExit   VS

        ADD     R1,R2,R3                ; R1 = last foreground colour, R2 = first foreground colour
        MOVS    R9,R3,ASR #31
        MOVPL   R9,#1                   ; R9 = SGN(R3)
        RSBMI   R3,R3,#0                ; R3 = ABS(R3)

        BL      getstepvalues

02      BL      getRGBfromR3R4R5        ; R0 = RGB equivalent of R3,R4,R5
        BL      setrealpalette
        PExit   VS
        CMP     R1,R2                   ; unless we've finished,
        SUBNE   R1,R1,R9                ; step back one more colour
        ADDNE   R3,R3,R6
        ADDNE   R4,R4,R7                ; increment colours
        ADDNE   R5,R5,R8
        BNE     %BT02                   ; and loop

        PExit

;......................................................................

errpseudopal ROUT

        ADR     R0,ErrorBlock_FontPalTooBig
        B       MyGenerateError
        MakeErrorBlock FontPalTooBig    ; "Invalid font colour"

;.........................................................................

; In    R3 = number of steps - 1 (this routine is not normally called if R3=0)
;       R4 = last RGB colour
;       R5 = first RGB colour  (we actually go from the foreground colour backwards)
; Out   R3,R4,R5 = Red<<16,Green<<16,Blue<<16 derived from R5
;       R6,R7,R8 = appropriate step values for R3,R4,R5

        MACRO
$l      Get_RGB_16  $red, $green, $blue, $RGB
$l
        ASSERT  $RGB <> $red
        ASSERT  $RGB <> $green

        Debuga  rgb,"Get_RGB_16: in",$RGB

        AND     $red,$RGB,#&FF00
        MOV     $red,$red,LSL #8                ; $red = red << 16
        AND     $green,$RGB,#&FF0000            ; $green = green << 16
        AND     $blue,$RGB,#&FF000000
        MOV     $blue,$blue,LSR #8              ; $blue = blue << 16

        Debug   rgb," out",$red,$green,$blue

        MEND

getstepvalues Entry "R0-R2"

        ADD     R1,R3,#1                        ; R1 = divisor

        Get_RGB_16 R6,R7,R8, R4
        Get_RGB_16 R3,R4,R5, R5

        SUB     R0,R6,R3
        BL      divide
        MOV     R6,R2                           ; R6 = red step

        SUB     R0,R7,R4
        BL      divide
        MOV     R7,R2                           ; R7 = green step

        SUB     R0,R8,R5
        BL      divide
        MOV     R8,R2                           ; R8 = blue step

        Debug   rgb,"rstep,gstep,bstep, nsteps =",R6,R7,R8,R1

        EXIT

;.........................................................................

; In    R3,R4,R5 = red, green, blue << 16
; Out   R0 = &BBGGRR00 equivalent

        MACRO
$l      GetGun  $rd, $rs
$l      MOVS    $rd, $rs, ASR #16
        MOVMI   $rd, #0                 ; be paranoid about over/underflow
        CMP     $rd, #&FF
        MOVGT   $rd, #&FF
        MEND

getRGBfromR3R4R5 Entry ""

        GetGun  R14,R3
        MOV     R0,R14,LSL #8

        GetGun  R14,R4
        ORR     R0,R0,R14,LSL #16

        GetGun  R14,R5
        ORR     R0,R0,R14,LSL #24

        EXIT

;.........................................................................

; In    R0 = RGB value to convert
; Out   R0 = colour number << (32-bpp)

getoutputfromRGB Entry "R1-R2"

        MOV     R2,R0                           ; in case error trashes R0

        Debuga  rgb,"ColourTrans_ReturnColourNumber: in",R0
        SWI     XColourTrans_ReturnColourNumber
        Debug   rgb,", out",R0
        LDRVCB  R14,bitsperpixel                ; this way it even copes with 16 and 32-bit modes!
        RSBVC   R14,R14,#32
        MOVVC   R0,R0,LSL R14
        EXIT    VC

        LDR     R1,[R0]                         ; report error unless "No such SWI"
        LDR     R14,=ErrorNumber_NoSuchSWI
        TEQ     R1,R14
        EXIT    NE

; R2 = &BBGGRR00 : convert from  Bbmn/GGmn/Rrmn/0000
;                            to  BGGRbrmn/00000000/00000000/00000000

        MOV     R0,#0
        TST     R2,#&80000000                   ; bit 7 of blue
        ORRNE   R0,R0,#&80000000
        TST     R2,#&00800000                   ; bit 7 of green
        ORRNE   R0,R0,#&40000000
        TST     R2,#&00400000                   ; bit 6 of green
        ORRNE   R0,R0,#&20000000
        TST     R2,#&00008000                   ; bit 7 of red
        ORRNE   R0,R0,#&10000000
        TST     R2,#&40000000                   ; bit 6 of blue
        ORRNE   R0,R0,#&08000000
        TST     R2,#&00004000                   ; bit 6 of red
        ORRNE   R0,R0,#&04000000

        AND     R1,R2,#&30000000                ; R1 bits 28,29 = blue bits 4,5
        AND     R14,R2,#&00300000
        ADD     R1,R1,R14,LSL #8+1              ; green has 2* weighting
        AND     R14,R2,#&00003000
        ADD     R1,R1,R14,LSL #16               ; R1 = red+2*green+blue << 28
        ADD     R1,R1,#&20000000                ; add 2 to round to nearest
        AND     R1,R1,#&C0000000                ; R1 bits 30,31 = tint

        ORR     R0,R0,R1,LSR #6                 ; R0 = BGGRbrmn << 24 (value for output table)

        Debug   rgb,"getoutputfromRGB:",R0

        CLRV
        EXIT
        LTORG

;.........................................................................

; In    R0 = value to store in output data table
;       R1 = which colour to store (0..R3+1)
;       R9 -> threshold table to use (depends on total number of colours)
; Out   [R2..] updated in appropriate positions (with reference to thresholds)

storeoutput Entry "R1-R3,R9"

        LDRB    R3,[R9,R1]!             ; R3 = index of last colour to store + 1
        CMP     R3,#16
        MOVGT   R3,#16                  ; last colour is always 16 (actually stored as &FF !!!)
        TEQ     R1,#0
        LDRNEB  R1,[R9,#-1]             ; R1 = index of first colour to store

        Debug   rgb,"storeoutput: value, from, to",R0,R1,R3

        SUBS    R3,R3,R1                ; R3 = number of colours to store
        ADDNE   R2,R2,R1,LSL #2
01      STRNE   R0,[R2],#4              ; store output in [R2]
        SUBNES  R3,R3,#1
        BNE     %BT01

        CLRV
        EXIT

;......................................................................

; In    R0 = RGB value to set (24-bit true colour)
;       R1 = logical colour number
; Out   ColourTrans called to set the palette
;       If that fails, PaletteV is called
;       and if that fails, OS_Word 12 is called directly
;       VC => R0 corrupt
;       VS => R0 ->error block

setrealpalette Entry "R1-R4,R9"

      [ usesetpalette
        ORR     R4,R1,#16 << 24         ; R4 bits 24..31 <> 0 => set a single entry
        MOV     R2,R0                   ; R2 = first flash state
        MOV     R3,R0                   ; R3 = second flash state
        MOV     R0,#-1                  ; R0 = -1 => write to screen
        MOV     R1,#-1                  ; R1 = -1 => current palette
        Debug   rgb,"Calling ColourTrans_WritePalette with",R0,R1,R2,R3,R4
        SWI     XColourTrans_WritePalette
        EXIT    VC

        LDR     R1,[R0]                 ; unless error = "No such SWI", report it
        LDR     R14,=ErrorNumber_NoSuchSWI
        TEQ     R1,R14
        EXIT    NE

        BIC     R0,R4,#&FF000000        ; R0 = logical colour
      |
        ! 0,"Does not use ColourTrans_WritePalette"

        MOV     R2,R0                   ; R2 = first flash state
        MOV     R3,R0                   ; R3 = second flash state
        MOV     R0,R1                   ; R0 = logicl colour
      ]

        MOV     R1,#16                  ; R1 = type (16), R2,R3 = RGB values
        MOV     R4,#2                   ; R4 = 2 => write palette
        Debug   rgb,"Calling PaletteV with",R0,R1,R2,R3,R4
        MOV     R9,#PaletteV
        SWI     XOS_CallAVector
        EXIT    VS

        TEQ     R4,#0
        EXIT    EQ

        AND     R14,R0,#&FF
        AND     R4,R1,#&FF
        ORR     R4,R14,R4,LSL #8
        BIC     R14,R2,#&FF             ; R14 = &BBGGRR00
        ORR     R4,R4,R14,LSL #8        ; R4  = &GGRRr1r0 (green,red,R1,R0)
        MOV     R14,R2,LSR #24          ; R14 = &000000BB (blue)
        Push    "R0,R1,R4,R14"
        Debug   rgb,"OS_Word 12:",R0,R1,R4,R14
        ADD     R1,sp,#2*4              ; R1 -> block
        MOV     R0,#12                  ; write palette
        SWI     XOS_Word
        Pull    "R0,R1,R4,R14",VC
        EXIT    VC

        STR     R0,[sp]
        Pull    "R0,R1,R4,R14"
        EXIT

;;----------------------------------------------------------------------------
;; settransfer
;; Entry:  aliascols = offset from 1st to last colour (-14 to +14 in 8 bits)
;;         wrchbase --> t1 ... t14
;;    Sets up threshold values for the given number of bits per pixel
;;    - set font/colour actually uses them
;;----------------------------------------------------------------------------

settransfer Entry "R1,aliascols,wrchbase"

        Debug   rgb,"settransfer: aliascols =",aliascols

        MOV     R14,aliascols
        CMP     R14,#128
        RSBGE   R14,R14,#256
        CMP     R14,#14                         ; max value
        BGT     %FT01                           ; error if too big

        MOV     R14,#setout_invalid
        STRB    R14,setoutputdataflag

        STRB    aliascols,aliascolours
        ADD     aliascols,R14,#1                ; use as counter now
        ADR     R1,threshoffsets
        LDRB    R14,[R1,R14]
        ADR     R1,thresholds
        ADD     R1,R1,R14                       ; R1 --> thresholds

copythresh
        SUBS    aliascols,aliascols,#1
        MOVMI   R14,#&FF                        ; last one is terminator
        LDRPLB  R14,[wrchbase],#1
        STRB    R14,[R1],#1
        BPL     copythresh

        EXIT

01      BL      xerr_FontBadTranBits
        EXIT

threshoffsets
        DCB     th2,th3,th4,th5,th6,th7,th8,th9
        DCB     th10,th11,th12,th13,th14,th15,th16
        ALIGN

;;----------------------------------------------------------------------------
;; Data area set up by SWI ReadVduVariables
;;----------------------------------------------------------------------------

vduinputbuffer
                DCD     VduExt_ScreenStart
                DCD     VduExt_GWLCol           ; gx0
                DCD     VduExt_GWBRow           ; gy0
                DCD     VduExt_GWRCol           ; gx1
                DCD     VduExt_GWTRow           ; gy1

                DCD     VduExt_OlderCsX
                DCD     VduExt_OlderCsY
                DCD     VduExt_OldCsX
                DCD     VduExt_OldCsY
                DCD     VduExt_GCsIX
                DCD     VduExt_GCsIY
                DCD     VduExt_NewPtX
                DCD     VduExt_NewPtY

                DCD     VduExt_NColour
                DCD     VduExt_ModeFlags

                ASSERT  modedata_h - vduoutputbuffer = . - vduinputbuffer
                DCD     VduExt_Log2BPP           ; derive XshftFactor from this
                DCD     VduExt_XEigFactor
                DCD     VduExt_YEigFactor
                DCD     VduExt_LineLength
                DCD     VduExt_YWindLimit       ; max internal y-coordinate
                DCD     -1                      ; terminator


;;-----------------------------------------------------------------------------
;; Routine to read VDU variables
;;
;; Exit:  bitsperpixel  = no. of bits per pixel
;;        Pixelsperword = no. of pixels per word
;;        linelen       = length of line (bytes)
;;
;; Registers:  hshift  = pixel columns --> words
;;             vmul    = pixel rows    --> byte offset
;;
;; All registers are preserved, since they overlap with wrchbase etc.
;;
;;----------------------------------------------------------------------------

setmodedata PEntry Paint_Mode,"R0-R11"

; this shouldn't ever happen, but I'm paranoid

        BL      restoreoutput                   ; in case output was switched to the buffer sprite

; read the relevant VDU variables using Tim's new SWI!!!

        ADR     R0,vduinputbuffer
        ADR     R1,vduoutputbuffer
        SWI     XOS_ReadVduVariables
      [ debugprofile
        STRVS   R0,[sp]
        PExit   VS
      |
        Pull    "R14",VS                        ; leave R0 alone
        Pull    "R1-R11,PC",VS
      ]

; now derive the ones he didn't give me

        LDR     hshift,modedata_h               ; ie. Log2BPP
        STR     hshift,log2bpp
        MOV     bpp,#1
        MOV     bpp,bpp,ASL hshift              ; bpp = 2^Log2BPP
        LDRB    R14,bitsperpixel                ; outputdata invalid now if bpp has changed
        TEQ     bpp,R14
        MOVNE   R14,#setout_invalid
        STRNEB  R14,setoutputdataflag
        STRB    bpp,bitsperpixel
        MOV     R0,#32                          ; calculate pixels per word
        MOV     R0,R0,ASR hshift
        STRB    R0,Pixelsperword
        RSB     hshift,hshift,#3+2              ; pixels>>hshift = bytes/4
        STR     hshift,modedata_h

        Debug   ch2,"bpp,ppw =",bpp,R0

        Debug   ch2,"hshift,vmul,linelen, =",hshift,vmul,#linelen
        Debug   ch2,"YWindLimit =",#ywindlimit

        MOV     R0,#&80000000                 ; construct mask word
        SUBS    bpp,bpp,#1
        MOVNE   R0,R0,ASR bpp                 ; duplicate bit
        STR     R0,outputmask

        Debug   ch2,"outputmask =",R0

        PExit
        LTORG


;;----------------------------------------------------------------------------
;; setoutputdata
;;
;; This routine does the donkey work with the transfer function
;; and text colour: it generates the values in [outputdata]
;;
;; Entry: currentfont  = number of current font
;;        backcolour   = background logical colour (&7F --> pseudo-palette)
;;        forecolour   = foreground logical colour (or pseudo-colour)
;;        aliascolours = offset from 1st fcol to last (can be -ve)
;;        bitsperpixel = no. of bits per pixel (depends on mode)
;;
;; Exit:  [outputdata] = 16 words corresponding to correct output
;;        outputmask   = word which masks out top (bpp) bits
;;        [rubdata]    = background colour extended to all pixels in word
;;
;;----------------------------------------------------------------------------

trysetoutputdata Entry ""

;; David De Vorchik (18dec95)
;; --------------------------
;; We must ensure that although the output data is currently valid, the blending
;; information has been correctly initialised as-well.  The only way this can become
;; confused is if the client calls Font_SetColourTable which allows them to directly
;; abused the colour information, yet leave the setout_valid flag set.
;;
;; Therefore we have introduced a flag bit which when set indicates that although
;; the data is valid, the blending information is not, therefore 'setblendingdata'
;; should be called.

	LDRB	R14,setoutputdataflag
	TST	R14,#setout_badblending		; are the blend_putdata vectors valid?
	BEQ	%FT10

	BIC	R14,R14,#setout_badblending	; blending data will now be validated
	STRB	R14,setoutputdataflag		; therefore clear the marker bit

	TEQ	R14,#setout_invalid
        BEQ     %FT10
	BL	setblendingdata			; don't call more than once!
	EXIT
10
        TEQ     R14,#setout_invalid
        EXIT    NE

        PullEnv                                 ; drop through

setoutputdata PEntryS Paint_Colours, "R0-R12"   ; on the safe side

      [ debugpal :LOR: debugrgb
        LDRB    R1,backcolour
        LDRB    R2,forecolour
        LDRB    R3,aliascolours
        Debug   pal,"Setoutputdata: current b,f,a =",R1,R2,R3
      ]

        LDR     R1,switch_buffer                ; if outputting to a buffer, just check that RGB is OK
        TEQ     R1,#0
        BNE     setout_checkRGB

        LDRB    R14,bitsperpixel
        CMP     R14,#8
        BLT     gosetoutputdata

;----------------------------------------------------------------------------
; set up [outputdata] from pseudo-palette block
;----------------------------------------------------------------------------

        LDRB    R14,forecolour
        CMP     R14,#maxpal
        MOVCS   R1,#0
        ADRCC   R1,pseudopalettes
        LDRCC   R1,[R1,R14,LSL #2]
        Debug   rgb,"setoutputdata: pseudo palette index, block =",R14,R1
        TEQ     R1,#0
        BLEQ    errpseudopal
        BVS     %FT99

        ASSERT  psb_aliascolours = 0
        LDR     R14,[R1],#psb_table             ; R1 -> output table
        STRB    R14,aliascolours                ; set correct number of anti-aliasing colours

        ADR     R2,outputdata
        LDMIA   R1!,{R3-R10}                    ; transfer colour table en block
        STMIA   R2!,{R3-R10}
        LDMIA   R1,{R3-R10}
        STMIA   R2,{R3-R10}

        BL      correctaliascolours             ; work out number of distinct colours

      [ blendingOn
	MOV	LR, # 14			; always enable blending
	STRB	LR, aliascolours
      |
	LDR	LR, plottype
	TST	LR, #paint_blended		; is blending enabled?
	MOVNE	LR, # 14			;   if so tweek alias colours
	STRNEB	LR, aliascolours
      ]
        B       endsetoutputdata
        LTORG

; setoutputdata called with output switched to a buffer - check that RGB colours are valid

setout_checkRGB
        LDR     R14,currentRGB_a
        TEQ     R14,#invalid_flag
        EXITS   NE

        PullEnv

xerr_FontBadRGB

        ADR     R0,ErrorBlock_FontBadRGB
        B       MyGenerateError
        MakeErrorBlock FontBadRGB

;--------------------------------------------------------------------------
; set up ordinary setoutputdata - ie. logical colours only in [outputdata]
;--------------------------------------------------------------------------

gosetoutputdata
        LDRB    R14,aliascolours
        CMP     R14,#128
        RSBGE   R14,R14,#256
        ADD     R14,R14,#1                      ; get no. of foreground colours
        STRB    R14,nsteps

        LDRB    colourbase,forecolour
        LDRB    aliascols,aliascolours
        MOV     R14,aliascols
        CMP     R14,#128
        RSBGE   R14,R14,#256                    ; make +ve
        ADRL    thresh,threshoffsets
        LDRB    R14,[thresh,R14]
        ADR     thresh,thresholds
        ADD     thresh,thresh,R14               ; thresh --> thresholds
        LDRB    bpp,bitsperpixel

        Debug   pal,"bpp,aliascols,colourbase =",bpp,aliascols,colourbase

        MOV     threshindex,#0
        MOV     valueindex,#0
        ADR     outputarray,outputdata
setflp
        LDRB    R14,[thresh,threshindex]       ; NB value off end is big
        Debug   pal,"threshold:",R14

        CMP     valueindex,R14                  ; so it must stop there
        ADDGE   threshindex,threshindex,#1      ; have hit boundary
        SUBS    R14,threshindex,#1
        LDRLTB  R14,backcolour                  ; 0 --> background colour
        BLT     gotf
        CMP     aliascols,#128                  ; move up or down?
        ADDLT   R14,colourbase,R14              ; foreground + index
        SUBGE   R14,colourbase,R14              ; foreground - index
gotf    MOV     R14,R14,ROR bpp                 ; shift to top of word
        STR     R14,[outputarray,valueindex,ASL #2]

        Debug   pal,"threshindex, outputdata :",threshindex,R14

        MOVS    R14,R14,ASL bpp                 ; check for overflow
        BNE     errsetoutputdata

        ADD     valueindex,valueindex,#1
        CMP     valueindex,#16
        BCC     setflp

;---------------------------------------------
; common exit point for setoutputdata
; check number of colours in output data
; generate output word for auto-rubout
;---------------------------------------------

endsetoutputdata
        MOV     R14,#setout_valid       ; ensure not called unnecessarily
        STRB    R14,setoutputdataflag

        LDR     outdata,outputdata      ; background colour
        LDRB    R0,bitsperpixel
        LDRB    R14,Pixelsperword
01
        ORR     outdata,outdata,outdata,LSR R0
        SUBS    R14,R14,#1
        BNE     %BT01
        STR     outdata,rubdata

	BL	setblendingdata		; prepare the data for the blending engine
        STRVS   R0,[sp,#Proc_RegOffset] ; keep error pointer

        PExit

errsetoutputdata
        BL      xerr_FontNotEnoughBits

99      STRVS   R0,[sp,#Proc_RegOffset] ; keep error pointer
        PExit

;.............................................................................

xerr_FontNotEnoughBits ROUT

        ADR     R0,ErrorBlock_FontNotEnoughBits
        B       MyGenerateError
        MakeErrorBlock FontNotEnoughBits

;.............................................................................

; Calculate actual value of aliascolours from [outputdata..]
; In    [aliascolours] = previous value
;       [outputdata..] set up by setoutputdata (256) or Font_SetColourTable
; Out   [aliascolours] = SGN(aliascolours) * (no_of_colours_in_table - 1)

correctaliascolours Entry "R0-R10"

        MOV     R0,#0
        ADR     R1,outputdata
        BL      check8
        MOV     R10,R9
        BL      check8
        TEQ     R10,R2
        ADDNE   R0,R0,#1
        SUBS    R0,R0,#1                ; R0 = number of colours -1
        MOVMI   R0,#0

        LDRB    R14,aliascolours
        CMP     R14,#0
        RSBMI   R0,R0,#0
        STRB    R0,aliascolours         ; can't alter aliascolours in non-256 colour modes

        EXIT

;.............................................................................

; Routine to count number of differences between 8 ordered words
; In    R1 -> 8 words
; Out   R0 = R0 + number of differences
;       R1 = R1 + 32
;       R2-R9 = the 8 words
; Note: when checking 16 words of outputdata, remember to check the middle 2 words!

check8
        LDMIA   R1!,{R2-R9}
        TEQ     R2,R3
        ADDNE   R0,R0,#1
        TEQ     R3,R4
        ADDNE   R0,R0,#1
        TEQ     R4,R5
        ADDNE   R0,R0,#1
        TEQ     R5,R6
        ADDNE   R0,R0,#1
        TEQ     R6,R7
        ADDNE   R0,R0,#1
        TEQ     R7,R8
        ADDNE   R0,R0,#1
        TEQ     R8,R9
        MOV     PC,LR

;;----------------------------------------------------------------------------
;; calcjustify - work out data for justification
;;----------------------------------------------------------------------------

; Called from paintchars if there is no coordblock (R2 bit 5 = 0)

; In    R2 = paint flags (NB: bits 16..31 must be clear)
;       [justifyx/y] = offset from start point to justify point (pixels)
; Out   [x/yspaceadd] = appropriate extra offset on each space
;       [x/yletteradd] = 0 (no inter-letter spacing)

calcjustify Entry "R6-R7"

        Debug   co :LOR: debugjust,"calcjustify: flags,justifyx,y =",R2,#justifyx,#justifyy

        MOV     R6,#0                   ; xspaceadd,yspaceadd = 0 for this scan
        MOV     R7,#0
        STR     R6,xletteradd           ; no inter-letter spacing
        STR     R7,yletteradd

        TST     R2,#paint_justify       ; just set all offsets to 0 if no justification
        STREQ   R6,xspaceadd            ; no justification
        STREQ   R7,yspaceadd
        EXIT    EQ

        LDR     R1,wrchblkptr           ; point to string
        MOV     R2,#bignum
        MOV     R3,#bignum
        MOV     R4,#" "                 ; split on spaces
;        MOV     R5,#bignum
        LDR     R5,paintendaddress
        CMP     R5,#0
        MOVLT   R5,#bignum
        SUBGT   R5,R5,R1

        Debug   just,"++ scanstring params R1-R7 = ",R1,R2,R3,R4,R5,R6,R7
        BL      scanstring
        Debug   just,"++ scanstring exit R1-R7 = ",R1,R2,R3,R4,R5,R6,R7
        EXIT    VS

        Debug   just,"++ x-size, y-size, n-spaces =",R2,R3,R4

        Push    "R3,R4"
        Push    "R2,R4"
        LDR     xcoord,justifyx         ; justifyx = difference
        BL      scalexco                ; R3 = scaled xco

        Debug   just,"++ justifyx before/after =",xcoord,R3

        LDR     R2,xco72
        SUB     R3,R3,R2                ; get offset
        CMP     R3,#0
        RSBLT   R3,R3,#0                ; *GPS* temp...
        Pull    "R2,R4"
        CMP     R2,#0
        RSBLT   R2,R2,#0                ; *GPS* temp...
        SUB     R2,R3,R2                ; work out gap
        Debug   just,"++ total space, num spaces =",R2,R4
        BL      getratio
        Debug   just,"++ xspaceadd before =",R5
        STR     R5,xspaceadd
        Debug   just,"++ xspaceadd =",R5

; the y-offset is really only needed for transformed fonts
; ignore it for now, cos it only causes problems

        Pull    "R2,R4"                 ; correct stack
        MOV     R5, #0                  ; ignore y-offset
        STR     R5, yspaceadd           ; interface is too coarse!

        EXIT

;.............................................................................

; In    R2 = dividend
;       R4 = divisor
; Out   R5 = result (R2/R4) (0 if division by zero)

getratio Entry "R2-R4"

        MOV     R5,#0
        MOV     R0,R2
        MOVS    R1,R4
        BEQ     zratio                  ; avoid division by zero
        BL      divide
        MOV     R5,R2                   ; R5 <-- result
zratio
        EXIT

;;----------------------------------------------------------------------------
;; Find nearest caret position given coord offsets
;;
;; Entry:  R1 --> string
;;         R2,R3 = x,y offset to caret position (1/1000ths point)
;;         R4,R5 = x,y justification offsets
;;         [x/yletteradd] = inter-letter spacing
;;         [plottype] :AND: scanstr_findcaret = 1
;;         [plottype] :AND: scanstr_rtnbbox => whether bbox should be returned
;; Exit:   R1,R2,R3,R4,R5 set up as for StringWidth
;;         (ie. values as at nearest character)
;;----------------------------------------------------------------------------

findcaret Entry "R6-R7"

        MOV     R6,R4                           ; set up parameters to scanstring
        MOV     R7,R5

        MOV     R4,#-1                          ; null split char
        MOV     R5,#bignum                      ; no chop-off

        MOV     R14,#0
        STR     R14,paintmatrix

        Debug   xx,"findcaret:  in: string,x,y,split,jx,jy, flags =",R1,R2,R3,R4,R6,R7,#plottype

        BL      scanstring                      ; R1-R5 = results

        Debug   xx,"findcaret: out: string,x,y,nsplit,len =",R1,R2,R3,R4,R5

        EXIT

;;----------------------------------------------------------------------------
;; Scan string, counting spaces and adding up character widths
;;
;; Entry: R1 --> string
;;        R2 = x-width to terminate by (in 1/1000ths point)
;;        R3 = y-width to terminate by (in 1/1000ths point)
;;        R4 = 'split' character (-1 ==> any)
;;        R5 = maximum index
;;        R6 = x-offset when space encountered (for justification)
;;        R7 = y-offset when space encountered
;;        [xletteradd] = x-offset when any non-ctrl character encountered
;;        [yletteradd] = y-offset when any non-ctrl character encountered
;;        [plottype] :AND: scanstr_rtnbbox => whether to bother with scanbbox
;;
;; Exit : R0 corrupt
;;        R1 --> point where scan terminated
;;        R2 = x-width of string (in 1/1000ths point)
;;        R3 = y-width of string (in 1/1000ths point)
;;        R4 = number of 'split' chars (up to point reached)
;;        R5 = index to point reached in string
;;        currentfont and colours are preserved
;;        futurefont/fcol/bcol are set up for the end of the string
;;        scanbbox x0,y0,x1,y1 = bounding box of string scanned (1/72000")
;;
;; During call: [inscanstring] = 1 => Font_SetColourTable should not set outputdata
;;
;;----------------------------------------------------------------------------

scanstring      ROUT
        LDRB    R0,currentfont
        Push    "R0,pixelsptr,metricsptr,wrchbase,LR"

        Debug   scanstring,"Scanstring entered with",R1,R2,R3,R4,R5,R6,R7

        STR     R6,xspaceadd
        STR     R7,yspaceadd

        STR     R1,scanstrptr
        STR     R2,xmaxwidth
        STR     R3,ymaxwidth
        STR     R4,splitchar
        STR     R5,maxindex

; calculate bounding box from R2,R3 (look at sign to determine which edge it is)
; this is irrelevant if scanstr_findcaret set, since it looks for closest match

        MOVS    R4,R2
        MOVLT   R4,#bignum
        MOVGE   R2,#-bignum

        MOVS    R14,R3
        MOVLT   R14,#bignum
        MOVGE   R3,#-bignum

        ADR     R0,minmaxx0
        Debug   scanstring,"Initial bounding box",R2,R3,R4,R14
        STMIA   R0,{R2,R3,R4,R14}

; copy current colours into future colours (these are also updated as the string is scanned)

        ASSERT  (:INDEX:currentdata :AND: 3) = 0
        ASSERT  (:INDEX:futuredata :AND: 3) = 0
        ASSERT  currentfont-currentdata = 12            ; 3 words of RGB data
        ASSERT  forecolour-currentfont = 1              ; then next 4 bytes
        ASSERT  backcolour-currentfont = 2
        ASSERT  aliascolours-currentfont = 3
        ASSERT  futuredata-currentdata = 16             ; convenient!
        ADR     R14,currentdata
        LDMIA   R14!,{R0,R2,R3,R4}      ; grab some spare registers!
        STMIA   R14!,{R0,R2,R3,R4}      ; store in futuredata workspace
        STMIA   R14,{R0,R2,R3,R4}       ; store in tfuturedata workspace
        MOV     R0, #0
        STR     R0, futurechanged
        Debug   scanstring,"** Cleared futureflag on entry = ", R0

; initialise currbbox and scanbbox

        MOV     R0,#bignum              ; start with null bbox (both copies)
        MOV     R2,#bignum
        MOV     R3,#-bignum
        MOV     R4,#-bignum
        ADR     R14,scanbboxx0
        Debug   scanstring,"Setting currbbox and scanbbox to",R0,R2,R3,R4
        STMIA   R14,{R0,R2,R3,R4}
        ADR     R14,currbboxx0
        STMIA   R14,{R0,R2,R3,R4}
        STR     R0,bestfindcaret        ; must be able to beat this!

        MOV     metricsptr,#0           ; force metrics to be cached later

        MOV     R2,#0                   ; x-width
        MOV     R3,#0                   ; y-width
        STR     R2,xglueadd
        STR     R3,yglueadd

        MOV     R14,#-1                 ; ensure GetTransform called the first time
        STR     R14,oldpaintmatrix      ; also reset on font change

        STRB    R14,inscanstring        ; flag to notify Font_SetColourTable

        MOV     R4,#0                   ; no of 'split chars'
        ADR     R14,scanstrpars
        STMIA   R14,{R1-R4}             ; save pars for later

scanjlp
        LDR     R14,plottype
        TST     R14,#scanstr_findcaret
        BNE     scanfindcaret

        Push    "R4,R6,R7"
        ADR     R14,minmaxx0
        LDMIA   R14,{R4-R7}
        CMP     R4,R2
        CMPLE   R5,R3
        CMPLE   R2,R6
        CMPLE   R3,R7
        Pull    "R4,R6,R7"

        LDRLE   R5,maxindex
        LDRLE   R14,scanstrptr          ; original ptr
        SUBLE   R14,R1,R14              ; current index
        CMPLE   R14,R5
        BGT     scanstrdone             ; return most recent OK params

        MOV     wrchbase,R1
        BL      readnextchar            ; Out: R0 = next char, R1 *not* updated
        LDR     R14,splitchar
        TEQ     R0,#0                   ; match if:
        TEQNE   R0,#lf                  ;
        TEQNE   R0,#cr                  ;  1) R0 = terminator
        TEQNE   R0,R14                  ;  2) R0 = split char
        CMPNE   R14,#-1                 ;  3) split char = -1
        BNE     %FT01

        ADR     R0,scanstrpars
        STMIA   R0,{R1-R4}              ; save pars for later
        LDR     R14,futurechanged
        CMP     R14,#0
        BEQ     %FT03
        ADR     R14,tfuturedata          ; copy temp future data to future data
        LDMIA   R14,{R1-R4}              ; when you get a splitchar...
        ADR     R14,futuredata
        STMIA   R14,{R1-R4}
        Debug   scanstring,"** Copied tfuturedata to futuredata"
        MOV     R14, #0
        STR     R14, futurechanged
        Debug   scanstring,"** Cleared futureflag"
03
        Debug   scanstring,"** In split char bit"
        ADR     R14,currbboxx0
        LDMIA   R14,{R1-R4}
        ADR     R14,scanbboxx0
        STMIA   R14,{R1-R4}
        LDMIA   R0,{R1-R4}              ; recover pars
01
        MOV     wrchbase,R1
        BL      readnextchar            ; R0 = external character code
        STR     R0,externalcharcode     ; remember for comparing with splitchar later
        CMP     R0,#32
        BLGE    mapchar                 ; R0 = internal character glyph code
        MOV     R1,wrchbase             ; R1 -> next character
        BVS     exitscanstr

        Debug   scanstring,"Character",R0
        BLT     scanctrl

        CMP     R0,#chcode_unmapped
        BEQ     scanjlp                 ; ignore completely if unmappable

        LDR     R14,splitchar

        LDR     wrchbase,externalcharcode
        TEQ     wrchbase,R14            ; compare EXTERNAL code with split char!!!

        CMPNE   R14,#-1
        ADDEQ   R4,R4,#1                ; add 1 for any printable if R14 = -1

        Debug   scanstring,"Calling Addwidth"
        BL      addwidth                ; metricsptr, pixelsptr may be set up / relocated

        BVC     scanjlp
        Debug   scanstring,"Jumping to exitscanstr"
        B       exitscanstr

scanfindcaret
        LDR     R5,maxindex
        LDR     R14,scanstrptr          ; original ptr
        SUB     R14,R1,R14              ; current index
        CMP     R14,R5
        BGT     scanstrdone             ; return most recent OK params

        Push    "R1-R4"

        ADR     R14,xglueadd
        LDMIA   R14,{R4,R5}             ; R4,R5 = glue offset between this and the next char

        LDR     R14,plottype            ; add/subtract half glue width (so caret goes in the middle)
        TST     R14,#paint_rightleft
        ADDEQ   R2,R2,R4,ASR #1
        ADDEQ   R3,R3,R5,ASR #1
        SUBNE   R2,R2,R4,ASR #1
        SUBNE   R3,R3,R5,ASR #1

        LDR     R5,xmaxwidth            ; check closeness to target point
        SUBS    R5,R5,R2
        RSBMI   R5,R5,#0
        LDR     R14,ymaxwidth
        SUBS    R14,R14,R3
        RSBMI   R14,R14,#0
        ADD     R5,R5,R14
        LDR     R14,bestfindcaret
        CMP     R5,R14

        Pull    "R1-R4",GE
        BGE     %BT01

        STR     R5,bestfindcaret        ; copy parameters if this is a better match

        LDMIA   sp,{R1-R4}
        ADR     R0,scanstrpars
        STMIA   R0,{R1-R4}              ; save current pars
        ADR     R14,currbboxx0
        LDMIA   R14,{R1-R4}
        ADR     R14,scanbboxx0          ; and bbox
        STMIA   R14,{R1-R4}

        Pull    "R1-R4"
        B       %BT01

scanctrl
        Debug   scanstring,"scanctrl - character =",R0
        TEQ     R0,#Comment
        BEQ     scanComment
        TEQ     R0,#Colour
        BEQ     scansetcolour           ; set up futurefcol/bcol
        TEQ     R0,#Colour3
        BEQ     scansetcolour3
        TEQ     R0,#Fontchar
        BEQ     scansetfont
        TEQ     R0,#Underline
        BEQ     scanunderline
        TEQ     R0,#MoveX
        BEQ     scanmovex
        TEQ     R0,#MoveY
        BEQ     scanmovey
        TEQ     R0,#ColourRGB
        BEQ     scanrgb
        TEQ     R0,#Matrix00
        BEQ     scanmatrix00
        TEQ     R0,#Matrix
        BEQ     scanmatrix
        TEQ     R0,#0                   ; check for legal terminator
        TEQNE   R0,#cr
        TEQNE   R0,#lf
        BEQ     scanstrdone

        BL      xerr_FontBadCtrlChar

exitscanstr
        Debug   scanstring,"Leaving scanstring"
        MOV     R14,#0                  ; indicate that Font_ScanString has finished
        STRB    R14,inscanstring

        Pull    "R14"
        STRB    R14,currentfont         ; preserve current font number
        Pull    "pixelsptr,metricsptr,wrchbase,PC"

scanmovex
        BL      readint3_R1             ; 3-byte signed integer, or 2 shorts, or 1 long
        ADD     R2,R2,R0                ; update x-coord
        B       scanjlp

scanmovey
        BL      readint3_R1             ; 3-byte signed integer, or 2 shorts, or 1 long
        ADD     R3,R3,R0                ; update y-coord
        B       scanjlp

scansetfont
        BL      readuint1_R1            ; 1 byte, or 1 short, or 1 long (unsigned)

        ASSERT  maxf = 256              ; must be in range
        STRB    R0,currentfont          ; will be restored later
        STRB    R0,tfuturefont
        Debug   scanstring,"** Stored future value at tfuturefont"
        MOV     R14,#1
        STR     R14,futurechanged
        Debug   scanstring,"** Set futureflag"
        MOV     R14,#-1                 ; ensure metrics matrix is recalculated
        STR     R14,oldpaintmatrix
        BL      SetMetricsPtrs          ; must also reset pointers
        BVC     scanjlp
        B       exitscanstr

scanunderline
        BL      readint1_R1             ; signed underline position
        BL      readuint1_R1            ; unsigned underline thickness
        B       scanjlp

scanComment
        BL      readuint1_R1
        CMP     R0,#32                  ; until we meet a ctrl-char (safer)
        BCS     scanComment
        B       scanjlp

scansetcolour
        BL      readuint1_R1
        CMP     R0,#&80
        AND     R0,R0,#&7F
        STRLTB  R0,tfuturefcol
        STRGEB  R0,tfuturebcol
        Debug   scanstring,"** Stored future value at tfuturefcol/bcol"
        MOV     R14,#1
        STR     R14,futurechanged
        Debug   scanstring,"** Set futureflag"
        B       scanjlp

scansetcolour3
        BL      readuint1_R1
        STRB    R0,tfuturebcol
        BL      readuint1_R1
        STRB    R0,tfuturefcol

; Bug fix by Chris:
;
; This used to parse anti-alias colour offsets incorrectly if they
; were negative because it didn't sign-extend the byte into a word.
; Consequently, offsets like -1 were treated as 255 which caused an
; error later on.

        BL      readint1_R1             ; signed colour offset
        STRB    R0,tfutureacol
        Debug   scanstring,"** Stored future value at tfuturefcol/bcol/acol"
        MOV     R14,#1
        STR     R14,futurechanged
        Debug   scanstring,"** Set futureflag"
        B       scanjlp

scanrgb
        ADR     R0,tfuturedata
        MOV     R14,#1
        Debug   scanstring,"** Set futureflag"
        STR     R14,futurechanged
        BL      getrgb_fromstring       ; updates R1, [R0,#0..2]
        Debug   scanstring,"** Stored future value at tfutureRGB"
        BVC     scanjlp
        B       exitscanstr

scanmatrix00
        ASSERT  (mat_X :AND: 3) = 0
        ADD     R1,R1,#3 + mat_X
        BIC     R1,R1,#3                        ; R1 -> byte after matrix

        Push    "R2-R8"

        LDMDB   R1,{R2-R5}                      ; Note that the matrix is stored as aligned 32-bit words,
        MOV     R7,#0                           ; regardless of the character width settings in Font_ScanString
        MOV     R8,#0
        ADR     R6,paintmatrixbuffer
        STMIA   R6,{R2-R5,R7-R8}

        LDR     R14,oldpaintmatrix              ; if oldpaintmatrix -> paintmatrixbuffer
        TEQ     R14,R6                          ; set oldpaintmatrix = -1 (not 0 as new one may be unit matrix!)
        MOVEQ   R14,#-1
        STREQ   R14,oldpaintmatrix

        BL      setpaintmatrix                  ; sets up [paintmatrix]

        Pull    "R2-R8"
        MOV     metricsptr,#0                   ; force metrics data to be reconsidered
        BVC     scanjlp
        B       exitscanstr

scanmatrix
        ASSERT  (mat_enduser :AND: 3) = 0
        ADD     R1,R1,#3 + mat_enduser
        BIC     R1,R1,#3                        ; R1 -> byte after matrix
        Push    "R6"
        SUB     R6,R1,#mat_enduser
        BL      setpaintmatrix                  ; sets up [paintmatrix]
        Pull    "R6"
        MOV     metricsptr,#0                   ; force metrics data to be reconsidered
        BVC     scanjlp
        B       exitscanstr

scanstrdone
        MOV     R14,#0                  ; indicate that Font_ScanString has finished
        STRB    R14,inscanstring

        ADR     R14,scanstrpars         ; get parameters before failure
        LDMIA   R14,{R1-R4}
        LDR     R14,scanstrptr          ; original value of R1
        SUB     R5,R1,R14

        Debug   scanstring,"recovered R1-R4 and derived R5",R1,R2,R3,R4,R5

        Pull    "R0,pixelsptr,metricsptr,wrchbase,LR"

        STRB    R0,currentfont          ; restore currentfont
        LDR     R6,xspaceadd
        LDR     R7,yspaceadd            ; preserve R6,R7

        CLRV
        MOV     PC,LR

;..........................................................................

xerr_FontBadCtrlChar ROUT

        ADR     R0,ErrorBlock_FontBadCtrlChar
        B       MyGenerateError
        MakeErrorBlock FontBadCtrlChar   ; "Illegal control character"

;..........................................................................

; In    R0 = char
;       [R1] = next char   <==== Doesn't seem to be used !!!
;       R2,R3 = current x,y position (1/72000")
;       metricsptr -> char mappings
; Out   R2,R3 updated
;       metricsptr may have been relocated

addwidth Entry "R1,R4,R5"

        Debug   scanstring,"Arrived in addwidth",R0,R2,R3

        ADR     R14,xglueadd
        LDMIA   R14,{R4,R5}             ; R4,R5 = glue width from last time

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        BNE     %FT01

        ADD     R4,R2,R4                ; R4,R5 = coordinates of this character
        ADD     R5,R3,R5

        BL      getcharwidth            ; out: R2,R3 = width of this character (excluding glue)
        EXIT    VS

        Debug   scanstring,"getcharwidth returned",R2,R3

        ADD     R2,R4,R2
        ADD     R3,R5,R3

; R2,R3 = coords for next time
; R4,R5 = coords of this char (if going left to right, else use R2,R3)

        Debug   scanstring,"next/this coords",R2,R3,R4,R5

02      LDR     R14,plottype
        TST     R14,#scanstr_rtnbbox    ; also update [currbbox] if bbox is to be returned
        BLNE    computebbox             ; uses R2,R3,R4,R5

        EXIT

01      SUB     R4,R2,R4                ; subtract glue from last time
        SUB     R5,R3,R5

        BL      getcharwidth            ; out: R2,R3 = width of this character (excluding glue)

        SUBVC   R2,R4,R2                ; if going from right to left, subtract char width
        SUBVC   R3,R5,R3
        BVC     %BT02

        EXIT

;..........................................................................

; In    R0,[R1] = character codes of this char and the next
;       [x/yglueadd] = glue from last time
;       metricsptr -> metrics info, or 0 => not done yet
; Out   R2,R3 = width of this character, plus xyspaceadd if R0=32
;       [x/yglueadd] = glue for next time
;       [currbbox] updated if [plottype] :AND: scanstr_rtnbbox
;       metricsptr may be set up / relocated

getcharwidth Entry "R4,R5"

        Debug   scanstring,"getcharwidth called for",R0

        TEQ     metricsptr,#0
        Debug   scanstring,"metricsptr is",metricsptr
        CacheHit Metrics,NE
        BLEQ    SetMetricsPtrs
        EXIT    VS

        LDR     R14,metchmapsize
        CMP     R14,#0
        MOVEQ   R6,R0                           ; no lookup table
        BEQ     %FT01
        CMP     R0,R14
        MOVHS   R2,#0
        MOVHS   R3,#0
        BHS     %FT20                           ; can't have default x,y offset (it must be 0)
        LDRB    R6,[metricsptr,R0]
01
      ; NOTE: unscaled metrics have 16-bit widths

        LDR     R5,xoffset
        Debug   scanstring,"xoffset",R5
        TEQ     R5,#0
        ; Load a signed halfword from the array
        LDSHA   R2, R5, R6, R5, "NE"
        BNE     %FT11
        LDR     R2,metmisc                      ; try for default x-offset
        TEQ     R2,#0
        LDRNEB  R14,[R2,#fmisc_xoffset]
        LDRNEB  R2,[R2,#fmisc_xoffset+1]
        MOVNE   R2,R2,LSL #24
        ADDNE   R2,R14,R2,ASR #16               ; (signed)
11
        Debug   scanstring,"xoffset",R2

        LDR     R5,yoffset
        Debug   scanstring,"yoffset",R5
        TEQ     R5,#0
        ; Load a signed halfword from the array
        LDSHA   R3, R5, R6, R5, "NE"
        MOVEQ   R3,#0                           ; there's no default y-offset (no known fonts bother with this!)
        Debug   scanstring,"yoffset",R3

        BL      scalewidth                      ; R2,R3 scaled by x/yscale / x/yfactor
20
        LDR     R14,metricsmatrix
        TEQ     R14,#0
        Debug   scanstring,"Calling transformwidth if NE",R14
        BLNE    transformwidth          ; R2,R3 transformed by metricsmatrix

        LDR     R14,externalcharcode    ; compare EXTERNAL character code with space!!!
        TEQ     R14,#" "
        ADREQ   R14,xspaceadd           ; justify width is treated as part of space
        LDMEQIA R14,{R4,R5}
        ADDEQ   R2,R2,R4
        ADDEQ   R3,R3,R5

        Debug   scanstring,"Calling getgluewidth",R2,R3

        BL      getgluewidth            ; inter-letter spacing is treated as glue

        Debug   scanstring,"Finished",R2,R3

        CLRV
        EXIT

;..........................................................................

; In    [metricsmatrix] -> metrics matrix
;       R2,R3 = untransformed character width
; Out   R2,R3 = transformed character width (origin translation ignored)

transformwidth Entry "R1"

        LDR     R1,metricsmatrix

        DebugM  me,"metrics matrix",R1
        Debuga  me,"transformwidth: before",R2,R3

        BL      transformpt             ; transform R2,R3

        ADD     R1,R1,#mat_X
        LDMIA   R1,{R1,R14}             ; R1,R14 = x,y offsets in matrix
        SUB     R2,R2,R1
        SUB     R3,R3,R14               ; R2,R3 = transformed offset

        Debug   me," after",R2,R3

        EXIT

;..........................................................................

; compute bounding box & see if it's inside the current one
; In    R0 = character code
;       R2,R3 = coordinates after this character (1/72000")
;       R4,R5 = coordinates before this character (1/72000")
;       metricsptr -> metrics for this char (NOT 0 - must have been obtained already)
;       currbboxx0/y0/x1/y1 = current bounding box
; Out   currbboxx0/y0/x1/y1 = updated bounding box
;       metricsptr may have been relocated

computebbox Entry "R0-R8"

        Debug   me,"computebbox: bboxx0,y0,x1,y1,matrix =",#bboxx0,#bboxy0,#bboxx1,#bboxy1,#metricsmatrix

        Debug   scanstring,"computebbox called character",R0
        Debug   scanstring,"coordinates before",R4,R5
        Debug   scanstring,"coordinates after",R2,R3

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        Push    "R4,R5",EQ              ; use previous coordinates if going left to right
        Push    "R2,R3",NE              ; use next coordinates if going from right to left

        LDR     R5,bboxx0               ; left
        Debug   scanstring,"If 0, branching to computebbox_fromoutlines",R5
        CMP     R5,#0
        BEQ     computebbox_fromoutlines

        LDR     R5,metricsmatrix
        Debug   scanstring,"If NE, branching to computebbox_transformed",R5
        TEQ     R5,#0
        BNE     computebbox_transformed

        LDR     R14,metchmapsize
        TEQ     R14,#0
        MOVEQ   R6,R0
        BEQ     %FT75
        CMP     R0,R14
        BHS     computebbox_nullbbox
        LDRB    R6,[metricsptr,R0]
75
        Pull    "R2,R3"                 ; R2,R3 = origin of this character
        Debug   scanstring,"Character origin",R2,R3
        Debug   scanstring,"Character max ",R7,R8

        LDR     R5,bboxx0
        LDRB    R14,[R5,R6,LSL #1]!
        LDRB    R5,[R5,#1]
        MOV     R14,R14,LSL #16
        ORR     R5,R14,R5,LSL #24
        MOVS    R5,R5,ASR #16
        BLNE    scalexwidth

        Debug   scanstring,"bboxx0 adding, to",R2,R5
        ADD     R5,R2,R5
        LDR     R14,currbboxx0
        Debug   scanstring,"currbboxx0 was",R14
        CMP     R5,R14
        STRLT   R5,currbboxx0
        [       debugscanstring
        BGE     %FT89
        Debug   scanstring,"currbboxx0 changed to",R5
89
        ]

        LDR     R5,bboxy0               ; bottom
        LDRB    R14,[R5,R6,LSL #1]!
        LDRB    R5,[R5,#1]
        MOV     R14,R14,LSL #16
        ORR     R5,R14,R5,LSL #24
        MOVS    R5,R5,ASR #16
        BLNE    scaleyheight
        Debug   scanstring,"bboxy0 adding, to",R3,R5
        ADD     R5,R3,R5
        LDR     R14,currbboxy0
        Debug   scanstring,"currbboxy0 was",R14
        CMP     R5,R14
        STRLT   R5,currbboxy0
        [       debugscanstring
        BGE     %FT88
        Debug   scanstring,"currbboxx0 changed to",R5
88
        ]

        LDR     R5,bboxx1               ; right
        LDRB    R14,[R5,R6,LSL #1]!
        LDRB    R5,[R5,#1]
        MOV     R14,R14,LSL #16
        ORR     R5,R14,R5,LSL #24
        MOVS    R5,R5,ASR #16
        BLNE    scalexwidth
        Debug   scanstring,"bboxx1 adding, to",R2,R5
        ADD     R5,R2,R5
        LDR     R14,currbboxx1
        Debug   scanstring,"currbboxx1 was",R14
        CMP     R5,R14
        STRGT   R5,currbboxx1
        [       debugscanstring
        BLE     %FT87
        Debug   scanstring,"currbboxx1 changed to",R5
87
        ]

        LDR     R5,bboxy1               ; top
        LDRB    R14,[R5,R6,LSL #1]!
        LDRB    R5,[R5,#1]
        MOV     R14,R14,LSL #16
        ORR     R5,R14,R5,LSL #24
        MOVS    R5,R5,ASR #16
        BLNE    scaleyheight
        Debug   scanstring,"bboxx0 adding, to",R3,R5
        ADD     R5,R3,R5
        LDR     R14,currbboxy1
        Debug   scanstring,"currbboxy1 was",R14
        CMP     R5,R14
        STRGT   R5,currbboxy1
        [       debugscanstring
        BLE     %FT86
        Debug   scanstring,"currbboxx1 changed to",R5
86
        ]

        CLRV
        EXIT

computebbox_nullbbox
        MOV     R1,#0
        MOV     R2,#0
        MOV     R3,#0
        MOV     R4,#0
        B       %FT01

computebbox_fromoutlines
        MOV     R1,R0                   ; R1 = character code
        PushP   "metricsptr",metricsptr
        BL      getoutlines_metricsbbox ; R1-R4 = bbox (millipoints)
        PullP   "metricsptr",metricsptr
        Debug   scanstring,"bbox",R1,R2,R3,R4
        B       %FT01

; . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

; In    R5 -> metrics matrix
;       [sp,#0*4,1*4] = coordinates of this char
;       metricsptr -> metrics

computebbox_transformed
        LDR     R14,metchmapsize
        CMP     R14,#0
        BEQ     %FT02
        CMP     R0,R14
        BHS     computebbox_transformed_nullbbox
        LDRB    R0,[metricsptr,R0]      ; R0 = internal character code
02      BL      transformbbox           ; R1-R4 = transformed bbox
01
        Pull    "R5,R6"                 ; R5,R6 = coordinates of this char, R1-R4 = bbox
        BLVC    computebbox_update

        EXIT

computebbox_transformed_nullbbox
        MOV     R1,#0
        MOV     R2,#0
        MOV     R3,#0
        MOV     R4,#0
        BL      transformbox
        B       %BT01

;..........................................................................

; In    R0 = character code
;       [R1] = code of next character
;       [plottype] :AND: paint_rightleft = 0 => R0 on left, [R1] on right, else vice-versa
;       [plottype] :AND: paint_kern = 1 => use kerning information
;       [x/yletteradd] = inter-letter offset
;       metricsptr -> metrics info
; Out   [x/yglueadd] = glue offset for next time (1/72000")
;       metricsptr, pixelsptr may be set up / relocated / set to 0

; Skip       : MoveX, MoveY, Colour, Colour3, ColourRGB, Comment, Underline
; Don't skip : terminator, Fontchar, Matrix00, Matrix

skiptable1      DCB     0,0,0,0,0,0,0,0,0,4,0,4,0,0,0,0,0,2, 4, 8,0,0,0,0,0, 3,0,0,0,0,0,0
                ASSERT  .-skiptable1 = 32
skiptable2      DCB     0,0,0,0,0,0,0,0,0,6,0,6,0,0,0,0,0,4, 8,12,0,0,0,0,0, 6,0,0,0,0,0,0
                ASSERT  .-skiptable2 = 32
skiptable4      DCB     0,0,0,0,0,0,0,0,0,8,0,8,0,0,0,0,0,8,16,16,0,0,0,0,0,12,0,0,0,0,0,0
                ASSERT  .-skiptable4 = 32

getgluewidth Entry "R0-R8,wrchbase"

        ADR     R14,xletteradd
        LDMIA   R14,{R5,R6}

        ADR     R14,xglueadd
        STMIA   R14,{R5,R6}

; optimise non-kerning case

        LDR     R14,plottype
        TST     R14,#paint_kern
        EXIT    EQ

; work out which is the next printable letter in the string

11
        MOV     wrchbase,R1
        BL      readnextchar_andremap   ; don't update R1 just yet
        BVS     %FT99
        MOV     R7,R0
        BGE     %FT13

        CMP     R0,#chcode_unmapped     ; skip this one if unmappable
        BEQ     %BT11

        LDR     R8,plottype
        ASSERT  scanstr_32bit > scanstr_16bit
        AND     R8,R8,#scanstr_16bit :OR: scanstr_32bit
        CMP     R8,#scanstr_16bit
        ADRLO   R8,skiptable1
        ADREQ   R8,skiptable2
        ADRHI   R8,skiptable4
        LDRB    R8,[R8,R7]
        TEQ     R8,#0                   ; if offset non-zero, skip this sequence and keep looking
        ADDNE   R1,R1,R8
        BNE     %BT11
        TEQ     R7,#Comment
        EXIT    NE                      ; otherwise give up (eg. font change, end of string etc.)

        BL      readuint1_R1            ; skip initial comment code

12
        BL      readuint1_R1

        CMP     R0,#32
        BHS     %BT12
        B       %BT11
13
        LDR     R0,[SP,#0*4]            ; recover initial character from stack

        TST     R14,#paint_rightleft
        MOVEQ   R1,R7                   ; R0 = left (1st) char, R1 = right (2nd) char
        MOVNE   R1,R0
        MOVNE   R0,R7                   ; R0 = left (2nd) char, R1 = right (1st) char

; metricsptr, pixelsptr must be correctly relocated by the following code

        LDR     R7,cacheindex
        LDRB    R14,currentfont
        LDR     R8,[R7,R14,LSL #2]      ; R8 -> font header
        LDRB    R8,[R8,#hdr_masterfont]
        LDR     R8,[R7,R8,LSL #2]       ; R8 -> master font header
        LDR     R7,[R8,#hdr_Kerns]!     ; R7 -> kerning block (if present)
        TEQ     R7,#0
        BLEQ    CacheKerns              ; R7 -> kern block; metricsptr, pixelsptr relocated
        BVS     %FT99

        CMP     R7,#1                   ; if no kerning data, cop out now
        EXIT    LS

        BL      getkernpair             ; CC if kern pair (in R3,R4)
        EXIT    CS

        MOV     R2,R3                   ; R2,R3 = unscaled offset
        MOV     R3,R4
        BL      scalewidth
        MOV     R4,R3
        MOV     R3,R2                   ; R3,R4 = scaled offset

        Debug   me,"Scaled kern pair offset =",R3,R4

        LDR     R1,metricsmatrix
        TEQ     R1,#0
        BNE     transformkern

donek   ADR     R14,xglueadd
        LDMIA   R14,{R5,R6}
        ADD     R5,R5,R3
        ADD     R6,R6,R4
        STMIA   R14,{R5,R6}
        EXIT

99      STRVS   R0,[sp]
        EXIT                            ; can return errors (eg. font cache full)

transformkern
        MOV     R2,R3                   ; R2,R3 = untransformed offset
        MOV     R3,R4
        BL      transformwidth          ; R2,R3 = transformed offset
        MOV     R4,R3
        MOV     R3,R2                   ; R3,R4 = transformed offset
        B       donek

;..........................................................................

; In: R0, R1 = left, right characters (internal if newmapping, else external)
;     R7 -> kerning block for font
;
; Out: CC: kern pair present       R3, R4 = x,y offset (1/1000em if newmapping, else mpts)
;      CS: kern pair not present   R3, R4 undefined
;      R5,R6,R8 corrupt

getkernpair Entry

        LDR     R6,[R7,#kern_flags]     ; R6 = metrics / kerning flags (superset of metrics flags)

        TST     R6,#flg_shortkerns      ; if short kerns, only kerning for both chars <= 255 (internal)
        BEQ     %FT15
        CMP     R0,#256
        CMPLO   R1,#256
        EXIT    HS                      ; exit with C set
15

        ADD     R8,R7,#kern_index
        EOR     R14,R0,R1,ROR #4
        EOR     R14,R14,R14,ROR #16
        EOR     R14,R14,R14,ROR #8
        AND     R14,R14,#&FF            ; R14 = R0 EOR (R1 ROR-in-8-bits 4)

        Debug   me,"getkernpair: CharL, CharR, Kern table, index =",R0,R1,R8,R14

        LDR     R5,[R8,R14,LSL #2]!     ; R5 = offset from index start to first kern pair for this hash index
        LDR     R8,[R8,#4]              ; R8 = offset to start of next set
        SUBS    R8,R5,R8                ; R8 = minus size of this set
        EXIT    HS                      ; quick cop-out (C set)

        ADD     R5,R7,R5
        ADD     R5,R5,#kern_index       ; R5 -> first kern pair
        SUB     R8,R5,R8                ; R8 -> end

        TST     R6,#flg_shortkerns
        BEQ     newlongkerns

        ORR     R14,R0,R1,LSL #8        ; R14 = L,R pair
        MOV     R14,R14,LSL #16

newshortkerns_lp
        LDR     R1,[R5],#4              ; R1 = CharL, CharR, Offset
        TEQ     R14,R1,LSL #16
        BEQ     donenewshortkern
        CMP     R5,R8
        BLO     newshortkerns_lp
        EXIT                            ; exit with C set

donenewshortkern
        ASSERT  flg_noxoffsets<256
        TSTS    R6,#(flg_noxoffsets*4),2  ; clear C
        MOVEQ   R3,R1,ASR #16
        MOVEQ   R4,#0
        MOVNE   R3,#0
        MOVNE   R4,R1,ASR #16
        B       donekern_cc

newlongkerns
        ORR     R14,R0,R1,LSL #16       ; R14 = L,R pair

newlongkerns_lp
        LDMIA   R5!,{R1,R3}             ; R1 = CharL, CharR, R3 = x-offset, y-offset
        TEQ     R1,R14
        BEQ     donenewkern
        CMP     R5,R8
        BLO     newlongkerns_lp
        EXIT                            ; exit with C set

donenewkern
        MOV     R4,R3,ASR #16           ; R4 = unscaled y-offset
        MOV     R3,R3,LSL #16
        MOVS    R3,R3,ASR #16           ; R3 = unscaled x-offset (clears C)

donekern_cc
        Debug   me,"Kern pair offset =",R3,R4
        EXIT

nokern
        SEC
        EXIT

;..........................................................................

; In    R8 -> master font header + hdr_Kerns (kerns not loaded)
;       master font metrics are loaded
;       metricsptr, pixelsptr may be set up
; Out   R7 -> kerning block (or 1 if no kerning)
;       R8, metricsptr, pixelsptr relocated if necessary


        ASSERT  metricsptr = R10
        ASSERT  pixelsptr = R9

                ^       0
kblk_link       #       4                       ; link to next kern pair block
kblk_lcode      #       2                       ; character code of left-hand letter of pair
kblk_rcode      #       2                       ; character code of right-hand letter of pair
kblk_xoff       #       2                       ; unscaled x-offset
kblk_yoff       #       2                       ; unscaled y-offset
kblk_size       #       0

CacheKerns Entry "R0-R6,R11"

        BL      grabblock_init                  ; start using scratch space and expand into RMA if required

        PushP   "CacheKerns: metricsptr",metricsptr
        PushP   "CacheKerns: pixelsptr",pixelsptr

        SUB     R7,R8,#hdr_Kerns                ; R7 -> master font header
        LDR     R5,[R7,#hdr_nchars]             ; R5 = no of entries (cached in GetMetricsHeader)
        LDR     R1,[R7,#hdr_MetricsPtr]         ; R1 -> master metrics

        ;bug fix for MED-02224 - sometimes the metrics aren't in memory at this point!
        CMP     R1,#0
        BNE     %FT01

        MOV     R6,R7
        BL      CacheMetrics
        MOV     R1,R6
        MOV     R5,R0

01      BL      lockblock                       ; lock while we allocate a new block
        LDRB    R9,[R1,#met_flags]

        Debug   me,"CacheKerns: master metrics, flags, nchars =",R1,R9,R5

        PushP   "CacheKerns: master metrics",R1

; now work out where the kerning information actually starts

        ADD     R7,R1,#met_chmap                ; R7 -> character map of master metrics

        TST     R9,#flg_moredata                ; no kerns if no extension area
        BEQ     cachenokerns

        Debuga  me,"metrics block",R1
        Debuga  me,", map",R7

        TST     R9,#flg_mapsized
        ADDEQ   R7,R7,#256                      ; map size = 256 if not sized
        LDRNEB  R0,[R7],#1
        LDRNEB  R1,[R7],#1
        ADDNE   R0,R0,R1,LSL #8                 ; R0 = size of map
        ADDNE   R7,R7,R0                        ; R7 -> data at end of map

        Debuga  me,", data",R7

        TST     R9,#flg_nobboxes
        ADDEQ   R7,R7,R5,LSL #3                 ; skip bboxes (if present)
        TST     R9,#flg_noxoffsets
        ADDEQ   R7,R7,R5,LSL #1                 ; skip x-offsets (if present)
        TST     R9,#flg_noyoffsets
        ADDEQ   R7,R7,R5,LSL #1                 ; skip y-offsets (if present)

        Debug   me,", table",R7

        LDRB    R0,[R7,#2]                      ; R7 -> table of 16-bits offsets to areas
        LDRB    R1,[R7,#3]
        ORR     R2,R0,R1,LSL #8                 ; R2 = offset to kern area
        LDRB    R0,[R7,#4]                      ; R7 -> table of 16-bits offsets to areas
        LDRB    R1,[R7,#5]
        ORR     R1,R0,R1,LSL #8                 ; R1 = offset to end of kern area

        Debuga  me,"Table offset[1], offset[2] =",R2,R1

        CMP     R2,R1                           ; no kerns?
        BHS     cachenokerns

        ADD     R7,R7,R2                        ; R7 -> start of kern data

        Debug   me,", kerns are at",R7

; get hold of the external -> internal code mapping for this font

        PushP   "Start of master kerns",R7      ; save this while we count the kern pairs

; now count the kern pairs that are mapped in and work out how much space they will need

        MOV     R4,#0                           ; R4 = maximum code found so far
        MOV     R6,#0                           ; R6 = number of kern pairs found so far

kloop1  TST     R9,#flg_16bitkerns              ; 0 => codes are 8-bit, 1 => codes are 16-bit
        LDRB    R0,[R7],#1
        LDRNEB  R1,[R7],#1
        ORRNE   R0,R0,R1,LSL #8                 ; R0 = left-hand character code

        TEQ     R0,#0                           ; finished if L char = 0
        BEQ     endkloop

kloop2  TST     R9,#flg_16bitkerns              ; 0 => codes are 8-bit, 1 => codes are 16-bit
        LDRB    R1,[R7],#1
        LDRNEB  R14,[R7],#1
        ORRNE   R1,R1,R14,LSL #8                ; R1 = right-hand character code

        TEQ     R1,#0
        BEQ     kloop1

        CMP     R0,R4
        MOVHI   R4,R0
        CMP     R1,R4
        MOVHI   R4,R1                           ; R4 = maximum code encountered

        ADD     R6,R6,#1                        ; R6 = number of kern pairs found so far

kloop2skip
        TST     R9,#flg_noxoffsets
        ADDEQ   R7,R7,#2
        TST     R9,#flg_noyoffsets
        ADDEQ   R7,R7,#2
        B       kloop2

endkloop

        Debug   me,"Number of kern pairs, highest code =",R6,R4

; now R4 = highest code found, R6 = number of kern pairs

        MOV     R14,R6,LSL #3                   ; R14 = space taken up by kern pairs (8 bytes each)

        CMP     R4,#255                         ; go for short kerns if 8-bit codes and x or y only
        BHI     %FT80
        TST     R9,#flg_noxoffsets:OR:flg_noyoffsets
        ORRNE   R9,R9,#flg_shortkerns
        MOVNE   R14,R6,LSL #2                   ; R14 = space taken up by short kern pairs (4 bytes each)
80
        ADD     R6,R14,#kern_data               ; R6 = size of block required
        BL      reservecache                    ; R6 -> new block, R8 relocated
95
        PullP   "Start of master kerns",R7      ; R7 -> start of master kern data
        PullP   "CacheKerns: Master metrics",R1
        BL      unlockblock                     ; don't need to lock this any more

        BVS     %FT100                          ; now we can give up if this didn't work

; step 1: initialise kern index to 0

        Debug   me,"Storing flags in new kern block for old kerns",R9,R6,R7

        ASSERT  kern_flags + 4 = kern_reserved1
        ASSERT  kern_flags + 8 = kern_reserved2

        ADD     R0,R6,#kern_flags-4
        MOV     R10,#0                          ; make sure the reserved words are set to 0
        MOV     R14,#0
        STMIB   R0,{R9,R10,R14}                 ; note STMIB so the add could use an immediate constant

        ADD     R6,R6,#kern_index

        MOV     R14,#0
        MOV     R0,#256
03      STR     R14,[R6],#4                     ; use this array to contain linked list heads
        SUBS    R0,R0,#1
        BNE     %BT03
        SUB     R6,R6,#256*4                    ; R6 -> index again

; step 2: rescan kerns, creating blocks in scratch space and the RMA to hold the results in linked lists

kloop3  TST     R9,#flg_16bitkerns              ; 0 => codes are 8-bit, 1 => codes are 16-bit
        LDRB    R10,[R7],#1
        LDRNEB  R14,[R7],#1
        ORRNE   R10,R10,R14,LSL #8              ; R10 = left-hand character code

        TEQ     R10,#0                          ; finished if L char = 0
        BEQ     endkloop2

kloop4  TST     R9,#flg_16bitkerns              ; 0 => codes are 8-bit, 1 => codes are 16-bit
        LDRB    R3,[R7],#1
        LDRNEB  R14,[R7],#1
        ORRNE   R3,R3,R14,LSL #8                ; R3 = right-hand character code

        TEQ     R3,#0
        BEQ     kloop3

        TST     R9,#flg_noxoffsets
        MOVNE   R4,#0
        BNE     %FT03

        LDRB    R4,[R7],#1
        LDRB    R14,[R7],#1
        ORR     R4,R4,R14,LSL #8

03      TST     R9,#flg_noyoffsets
        MOVNE   R5,#0
        BNE     %FT04

        LDREQB  R5,[R7],#1
        LDREQB  R14,[R7],#1
        ORREQ   R5,R5,R14,LSL #8

04
        Debug   me,"CacheKerns: CharL, CharR, x-offset, y-offset =",R10,R3,R4,R5

        EORVC   R14,R10,R3,ROR #4
        EORVC   R14,R14,R14,ROR #16
        EORVC   R14,R14,R14,ROR #8
        ANDVC   R0,R14,#&FF                     ; R0 = R10 EOR (R3 ROR-in-8-bits 4)

        ORRVC   R11,R10,R3,LSL #16              ; R11 = lchar,rchar

        MOV     R3,#kblk_size
        BL      grabblock_claim                 ; R2 -> block (in ScratchSpace or RMA)

        Debug   me,"Kern hash table, index, new block =",R6,R0,R2

        ASSERT  kblk_link = 0
        ASSERT  kblk_lcode = 4
        ASSERT  kblk_rcode = 6
        ASSERT  kblk_xoff = 8
        ASSERT  kblk_yoff = 10
        ASSERT  kblk_size = 12

        LDRVC   R3,[R6,R0,LSL #2]               ; R3 = link to next block
        ORRVC   R14,R4,R5,LSL #16               ; R14 = xoffset,yoffset
        STMVCIA R2,{R3,R11,R14}                 ; store link, leftcode, rightcode, xoffset, yoffset
        STRVC   R2,[R6,R0,LSL #2]               ; put new block at head of list
        BVC     kloop4
        BVS     %FT99                           ; error - discard block and exit

kloop4skip
        TST     R9,#flg_noxoffsets
        ADDEQ   R7,R7,#2
        TST     R9,#flg_noyoffsets
        ADDEQ   R7,R7,#2
        B       kloop4

endkloop2

; step 3: scan hash table, copying relevant entries into actual block in correct format

        MOV     R3,#0                           ; R3 = index
        ADD     R4,R6,#kern_data - kern_index   ; R4 = output pointer

05      LDR     R2,[R6,R3,LSL #2]               ; R2 -> head of list of kerns
        SUB     R14,R4,R6                       ; R14 = offset from start of index to kern data
        STR     R14,[R6,R3,LSL #2]

06      TEQ     R2,#0
        BEQ     %FT07

08      LDMIA   R2,{R2,R5,R11}                  ; R2=link, R5=lcode,rcode, R11=xoffset,yoffset
        TST     R9,#flg_shortkerns
        STMEQIA R4!,{R5,R11}                    ; lcode(16),rcode(16),xoff(16),yoff(16)
        BEQ     %BT06

        MOV     R14,R5,LSR #8                   ; R14=rcode >> 8
        MOV     R10,R5,LSL #24                  ; R10=lcode << 24
        ORR     R5,R14,R10,LSR #24              ; R5=(rcode << 8) + lcode
        TST     R9,#flg_noxoffsets
        MOVNE   R11,R11,LSR #16
        MOV     R11,R11,LSL #16                 ; R11=offset << 16
        ORR     R5,R5,R11
        STR     R5,[R4],#4
        B       %BT06                           ; lcode(8),rcode(8),off(16)

07      ADD     R3,R3,#1
        CMP     R3,#256
        BNE     %BT05

        SUB     R4,R4,R6                        ; store offset of end of all kerns in last word of index
        STR     R4,[R6,R3,LSL #2]

        SUB     R6,R6,#kern_index               ; R6 -> start of block again

      [ debugbrk :LOR: debugme
        ADD     R4,R4,#kern_index
        LDR     R14,[R6,#std_size]
        BIC     R14,R14,#size_flags
        Debug   me,"Estimated kern block size, actual =",R14,R4
        TEQ     R4,R14
        BreakPt "Estimated kern block size incorrect",NE
      ]

101     BL      makelink                        ; [R8] = R6 (successfully completed)

100
        PullP   "CacheKerns: pixelsptr",pixelsptr
        PullP   "CacheKerns: metricsptr",metricsptr

        BL      grabblock_close                 ; preserves error state

        MOV     R7,R6                           ; R7 -> new kern block

        EXIT

99
        DiscardBlock R6,R1,R2
        B       %BT100

cachenokerns
        PullP   "master metrics",R1
        BL      unlockblock                     ; unlock master metrics
        MOV     R6,#1                           ; R6=1 => no kern info
        B       %BT101

;..........................................................................

; Initialise global stuff for grabblock

grabblock_init Entry ""

        Debug   me,"grabblock_init"

        MOV     R14,#ScratchSpace               ; start at beginning of scratch space
        STR     R14,grabb_current
;        ADD     R14,R14,#ScratchSpaceSize      ; can't use ScratchSpace cos of graphics in error box
        STR     R14,grabb_limit
        MOV     R14,#0                          ; zero list of claimed RMA blocks
        STR     R14,grabb_head

        EXIT

Grabb_RMABlockSize      *       256

; . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

; In    R3 = size of block required (word-aligned)
; Out   R2 -> block claimed (in ScratchSpace or RMA)

grabblock_claim Entry "R0,R3"

        LDR     R2,grabb_current
        LDR     R0,grabb_limit
01      ADD     R14,R2,R3
        CMP     R14,R0
        BHI     %FT20
        STR     R14,grabb_current
      [ debugme
        Debug   me,"grabblock_claim returns block,size",R2,R3
00
      ]
        CLRV
        EXIT

20      MOV     R0,#ModHandReason_Claim
        CMP     R3,#Grabb_RMABlockSize          ; grab at least this much, to speed things up for small blocks
        MOVLT   R3,#Grabb_RMABlockSize
        ADD     R3,R3,#4                        ; allow for link word
        SWI     XOS_Module

        Debug   me,"Claimed RMA block,size",R2,R3

        ADDVC   R0,R2,R3                        ; limit is now at end of this block
        STRVC   R0,grabb_limit

        LDRVC   R14,grabb_head
        STRVC   R2,grabb_head
        STRVC   R14,[R2],#4                     ; first word of block is link to others

        LDRVC   R3,[sp,#4]                      ; recover block size required from stack
        BVC     %BT01                           ; must work this time

        STR     R0,[sp]
        EXIT

; . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

; In    [grabb_head] -> linked list of blocks claimed from RMA
; Out   blocks freed, error state preserved

grabblock_close EntryS "R0-R2"

        Debug   me,"grabblock_close"

        LDR     R2,grabb_head
        TEQ     R2,#0
        EXITS   EQ

01      LDR     R1,[R2]                         ; R1 -> next block
        Debug   me,"grabblock_close: losing block at",R2
        MOV     R0,#ModHandReason_Free
        SWI     XOS_Module                      ; ignore errors
        MOVS    R2,R1
        BNE     %BT01

        STR     R2,grabb_head
        EXITS                                   ; preserve error state

;..........................................................................

; In    R1-R4 = bounding box of this character relative to its origin (1/72000")
;       R5,R6 = current x,y coordinate (1/72000")
;       [currbboxx0/y0/x1/y1] = current string bounding box
; Out   [currbboxx0/y0/x1/y1] updated
;       R1-R4 = bbox of char with current position added in

computebbox_update Entry "R7-R10"

        Debug   bb2,"computebbox_update: x,y, x0,y0,x1,y1 =",R5,R6,R1,R2,R3,R4

        ADD     R1,R5,R1
        ADD     R2,R6,R2
        ADD     R3,R5,R3
        ADD     R4,R6,R4

        ADR     R14,currbboxx0
        LDMIA   R14,{R7-R10}

        CMP     R1,R7
        MOVLT   R7,R1
        CMP     R2,R8
        MOVLT   R8,R2
        CMP     R3,R9
        MOVGT   R9,R3
        CMP     R4,R10
        MOVGT   R10,R4

        STMIA   R14,{R7-R10}

        Debug   bb2,"computebbox_update: x0,y0,x1,y1 =",R7,R8,R9,R10

        EXIT

;;----------------------------------------------------------------------------
;; routines associated with VDU 25,&D0-&D7,x,y  -  paintchars
;;----------------------------------------------------------------------------

errnometricsdata
        ADR     R0,ErrorBlock_FontNoFont
        B       MyGenerateError
        MakeErrorBlock FontNoFont       ; "Undefined font"

errnopixeldata
        ADR     R0,ErrorBlock_FontNoPixels
        B       MyGenerateError
        MakeErrorBlock FontNoPixels     ; "No pixel data for this font"

;------------------------------------------------------------------------------
; scalex/yco: scale x- and y- coords to units of 1/72000th inch
; In    xcoord,ycoord = coordinates (pixels)
;       [x/yscalefactor] = relevant multiplication factors
; Out   R3 = x/y coordinate (1/72000")
;------------------------------------------------------------------------------

scalexco Entry ""

        LDR     R14,xscalefactor
        MUL     R3,xcoord,R14
        LDR     R14,modedata_px
        MOV     R3,R3,LSL R14

        EXIT

scaleyco Entry ""

        LDR     R14,yscalefactor
        MUL     R3,ycoord,R14
        LDR     R14,modedata_py
        MOV     R3,R3,LSL R14

        EXIT

;------------------------------------------------------------------------------
; In    wrchbase -> next char to read
;       [mapping2] -> current mapping from external to internal codes
; Out   pchar = next character, remapped if it was printable
;       wrchbase updated
;       LT => ctrl-code read (not remapped)
;       VS => error returned from mapchar

readnextchar_andremap Entry

        BL      readnextchar                    ; pchar = next code, wrchbase updated
        CMP     R0,#32
        BLGE    mapchar                         ; GE => was printable

        EXIT

;..............................................................................

; In    wrchbase -> next char to read
; Out   pchar = next character
;       wrchbase updated

readnextchar Entry "R1-R4,R7"

        MOV     R1,wrchbase

        BL      readuint1_R1

; If the returned number is <= &7F, the encoding is immaterial.
        CMP     R0,#&7F
        BLS     %FT99

        MOV     R2,R0

        LDRB    R0,currentfont
        BL      getfontheaderptr                ; can return errors
        EXIT    VS

        MOV     R0,R2

        ADD     R3,R7,#hdr_encoding
        LDMIA   R3,{R3,R4}                      ; remember font's target encoding, so we can check if it was "utf8"

; Unless font's encoding is UCS, we've finished

        ADR     R14,encoding_UTF8
        LDMIA   R14,{R2,R14}
        TEQ     R2,R3
        TEQEQ   R14,R4
        BNE     %FT99

; OK, it's UCS - check character width to see if we're reading UTF-8, UTF-16 or UCS-4

        LDR     R14,plottype
        ASSERT  paint_16bit < paint_32bit
        AND     R14,R14,#paint_16bit:OR:paint_32bit
        CMP     R14,#paint_16bit
        BHI     %FT99
        BEQ     %FT20

; UTF-8 parsing:
;
; Top n bits of first byte => how many bytes are in the code
; Top 2 bits of each continuation byte must be 10
; Other bits of first byte and continuations form the output byte
; most significant bits are read first
; So:
;       0xxxxxxx                    => &0000-&007F ( 7 bits)
;       110xxxxx 10xxxxxx           => &0080-&07FF (11 bits)
;       1110xxxx 10xxxxxx 10xxxxxx  => &0800-&FFFF (16 bits)
; etc. (max 6 bytes, which starts with 1111110x)
; It's also illegal to use an unnecessarily long code, eg.
;       11000000 10000000 => &0000, but this isn't allowed

        MOVS    R14,R0,LSL #25          ; CS => bit 7 was set, MI => bit 6 was set
        BCC     %FT99                   ; bit 7 clear => 1 byte code
        BPL     %FT98                   ; bit 6 clear => continuation byte (illegal - don't read it again)

        CMP     R0,#&FE                 ; &FE, &FF => illegal - don't read it again
        BHS     %FT98

        MOV     R4,#&80                 ; R4 = min value allowable
        MOV     R3,#&800                ; R3 = next bit to check for (NB: we gain only 4 bits the 1st time)
        BIC     R0,R0,#&C0              ; clear used control bits

01      LDRB    R2,[R1],#1
        AND     R14,R2,#&C0
        TEQ     R14,#&80
        BNE     %FT97                   ; it wasn't a continuation byte - step back so it'll be re-read

        AND     R2,R2,#&3F              ; R2 = next 6 bits to add in
        ORR     R0,R2,R0,LSL #6         ; add in next 6 bits at the bottom

        TST     R0,#1 :SHL: 31          ; top-bit set is illegal
        BNE     %FT98

        TST     R0,R3                   ; if next control bit set, read another continuation byte
        BICNE   R0,R0,R3                ; clear used control bit in result
        MOVNE   R4,R3                   ; R4 = min value for next time (same as old control bit)
        MOVNE   R3,R3,LSL #5            ; R3 = control bit for next time (5 bits further up)
        BNE     %BT01

; finished reading the character - was it in the right range?

        CMP     R0,R4
        BHS     %FT99                   ; OK if R0 >= R4
        B       %FT98                   ; otherwise return &FFFD

; UTF-16 parsing:
;
; &DC00-&DFFF is illegal
; &D800-&DBFF => next char must be &DC00-&DFFF:
;                result is &10000 + ( (first-&D800) << 10 ) + ( second - &DC00 )
; Illegal => result is &FFFD

20      CMP     R0,#&D800
        BLO     %FT99
        CMP     R0,#&DC00
        BHS     %FT95

      [ NoARMv4
        LDR     R2,[R1],#2
        MOV     R2,R2,LSL #16
        MOV     R2,R2,LSR #16
      |
        LDRH    R2,[R1],#2
      ]
        SUB     R2,R2,#&DC00
        CMP     R2,#&400
        BHS     %FT96                           ; wasn't a continuation code - step back

        SUB     R0,R0,#&D800
        ADD     R0,R2,R0,LSL #10
        ADD     R0,R0,#&10000                   ; result is &10000 + ( (first-&D800) << 10 ) + (second-&DC00)
        B       %FT99

95      CMP     R0,#&E000                       ; R0 is >= &DC00 here
        BHS     %FT99
        B       %FT98                           ; continuation code in wrong place

96      SUB     R1,R1,#1                        ; step back 16 bits

97      SUB     R1,R1,#1                        ; step back 8 bits to character just read

98      LDR     R0,=&FFFD                       ; illegal character

99      MOV     wrchbase,R1

        Debug   just,"readnextchar returns",R0

        EXIT
        LTORG

encoding_UTF8   DCB     "utf8",0,0,0,0          ; first 4 bytes of encoding is enough to identify UCS
                ALIGN                           ; just in case!

;..............................................................................

; In    R1 -> next char to read
; Out   R0 = next character
;       R1 updated

readuint1_R1

        LDR     R0,plottype
        ASSERT  paint_16bit < paint_32bit
        AND     R0,R0,#paint_16bit:OR:paint_32bit
        CMP     R0,#paint_16bit
        LDRLOB  R0,[R1],#1
        LDRHI   R0,[R1],#4                      ; assume word-aligned
      [ NoARMv4
        LDREQ   R0,[R1],#2                      ; assume bottom 16 bits are OK for half-word access
        MOVEQ   R0,R0,LSL #16
        MOVEQ   R0,R0,LSR #16
      |
        LDREQH  R0,[R1],#2
      ]

        MOV     PC,LR

readuint1_wrchbase
        Entry  "R1"
        MOV     R1,wrchbase
        BL      readuint1_R1
        ASSERT  pchar = R0
        MOV     wrchbase,R1
        EXIT

;..............................................................................

; In    R1 -> next char to read
; Out   R0 = next character
;       R1 updated

readint1_R1

        LDR     R0,plottype
        ASSERT  paint_16bit < paint_32bit
        AND     R0,R0,#paint_16bit:OR:paint_32bit
        CMP     R0,#paint_16bit

      [ NoARMv4
        LDRLOB  R0,[R1],#1
        MOVLO   R0,R0,LSL #24                   ; sign-extend
        MOVLO   R0,R0,ASR #24
        LDRHI   R0,[R1],#4                      ; assume word-aligned
        LDREQ   R0,[R1],#2                      ; assume bottom 16 bits are OK for half-word access
        MOVEQ   R0,R0,LSL #16
        MOVEQ   R0,R0,ASR #16
      |
        LDRLOSB R0,[R1],#1
        LDRHI   R0,[R1],#4                      ; assume word-aligned
        LDREQSH R0,[R1],#2
      ]

        MOV     PC,LR

readint1_wrchbase
        Entry  "R1"
        MOV     R1,wrchbase
        BL      readint1_R1
        ASSERT  pchar = R0
        MOV     wrchbase,R1
        EXIT

;..............................................................................

; In    R1 -> next char to read
; Out   R0 = next value (3 bytes, or 2 shorts, or 1 long)
;       R1 updated

readint3_R1 Entry

        LDR     R14,plottype
        ASSERT  paint_16bit < paint_32bit
        AND     R14,R14,#paint_16bit:OR:paint_32bit
        CMP     R14,#paint_16bit

    [ NoARMv6 :LOR: NoUnaligned
        LDRLOB  R0,[R1],#1              ; byte 0
        LDRLOB  R14,[R1],#1
        ORRLO   R0,R0,R14,ASL #8        ; byte 1
      [ NoARMv4
        LDRLOB  R14,[R1],#1             ; byte 2
        ORRLO   R0,R0,R14,ASL #16
        MOVLO   R0,R0,ASL #8            ; sign-extend
        MOVLO   R0,R0,ASR #8
      |
        LDRLOSB R14,[R1],#1             ; byte 2
        ORRLO   R0,R0,R14,ASL #16
      ]

      [ NoARMv4
        LDREQ   R0,[R1],#2              ; note that this could span a word boundary
        LDREQ   R14,[R1],#2
        MOVEQ   R0,R0,LSL #16
        MOVEQ   R0,R0,LSR #16
      |
        LDREQH  R0,[R1],#2              ; note that this could span a word boundary
        LDREQH  R14,[R1],#2
      ]
        ORREQ   R0,R0,R14,LSL #16

        LDRHI   R0,[R1],#4
    |
        LDR     R0,[R1],#4              ; unaligned load
        SUBLO   R1,R1,#1
      [ NoARMT2
        MOVLO   R0,R0,ASL #8            ; sign-extend
        MOVLO   R0,R0,ASR #8
      |
        DCI     &37B70050 ; SBFXLO  R0,R0,#0,#24
      ]
    ]

        EXIT

readint3_wrchbase
        Entry  "R1"
        MOV     R1,wrchbase
        BL      readint3_R1
        ASSERT  pchar = R0
        MOV     wrchbase,R1
        EXIT

;..............................................................................

; In    R1 -> next char to read
; Out   R0 = RGB value read from string (format depends on plottype)
;       R1 updated

readRGB_R1 Entry

        LDR     R14,plottype
        ASSERT  paint_16bit < paint_32bit
        AND     R14,R14,#paint_16bit:OR:paint_32bit
        CMP     R14,#paint_16bit

    [ NoARMv6 :LOR: NoUnaligned
        LDRLOB  R0,[R1],#1              ; R1 = background RGB (later)
        LDRLOB  R14,[R1],#1
        ORRLO   R0,R0,R14,LSL #8
        LDRLOB  R14,[R1],#1
        ORRLO   R0,R0,R14,LSL #16
        MOVLO   R0,R0,LSL #8            ; &BBGGRR00

      [ NoARMv4
        LDREQ   R0,[R1],#2              ; note that this could span a word boundary
        LDREQ   R14,[R1],#2
        MOVEQ   R0,R0,LSL #16
        MOVEQ   R0,R0,LSR #16           ; &0000RR00
      |
        LDREQH  R0,[R1],#2              ; note that this could span a word boundary
        LDREQH  R14,[R1],#2
      ]
        ORREQ   R0,R0,R14,LSL #16       ; &BBGGRR00

        LDRHI   R0,[R1],#4              ; easy in 32-bit mode
    |
        LDR     R0,[R1],#4              ; unaligned load
        SUBLO   R1,R1,#1
        MOVLO   R0,R0,LSL #8            ; &BBGGRR00
    ]

        EXIT

;..............................................................................

; In    R0 = external character code
;       [mapping2] -> external=>internal mapping
; Out   R0 = internal character code, or -1 => no mapping

mapchar EntryS  "R1-R4,R6,R7"

        MOV     R4,R0                           ; R4 = external character code

        LDRB    R0,currentfont
        BL      getfontheaderptr                ; can return errors
        EXIT    VS

        MOV     R6,R7
        BL      getmapping_fromR6               ; can return errors (and relocate cache!)
        EXIT    VS

        MOV     R0,R4

        TEQ     R1,#0
        BEQ     %FT90                           ; that was too easy!

; new-style lookup structure can cope with sparse mappings
; first do a binary chop to locate the relevant lkent array entry

        ASSERT  lookup_entries = 0
        MOV     R2,#0                           ; R2 = min entry it could be (0)
        LDR     R3,[R1],#lookup_entry0
        ASSERT  lkent_end = 8
        ADD     R7,R1,R3,LSL #3                 ; R7 -> lktab array
        SUBS    R3,R3,#1                        ; R3 = max entry it could be (number of entries-1)
        BLT     %FT80                           ; no entries in lookup table!
01
        Debug   enc,"**** Binary chop lookup: R0,R2,R3",R0,R2,R3

        CMP     R2,R3
        BGT     %FT80                           ; if R2 > R3, it's not in any of the ranges
        BEQ     %FT10                           ; if R2 = R3, there's only one possible range left

        ADD     R6,R2,R3
        ADD     R6,R6,#1                        ; round up if midway, as this gives better pruning
        MOV     R6,R6,LSR #1                    ; R6 = entry midway between R2 and R3 (must be >= R2 and <= R3)

        ASSERT  lkent_min = 0
        ASSERT  lkent_end = 8
        LDR     R14,[R1,R6,LSL #3]              ; R14 = start of this range of characters

        CMP     R0,R14
        MOVGE   R2,R6                           ; if code >= this one, set R2 (min) = this one
        SUBLT   R3,R6,#1                        ; if code < this one, set R3 (max) = this one - 1
        BNE     %BT01                           ; EQ => must be in this one!

10
        ASSERT  lkent_min = 0
        ASSERT  lkent_end = 8
        LDR     R14,[R1,R2,LSL #3]!             ; R14 = start of this range of characters, R1 -> entry

        SUBS    R14,R0,R14                      ; R14 = offset within this range
        BLO     %FT80

        ASSERT  (lkent_n :AND: 3)=0
        ASSERT  lkent_offset = lkent_n + 2
        LDR     R2,[R1,#lkent_n]

        Debug   enc,"R0 offset within range, lkent_n/offset =",R14,R2

        ASSERT  lkflag_istable = 1 :SHL: 15
        MOV     R3,R2,LSL #17
        MOV     R3,R3,LSR #17                   ; R3 = bottom 15 bits (actual n)
        CMP     R14,R3
        BHS     %FT80                           ; out of range

        ASSERT  lkflag_istable = 1 :SHL: 15
        MOVS    R2,R2,LSR #16                   ; R2 = glyph offset, CS => lkent_istable set
        ADDCS   R2,R2,R14                       ; add in offset within the range of characters
        ASSERT  lktab_end = 2
        LDRCSB  R2,[R7,R2,LSL #1]!
        LDRCSB  R14,[R7,#1]
        ORRCS   R2,R2,R14,LSL #8                ; R2 = offset (read from lktab array, or constant)

        ADD     R0,R0,R2
        MOV     R0,R0,LSL #16
        MOV     R0,R0,LSR #16                   ; use modulo 16 arithmetic, to allow 32-bit external codes
        B       %FT90

80      MOV     R0,#chcode_unmapped             ; no mapping
90
        Debug   me,"mapchar: in/out",R4,R0

        EXITVC

;..............................................................................

; In    R1 = external character code
;       [mapping2] -> external=>internal mapping
; Out   R1 = internal character code
;       VS => R0 -> error message (VC => R0 preserved)

mapchar_R1 Entry "R0"

        MOV     R0,R1
        BL      mapchar
        MOVVC   R1,R0

        STRVS   R0,[SP]
        EXIT

;;----------------------------------------------------------------------------
;; VDU 25,&D0-&D7,x,y : PRINT "<string>"
;; SYS "Font_Paint",,flags,string,x,y,coordblk,matrix,length
;;
;; In   R0 = font handle (if R2 bit 8 set)
;;      R1 -> string
;;      R2 = flags (see Font_Paint)
;;      R3,R4 = coords
;;      R5 -> coordblk (if R2 bit 5 set)
;;      R6 -> matrix (if R2 bit 6 set)
;;      R7 = string length (if R2 bit 7 set)
;;      [switch_buffer] non-zero => output Draw object into buffer
;; Out  chars painted and ChangedBox updated, or buffer updated
;;----------------------------------------------------------------------------

paintchars PEntry Painting, ""                  ; NB: several bits later on go ' Pull "PC" '

        Debug   ch2,"Paintchars"

      [ debugxx
        BL      debugpaintparams
      ]

        ProfIn  Paint_Init

        LDR     R14,=paint_reserved
        TST     R2,#paint_coordblk
        ORRNE   R14,R14,#paint_osunits :OR: paint_justify  ; 1/256 OS unit not yet implemented
        TST     R2,R14                                     ; and justification is done by the coordblk
        BLNE    err_reserved
        BNE     %FT99

        TST     R2,#paint_font                  ; set up current font if R2 bit 8 set
        TEQNE   R0,#0                           ; and R0 <> 0
        STRNEB  R0,currentfont

        STR     R1,wrchblkptr
        STR     R2,plottype
        BL      checkblend

; set up terminating address of string from R7, else use -1 if R2 bit 7 clear

        MOV     wrchbase,R1                     ; wrchbase (R11) -> points to string
        TST     R2,#paint_length
        ADDNE   R7,wrchbase,R7
        MOVEQ   R7,#&FFFFFFFF                   ; must use unsigned comparison, and ignore V set
        STR     R7,paintendaddress

; convert coordinates to millipoints (input units depend on R2 bits 4,5)

        Push    "ycoord,xcoord"                 ; these are pulled MUCH later!

        TST     R2,#paint_osunits               ; 1 ==> convert to 1/72000th"
        STREQ   R3,xco72                        ; coords in 1/72000th inch
        STREQ   R4,yco72
        BLEQ    calcxcoord                      ; obtain xcoord,ycoord
        LDRNE   R14,modedata_px
        MOVNE   xcoord,R3,ASR R14               ; scale down to pixels
        LDRNE   R14,modedata_py
        MOVNE   ycoord,R4,ASR R14
        BLNE    scalexco                        ; obtain xco72,yco72
        STRNE   R3,xco72
        BLNE    scaleyco
        STRNE   R3,yco72

; set up flags for setoutputdata and setrubout (delayed until a char is printed)

        MOV     R0,#0
        STRB    R0,setruboutflag                ; setmodedata done already
        STR     R0,ul_word                      ; underlining off to start with
        STR     R0,switch_underline             ; haven't constructed it yet
        STR     R0,switch_rubout

; fiddle around with previous graphics points (unless R2 bit 5 set)

        TST     R2,#paint_coordblk
        BEQ     %FT11

        LDMIA   R5!,{R1,R3}
        STR     R1,xspaceadd
        STR     R3,yspaceadd

        LDMIA   R5!,{R1,R3}
        STR     R1,xletteradd
        STR     R3,yletteradd

        TST     R2,#paint_rubout
        BEQ     %FT12

        Push    "x0,y0,x1,y1"
        LDMIA   R5,{x0,y0,x1,y1}
        BL      calcrubout                      ; works out rubout coords from 1/72000" input
        Pull    "x0,y0,x1,y1"
        BGE     %FT13                           ; don't forget to update the changed box
        BLT     %FT01                           ; (if there was a rubout box, that is!)

11      ADR     R0,y_old0

        TST     R2,#paint_justify               ; set up justifyx, justifyy
        LDMNEDA R0!,{R1,R3}
        STRNE   R1,justifyx
        STRNE   R3,justifyy                     ; NB: this will be ignored (too inaccurate)

        TST     R2,#paint_rubout                ; set up rubx0,y0,x1,y1
12      MOVEQ   R14,#bignum
        STREQ   R14,ruboutx
        MOVEQ   R14,#1
        STREQB  R14,setruboutflag               ; don't bother to set rubout
        BEQ     %FT01

        LDMDA   R0!,{R1,R3}
        STR     R1,rubx1
        STR     R3,ruby1
        LDMDA   R0!,{R1,R3}
        STR     R1,rubx0
        STR     R1,ruboutx                      ; ensure ruboutx <> bignum  -  real value set up in setrubout
        STR     R3,ruby0

13      LDR     R14,changedbox
        LDR     R14,[R14]                       ; bit 0 set => changed box enabled
        LDR     R1,switch_flags                 ; no screen update if going to buffer
        BIC     R14,R14,R1,LSR #swb_enabled-0
        TST     R14,#1
        BLNE    changedrubout                   ; update ChangedBox if nec.
01

; work out justification the old way (only necessary if no paint_coordblk)

        Debug   just,"R2 (flags) = ",R2

        TST     R2,#paint_coordblk
        BLEQ    calcjustify                     ; works out x/yspaceadd from justifyx/y (if R2 bit 0 set)

; read initial setting of matrix (if any)

        LDR     R14,plottype
        TST     R14,#paint_matrix
        MOVEQ   R6,#0
        BL      setpaintmatrix                  ; set up initial paint matrix

        Pull    "ycoord,xcoord"                 ; these were pushed MUCH earlier!

        Debug   co,"pixels: xcoord,ycoord =",xcoord,ycoord
        Debug   co,"milpts: x,y letter, x,y space add =",#xletteradd,#yletteradd,#xspaceadd,#yspaceadd
        Debug   co,"pixels: rubx0,y0,x1,y1 =",#rubx0,#ruby0,#rubx1,#ruby1

        ProfOut Paint_Init
Profile_Last SETS "Painting"                    ; restore correct value for PExit

; get ready to start printing the string

        MOV     R14,#-1                         ; always recompute render matrix the first time
        STR     R14,oldpaintmatrix              ; also reset to -1 on a font change

        MOV     metricsptr,#0                   ; --> haven't set it up yet
        MOV     pixelsptr,#0

        STR     metricsptr,xglueadd             ; zero glue offsets for first character
        STR     metricsptr,yglueadd

; metricsptr, pixelsptr, wrchbase, xcoord, ycoord used in the following loop

paintlp
        LDR     R14,paintendaddress
        CMP     wrchbase,R14                    ; unsigned comparison
        BHS     %FT98                           ; V not set if this test succeeds (I hope!)

        BL      readnextchar                    ; In: wrchbase->next char
        STR     R0,externalcharcode             ; for addcharwidth to check for <space> for justification
        CMP     R0,#32
        BLGE    mapchar
        PExit   VS
        BLT     %FT20

        CMP     pchar,#chcode_unmapped          ; ignore completely if unmappable
        BNE     paintchar
        B       paintlp

20      TEQ     pchar,#Colour                   ; set foreground/background colour
        BEQ     setcolour
        TEQ     pchar,#Colour3
        BEQ     setcolour3
        TEQ     pchar,#ColourRGB
        BEQ     setcolourRGB
        TEQ     pchar,#Fontchar                 ; set font number
        BEQ     setfont
        TEQ     pchar,#Comment                  ; ignore string?
        BEQ     yesComment
        TEQ     pchar,#Underline
        BEQ     setunderline
        TEQ     pchar,#MoveX
        BEQ     movex
        TEQ     pchar,#MoveY
        BEQ     movey
        TEQ     pchar,#Matrix00
        BEQ     setmat00
        TEQ     pchar,#Matrix
        BEQ     setmat

        TEQ     pchar,#0                        ; check for legal terminator
        TEQNE   pchar,#cr
        TEQNE   pchar,#lf
        BLNE    xerr_FontBadCtrlChar
        PExit   VS

; ensure rest of rubout box is dealt with

98
        BL      calcxcoord                      ; x/ycoord <- x/yco72
        BL      ruboutrest
99
        PExit
        LTORG

;.............................................................................

; Entry:  [rubx0,y0,x1,y1] = rubout box coordinates
; Exit:   [[changedbox]+4..] updated

changedrubout Entry "x0,y0,x1,y1"

        ADR     R14,rubx0
        LDMIA   R14,{x0,y0,x1,y1}
        BL      changebox

        EXIT

;.............................................................................

; set paint matrix from a matrix in the string

setmat
        ASSERT  (mat_enduser :AND: 3) = 0
        ADD     wrchbase,wrchbase,#3 + mat_enduser
        BIC     wrchbase,wrchbase,#3            ; wrchbase -> byte after matrix
        SUB     R6,wrchbase,#mat_enduser
        BL      setpaintmatrix                  ; sets up [paintmatrix]

        B       resetpixelsptr                  ; pixelsptr and metricsptr must be reset

;.............................................................................

; set paint matrix from a matrix in the string, assuming X=Y=0

setmat00
        ASSERT  (mat_X :AND: 3) = 0
        ADD     wrchbase,wrchbase,#3 + mat_X
        BIC     wrchbase,wrchbase,#3            ; wrchbase -> byte after matrix

        LDMDB   wrchbase,{R0-R3}                ; Note that the matrix is stored as aligned 32-bit words,
        MOV     R4,#0                           ; regardless of the character width settings in Font_Paint
        MOV     R5,#0
        ADR     R6,paintmatrixbuffer
        STMIA   R6,{R0-R5}

        LDR     R14,oldpaintmatrix              ; if oldpaintmatrix -> paintmatrixbuffer
        TEQ     R14,R6                          ; set oldpaintmatrix = -1 (not 0 as new one may be unit matrix!)
        MOVEQ   R14,#-1
        STREQ   R14,oldpaintmatrix

        BL      setpaintmatrix                  ; sets up [paintmatrix]

        B       resetpixelsptr                  ; pixelsptr and metricsptr must be reset

;.............................................................................

; In    R6 -> new paint matrix
; Out   R6 = [paintmatrix] = original R6 or 0 if it was the unit matrix

setpaintmatrix EntryS "R0-R5"

        Debug   matrix,"setpaintmatrix: R6 =",R6

        MOV     R14,#MAT_MARKER                 ; mark stashed copy invalid
        STR     R14,fppaintmatrixbuffer + mat_marker

        TEQ     R6,#0
        STREQ   R6,paintmatrix
        EXITS   EQ

        LDMIA   R6,{R0-R5}
        TEQ     R0,#1:SHL:16
        TEQEQ   R1,#0
        TEQEQ   R2,#0
        TEQEQ   R3,#1:SHL:16
        TEQEQ   R4,#0
        TEQEQ   R5,#0
        MOVEQ   R6,#0
        STR     R6,paintmatrix

        DebugM  matrix,"New paintmatrix =",R6

        EXITS

;.............................................................................

; move about within string (usually for TABs or super/subscript)

movex
        BL      readint3_wrchbase
        LDR     R14,xco72
        Debuga  co2,"Old xco72, MoveX by, New xco72 =",R14,R0
        ADD     R14,R14,R0              ; update x-coord
        Debug2  co2,R14
        STR     R14,xco72
        B       paintlp

;.............................................................................

movey
        BL      readint3_wrchbase
        LDR     R14,yco72
        Debuga  co2,"Old yco72, MoveY by, New yco72 =",R14,R0
        ADD     R14,R14,R0              ; update y-coord
        Debug2  co2,R14
        STR     R14,yco72
        B       paintlp

;.............................................................................

; ignore characters until the next control character

yesComment
        BL      readuint1_wrchbase
        CMP     pchar,#32
        BCS     yesComment
        B       paintlp

;.............................................................................

; set foreground/background colour

setcolour
        BL      readuint1_wrchbase
        SUBS    R14,pchar,#&80
        STRCCB  pchar,forecolour
        STRCSB  R14,backcolour
        MOV     R14,#setout_invalid     ; must do this cos of Font_SetColourTable
        STRB    R14,setoutputdataflag

        BL      invalidateRGB           ; error if output switched to a buffer
        B       setcol_continue         ; copes with VS

;.............................................................................

; set f/c/a colours for text printing

setcolour3
        BL      readuint1_wrchbase
        AND     pchar,pchar,#&7F
        STRB    pchar,backcolour        ; background colour

        BL      readuint1_wrchbase
        STRB    pchar,forecolour        ; foreground colour

        BL      readint1_wrchbase
        STRB    pchar,aliascolours      ; anti-alias colour offset

        MOV     R14,#setout_invalid
        STRB    R14,setoutputdataflag

        BL      invalidateRGB           ; for Font_SwitchOutputToBuffer

setcol_continue
        MOV     pixelsptr,#0            ; may swap between 1 and 4-bpp (but render matrix is unaltered)
        BVC     paintlp

        ProfOut Painting
        Pull    "PC"                    ; error

;.............................................................................

; set f/c/a colours from RGB input, by calling ColourTrans_SetFontColours

setcolourRGB
        wsaddr  R0,currentdata                  ; R0 -> destination buffer [back,fore,offset]
        MOV     R1,wrchbase                     ; R1 -> source string (r,g,b,R,G,B,max)
        BL      getrgb_fromstring               ; marks outputdata invalid
        MOV     wrchbase,R1

        BL      restoreoutput                   ; in case output was switched to the buffer sprite

        wsaddr  R0,currentdata
        LDMIA   R0,{R1-R3}                      ; R1,R2,R3 = bg RGB, fg RGB, max offset

        Push    "R1-R3"                         ; save true RGB values

        MOV     R0,#0                           ; R0 = 0 => don't touch font handle
        SWI     XColourTrans_SetFontColours     ; calls Font_SetColourTable or Font_SetFontColours or Font_SetPalette
        STRVCB  R1,backcolour
        STRVCB  R2,forecolour
        STRVCB  R3,aliascolours

        Debug   rgb,"ColourTrans_SetFontColours returns",R1,R2,R3

        Pull    "R1-R3"
        wsaddr  R14,currentdata                 ; restore correct RGB colours
        STMIA   R14,{R1-R3}                     ; which were corrupted by ColourTrans calling Font_SetFontColours

        STR     R1,switch_backcolour            ; get correct colours for underline/rubout rectangles
        STR     R2,switch_forecolour

        B       setcol_continue                 ; deals with errors

;.............................................................................

; In    R0 -> place to store rgb back,fore,offset (words)
;       R1 -> string containing r,g,b,R,G,B,max
; Out   R1 -> byte after R,G,B
;       [R0,#0,4,8] = bg,fg,max offset
;       [R0,#aliascolours] = &80 => absolute colours are out-of-date

getrgb_fromstring Entry "R0-R3"

        BL      readRGB_R1
        Push    "R0"                    ; R1 = background RGB (on stack for later)
        BL      readRGB_R1
        MOV     R2,R0                   ; R2 = foreground RGB
        BL      readuint1_R1
        MOV     R3,R0                   ; R3 = max offset (unsigned)
        STR     R1,[sp,#2*4]            ; update R1 for return
        Pull    "R1"

        LDR     R0,[sp]                 ; set current/future RGB values
        STMIA   R0,{R1-R3}              ; max >= 0 => RGB, not absolute

        Debuga  rgb,"rgb in string:",R1,R2,R3
        Debug   rgb," - stored at",R0

        LDR     R14,currentRGB_b        ; for SwitchOutputToBuffer
        STR     R14,switch_backcolour
        LDR     R14,currentRGB_f
        STR     R14,switch_forecolour

        MOV     R14,#setout_invalid     ; not sure whether this will change the colours
        STRB    R14,setoutputdataflag   ; better be on the safe side

        EXIT

;.............................................................................

; change font (in the middle of a string)

setfont
        ASSERT  maxf = 256
        BL      readuint1_wrchbase
        STRB    pchar,currentfont
        STRB    pchar,tfuturefont
        Debug   scanstring,"** Stored future value at tfuturefont 2"
        MOV     R14,#1
        STR     R14,futurechanged
        Debug   scanstring,"** Set futureflag"
;        STRB    pchar,futurefont
        MOV     R14,#-1                 ; force rendermatrix to be recalculated
        STR     R14,oldpaintmatrix

resetpixelsptr
        MOV     metricsptr,#0
        MOV     pixelsptr,#0

        B       paintlp

;.............................................................................

; set up underline data
; posn = n/256 of the y-size of the font (sign-extended)
; size = n/256 of the y-size of the font

setunderline
        LDR     R14,plottype            ; new underline starts after 1/2 the glue
        TST     R14,#paint_rightleft

        LDR     R0,xco72
        LDR     R14,xglueadd
        ADDEQ   R0,R0,R14,ASR #1
        SUBNE   R0,R0,R14,ASR #1
        STR     R0,switch_ulxcoord

        LDR     R0,yco72
        LDR     R14,yglueadd
        ADDEQ   R0,R0,R14,ASR #1
        SUBNE   R0,R0,R14,ASR #1
        STR     R0,switch_ulycoord

        LDRB    R14,currentfont
        LDR     R0,cacheindex
        TEQ     R0,#0
        LDRNE   R0,[R0,R14,ASL #2]      ; R0 --> font header
        TEQNE   R0,#0
        BLEQ    errnometricsdata
        ProfOut Painting,VS
        Pull    "PC",VS

        LDR     R1,[R0,#hdr_ysize]      ; get font y-size (1/16ths point)
        Debug   ul,"Font size (1/16th point) =",R1
        BL      readint1_wrchbase
        MUL     R3,R0,R1
        Debug   ul,"Underline posn (1/16ths point) =",R3
        ADD     R14,R3,R3,ASL #1        ; R3 <- R3 * 1000 / 4096
        RSB     R3,R14,R3,ASL #7        ; (ie:  R3 *  125 /  512)
        MOV     R3,R3,ASR #9
        Debug   ul,"Underline posn (1/72000"""") =",R3

        Push    "R3"
        BL      readuint1_wrchbase
        MUL     R3,R0,R1
        Pull    "R1"
        Debug   ul,"Underline height (1/16ths point) =",R3
        ADD     R14,R3,R3,ASL #1        ; R3 <- R3 * 1000 / 4096
        RSB     R3,R14,R3,ASL #7        ; (ie:  R3 *  125 /  512)
        MOV     R3,R3,ASR #9
        Debug   ul,"Underline height (1/72000"""") =",R3

      [ newunderline
        STR     R1,switch_ulytop72000   ; in case of non axis-aligned stuff
        SUB     R14,R1,R3
        STR     R14,switch_ulybot72000
      ]

        LDR     R0,switch_ulycoord      ; get posn in 1/72000"
        ADD     R0,R0,R1
        Debug   ul,"Underline top y-coord (1/72000"""") =",R0
        STR     R0,switch_ultop         ; only used if output switched
        STR     R3,switch_ulheight      ; both stored in 1/72000"
     ;; ADD     R0,R0,R3,ASR #1         ; DON'T add 1/2 line height !!!
        MOV     R0,R0,ASL #4            ; we want to know the 1/16ths too
        Push    "R0,R3"

        BL      dividey
        AND     R0,R2,#&0F
        STRB    R0,ul_top
        Debug   ul,"ul_top =",R0
        MOV     ycoord,R2,ASR #4        ; get integer part
        STR     ycoord,ul_ycoord        ; coord of top pixel

        Pull    "R0,R3"
        SUB     R0,R0,R3,ASL #4         ; get coord of bottom of line (*16)
        BL      dividey
        SUB     R14,ycoord,R2,ASR #4    ; get difference in pixel height
        Debug   ul,"Middle height + 1 =",R14
        SUBS    R14,R14,#1
        MOVLT   R14,#0
        STRB    R14,ul_midheight
        Debug   ul,"ul_midheight =",R14

        LDRLTB  R14,ul_top              ; if same pixel, subtract the two
        MOVGE   R14,#15                 ; else subtract from 15
        AND     R0,R2,#&0F
        SUB     R0,R14,R0               ; colour of bottom pixel
        STRLTB  R0,ul_top
        MOVLT   R0,#0
        STRB    R0,ul_bot
        Debug   ul,"ul_bot =",R0

        LDR     R0,switch_ulxcoord
        BL      dividex
        STR     R2,ul_xcoord

        Debug   ul,"ul_word (top,midheight,bot) =",#ul_word
        Debug   ul,"ul_x,ycoord =",#ul_xcoord,#ul_ycoord

        MOV     R14,#0                  ; start new underline rectangle
        STR     R14,switch_underline

        B       paintlp

;.............................................................................

; If output switched, update rectangle object for the underline
;
; In    R1 = switch_buffer pointer
;       switch_ultop     =   top of underline (1/72000")
;       switch_ulheight  =   height of underline (1/72000")
;       switch_ulxcoord  =   start x-coord of underline (1/72000")
;       switch_nextx/ycoord =  end x,y coords of underline (1/72000")

        ASSERT  inptr = R4
        ASSERT  xcoord > R3
        ASSERT  ycoord > R3

switched_underline  ROUT        ; registers from 'underline' already stacked

        Push    "R1-R3"

        LDR     R14,switch_lastfore     ; start new underline object if colours different
        LDR     R2,switch_forecolour
        TEQ     R2,R14
        LDRNE   R14,switch_underline    ; NB: DO NOT restart if no object yet under way
        TEQNE   R14,#0
        MOVNE   R14,#0
        STRNE   R14,switch_underline    ; force new underline object to start here
        LDRNE   R14,switch_ullastx
        STRNE   R14,switch_ulxcoord     ; with new start point (x,y)

        ADR     R2,switch_underline
        BL      makerectangleobject     ; in case this is the first time
        ProfOut Paint_Underline, VS
        Pull    "R1-R3,inptr,xcoord,ycoord,PC",VS

        LDR     R14,switch_flags
        TST     R14,#swf_justcount
        ProfOut Paint_Underline, NE
        Pull    "R1-R3,inptr,xcoord,ycoord,PC",NE

        LDR     R0,switch_ultop
        LDR     R14,switch_ulheight
        SUB     R3,R0,R14

        MOV     R0,R0,LSL #8            ; get the answer in 1/256 OS unit
        LDR     R1,yscalefactor
        BL      divide                  ; rounds towards -infinity

        ASSERT  xcoord > R4             ; no register clashes

        MOV     R4,R2                   ; R4 = y1

        MOV     R0,R3,LSL #8
        LDR     R1,yscalefactor
        BL      divide                  ; R2 = y0

        Push    "R2"

        LDR     xcoord,switch_nextxcoord  ; xcoord = end of underline (1/72000")
        STR     xcoord,switch_ullastx     ; stash in case of colour change
        LDR     ycoord,saved_nextycoord

        MOV     R0,xcoord,LSL #8
        LDR     R1,xscalefactor
        BL      divide
        MOV     R3,R2                   ; R3 = x1

        LDR     R0,switch_ulxcoord
        MOV     R0,R0,LSL #8
        LDR     R1,xscalefactor
        BL      divide
        MOV     R1,R2                   ; R1 = x0

        Pull    "R2"                    ; R2 = y0

        LDR     R0,switch_underline
        BL      setrectanglesize

        ProfOut Paint_Underline
        Pull    "R1-R3,inptr,xcoord,ycoord,PC"

;.............................................................................

; In    R1 -> output buffer
;       R2 -> switch_underline or switch_rubout
; Out   [switch_underline/rubout] -> rectangle object for underline/rubout
;       colour taken from [switch_fore/backcolour]
;           and copied into [switch_lastfore/back]
;       R1 = [switch_buffer] = updated buffer pointer / counter

makerectangleobject Entry "R2-R7"

        LDR     R14,[R2]                ; if this exists already,
        TEQ     R14,#0
        EXIT    NE                      ; then just exit!

        Debuga  ul,"Starting rectangle at",R1
        Debug   ul," for object at",R2

        STR     R1,[R2]                 ; remember current pointer

        ASSERT  switch_backcolour-switch_rubout = switch_forecolour-switch_underline
        LDR     R4,[R2,#switch_backcolour-switch_rubout]

        ASSERT  switch_lastback-switch_rubout = switch_lastfore-switch_underline
        STR     R4,[R2,#switch_lastback-switch_rubout]

        Debug   rgb,"Rectangle RGB colour is",R4

        LDR     R14,switch_flags
        TST     R14,#swf_justcount
        ADDNE   R1,R1,#rectobj_size
        BNE     %FT01

        LDR     R3,[R1,#4]
        SUBS    R3,R3,#rectobj_size
        BLLT    xerr_buffoverflow
        Pull    "R2-R7,PC",VS

        MOV     R2,#2
        MOV     R5,#rectobj_size
        STMIA   R1!,{R2,R5,R6-R9}       ; bounding box is irrelevant here
        ASSERT  pathobj_fillcolour = 6*4
                                        ; R4 = colour already
        MOV     R5,#&FFFFFFFF           ; no outline
        MOV     R6,#0                   ; no outline thickness
        MOV     R7,#0                   ; path style
        STMIA   R1!,{R4-R7}
        ASSERT  pathobj_header-pathobj_fillcolour = 4*4

        MOV     R14,#2                  ; move
        STR     R14,[R1],#12            ;       x0,y0
        MOV     R14,#8                  ; line
        STR     R14,[R1],#12            ;       x1,y0
        STR     R14,[R1],#12            ;       x1,y1
        STR     R14,[R1],#12            ;       x0,y1
        MOV     R14,#5                  ; close
        STR     R14,[R1],#4
        MOV     R14,#0                  ; terminator
        STR     R14,[R1],#4
        ASSERT  rectobj_size - pathobj_header = 4*12+8

        STR     R14,[R1,#0]
        STR     R3,[R1,#4]              ; size remaining

01      STR     R1,switch_buffer        ; update buffer pointer

        EXIT

;.............................................................................

; In    R0 -> rectangle object (in Draw file format)
;       R1-R4 = new coordinates
; Out   object data updated

setrectanglesize Entry "R1-R4"

        LDR     R14,switch_flags        ; this test is for the rubout stuff
        TST     R14,#swf_justcount
        EXIT    NE

        SUBS    R14,R3,R1               ; swap so larger values come later
        SUBLT   R3,R3,R14
        ADDLT   R1,R1,R14

        SUBS    R14,R4,R2
        SUBLT   R4,R4,R14
        ADDLT   R2,R2,R14

        Debuga  ul,"Rectangle at",R0
        Debug   ul," : size =",R1,R2,R3,R4

        ADD     R14,R0,#obj_bbox
        STMIA   R14,{R1-R4}

        STR     R1,[R0,#rectobj_x0y0+4]
        STR     R2,[R0,#rectobj_x0y0+8]

        STR     R3,[R0,#rectobj_x1y0+4]
        STR     R2,[R0,#rectobj_x1y0+8]

        STR     R3,[R0,#rectobj_x1y1+4]
        STR     R4,[R0,#rectobj_x1y1+8]

        STR     R1,[R0,#rectobj_x0y1+4]
        STR     R4,[R0,#rectobj_x0y1+8]

        EXIT

;.............................................................................

; underline - actually draw the relevant data on the screen
; Entry:  ul_top           =   colour of top pixel (0-15)
;         ul_midheight     =   height of middle section (colour 15)
;         ul_bot           =   colour of bottom pixel (0-15)
;         ul_xcoord        =   start pixel coord
;         ul_ycoord        =   top pixel coord
;         ul_nextxcoord    =   end pixel coord

underline PEntry Paint_Underline, "inptr,xcoord,ycoord"

        LDR     R1,switch_buffer
        TEQ     R1,#0
        BNE     switched_underline

        LDR     R1,ul_nextxcoord        ; done in addcharwidth

        LDR     xcoord,ul_xcoord
        LDR     ycoord,ul_ycoord        ; coord of top-left of underline

        STR     R1,ul_xcoord            ; update coordinate (anticipate)

        SUBS    R14,R1,xcoord           ; swap so R1 >= xcoord
        SUBLT   R1,R1,R14
        ADDLT   xcoord,xcoord,R14

        LDR     R14,grafx0
        CMP     xcoord,R14
        MOVLT   xcoord,R14
        LDR     R14,grafx1
        CMP     R1,R14
        ADDGT   R1,R14,#1               ; point to next pixel on

        CMP     xcoord,R1
        PExit   GE

        LDR     R14,changedbox
        LDR     R14,[R14]
        TST     R14,#1                  ; is changedbox enabled?
        BLNE    changedunderline

        SUB     ulcount,R1,xcoord       ; ulcount = no. of pixels to process

        BL      getaddr
        LDR     R14,log2bpp
        MOV     outcount,outcount,LSL R14       ; release pixelsperword
        MOV     ulcount,ulcount,LSL R14         ; ulcount = number of bits to process

        Debug   ul,"Outptr,outcount =",outptr,outcount

; outptr --> first word
; outcount = number of bits to process (in this word)

        LDRB    R0,ul_top
        TEQ     R0,#0
        BLNE    ulrow
        LDR     R14,linelen
        ADD     outptr,outptr,R14
        SUB     ycoord,ycoord,#1

        LDR     R0,outputdata+15*4      ; get output word for middle section
        LDRB    R14,bitsperpixel
        MOV     R1,#32

01      ORR     R0,R0,R0,LSR R14        ; extend mask rightwards
        SUBS    R1,R1,R14
        BNE     %BT01

        LDRB    R1,ul_midheight
        Debug   ul,"Underline middle height/colour =",R1,R0
        B       %FT02

01      BL      ulrow2                  ; R0 = total word, not just index
        LDR     R14,linelen
        ADD     outptr,outptr,R14
        SUB     ycoord,ycoord,#1

02      SUBS    R1,R1,#1
        BPL     %BT01

        LDRB    R0,ul_bot
        TEQ     R0,#0
        BLNE    ulrow

        Debug   ul,"1/2 way back"

        PExit

;.............................................................................

ulrow   Entry   "R1"

        ADR     R14,outputdata
        LDR     R0,[R14,R0,ASL #2]      ; get data relevant to this colour
        Debug   ul,"Underline input word =",R0
        LDRB    R14,bitsperpixel
        MOV     R1,#32
01
        ORR     R0,R0,R0,LSR R14        ; extend mask rightwards
        SUBS    R1,R1,R14
        BNE     %BT01

        Debug   ul,"Underline output word =",R0

        PullEnv                         ; drop through


ulrow2  Entry   "R1,R2,ulcount,outptr,outcount"

        LDR     R14,grafy0
        CMP     ycoord,R14
        BLT     exitulrow
        LDR     R14,grafy1
        CMP     ycoord,R14
        BGT     exitulrow
01
        MOV     R1,outcount             ; old value
        CMP     outcount,ulcount
        MOVGT   outcount,ulcount
        MOV     R14,#&80000000          ; calculate mask
        SUB     R2,outcount,#1
        MOV     R14,R14,ASR R2          ; shift in correct number of bits
        SUB     R1,R1,outcount
        MOV     R14,R14,LSR R1          ; R14 = mask

        Debug   ul,"Outcount,ulcount,Underline mask:",outcount,ulcount,R14

        ADDS    R1,R14,#1
        LDRNE   R1,[outptr]
        SUBS    ulcount,ulcount,outcount
        MOVNE   outcount,#32
        BIC     R1,R1,R14
        AND     R14,R0,R14
        ORR     R1,R1,R14
        STR     R1,[outptr],#4
        BNE     %BT01

exitulrow
        EXIT

;.............................................................................

; Entry:  xcoord = left-hand pixel of underline (clipped)
;         R1 = right-hand pixel + 1             (clipped)
;         ycoord = top pixel coord              (unclipped)
;         [ul_midheight] = height of middle section of underline (ex. top&bot)

changedunderline Entry "x0,y0,x1,y1"

        ASSERT  x0=ycoord
        ASSERT  y0=xcoord
        MOV     y1,ycoord               ; must be in this order
        MOV     x0,xcoord
        SUB     x1,R1,#1
        LDRB    R14,ul_midheight
        SUB     y0,y1,R14
        SUB     y0,y0,#1                ; allow 1 row for top & bottom
        BL      changebox

        EXIT

;.............................................................................

; Entry:  x0,y0,x1,y1 = pixel box to be changed (not nec. clipped to graphics window)
; Exit:   [[changedbox]+4..] updated, x0,y0,x1,y1 = clipped coordinates (pixels)

; amg: explicitly clear V on both returns, since it was causing a spurious error
; to be raised

changebox Entry "gx0,gy0,gx1,gy1"

        ADR     R14,grafx0
        LDMIA   R14,{gx0,gy0,gx1,gy1}
        CMP     x0,gx0
        MOVLT   x0,gx0
        CMP     y0,gy0
        MOVLT   y0,gy0
        CMP     x1,gx1
        MOVGT   x1,gx1
        CMP     y1,gy1
        MOVGT   y1,gy1

        CMP     x0,x1
        CMPLE   y0,y1

        BLE      %FT10

        CLRV
        Pull    "gx0,gy0,gx1,gy1,PC"
10

        LDR     R14,changedbox
        ADD     R14,R14,#4
        LDMIA   R14,{gx0,gy0,gx1,gy1}
        CMP     gx0,x0
        MOVGT   gx0,x0
        CMP     gy0,y0
        MOVGT   gy0,y0
        CMP     gx1,x1
        MOVLT   gx1,x1
        CMP     gy1,y1
        MOVLT   gy1,y1
        STMIA   R14,{gx0,gy0,gx1,gy1}
        CLRV

        EXIT

;-----------------------------------------------------------------------
; Routines to scale from millipoints to pixels
;-----------------------------------------------------------------------

; calcrubout - obtain rubx0,y0,x1,y1 from x0,y0,x1,y1

; In    R2 = flags for Font_Paint (bit 4 => inits to be used)
;       x0,y0,x1,y1 = rubout box (1/256 OS unit or 1/72000"
; Out   [rubx0,y0,x1,y1] = rubout box (pixels, inclusive)
;       setruboutflag = 1, [ruboutx] = bignum if no rubout
;       GE => rubout
;       LT => no rubout

calcrubout Entry "R0-R2,x0,y0,x1,y1"

        TST     R2,#paint_rubout
        BEQ     %FT01

        ASSERT  x0 > R2

        Debug   rub,"New-style rubout: milpt x0,y0,x1,y1 =",x0,y0,x1,y1

        MOV     R0,x0
        BL      dividex
        CMP     R0,#2                   ; check for middle of pixel
        ADDGE   R2,R2,#1
        MOV     x0,R2

        MOV     R0,y0
        BL      dividey
        CMP     R0,#2                   ; check for middle of pixel
        ADDGE   R2,R2,#1
        MOV     y0,R2

        MOV     R0,x1
        BL      dividex
        CMP     R0,#2                   ; check for middle of pixel
        SUBLT   R2,R2,#1
        MOV     x1,R2

        MOV     R0,y1
        BL      dividey
        CMP     R0,#2                   ; check for middle of pixel
        SUBLT   R2,R2,#1
        MOV     y1,R2

        Debug   rub,"New-style rubout: pixel x0,y0,x1,y1 =",x0,y0,x1,y1

        CMP     x1,x0
        CMPGE   y1,y0

        ADRGE   R14,rubx0
        STMGEIA R14,{x0,y0,x1,y1}
        STRGE   x0,ruboutx              ; ensure ruboutx <> bignum  -  real value set up in setrubout

        EXIT    GE                      ; GE => there was a rubout box

01      MOV     R14,#bignum
        STR     R14,ruboutx
        MOV     R14,#1                  ; no need to set up rubout data
        STRB    R14,setruboutflag

        CMP     R14,#2                  ; LT => there was no rubout box
        EXIT

;........................................................................

; calcxcoord - obtain screen xcoord from internal variables xco72,yco72
;            - yco72 is for use with transformed fonts

; In    [x/yco72] = current point (1/72000")
;       [x/yscalefactor] = scale factor (1/72000" <--> OS units)
;       [modedata_px/y] = OS unit / pixel shift amount
; Out   x/ycoord = x/y coordinate (pixels)
;       [antialiasx/y] = subpixel offset (0..3)

calcxcoord PEntryS Paint_Metrics, "R0-R3"

        LDR     R0,xco72                ; dividend
        BL      dividex
        MOV     xcoord,R2               ; answer
        STRB    R0,antialiasx

; Comment from Chris:
;
; This is a bit naughty because on a mode change, 'oldyco72' isn't correct if you
; change to a mode with different Eigen factors.  Consequently, if there was only
; one line of text to render, it would appear in the wrong vertical position!
; I've fixed it by invalidating oldyco72 upon a mode change.  This bug seems to
; have been present for a *long* time, I'm surprised it took this long for somebody
; to find it (2 June 1992).

        LDR     R0,yco72                ; dividend
        LDR     R14,oldyco72
        TEQ     R0,R14
        LDREQ   ycoord,oldycoord        ; bank on y-coord not changing often
        STRNE   R0,oldyco72
        BLNE    dividey
        MOVNE   ycoord,R2
        STRNEB  R0,antialiasy

        STR     xcoord,oldxcoord        ; used in paintdraw
        STR     ycoord,oldycoord
        PExitS

;........................................................................

; In    R0 = dividend (coord in 1/72000")
;       [xscalefactor] << modedata_px = divisor
; Out   R0 = remainder in range 0..3
;       R2 = coord (pixels)

dividex Entry ""

        LDR     R1,xscalefactor         ; divisor
        LDR     R2,modedata_px
        MOV     R1,R1,LSL R2            ; convert to pixels
        MOV     R0,R0,LSL #2
        BL      divide                  ; rounds down (not towards 0)
        AND     R0,R2,#3                ; subpixel position (0..3)
        MOV     R2,R2,ASR #2            ; pixel position

        EXIT

dividey Entry ""

        LDR     R1,yscalefactor         ; divisor
        LDR     R2,modedata_py
        MOV     R1,R1,LSL R2            ; convert to pixels
        MOV     R0,R0,LSL #2
        BL      divide                  ; rounds down (not towards 0)
        AND     R0,R2,#3                ; subpixel position (0..3)
        MOV     R2,R2,ASR #2            ; pixel position

        EXIT

;;----------------------------------------------------------------------------
;; paintchar - this is it lads!
;;
;; Entry: pchar         = char to print (INTERNAL glyph code)
;;        [externalcharcode] = external character code (for addcharwidth)
;;        xcoord,ycoord = coordinates
;;        metricsptr -->  metrics info (0 ==> not known yet)
;;        pixelsptr  -->  pixel info   (0 ==> not known yet)
;;
;; Exit:  character printed
;;        xcoord, ycoord updated
;;----------------------------------------------------------------------------

paintchar ROUT

        ProfIn  Paint_Char

; delay setting-up till a char is to be printed

        BLVC    trysetoutputdata
        BLVC    trysetrubout

        LDRVC   R14,ul_nextxcoord               ; for rubout colour changes
        STRVC   R14,lastxcoord

; work out position after painting character in advance, for underlining

        BLVC    addcharwidth                    ; [ul_nextxcoord], [switch_nextx/ycoord] set up
                                                ; may relocate metricsptr, pixelsptr
; obtain pointer to character

        Push    "pchar,ycoord,xcoord,pixelsptr,metricsptr,base"

; Comment from Chris:
;
; There is a problem with xcoord and ycoord when outputting to a buffer at small
; scales.  Outputting to the VDU is okay because of anti-aliasing and sub-pixel
; adjustment.  Outputting to a buffer used to output *very* badly aligned paths
; if the scale was small and the text was rotated.
;
; I've made a dramatic improvement by adding the anti-alias offsets into the
; path coordinates, i.e. treating them as a their fractional part.  The alignment
; iregularities have gone, but the origin of the text still tends towards the
; origin very slightly.  I suspect the origin calculation needs similar adjustment.

        BLVC    calcxcoord                      ; sets up x/ycoord, [antialiasx/y]
        Debug   co2,"xco72,yco72,xcoord,ycoord =",#xco72,#yco72,xcoord,ycoord

        BLVC    SetPixelsPtr                    ; pixelsptr --> index
        DebugE  trn,"Error in paintchar:"
      [ pchar = R0                              ; inptr -> character data (version 2.69 onwards)
        STRVS   R0,[sp]
      ]
        Pull    "pchar,ycoord-base,PC",VS

        ADD     R14,sp,#3*4
        STMIA   R14,{pixelsptr,metricsptr}      ; keep these for next time

        Debug   ch,"PaintChar: inptr =",inptr

; finished with: metricsptr, pixelsptr
; ------------------------------------

; NOTE == No further cacheing is allowed until after the char is painted    ==
;      == This is because the cache might move if a block has to be deleted ==

        STR     inptr,xsize                     ; only matters if inptr=0!

        MOVS    R0,inptr
        BEQ     ruboutonly

; add in character origin & get bounding box (pixels)
; NB: getbbox updates [charflags] - needed later to decide on format

        ASSERT  inptr = R4
        Push    "R1-R3"
        BL      getbbox                 ; R0 updated, R1-R4 = bbox * mag
        ADD     xcoord,xcoord,R1        ; [charflags], [inputxsize] done too
        ADD     ycoord,ycoord,R2
        STR     R3,xsize
        STR     R4,rowcount
        ORRS    inptr,R3,R4             ; treat 0x0 char as null
        Pull    "R1-R3"
        MOVNE   inptr,R0                ; update inptr

        LDRNE   R14,changedbox
        LDRNE   R14,[R14]
        LDRNE   R0,switch_flags         ; screen not updated if output to buffer
        BICNE   R14,R14,R0,LSR #swb_enabled-0
        TSTNE   R14,#1
        BLNE    changedchar                     ; update changedbox

; see if we need to rub out ahead of this char
; ensures that rubout reaches the rightmost of xcoord+xsize and newxcoord

ruboutonly
        LDR     R14,ruboutx
        TEQ     R14,#bignum
        BLNE    rubout                          ; rub out ahead of char
        BVS     justmove

; do underlining (if enabled) - note that [sp,#0] = char to print
; note that this doesn't call addcharwidth, as that can cause cacheing

        LDR     R14,ul_word
        TEQ     R14,#0
        BLNE    underline               ; optimise for non-underline
        BVS     justmove                ; if switched, can get "Buffer overflow"
        Debug   ul,"Made it back - inptr,x,y =",inptr,xcoord,ycoord

; paint the character (unless it is undefined)

        TEQ     inptr,#0                        ; finally make mind up!
        BEQ     justmove

; check whether this is an outline, to be passed to the Draw module

        LDRB    R14,charflags
        TST     R14,#chf_outlines
        BNE     paintdraw               ; inptr -> outline char

; currently we can't output bitmapped characters to the draw buffer

        LDR     R0,switch_flags
        TST     R0,#swf_enabled
        BEQ     %FT11

        TST     R0,#swf_nobitmaps       ; did the user explicitly object?
        ADRNE   R0,ErrorBlock_FontNoBitmaps2
        BLNE    MyGenerateError
        B       justmove
        MakeErrorBlock FontNoBitmaps2
11

; check for 4-bpp magnified font
        TST     R14,#chf_1bpp
        BNE     %FT01
        LDR     R14,xmag
        LDR     R0,ymag
        ADD     R14,R14,R0
        CMP     R14,#2                  ; unless both factors are 1,
        BreakPt "Bad magnification",LT
        BGT     paintmagnified          ; do this the long-winded way.
01

; check for necessity of clipping (x-clip => use slow routine)

        AND     R14, inptr, #&E0000000
        STR     R14, paint_inptr        ; save the top three bits
        MOV     inptr,inptr,LSL #3      ; inptr = bit address of input
                                        ; NB: assumes font cache in bottom 256Mb
        ADR     R14,grafx0
        LDMIA   R14,{gx0,gy0,gx1,gy1}
        ADD     gx1,gx1,#1              ; make window half-open
        ADD     gy1,gy1,#1
        CMP     xcoord,gx0              ; clip if xcoord < grafx0
        CMPGE   ycoord,gy0              ; clip if ycoord < grafy0
        LDRGE   R14,xsize
        ADDGE   R14,R14,xcoord
        CMPGE   gx1,R14                 ; clip if xcoord+xsize > grafx1
        LDRGE   R14,rowcount
        ADDGE   R14,R14,ycoord
        CMPGE   gy1,R14                 ; clip if ycoord+ysize > grafy1
        BLT     paintclipped

; now calculate addresses and masks - comes back here if only vertical clipping

quickclip
        BL      getaddr
        LDR     R14,log2bpp
        MOV     outcount,outcount,ASL R14       ; release pixelsperword
        SUB     R14,outcount,#1
        MOV     outdata,#1
        MOV     outdata,outdata,ASL R14         ; release outcount

; finished with: xcoord, ycoord
; -----------------------------

; set up pointers etc. needed by the inner loop

        LDRB    bpp,bitsperpixel                ; should be set up by mode
	LDR	outputarray,blend_outputdata	; set up ptr into output data for blended output
        LDR     Fmask,outputmask

; loop, plotting each row in turn (from the bottom)

        Debug   ch,"Paintchar: R12 =",R12
        Debug   ch,"Paintchar: in,out,xs,ys =",inptr,outptr,#xsize,#rowcount

        LDRB    fvalue,charflags
        TST     fvalue,#chf_1bpp
        BLNE    unpack_1bpp_firstrow            ; NB: can branch off to optimised routine
        Debug ch,"returned",inptr

        MOV     outmask,#0
        LDR     rowcounter,rowcount
        LDR     colcount,xsize

        CMP     colcount,#?ScratchSpace*2    ; Patch by Chris to trap ScratchSpace overflow
       [ debugunpack
        BLE     %FT01
        Debug   unpack,"quickclip: colcount implies buffer is too small, =",colcount
01
       ]
        SETV    GT
        PExit   GT

        MOV     outptrrow,outptr
        STR     outdata,outdatarow

        MACRO
$lab    Pixel   $bit
$lab    ANDS    R14,indata,#((1:SHL:in_bpp)-1):SHL:$bit
        ASSERT  ((.-Module_BaseAddr):MOD:16)=Urk
      [ $bit > 2
        LDRNE   R14,[outputarray,R14,LSR #$bit-2]
      |
        LDRNE   R14,[outputarray,R14,LSL #2-$bit]
      ]
        MOVEQ   outmask,outmask,LSR bpp
        ORRNE   outmask,Fmask,outmask,LSR bpp
        ORRS    outdata,R14,outdata,LSR bpp     ; stuff it into output word
        BLCS    putdata
        SUBS    colcount,colcount,#1
        BLEQ    nextpixelrow
        MEND

        ProfIn  Paint_Char4bpp

        AND     R14,inptr,#31                   ; R14 = bit posn within word
        MOV     inptr,inptr,LSR #3              ; inptr = byte address

        LDR     indata,paint_inptr
        ORR     inptr,inptr,indata

        LDR     indata,[inptr,-R14,LSR #3]!     ; inptr -> word address, indata = data
        ADD     inptr,inptr,#4
        ASSERT  lp1-lp0 = 32
        ADD     R14,PC,R14,LSL #5-2             ; old R14 = pixel position * 4
        ADD     PC,R14,#lp0-.-4

        AlignCd (Urk+8):MOD:16
indatalp
        LDR     indata,[inptr],#4
lp0     Pixel   0
lp1     Pixel   4
lp2     Pixel   8
lp3     Pixel   12
lp4     Pixel   16
lp5     Pixel   20
lp6     Pixel   24
lp7     Pixel   28
        B       indatalp

nextpixelrow Entry ""

        TEQ     outdata,#&80000000
        BLNE    putdata1                        ; output remaining word

        LDR     R14,linelen
        LDR     colcount,xsize
        LDR     outdata,outdatarow
        SUB     outptrrow,outptrrow,R14         ; go up a row
        MOV     outptr,outptrrow
        SUBS    rowcounter,rowcounter,#1

        LDRNEB  R14,charflags
        TSTNE   R14,#chf_1bpp
        BLNE    unpack_1bpp

        TEQ     rowcounter,#0
        EXIT    NE

        ProfOut Paint_Char4bpp

        PullEnv                                 ; drop through

;------------------------------------------------------
; update xcoord, ycoord after printing character
;------------------------------------------------------

justmove
        ProfOut Paint_Char

        Debug   co2,"justmove: about to pull registers"

        ASSERT  pchar = R0
        STRVS   R0,[sp]
        Pull    "pchar,ycoord-base"
        ProfOut Painting,VS
        Pull    "PC",VS                         ; paintdraw can give error

        LDR     xcoord,saved_nextxcoord
        LDR     ycoord,saved_nextycoord
        STR     xcoord,xco72
        STR     ycoord,yco72

        B       paintlp

;............................................................................

; In    R0 = pchar = character being printed
;       [x/yco72] = current position in 1/72000"
;       metricsptr -> metrics info (or 0 => not done yet)
;       wrchbase -> next character in string
;       [plottype] :AND: paint_rightleft = 1 =>
; Out   [x/yco72] = coordinate before printing this character
;       [switch_nextx/ycoord] = position after printing this character
;       [ul_nextxcoord] = x pixel coordinate of next position (for underlining)
;       metricsptr, pixelsptr may be set up / relocated

addcharwidth PEntry Paint_Metrics, "R1,R5,ycoord,xcoord"

        LDR     xcoord,xco72
        LDR     ycoord,yco72

        ADR     R14,xglueadd                    ; R2,R3 = glue offset from last time
        LDMIA   R14,{R2,R3}

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        BNE     %FT01

        ADD     xcoord,xcoord,R2                ; add glue offset first
        ADD     ycoord,ycoord,R3

        STR     xcoord,xco72                    ; [x/yco72] = coords for this time
        STR     ycoord,yco72

        MOV     R1,wrchbase                     ; R0,[R1] = char codes (this, next)
        BL      getcharwidth
        PExit   VS

        ADD     R2,xcoord,R2                    ; R2,R3 = coords for next time
        ADD     R3,ycoord,R3

02      STR     R2,saved_nextxcoord             ; values of [xyco72] for next time
        STR     R3,saved_nextycoord

; now add/subtract 1/2 the glue width to get the colour change position for rubout and underline
; rubout and underline only work properly if baseline is still on the x-axis

        Push    "R0-R2"

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        LDR     R14,xglueadd
        ADDEQ   R0,R2,R14,ASR #1                ; add 1/2 glue width if going left to right
        SUBNE   R0,R2,R14,ASR #1                ; subtract 1/2 glue width if going right to left
        STR     R0,switch_nextxcoord
        BL      dividex
        STR     R2,ul_nextxcoord

      [ newunderline
        LDR     R14,yglueadd
        ADDEQ   R0,R3,R14,ASR #1                ; add 1/2 glue width if going left to right
        SUBNE   R0,R3,R14,ASR #1                ; subtract 1/2 glue width if going right to left
        STR     R0,switch_nextycoord
      ]

        Pull    "R0-R2"
        PExit

01      SUB     xcoord,xcoord,R2                ; if going right to left, subtract char width + glue
        SUB     ycoord,ycoord,R3

        MOV     R1,wrchbase                     ; R0,[R1] = char codes (this, next)
        BL      getcharwidth

        SUBVC   R2,xcoord,R2                    ; R2,R3 = coords for next time
        SUBVC   R3,ycoord,R3
        STRVC   R2,xco72                        ; [x/yco72] = coords for this time
        STRVC   R3,yco72
        BVC     %BT02

        PExit

;;-----------------------------------------------------------
;; unpacking routines for 1-bpp fonts
;;-----------------------------------------------------------

; In    bpp = output bits per pixel
;       Fmask = output mask at top of word
;       outputarray -> colour table

unpack_1bpp_clipped
        MOV     fvalue,#1               ; is this being called by paintclipped?
        B       go_unpack_1bpp

; In    fvalue = char flags
;       inptr -> start of the character (BIT address)
; Out   inptr -> start of unpacked data (BIT address)
;       colcount = start bit position in first byte

unpack_1bpp_firstrow
        TST     fvalue,#chf_fvalue      ; can't optimise packed characters
        TEQEQ   bpp,#1
        BEQ     paintchar_1bpp_fast

unpack_1bpp_firstrow_clipped
        Push    "LR"

        Debug   ch,"unpack_1bpp_firstrow: inptr,xsize =",inptr,#xsize

        AND     colcount,inptr,#7
        RSB     R14,colcount,#0         ; these are surplus pixels that need doing
        STR     R14,extradone
        MOV     inptr,inptr,LSR #3      ; inptr -> byte address

        LDR     R14,paint_inptr
        ORR     inptr,inptr,R14

        MOV     R14,#0
        STRB    R14,rwastage
        STR     R14,repeatcount

        MOV     R14,#1
        STR     R14,currentcount        ; this is always 1 too big!

        Debug   ch,"1-bpp: charflags =",fvalue

        TST     fvalue,#chf_fvalue
        MOVNE   inptr,inptr,LSL #1
        STR     inptr,inptr_4bpp

        TST     fvalue,#chf_initbit
        MOVEQ   R14,#&F0000000          ; start off inverted (currcount=0)
        MOVNE   R14,#&0
        STR     R14,currentstate

        Debug   ch,"unpack_1bpp_firstrow in: inptr,colcount =",inptr,colcount

        BL      unpack_1bpp

        Debug   ch,"unpack_1bpp_firstrow postcall: inptr,colcount =",inptr,colcount

 Debug ch,"changing top three bits"
 AND R14,inptr,#&E0000000
 STR R14,paint_inptr

        MOV     inptr,inptr,LSL #3              ; convert back to bit address
        ADD     inptr,inptr,colcount,LSL #2     ; and move to appropriate bit posn

        Debug   ch,"unpack_1bpp_firstrow out: inptr,colcount =",inptr,colcount

        Pull    "PC"

; In    inptr -> start of row (byte address)
;       extradone = number of bits already dealt with
; Out   inptr -> start of unpacked data

unpack_1bpp
        ASSERT  fvalue = R0             ; safe to corrupt this here
        MOV     fvalue,#0

go_unpack_1bpp
        STRB    fvalue,clipflag
        PEntry  Paint_Char1bpp, "indata,outdata,outptr,colcount"

        ASSERT  chf_fvalue = &F0
        LDRB    fvalue,charflags
        MOVS    fvalue,fvalue,LSR #4
        BNE     runencoded

        LDR     colcount,xsize
        LDR     R14,extradone
        SUB     R14,colcount,R14
        ADD     colcount,R14,#7         ; round up to next byte boundary
        BICS    colcount,colcount,#7    ; (so it will produce a whole no of words)
        SUB     R14,colcount,R14
        STR     R14,extradone
        PExit   EQ

        LDR     inptr,inptr_4bpp
        MOV     outptr,#ScratchSpace            ; bug fix by Chris - unpack into ScratchSpace
        CMP     colcount,#?ScratchSpace*2    ; trap ScratchSpace overflow
       [ debugunpack
        BLE     %FT00
        Debug   unpack,"unpack_1bpp: colcount implies buffer is too small, =",colcount
00
       ]
        SETV    GT                      ; n
        PExit   GT

        Debug   ch,"unpack_1bpp: inptr,extradone,outptr,colcount =",inptr,#extradone,outptr,colcount

01      LDRB    indata,[inptr],#1       ; 1 byte of input => 1 word of output
        MOV     outdata,#&80000000

02      MOVS    indata,indata,LSR #1
        MOV     outdata,outdata,RRX     ; put into top bit
        MOVS    outdata,outdata,ASR #3  ; extend into top 4 bits
        BCC     %BT02
        STR     outdata,[outptr],#4
        SUBS    colcount,colcount,#8    ; we've done 8 pixels by now
        BNE     %BT01
03
        STR     inptr,inptr_4bpp        ; for next time

        MOV     inptr,#ScratchSpace     ; bug fix by Chris - use ScratchSpace instead
        PExit

; input stream is a sequence of 4-bit packed run-lengths
; there is also a 'repeat count' facility, to repeat the previous row

runencoded
        Push    "currcount,currstate,shift"

        LDR     R14,repeatcount
        SUBS    R14,R14,#1
        BMI     runpacked
        STR     R14,repeatcount

        LDRB    currcount,lwastage
        LDRB    shift,rwastage          ; new output bit position
        STRB    shift,lwastage
        LDR     colcount,xsize
        CMP     colcount,#?ScratchSpace*2    ; Patch by Chris to trap ScratchSpace overflow
       [ debugunpack
        BLE     %FT00
        Debug   unpack,"runencoded: colcount implies buffer is too small, =",colcount
00
       ]
        SETV    GT
        PExit   GT
        ADD     R14,shift,colcount,LSL #2
        AND     R14,R14,#31
        STRB    R14,rwastage            ; this will be the end part
        SUBS    shift,shift,currcount   ; amount to shift input words by
        MOV     inptr,#ScratchSpace     ; bug fix by Chris - use ScratchSpace instead
        ADD     colcount,colcount,#7    ; make sure we do enough pixels
        MOV     R14,#0
        LDR     indata,[inptr]
        RSBMI   shift,shift,#0
        RSB     currcount,shift,#32     ; currcount = 32-shift
        BMI     %FT02

01      ORR     R14,R14,indata,LSL shift
        STR     R14,[inptr],#4
        MOV     R14,indata,LSR currcount
        LDR     indata,[inptr]
        SUBS    colcount,colcount,#8    ; 8 pixels at a time (nominally)
        BHI     %BT01
        B       exit

02      MOV     R14,indata,LSR shift
        LDR     indata,[inptr,#4]
        ORR     R14,R14,indata,LSL currcount
        STR     R14,[inptr],#4
        SUBS    colcount,colcount,#8
        BHI     %BT02
        B       exit

runpacked
        LDRB    shift,rwastage          ; carry on from here
        STRB    shift,lwastage
        MOV     outptr,#ScratchSpace    ; bug fix by Chris - use ScratchSpace instead
        MOV     outdata,#&80000000
        MOV     outdata,outdata,LSR shift
        LDR     colcount,xsize
        ADD     shift,shift,colcount,LSL #2
        AND     shift,shift,#31
        STRB    shift,rwastage
        LDR     inptr,inptr_4bpp
        LDR     currstate,currentstate         ; 0 or &F0000000
        LDR     currcount,currentcount
01
        SUBS    currcount,currcount,#1
        BEQ     nextrun
02
        ORRS    outdata,currstate,outdata,LSR #4
        STRCS   outdata,[outptr],#4
        MOVCS   outdata,#&80000000
        SUBS    colcount,colcount,#1
        BNE     %BT01

        RSB     shift,shift,#32
        MOV     outdata,outdata,LSR shift
        STR     outdata,[outptr]        ; stash last word

        STR     inptr,inptr_4bpp
        STR     currstate,currentstate
        STR     currcount,currentcount

exit
        MOV     inptr,#ScratchSpace     ; bug fix by Chris - use ScratchSpace instead
        LDRB    shift,lwastage
        TEQ     shift,#0
        BEQ     %FT99
        LDRB    R14,clipflag            ; indata is shifted down here
        TEQ     R14,#0
        LDR     R14,[inptr],#4
        MOVNE   R14,R14,LSR shift
        STR     R14,[sp,#3*4]           ; amend 'current' word
99
        Pull    "currcount,currstate,shift"
        PExit

nextrun
        BL      getpacked               ; currcount <-- next counter value
        EOR     currstate,currstate,#&F0000000
        B       %BT02           ; currcount is always 1 too big!


; In    inptr --> next nibble
;       fvalue = 'f' value for char
; Out   currcount = packed run length
;       [repeatcount] may be updated
;
; 0 (n-1)0 (n+1)(nibbles) = value of (nibbles), most significant first
;   1 .. f                = value
; f+1 .. 13 (nibble)      = (n-f-1)*16 + (nibble) + f+1  = (n-f)*16+nibble+f-15
;  14 (packed)            = repeat count of (packed)
;  15                     = repeat count of 1

        MACRO
$lab    getnib  $r
$lab    MOVS    $r,inptr,LSR #1
        ADD     inptr,inptr,#1
        LDRB    $r,[$r]
        ANDCCS  $r,$r,#&F
        MOVCSS  $r,$r,LSR #4
        MEND

getpacked
        getnib  currcount
        BEQ     %FT01                   ; read extended number
        CMP     currcount,fvalue
        MOVLS   PC,LR
        CMP     currcount,#14
        BGE     %FT02
        Push    "LR"
        getnib  R14
        SUB     currcount,currcount,fvalue
        ORR     currcount,R14,currcount,LSL #4
        ADD     currcount,currcount,fvalue
        SUB     currcount,currcount,#15
        Pull    "PC"

01
        Push    "fvalue,LR"
        MOV     fvalue,#1               ; one digit read so far

llp1    getnib  currcount
        ADDEQ   fvalue,fvalue,#1
        BEQ     llp1

llp2    getnib  R14
        ADD     currcount,R14,currcount,LSL #4
        SUBS    fvalue,fvalue,#1
        BNE     llp2
        Pull    "fvalue,PC"

02
        MOVGT   currcount,#1            ; 15
        Push    "LR"
        BLEQ    getpacked               ; 14 (packed)
        Pull    "LR"
        STR     currcount,repeatcount
   ;;   Debuga  cc," rp",currcount
        B       getpacked               ; now read currcount again


;-----------------------------------------------------------
; putdata - ORR outdata to outptr
; putdata1 botches it since loop incomplete
;-----------------------------------------------------------

        AlignCd (4+Urk):MOD:16
putdata1
        MOV     outmask,outmask,LSR bpp
        MOVS    outdata,outdata,LSR bpp         ; shift to correct position
        BCC     putdata1
putdata
	LDR	PC, blend_putdata		; call the blending functions
	AlignCd Urk:MOD:16
putdata_mono
        ORRS    pchar,outdata,outmask
        BEQ     blend_nodata
        ADDS    pchar,outmask,#1
        LDRNE   pchar,[outptr]
        BICNE   pchar,pchar,outmask
        ORR     pchar,pchar,outdata
        MOV     outdata,#&80000000              ; set marker bit
        STR     pchar,[outptr],#4       ; NB 4 instructions ahead!
        MOV     outmask,#0
        MOV     PC,LR

putdata1M
        MOV     outmask,outmask,LSR bpp
        MOVS    outdata,outdata,LSR bpp         ; shift to correct position
        BCC     putdata1M
putdataM
	LDR	PC, blend_putdataM

putdataM_mono
        Push    "R1,R2,LR"
        LDR     R1,linelen
        MOV     R2,outptr
        LDR     R14,this_ymagcnt
01
        ADDS    pchar,outmask,#1
        LDRNE   pchar,[outptr]             ; mask is solid,so skip the screen read
        SUBS    R14,R14,#1
        BIC     pchar,pchar,outmask
        ORR     pchar,pchar,outdata
        STR     pchar,[outptr],-R1
        BNE     %BT01
        ADD     outptr,R2,#4
        MOV     outdata,#&80000000              ; set marker bit
        MOV     outmask,#0
        Pull    "R1,R2,PC"

;...........................................................................

; Entry:  xcoord,ycoord,xsize,rowcount = character box
; Exit:   [[changedbox]+4..] updated

changedchar Entry "x0,y0,x1,y1"

        ASSERT  x0=ycoord
        ASSERT  y0=xcoord
        MOV     x0,xcoord               ; swap them over!
        LDR     y0,[sp,#0*4]
        LDR     R14,xsize
        ADD     x1,x0,R14
        SUB     x1,x1,#1
        LDR     R14,rowcount
        ADD     y1,y0,R14
        SUB     y1,y1,#1
        BL      changebox

        EXIT

;;----------------------------------------------------------------------------
;; paintchar_1bpp_fast
;;    - Optimised routine for 1-bpp char => 1-bpp mode in foreground colour 1
;;----------------------------------------------------------------------------

; In    rowcounter, colcount = pixels per row/column
;       outptr, outptrrow -> address of bottom-left of character
;       outcount = number of bits to be output in first word
;       outdata, outmask = 0
;       inptr -> lower-left corner of character (BIT address)
;
;       bpp, outputarray, Fmask not required for this routine

paintchar_1bpp_fast ROUT

        ProfIn  Paint_Char1bpp_fast

        Debug   ch,"outptr,outcount,rowcount,colcount =",outptr,outcount,#rowcount,#xsize

        ASSERT  inptr = R4
        ASSERT  outptr = R5
        ASSERT  outcount = R6

        MOV     R1,outptr
        LDR     R2,linelen
        LDR     R3,rowcount
        RSB     R5,outcount,#32
        LDR     R14,xsize
        MOV     R7,R2,LSL #3
        SUB     R7,R7,R5
        SUB     R7,R7,R14

        Debug   ch,"outptr,linelen,rowcount =",R1,R2,R3
        Debug   ch,"inptr,lwaste,rwaste =",R4,R5,R7

; In    R1 -> lower-left of row on screen
;       R2 = rows length (bytes)
;       R3 = number of rows
;       R4 -> input data
;       R5 = number of bits to ignore on left of screen
;       R7 = number of bits to ignore on right of screen

        RSB     R11,R7,R2,LSL #3
        SUB     R11,R11,R5              ; R11 = no of (useful) bits per row
        MOV     R8,R5,LSR #5            ; R8 = no of words to skip on left
        ADD     R6,R8,R7,LSR #5         ; R6 = no of words to skip on l/r
        MOV     R6,R6,LSL #2            ;      convert to bytes
        SUB     R6,R6,R2,LSL #1         ; R6 = offset between rows
        ADD     R6,R6,#4                ; correct for the word in the middle
        LDR     R2,[R1,R8,LSL #2]!      ; R1 -> first useful word of bottom row
        AND     R5,R5,#31               ; R5 = next bit in output word
        RSB     R9,R5,#32               ; R9 = 32-r5
        AND     R8,R4,#31               ; R8 = bit position within first word
        MOV     R4,R4,LSR #3

        LDR     R0,paint_inptr
        ORR     R4,R4,R0

        LDR     R0,[R4,-R8,LSR #3]!     ; R0 = input word

        Debug   ch,"R11,R6,R1,R2 =",R11,R6,R1,R2
        Debug   ch,"R5,R9,R8 =",R5,R9,R8

; In    R11 = number of output bits per row
;       R6 = amount to add on between rows
;       R1 -> first useful output word
;       R2 = original value of output word
;       R5 = bit number of first output bit
;       R9 = 32-R5 (no of useful bits in first output word)
;       R8 = next bit in input word

        Debug   ch2,""
        Debug   ch2,"        R0       R1       R2       R3       R4       R5       R6       R7        R8       R9      R10      R11"

        ASSERT  outputarray = R10       ; not re-used yet
        LDR     outputarray,[outputarray,#15*4]
        TEQ     outputarray,#0
        BEQ     %FT61

51      MOV     R7,R11                  ; R7 = bits remaining on this row
        RSB     R10,R8,#32              ; R10= 32-r8 (bits left in input)
        Debuga  ch2,"51 :",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11

52      MOV     R14,R0,LSR R8
        Debuga  ch2,"52 :",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        SUBS    R7,R7,#32               ; can we complete this word?
        BLE     %FT53
        LDR     R0,[R4,#4]!
        ORR     R14,R14,R0,LSL R10      ; R14 = next 32 bits of input
        ORR     R2,R2,R14,LSL R5
        STR     R2,[R1],#4              ; must complete the output word
        Debuga  ch2,"52a:",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        LDR     R2,[R1]
        ORR     R2,R2,R14,LSR R9
        B       %BT52

53
        Debuga  ch2,"53 :",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        ADDS    R8,R8,R7                ; do we need any more input words?
        ADDLT   R8,R8,#32
        LDRGE   R0,[R4,#4]!
        ORRGE   R14,R14,R0,LSL R10      ; R14 = next R7+32 bits of input
        Debuga  ch2,"53a:",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        ADDS    R7,R7,R5                ; R7 = bits in next word - 32
        ADDLE   R7,R7,#32
        MOVLE   R14,R14,LSL R5
        ORRGT   R2,R2,R14,LSL R5
        STRGT   R2,[R1],#4
        LDRGT   R2,[R1]
        MOVGT   R14,R14,LSR R9
        Debuga  ch2,"53b:",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        RSB     R7,R7,#32
        MOV     R14,R14,LSL R7          ; remove bits from start of next row
        ORR     R2,R2,R14,LSR R7
        STR     R2,[R1]
        SUBS    R3,R3,#1
        LDRNE   R2,[R1,R6]!             ; get first word of next row
        BNE     %BT51

        ProfOut
        B       justmove

61      MOV     R7,R11                  ; R7 = bits remaining on this row
        RSB     R10,R8,#32              ; R10= 32-r8 (bits left in input)
        Debuga  ch2,"61 :",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11

62      MOV     R14,R0,LSR R8
        Debuga  ch2,"62 :",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        SUBS    R7,R7,#32               ; can we complete this word?
        BLE     %FT63
        LDR     R0,[R4,#4]!
        ORR     R14,R14,R0,LSL R10      ; R14 = next 32 bits of input
        BIC     R2,R2,R14,LSL R5
        STR     R2,[R1],#4              ; must complete the output word
        Debuga  ch2,"62a:",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        LDR     R2,[R1]
        BIC     R2,R2,R14,LSR R9
        B       %BT62

63
        Debuga  ch2,"63 :",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        ADDS    R8,R8,R7                ; do we need any more input words?
        ADDLT   R8,R8,#32
        LDRGE   R0,[R4,#4]!
        ORRGE   R14,R14,R0,LSL R10      ; R14 = next R7+32 bits of input
        Debuga  ch2,"63a:",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        ADDS    R7,R7,R5                ; R7 = bits in next word - 32
        ADDLE   R7,R7,#32
        MOVLE   R14,R14,LSL R5
        BICGT   R2,R2,R14,LSL R5
        STRGT   R2,[R1],#4
        LDRGT   R2,[R1]
        MOVGT   R14,R14,LSR R9
        Debuga  ch2,"63b:",R0,R1,R2,R3,R4,R5
        Debug   ch2,"",R6,R7,R8,R9,R10,R11
        RSB     R7,R7,#32
        MOV     R14,R14,LSL R7          ; remove bits from start of next row
        BIC     R2,R2,R14,LSR R7
        STR     R2,[R1]
        SUBS    R3,R3,#1
        LDRNE   R2,[R1,R6]!             ; get first word of next row
        BNE     %BT61

        ProfOut
        B       justmove

;;----------------------------------------------------------------------------
;; paintclipped
;;    - This routine is used to draw characters that are not entirely visible
;;    - It is 2.5 times slower than paintchar - only called when necessary
;;----------------------------------------------------------------------------

paintclipped ROUT

; first see if the clipping only occurs in the y-direction

        ProfIn  Paint_CharClip

        LDRB    xsiz,charflags                  ; NB: check this first (before fiddling about with ycoord)
        TST     xsiz,#chf_1bpp
        TSTNE   xsiz,#chf_fvalue
        BNE     %FT02                           ; can't do this if packed 1-bpp

        LDR     xsiz,xsize
        ADD     xsiz,xcoord,xsiz
        CMP     xcoord,gx0
        CMPGE   gx1,xsiz
        BLT     %FT02                           ; character not entirely contained horizontally

; from here on we MUST go to quickclip, since ycoord is altered

        LDR     R0,rowcount
        ADD     R14,ycoord,R0
        SUBS    R14,R14,gy1
        SUBGT   R0,R0,R14

        SUBS    R14,gy0,ycoord
        Debug   ch,"ycoord +=",R14              ; only if R14 > 0
        ADDGT   ycoord,ycoord,R14
        SUBGTS  R0,R0,R14
        BLE     %FT01                           ; no nasty bottom-clipping

        LDR     xsiz,xsize
        MUL     R14,xsiz,R14
        LDRB    xsiz,charflags
        TST     xsiz,#chf_1bpp
        ADDEQ   inptr,inptr,R14,LSL #2
        ADDNE   inptr,inptr,R14                 ; inptr -> first visible pixel

        Debug   ch,"paintclipped: inptr =",inptr

01      CMP     R0,#0
        STRGT   R0,rowcount
        ProfOut
        BGT     quickclip
        B       justmove
02
        ProfOut

; get clipx0,clipy0,clipx1,clipy1 (in pixels)

        BL      getclipping                     ; can branch to clipoff

        ProfIn  Paint_CharClipped

; now calculate addresses and masks

        BL      getaddr
        LDR     R14,log2bpp                     ; release pixelsperword
        MOV     outcount,outcount,ASL R14
        SUB     R14,outcount,#1
        MOV     outdata,#1
        MOV     outdata,outdata,ASL R14

; finished with: xcoord, ycoord, outcount
; ---------------------------------------
;
; set up pointers etc. needed by the inner loop

        LDRB    bpp,bitsperpixel                ; should be set up by mode
	LDR	outputarray, blend_outputdata
        LDR     Fmask,outputmask

; loop, plotting each row in turn (from the bottom)

        Debug   ch,"Paintclipped: in,out,xs,ys =",inptr,outptr,#xsize,#rowcount

        LDRB    fvalue,charflags                ; NB: fvalue must be set up on entry to unpack_1bpp_firstrow
        TST     fvalue,#chf_1bpp

        BLNE    unpack_1bpp_firstrow_clipped    ; NB: can't branch off to optimised routine

        Debug ch,"came back as",inptr

        LDR     rowcounter,rowcount
        AND     incount,inptr,#31
        MOV     inptr,inptr,LSR #3
        LDR     indata,paint_inptr
        ORR     inptr,inptr,indata

        LDR     indata,[inptr,-incount,LSR #3]! ; indata = first word
        ADD     inptr,inptr,#4
        MOV     indata,indata,LSR incount       ; shift to correct position
        RSB     incount,incount,#32             ; incount = no of bits left
cyloop
        Push    "outptr,outdata"
        LDR     colcount,xsize
        MOV     outmask,#0
cxloop
        LDR     R14,clipx1
        CMP     colcount,R14                    ; inclusive test
        LDRCS   R14,clipx0
        RSBCSS  R14,colcount,R14                ; inclusive test
        LDRCS   R14,clipy1
        CMPCS   rowcounter,R14                  ; inclusive test
        LDRCS   R14,clipy0
        RSBCSS  R14,rowcounter,R14              ; inclusive test

        MOVCCS  R14,#0
        ANDCSS  R14,indata,#(1:SHL:in_bpp)-1    ; get next pixel
        LDRNE   R14,[outputarray,R14,ASL #2]    ; get appropriate output

        MOVEQ   outmask,outmask,LSR bpp
        ORRNE   outmask,Fmask,outmask,LSR bpp
        ORRS    outdata,R14,outdata,LSR bpp     ; stuff it into output word

        BCC     %FT01
        TEQ     outmask,#0
        MOVEQ   outdata,#&80000000              ; set marker bit
        ADDEQ   outptr,outptr,#4
        BLNE    putdata                         ; ensure no address exceptions
01
        MOV     indata,indata,LSR #in_bpp       ; always 4 bits of input
        SUBS    incount,incount,#in_bpp
        MOVEQ   incount,#32
        LDREQ   indata,[inptr],#4               ; get next word if nec.

        SUBS    colcount,colcount,#1
        BNE     cxloop

        TEQ     outdata,#&80000000              ; if non-zero,
        TEQNE   outmask,#0
        BLNE    putdata1                        ; output remaining word
        Pull    "outptr,outdata"

        LDR     R14,linelen
        SUB     outptr,outptr,R14               ; go up a row
        SUBS    rowcounter,rowcounter,#1
        ProfOut ,EQ
        BEQ     justmove                        ; common exit

        LDRB    R14,charflags
        TST     R14,#chf_1bpp
        BEQ     cyloop

        BL      unpack_1bpp_clipped             ; it works cos all rows scanned
        TEQ     incount,#32
        LDREQ   indata,[inptr],#4               ; bodge - wrong word was read
        B       cyloop


;-----------------------------------------------------------------------------
; get clipx0,clipy0,clipx1,clipy1 (in pixels)
;-----------------------------------------------------------------------------

getclipping PEntry Paint_CharClip, "inptr,xcoord,ycoord"

        LDR     xsiz,xsize                      ; get character size
        LDR     ysiz,rowcount

        LDR     R14,grafx0                      ; set up x-clipping
        SUBS    R14,R14,xcoord
        MOVLT   R14,#0
        SUBS    R14,xsiz,R14
        BLE     clipoff
        STR     R14,clipx0                      ; max counter (inclusive)

        LDR     R14,grafx1
        SUBS    R14,R14,xcoord
        BLT     clipoff
        SUBS    R14,xsiz,R14
        MOVLE   R14,#1
        STR     R14,clipx1                      ; min counter (inclusive)

        LDR     R14,grafy0                      ; set up y-clipping
        SUBS    R14,R14,ycoord
        MOVLT   R14,#0
        SUBS    R14,ysiz,R14
        BLE     clipoff
        STR     R14,clipy0                      ; max counter (inclusive)

        LDR     R14,grafy1
        SUBS    R14,R14,ycoord
        BLT     clipoff
        SUBS    R14,ysiz,R14
        MOVLE   R14,#1
        STR     R14,clipy1                      ; min counter (inclusive)

        PExit

clipoff
        PullEnv
        ProfOut
        B       justmove


;;----------------------------------------------------------------------------
;; paintmagnified
;;    - This routine is used to draw 'magnified' characters
;;    - It is 2.5 times slower than paintchar - only called when necessary
;;----------------------------------------------------------------------------

paintmagnified ROUT

; get clipx0,clipy0,clipx1,clipy1 (in pixels)

        BL      getclipping

        ProfIn  Paint_CharMag

; now calculate addresses and masks

        BL      getaddr
        LDR     R14,log2bpp                     ; release pixelsperword
        MOV     outcount,outcount,ASL R14
        SUB     R14,outcount,#1
        MOV     outdata,#1
        MOV     outdata,outdata,ASL R14         ; release outcount

        Debug   ch,"Paintmag: in,out,xs,ys =",inptr,outptr,#xsize,#rowcount

; finished with: xcoord, ycoord, outcount
; ---------------------------------------
;
; set up pointers etc. needed by the inner loop
; NB: no need to do the 1-bpp stuff for this one (can't happen)

        LDRB    bpp,bitsperpixel                ; should be set up by mode
        LDR     Fmask,outputmask

; skip rows at bottom of char

        ASSERT  in_bpp=4
        MOV     inptr,inptr,LSL #1              ; inptr -> first nibble
        LDR     rowcounter,rowcount
03
        LDR     ymagcnt,ymag
01
        LDR     R14,clipy0
        RSBS    R14,rowcounter,R14              ; if rowcounter <= R14,
        BCS     %FT02                           ; we've finished
        LDR     R14,linelen
        SUB     outptr,outptr,R14               ; outptr -= 1 row on screen
        SUB     rowcounter,rowcounter,#1
        SUBS    ymagcnt,ymagcnt,#1
        BNE     %BT01
        LDR     R14,inputxsize
        ADD     inptr,inptr,R14                 ; inptr += 1 row of pixels
        B       %BT03
02
        ASSERT  in_bpp=4
        AND     incount,inptr,#7
        MOV     incount,incount,LSL #2          ; 2^2 bits per pixel
        MOV     inptr,inptr,LSR #3

        LDR     indata,paint_inptr
        MOV     inptr,inptr,LSL #2              ; inptr --> input word
        ORR     inptr,inptr,indata              ; merge back top 3 bits lost

        LDR     indata,[inptr],#4               ; cache pixel data in a reg.
        MOV     indata,indata,LSR incount
        RSB     incount,incount,#32             ; 32 bits per input word

        LDR     R14,clipy1
        SUB     rowcounter,rowcounter,R14
        ADD     rowcounter,rowcounter,#1        ; silly clipx values (1..n)
myloop
        Push    "outptr,outdata"
        CMP     ymagcnt,rowcounter
        MOVGT   ymagcnt,rowcounter
        STR     ymagcnt,this_ymagcnt
        MOV     outmask,#0
        LDR     colcount,xsize
mxloop1
        LDR     xmagcnt,xmag
mxloop2
        LDR     R14,clipx1
        CMP     colcount,R14                    ; inclusive test
        LDRCS   R14,clipx0
        RSBCSS  R14,colcount,R14                ; inclusive test

        ASSERT  xmagcnt=pchar
        ASSERT  ymagcnt=outputarray

        Push    "xmagcnt"

        MOVCCS  R14,#0
        ANDCSS  R14,indata,#(1:SHL:in_bpp)-1    ; get next pixel
     	LDRNE	outputarray,blend_outputdata
        LDRNE   R14,[outputarray,R14,ASL #2]    ; get appropriate output

        MOVEQ   outmask,outmask,LSR bpp
        ORRNE   outmask,Fmask,outmask,LSR bpp
        ORRS    outdata,R14,outdata,LSR bpp     ; stuff it into output word

        BCC     %FT01
        TEQ     outmask,#0
        MOVEQ   outdata,#&80000000              ; set marker bit
        ADDEQ   outptr,outptr,#4
        BLNE    putdataM                        ; ensure no address exceptions
01
        Pull    "xmagcnt"

        SUBS    xmagcnt,xmagcnt,#1
        SUBNE   colcount,colcount,#1
        BNE     mxloop2

        MOV     indata,indata,LSR #in_bpp       ; always 4 bits of input
        SUBS    incount,incount,#in_bpp
        MOVEQ   incount,#32
        LDREQ   indata,[inptr],#4               ; get next word if nec.
02
        SUBS    colcount,colcount,#1
        BNE     mxloop1

        TEQ     outdata,#&80000000              ; if non-zero,
        TEQNE   outmask,#0
        BLNE    putdata1M                       ; output remaining word

        Pull    "outptr,outdata"

        LDR     ymagcnt,this_ymagcnt
        LDR     R14,linelen
01
        SUB     outptr,outptr,R14               ; go up <ymagcnt> rows
        SUBS    ymagcnt,ymagcnt,#1
        BNE     %BT01

        LDR     ymagcnt,this_ymagcnt
        SUBS    rowcounter,rowcounter,ymagcnt
        LDRNE   ymagcnt,ymag
        BNE     myloop

; carry on where the other routine left off

        ProfOut
        B       justmove

;;----------------------------------------------------------------------------
;; Paint character straight from outline definition
;; Entry: inptr -> character
;;        [sp] = character code
;;        SetPixelsPtr has set up [rendermatrix] etc.
;;        [scratchblock] -> an area set aside at the top of the cache
;;        [pbox] = offset applied to coords in drawchar
;; Exit via 'justmove'
;;
;; This routine calls 'drawchar' to paint the character,
;; or to stuff the draw path output into the user's buffer.
;; In this case it will also set up the draw object header etc.
;;
;;----------------------------------------------------------------------------

        ASSERT  pchar = R0
        ASSERT  inptr = R4
        ASSERT  xcoord = R8
        ASSERT  ycoord = R7

paintdraw       ROUT

        ProfIn  Paint_CharDraw

        Push    "R0-R11"                ; nasty register naming problems!

        LDR     R1,switch_buffer        ; see where output is going
        TEQ     R1,#0
        BNE     paintbuffer_in          ; NB: sets up stack for paintdraw_common

        BL      restoreoutput           ; ensure output is directed to screen

        ; Record the current colour
SavedColourSize * 10*4

        SUB     sp, sp, #SavedColourSize
        MOV     r0, #&80                ; record the graphics foreground colour
        ADD     r1, sp, #2*4
        SWI     XOS_SetColour
        STRVS   r0, [sp, #SavedColourSize]! ; correct stack - sneaky!
        BVS     %FT99
        STMIA   sp, {r0,r1}
        ; Now set the colour we want
        MOV     R0, #&00                ; overwrite, graphics, not pattern block
        LDR     R1, outputdata+15*4
        ;this has the colour number aligned so it begins at bit 31
        ;therefore we need to shift it right by 32-bpp
        LDRB    R14,bitsperpixel                ; outputdata invalid now if bpp has changed
        RSBS    R14,R14,#32
        MOVNE   R1,R1,LSR R14
        SWI     XOS_SetColour

; calculate matrix to map character into the correct position

paintdraw_common

; Modified by Chris to fix output_to_buffer alignment irregularities...

        LDR     R14,switch_buffer       ; outputting to buffer?
        TEQ     R14,#0
        LDREQ   xcoord,oldxcoord        ; forget character origin
        LDREQ   ycoord,oldycoord
        BEQ     %FT02

; Bug fix by Chris:
;
; This fixes the output_to_buffer alignment irregularities except for the
; origin sometimes tending towards zero - I think that's a problem prior
; to this routine.
;
; What I do is extend xco72 and yco72 to have a 2 bit fractional part,
; i.e. call calcxcoord for xco72<<2 and yco72<<2.  Then, we add in the
; sub-pixel fractional part (range 0-3).
;
; See comments in "WishList" for more details.

        LDR     R14,xco72               ; this and the bit below for now...
        MOV     R14,R14,ASL #2
        STR     R14,xco72
        LDR     R14,yco72
        MOV     R14,R14,ASL #2
        STR     R14,yco72
        BL      calcxcoord
        LDR     R14,xco72
        MOV     R14,R14,ASR #2
        STR     R14,xco72
        LDR     R14,yco72
        MOV     R14,R14,ASR #2
        STR     R14,yco72
02

; carry on...

        MOV     R14,#0                  ; no offset in drawchar
        STR     R14,pboxX               ; (coords rel. to current point)
        STR     R14,pboxY

        MOV     R14,#1 :SHL: 15         ; construct matrix for drawchar
        LDR     R1,modedata_px          ; R1 = VduExt_XEigFactor
        MOV     R2,#0                   ; R2 = 0
        MOV     R3,#0                   ; R3 = 0
        LDR     R4,modedata_py          ; R4 = VduExt_YEigFactor

; Comment from Chris:
;
; The MOV Rn,coord,LSL #6 instructions are part of the fix to ouput_to_buffer.
; The normal code (and the old output_to_buffer code) do the LSL #8.
; The patch does a LSL #6 because the coordinate has a fractional part in the
; lower 2 bits (actually the sub-pixel position).
;
; EQ => not outputting to buffer, NE => outputting to buffer

        MOVEQ   R5,xcoord,LSL #8        ; not output_to_buffer
        MOVNE   R5,xcoord,LSL #6        ; output_to_buffer so has 2 bit fraction
        MOV     R5,R5,LSL R1            ; R5 = x coord (OS units << 8)

        MOVEQ   R6,ycoord,LSL #8        ; not output_to_buffer
        MOVNE   R6,ycoord,LSL #6        ; output_to_buffer so has 2 bit fraction
        MOV     R6,R6,LSL R4            ; R6 = y coord (OS units << 8)

; End of output_to_buffer patch.

        MOV     R1,R14,LSL R1
        MOV     R4,R14,LSL R4

        Debug   th,"Paintdraw: matrix =",R1,R2,R3,R4,R5,R6

        Push    "R1-R6"
        MOV     R2,sp                   ; R2 -> matrix

; don't ask me how these were worked out. but they're wrong now! (amg)
        LDR     R3,[sp,#SavedColourSize+((6+4)*4)]      ; R3 -> character outline
        LDR     R4,[sp,#SavedColourSize+((6+12)*4)]     ; R4 = character code (pchar on stack)
        Debug   th,"Paintdraw: char outline, code =",R3,R4
        BL      drawchar
        ADD     sp,sp,#6*4

        LDR     R1,switch_buffer
        TEQ     R1,#0
        BNE     paintbuffer_out


; restore the saved colour etc

        MOV     R3,R0                   ; reset colour even if error
        SavePSR R4
        LDMIA   sp, {r0,r1}
        SWI     XOS_SetColour
        ADD     sp, sp, #SavedColourSize
        MOVVC   R0,R3                   ; report 'proper' error if gcol/tint OK
        RestPSR R4,VC,f

99      STRVS   R0,[sp]
        Pull    "R0-R11"
        ProfOut
        B       justmove

                ^       0
obj_type        #       4               ; offsets from object start
obj_size        #       4
obj_bbox        #       16

pathobj_fillcolour  #   4
pathobj_linecolour  #   4
pathobj_linewidth   #   4
pathobj_style       #   4
pathobj_header      #   0

rectobj_x0y0    #       12              ; move x0, y0
rectobj_x1y0    #       12              ; line x1, y0
rectobj_x1y1    #       12              ; line x1, y1
rectobj_x0y1    #       12              ; line x0, y1

                #       8               ; room for closepath, terminator
rectobj_size    #       0


style_join      *       1 :SHL: 0       ; values in [Rn,#obj_style]
style_endcap    *       1 :SHL: 2
style_startcap  *       1 :SHL: 4

style_nonzero   *       0 :SHL: 6
style_evenodd   *       1 :SHL: 6

style_dash      *       1 :SHL: 7
style_capwidth  *       1 :SHL: 16
style_caplength *       1 :SHL: 24

path_end        *       0               ; tag values in path
path_move       *       2
path_close      *       5
path_line       *       8
path_curve      *       6


; Construct appropriate header for character, and fill it in afterwards
; NB: Sets up same stack frame as other entry point
;     because paintdraw_common accesses stack higher up

paintbuffer_in
        LDR     R14,switch_flags        ; don't access buffer if just counting!
        TST     R14,#swf_justcount
        STRNE   R1,[sp,#-10*4]!
        ADDNE   R1,R1,#pathobj_header
        BNE     %FT01

        STR     R1, [sp,#-10*4]!         ; save character header

        LDR     R14,switch_forecolour
        STR     R14,[R1,#pathobj_fillcolour]
        MOV     R14,#0
        STR     R14,[R1,#pathobj_linewidth]
        MOV     R14,#-1
        STR     R14,[R1,#pathobj_linecolour]

; decode the font file to suss which fill algorithm is being used and
; then write a suitable record into the draw object.

        Push    "R0"
        LDRB    R14,outlinefont
        LDR     R0,cacheindex
        LDR     R0,[R0,R14,LSL #2]      ; get a suitable pointer to the outline data
        LDRB    R0,[R0,#hdr1_flags]
        TST     R0,#pp_fillnonzero      ; should I use the non-zero fill algorithm?
        MOVEQ   R14,#style_evenodd
        MOVNE   R14,#style_nonzero
        STR     R14,[R1,#pathobj_style] ; store a style record
        Pull    "R0"                    ; preserve that all important R0

        ASSERT  obj_type = 0
        MOV     R14,#2                  ; path object
        STR     R14,[R1],#pathobj_header

        MOV     R14,#0
        STR     R14,[R1]
        STR     R2,[R1,#4]              ; size remaining (for Draw module)
01
        STR     R1,switch_buffer
        B       paintdraw_common        ; output path to object


; R1 -> terminator at end of character just output
; NB: 3 words on stack rather than one, for paintdraw_common

paintbuffer_out
        LDR     R2,[sp],#10*4            ; R2 -> character header
        BVS     %BT99

        LDR     R3,switch_flags

        SUB     R14,R1,R2               ; if path was null,
        CMP     R14,#pathobj_header     ; forget the whole object!
        BLE     nullpath

        ADD     R1,R1,#4                ; otherwise update to include terminator
        STR     R1,switch_buffer

        TST     R3,#swf_justcount       ; if just counting, don't access buffer!
        BNE     %BT99

        LDR     R14,[R1,#0]
        SUBS    R14,R14,#4
        BLT     err_bufferoverflow
        STR     R14,[R1,#4]             ; update free space field

        MOV     R14,#0                  ; put in new terminator (just in case)
        STR     R14,[R1,#0]

        SUB     R1,R1,R2                ; object size
        STR     R1,[R2,#obj_size]

; scan path to find bounding box
; (should really call Draw_Flattenpath first, but I haven't got a buffer!)

        ADD     R1,R2,#pathobj_header   ; R1 -> path to scan
        MOV     R3,#bignum
        MOV     R4,#bignum
        MOV     R5,#-bignum
        MOV     R6,#-bignum
01
        LDR     R14,[R1],#4
        TEQ     R14,#path_move
        TEQNE   R14,#path_line
        BEQ     %FT02
        TEQ     R14,#path_close
        BEQ     %BT01
        TEQ     R14,#path_curve
        BNE     %FT03                   ; assume it's a terminator!

        BL      coord_bbox              ; 3 coords for bezier curves
        BL      coord_bbox
02      BL      coord_bbox              ; 1 for move and line
        B       %BT01

03      ADD     R2,R2,#obj_bbox
        STMIA   R2,{R3-R6}
        B       %BT99                   ; finished!

;................................................................................

; In    R1 -> end of path
;       R2 -> character header
;       R3 = [switch_flags]
;       R14 = size of character header to be deleted

nullpath
        STR     R2,switch_buffer

        TST     R3,#swf_justcount       ; if just counting, don't access buffer!

        LDREQ   R1,[R1,#4]              ; else move the terminator and free space
        ADDEQ   R1,R1,R14               ; to where the character header was
        STREQ   R1,[R2,#4]              ; (adding in the extra free space)
        MOVEQ   R14,#0
        STREQ   R14,[R2,#0]
        B       %BT99

;................................................................................

; update R3,R4,R5,R6 w.r.t coord pair in [R1]
; update R1 also

coord_bbox
        LDR     R0,[R1],#4
        CMP     R0,R3
        MOVLT   R3,R0                   ; update x0
        CMP     R0,R5
        MOVGT   R5,R0                   ; update x1
        LDR     R0,[R1],#4
        CMP     R0,R4
        MOVLT   R4,R0                   ; update y0
        CMP     R0,R6
        MOVGT   R6,R0                   ; update y1
        MOV     PC,LR

err_bufferoverflow
        BL      xerr_buffoverflow
        B       %BT99

;................................................................................

; In    [backcolour] = background colour
;       [forecolour] = first foreground colour
;       [aliascolours] = offset to 'real' foreground colour
; Out   RGB colours marked invalid
;       Error returned from setoutputdata if RGB colours invalid and output is currently switched to a buffer

invalidateRGB Entry ""

        MOV     R14,#invalid_flag       ; RGB colours invalid
        STR     R14,currentRGB_a        ; (must be derived from absolute colours)

        EXIT

;;----------------------------------------------------------------------------
;; Paint caret  -  routine to draw the caret
;; Entry:  R0 = colour
;;         R1 = height (OS coords)
;;         R2 = flags:
;;              bit 4 set ==> use OS coords, else 1/72000th inch
;;              bit 5 set ==> caret stem is 2 pixels wide, else 1
;;         R3,R4 = x,y coords
;;----------------------------------------------------------------------------

cb1_s   *       2_00010000                  ; single thickness
cb1_m   *       2_00101000
cb1_b   *       2_11000110

cb2_s   *       2_00011000                  ; double thickness
cb2_m   *       2_00111100
cb2_b   *       2_11100111

paintcaret Entry ""

        LDR     R14,modedata_py
        MOV     R1,R1,ASR R14                   ; divide down to pixels

        Push    "R0,R1"                         ; save for later

        ASSERT  ((:INDEX:carbits_small) :AND: 3) = 0
        ASSERT  carbits_small+1 = carbits_middle
        ASSERT  carbits_middle+1 = carbits_big
        TST     R2,#caret_double
        LDREQ   R5,=cb1_s + (cb1_m :SHL: 8) + (cb1_b :SHL: 16)
        LDRNE   R5,=cb2_s + (cb2_m :SHL: 8) + (cb2_b :SHL: 16)
        STR     R5,carbits_small
        LDRNE   R14,modedata_px
        MOVNE   R6,#2
        MOVNE   R6,R6,LSL R14                   ; width of stem (OS units)
        LDRNE   R14,modedata_py
        MOVNES  R6,R6,LSR R14                   ; ensure at least 1 pixel
        MOVEQ   R6,#1                           ; thickness of crossbar (pixels)
        STRB    R6,carbits_cross

        TST     R2,#caret_osunits               ; 1 ==> convert to 1/72000th"
        STREQ   R3,xco72                        ; coords in 1/72000th inch
        STREQ   R4,yco72

        BLEQ    calcxcoord                      ; obtain xcoord,ycoord

        LDRNE   R14,modedata_px
        MOVNE   xcoord,R3,ASR R14               ; scale down to pixels
        LDRNE   R14,modedata_py
        MOVNE   ycoord,R4,ASR R14

        SUB     xcoord,xcoord,#4                ; pattern is now wider

        BL      getaddr                         ; get outptr,outcount

        Pull    "R0,R1"

        Debug   cr,"Scrtop =",#scrtop
        Debug   cr,"Caret x,y =",xcoord,ycoord
        Debug   cr,"Address/count =",outptr,outcount

        LDRB    R14,bitsperpixel
        RSB     R14,R14,#32
        MOV     outcolour,R0,ASL R14            ; shift to top of word
        MOV     rowcnt,R1
        Debug   cr,"Output colour =",outcolour

        LDRB    bpp,bitsperpixel
        LDRB    pixelsperword,Pixelsperword

        MOV     indata,#8*2                     ; work out width of crossbar
        LDR     R14,modedata_px                 ; by comparing OS unit height
        MOV     indata,indata,LSL R14           ; (not pixel height)
        LDR     R14,modedata_py
        MOV     indata,indata,LSR R14           ; indata = cutoff for midcaret
        ADD     R14,indata,indata,LSR #1        ; R14 = indata * 3/2
        CMP     rowcnt,R14
        PullEnv
        BGE     bigcaret
        CMP     rowcnt,indata
        BGE     midcaret                        ; else drop through

;...............................................................................

smallcaret Entry ""

carlp   SUBS    rowcnt,rowcnt,#1
        LDRGEB  indata,carbits_small
        BLGE    caretrow
        BGT     carlp

        EXIT

;...............................................................................

bigcaret Entry ""

        LDRB    indata,carbits_big
        BL      caretcross

        LDRB    R14,carbits_cross
        SUB     rowcnt,rowcnt,R14,LSL #1
        BL      midcaret

        LDRB    indata,carbits_big
        BL      caretcross

        EXIT

;...............................................................................

midcaret Entry ""

        LDRB    indata,carbits_middle
        BL      caretcross
        LDRB    R14,carbits_cross
        SUB     rowcnt,rowcnt,R14,LSL #1

        BL      smallcaret

        LDRB    indata,carbits_middle
        BL      caretcross

        EXIT

;...............................................................................

caretcross Entry "indata,rowcnt"

        LDRB    rowcnt,carbits_cross
        ASSERT  indata < rowcnt
01      LDR     indata,[sp]
        BL      caretrow
        SUBS    rowcnt,rowcnt,#1
        BNE     %BT01

        EXIT

;...............................................................................

; In    indata            = pixel mask to use (1-bpp, low bit first)
;       outcolour         = colour (in top pixel of word)
;       outptr           -> address on screen
;       outcount          = number of pixels to do in this word
;       bpp               = bits per pixel
;       pixelsperword     = number of pixels in a word
;       xcoord, ycoord    = current screen coordinates (pixels)
;       [grafx0/y0/x1/y1] = clip window (pixels, inclusive)
; Out   data EOR-ed onto screen

caretrow EntryS ""

        Debug   cr,"Caret data:",indata

        LDR     R14,grafy0
        CMP     ycoord,R14
        LDRGE   R14,grafy1
        CMPGE   R14,ycoord
        BLT     nocaret

        Push    "xcoord,outcount,outptr"
        MOV     R0,#0                           ; outdata

getcarword
        LDR     R14,grafx0
        CMP     xcoord,R14
        LDRGE   R14,grafx1
        CMPGE   R14,xcoord
        BICLT   indata,indata,#1            ; bodge - clear bit if off window

        MOVS    indata,indata,LSR #1        ; C set & N unset --> GE
        MOV     R0,R0,LSR bpp
        ORRCS   R0,R0,outcolour             ; OR in at top

        ADD     xcoord,xcoord,#1
        SUBS    outcount,outcount,#1
        BNE     getcarword

        Debug   cr,"Caret ptr/data =",outptr,R0

        TEQ     R0,#0
        LDRNE   R14,[outptr]            ; avoid address exceptions
        EORNE   R14,R14,R0
        STRNE   R14,[outptr]
        ADD     outptr,outptr,#4
        MOV     R0,#0
        MOV     outcount,pixelsperword
        TEQ     indata,#0
        BNE     getcarword

        Pull    "xcoord,outcount,outptr"

nocaret
        ADD     ycoord,ycoord,#1
        LDR     R14,linelen
        SUB     outptr,outptr,R14               ; move up a line

        EXITS

;----------------------------------------------------------------------------
; getaddr
; Entry: xcoord,ycoord = coordinates (internal)
; Exit : outptr,outcount,pixelsperword are set up
;----------------------------------------------------------------------------

getaddr Entry   "xcoord,ycoord,hshift,vmul"
        LDRB    pixelsperword,Pixelsperword
        LDR     hshift,modedata_h
        LDR     outptr,ywindlimit               ; maximum y-coordinate
        LDR     vmul,linelen
        LDR     R14,scrtop                       ; obtain screen origin

        SUB     outcount,pixelsperword,#1       ; get mask
        AND     outcount,outcount,xcoord        ; offset within a word
        RSB     outcount,outcount,pixelsperword

        RSB     ycoord,ycoord,outptr            ; invert ycoord
        MLA     outptr,vmul,ycoord,R14
        MOV     xcoord,xcoord,ASR hshift        ; obtain byte offset / 4
        ADD     outptr,outptr,xcoord,ASL #2     ; obtain address of word

        Debug   ch2,"outptr,outcount =",outptr,outcount

        EXIT

;-----------------------------------------------------------------------
; extendmask - extend mask to the right (ie. to high order bits)
; In    [outputmask] = mask for top pixel in word
;       outcount = no. of pixels to do this word (to the right)
;       [bitsperpixel] = bits per pixel for the output buffer
; Out   outmask = mask extended to the right (high order) bits
;-----------------------------------------------------------------------

extendmask Entry "bpp,outcount"

        LDRB    bpp,bitsperpixel
        LDR     outmask,outputmask
01      SUBS    outcount,outcount,#1
        ORRNE   outmask,outmask,outmask,LSR bpp
        BNE     %BT01

        EXIT

;;----------------------------------------------------------------------------
;; rubout - stay ahead of this character
;; bounding box is supplied by the user
;;
;; Entry: xcoord    = left of this char
;;        [xsize]   = width of char in pixels
;;        ruboutx,y = coord of bit to rub out
;; Exit : column(s) rubbed out as required
;;----------------------------------------------------------------------------

ruboutrest PEntry Paint_Rubout, ""

        Debug   rub,"Ruboutrest!"

; first check if we still need to set everything up (ie. no chars printed)

        LDR     R14,ruboutx             ; rubout finished
        TEQ     R14,#bignum             ; check before to try and avoid setoutputdata
        PExit   EQ

        BL      trysetoutputdata
        BLVC    trysetrubout
        PExit   VS

        LDR     R14,ruboutx             ; rubout finished
        TEQ     R14,#bignum             ; also check afterwards in case the box was null
        PExit   EQ

; call rubout once first in case there's been a colour change at the end of the string

        LDR     R14,ul_nextxcoord
        STR     R14,lastxcoord
        BL      rubout                  ; watch for colour change at end of string
        PExit   VS

; botch current xcoord to force rubout of rest of box

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        LDREQ   xcoord,rubx1            ; ensure rest gets done
        ADDEQ   xcoord,xcoord,#1        ; N.B. -- it's exclusive!!!
        LDRNE   xcoord,rubx0

        ProfOut
        PullEnv                         ; drop through

;----------------------------
; normal entry point
;----------------------------

; In    xcoord = current x-coord (pixels) after subtracting bbox origin
;       [xsize] = character bbox width (pixels)
;       [ul_nextxcoord] = x-coord after this character (pixels)

rubout  PEntry  Paint_Rubout, "inptr,xcoord,ycoord"

      [ debugbrk
        LDR     R14,ruboutx
        TEQ     R14,#bignum
        BreakPt "rubout called when ruboutx = bignum!",EQ
      ]

; first check for a colour change

        LDR     R14,switch_buffer
        TEQ     R14,#0
        BNE     switchnewcolour         ; check for 24-bit colour change

        LDR     newcolour,rubdata
        LDR     R14,oldrubdata
        TEQ     newcolour,R14
        BNE     getnewrubaddr           ; calls rubout - must not recurse!
gotnewrubaddr

        LDR     R14,switch_buffer       ; work is all done in setrubout
        TEQ     R14,#0                  ; and getnewrubaddr in this case
        PExit   NE

        LDR     R14,plottype
        TST     R14,#paint_rightleft    ; use completely different code!
        BNE     rubout_toleft

        LDR     endxco,xsize
        ADD     endxco,endxco,xcoord
        LDR     R14,ul_nextxcoord
        CMP     endxco,R14
        MOVLT   endxco,R14              ; endxco = rightmost of char width and bbox

tryrub_LR
        LDR     xcoord,ruboutx
        LDR     ycoord,ruby0

        Debug   rub,"rubout (L-R): endxco,ruboutx,y =",endxco,xcoord,ycoord

        CMP     xcoord,#bignum
        CMPNE   endxco,xcoord
        PExit   LE

        Push    "endxco"

; get output ptr and update for next time

        LDR     outptr,ruboutptr
        ADD     R14,outptr,#4
        STR     R14,ruboutptr
        LDR     outmask,startrubmask
        MOV     R14,#&FFFFFFFF
        STR     R14,startrubmask               ; do whole word next time

        LDR     R14,ruboutx
        LDRB    R1,ruboutcount
        ADD     R14,R14,R1
        STR     R14,ruboutx
        LDRB    R14,Pixelsperword             ; for updating ruboutx
        STRB    R14,ruboutcount

; cope with hitting right-hand end (compare word addresses)

        LDR     R14,maxrubptr
        CMP     outptr,R14
        BGT     endruboutlp                     ; may have to do this again!
        LDREQ   R14,endrubmask
        ANDEQ   outmask,outmask,R14             ; combine masks if nec.

; get required output data word

gorubout
        LDR     outdata,rubdata

        LDR     linegap,linelen
        LDR     outcount,rubouth
        AND     outdata,outdata,outmask         ; only affect required pixels

        Debuga  rub,"rubout: outptr,outmask,outdata,outcount ="
        Debug2  rub,outptr,outmask,outdata,outcount

ruboutlp
        ADDS    R14,outmask,#1                  ; if mask is solid, skip the screen read
        LDRNE   R14,[outptr]
        SUBS    outcount,outcount,#1
        BIC     R14,R14,outmask
        ORR     R14,R14,outdata
        STR     R14,[outptr],-linegap
        BNE     ruboutlp

endruboutlp
        Pull    "endxco"

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        BEQ     tryrub_LR
        BNE     tryrub_RL

; . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

rubout_toleft
        LDR     endxco,ul_nextxcoord
        CMP     endxco,xcoord
        MOVGT   endxco,xcoord                   ; endxco = leftmost of char width and bbox

tryrub_RL
        LDR     xcoord,ruboutx
        LDR     ycoord,ruby0

        Debug   rub,"rubout (R-L): endxco,ruboutx,rubouty =",endxco,xcoord,ycoord

        CMP     endxco,xcoord
        PExit   GE

        Push    "endxco"

; get output ptr and update for next time

        LDR     outptr,ruboutptr
        SUB     R14,outptr,#4
        STR     R14,ruboutptr
        LDR     outmask,startrubmask
        MOV     R14,#&FFFFFFFF
        STR     R14,startrubmask               ; do whole word next time

        LDR     R14,ruboutx
        LDRB    R1,ruboutcount
        SUB     R14,R14,R1
        STR     R14,ruboutx
        LDRB    R14,Pixelsperword             ; for updating ruboutx
        STRB    R14,ruboutcount

; cope with hitting left-hand end (compare word addresses)

        LDR     R14,maxrubptr
        CMP     outptr,R14
        BLT     endruboutlp                     ; may have to do this again!
        LDREQ   R14,endrubmask
        ANDEQ   outmask,outmask,R14             ; combine masks if nec.

        B       gorubout

;;----------------------------------------------------------------------------
;; setrubout - set up data for rubout
;;
;; Entry: rubx0,ruby0,rubx1,ruby1 = bounding box for rubout
;; Exit : ruboutx = point reached so far (left or right)
;;      : rubouth = height of bit to rub out (pixels)
;;      : ruboutptr = address of start of rubout
;;      : maxrubptr = address of end of rubout
;;      : startrubmask = mask of 1st word to rub out
;;      : endrubmask = mask of last word
;;
;;----------------------------------------------------------------------------

trysetrubout Entry ""

        LDRB    R14,setruboutflag               ; setruboutflag <> 0 => done already
        TEQ     R14,#0
        EXIT    NE

        PullEnv                                 ; drop through if necessary

setrubout PEntry Paint_Rubout, "R0-R12"         ; play it safe!

        Debug   xx,"Setrubout: rub x0,y0,x1,y1, ruboutx =",#rubx0,#ruby0,#rubx1,#ruby1,#ruboutx

        Debug   rub,"SetRubout!"

        MOV     R14,#&FF
        STRB    R14,setruboutflag

        LDR     R14,ruboutx
        TEQ     R14,#bignum
        PExit   EQ

; if output switched to buffer, create a rectangle of the correct size

        LDR     R1,switch_buffer
        TEQ     R1,#0
        BNE     rubout_rectangle

; first clip the rubout window to the current graphics window

        ADR     R14,grafx0
        LDMIA   R14,{gx0,gy0,gx1,gy1}

        ADR     R14,rubx0
        LDMIA   R14,{x0,y0,x1,y1}

        CMP     x0,gx0
        MOVLT   x0,gx0
        CMP     y0,gy0
        MOVLT   y0,gy0
        CMP     x1,gx1
        MOVGT   x1,gx1
        CMP     y1,gy1
        MOVGT   y1,gy1
        Debug   xx,"Setrubout: clipped x0,y0,x1,y1 =",x0,y0,x1,y1
        CMP     x0,x1
        CMPLE   y0,y1
        BGT     setnorubout

        STMIA   R14,{x0,y0,x1,y1}

; can't use x0,y0,x1,y1 from now on cos they clash with xcoord,ycoord

        ASSERT  xcoord = y0                     ; horrible, isn't it?
        ASSERT  ycoord = x0

; calculate address of first word of rubout

setrubout_common
        LDR     R2,plottype
        TST     R2,#paint_rightleft
        LDR     x1,rubx1
        ADD     x1,x1,#1
        LDREQ   xcoord,rubx0
        MOVNE   xcoord,x1
        LDRNE   x1,rubx0                        ; xcoord = start coord, x1 = end coord
        STR     xcoord,ruboutx
        LDR     ycoord,ruby0
        BL      getrubaddr                      ; mask is reversed if going right to left

        Debug   rub,"-- startrubx,ruby0,ruboutptr,startrubmask =",xcoord,ycoord,#ruboutptr,#startrubmask

; calculate address of last word of rubout

        MOV     xcoord,x1
        BL      getaddr
        BL      extendmask
        LDR     R14,plottype
        TST     R14,#paint_rightleft
        MOVEQ   R14,#-1                         ; mask is reversed if going left to right
        EOREQ   outmask,outmask,R14
        STR     outptr,maxrubptr
        STR     outmask,endrubmask

        Debug   rub,"--   endrubx,ruby1,maxrubptr,  endrubmask =",xcoord,#ruby1,#maxrubptr,#endrubmask

; calculate height in pixels

        LDR     R14,ruby1
        ADD     R14,R14,#1                      ; exclusive coord
        SUBS    R14,R14,ycoord                  ; get pixel difference
        STRGT   R14,rubouth
        Debug   rub,"-- rubouth =",R14
        PExit   GT

setnorubout
        Debug   rub :LOR: debugxx,"-- No rub-out"

        MOV     R14,#bignum                     ; a big number!
        STR     R14,ruboutx
        PExit

;................................................................................

; In    R1 -> output buffer
;       Stack from setrubout entry
; Out   rectangle object created, with the full rubout coordinates in it

rubout_rectangle
        ADR     R14,rubx0               ; no rubout if
        LDMIA   R14,{x0,y0,x1,y1}
        CMP     x0,x1
        CMPLE   y0,y1
        BGT     setnorubout

        BL      makeruboutrectangle     ; can give "Buffer overflow"

        BLVC    getswitchruboutbox      ; R0, R1-R4 set up
        STRVC   R1,switch_lastrubx1     ; in case of colour changes
        BLVC    setrectanglesize        ; stick them in the object unless counting
        BVC     setrubout_common

        STRVS   R0,[sp]
        PExit

;................................................................................

; In    [rubx0/y0/x1/y1] = rubout box coords (pixels)
; Out   R0 -> current rubout rectangle object (or 0 if none)
;       R1-R4 = rubout box coords (1/256th OS unit)

getswitchruboutbox Entry ""

        ADR     R14,rubx0
        LDMIA   R14,{R1-R4}
        ADD     R3,R3,#1                ; top-right must be exclusive
        ADD     R4,R4,#1

        LDR     R14,modedata_px         ; scale to 1/256 OS unit
        ADD     R14,R14,#8
        MOV     R1,R1,LSL R14
        MOV     R3,R3,LSL R14

        LDR     R14,modedata_py
        ADD     R14,R14,#8
        MOV     R2,R2,LSL R14
        MOV     R4,R4,LSL R14

        LDR     R0,switch_rubout        ; for convenience

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        EORNE   R1,R1,R3
        EORNE   R3,R3,R1
        EORNE   R1,R1,R3                ; R1 = first x, R3 = last x

        EXIT

;................................................................................

; In    [switch_lastrubx1] = end of previous rubout box
;       [xco72] = current x-coord (1/72000")
; Out   R1 = [switch_lastrubx1] (old version)
;       R3 = [switch_lastrubx1] = current x-coord (1/256th OS unit)

getswitchxcoords Entry "R0,R2"

        LDR     R0,xco72
        MOV     R0,R0,LSL #8
        LDR     R1,xscalefactor
        BL      divide
        MOV     R3,R2

        LDR     R1,switch_lastrubx1
        STR     R3,switch_lastrubx1     ; for next time

        EXIT

;------------------------------------------------------------
; getnewrubaddr - branched to by rubout if a colour change occurs
; NB: this bit is only called if output is not to buffer
;------------------------------------------------------------

getnewrubaddr
        Debug   rub,"-- colour change:",R14,newcolour

; botch needed to ensure rubout has got far enough

        STR     R14,rubdata             ; store old colour
        Push    "newcolour,LR"          ; new value of rubdata
        BL      rubout                  ; recursive!
        Pull    "newcolour,LR"
        STR     newcolour,rubdata

        LDR     xcoord,lastxcoord       ; ul_nextxcoord from last time

        LDR     R14,grafx0
        CMP     xcoord,R14              ; don't move outside clip window!
        MOVLT   xcoord,R14
        LDR     R14,grafx1
        CMP     xcoord,R14
        ADDGT   xcoord,R14,#1           ; remember that grafx1 is inclusive

        STR     xcoord,ruboutx          ; set back to before this char
        LDR     ycoord,ruby0
        BL      getrubaddr

        B       gotnewrubaddr           ; then carry on

;................................................................................

; In    stack holds registers from rubout entry
;       R1 = [switch_buffer], non-zero

Proc_RegList    SETS    "inptr,xcoord,ycoord"        ; stack at rubout entry

switchnewcolour
        LDR     R14,switch_lastback     ; check the 24-bit RGB colour!
        LDR     R0,switch_backcolour    ; (switch_lastback is set up in makerectangleobject)
        TEQ     R0,R14
        PExit   EQ

        ASSERT  R4 = inptr              ; already stacked!
        Push    "R0-R3"

        BL      getswitchruboutbox      ; R1-R4 = 1/256 OS coords
        BL      getswitchxcoords        ; R1,R3 = x-coordinates of current box
        TEQ     R0,#0
        BLNE    setrectanglesize

        LDR     R1,switch_buffer
        BL      makeruboutrectangle     ; ensures that it's behind underline

        BLVC    getswitchruboutbox      ; R1-R4 = whole box coords
        LDRVC   R1,switch_lastrubx1     ; start where the other one left off
        BLVC    setrectanglesize
01
        STRVS   R0,[sp]
        ProfOut
        Pull    "R0-R3,$Proc_RegList,PC"

;................................................................................

; In    R1 = [switch_buffer]
; Out   [switch_buffer] = updated
;       [switch_rubout] and possibly [switch_underline] are updated
;       (If underline is active, it must go in front of the rubout box)

makeruboutrectangle Entry "R1-R5"

        MOV     R14,#0
        STR     R14,switch_rubout       ; start new rectangle

        ADR     R2,switch_rubout
        BL      makerectangleobject
        EXIT    VS                      ; "Buffer overflow" possible

        LDR     R14,switch_flags        ; that's enough if just counting
        TST     R14,#swf_justcount
        EXIT    NE

        LDR     R2,switch_underline     ; if underline in progress,
        TEQ     R2,#0                   ; we must ensure that it's in front
        EXIT    EQ

        LDR     R1,switch_rubout
        STR     R1,switch_underline     ; swap over the pointers
        STR     R2,switch_rubout

        MOV     R3,#rectobj_size        ; and then the data
01      LDR     R4,[R1],#4
        LDR     R5,[R2],#4
        STR     R5,[R1,#-4]
        STR     R4,[R2,#-4]
        SUBS    R3,R3,#4
        BNE     %BT01

        Pull    "R1-R5,PC"

;------------------------------------------------------------
; getrubaddr - used by setrubout
;------------------------------------------------------------

; In    x/ycoord = coordinates (pixels) of lower start corner of rubout
;       [plottype] :AND: paint_rightleft = direction
; Out   [ruboutptr] -> address of first word
;       [ruboutcount] = offset to add to ruboutx once this word is filled
;       [startrubmask] = mask (bit set => fill)
;       [oldrubdata] = [rubdata] (colour change accomplished)

getrubaddr Entry "outmask,pixelsperword,outcount,outptr"

        LDR     R14,rubdata                     ; from setoutputdata
        STR     R14,oldrubdata

        BL      getaddr
        STR     outptr,ruboutptr
        BL      extendmask                      ; outcount, outmask = as for left-right

        LDR     R14,plottype
        TST     R14,#paint_rightleft
        SUBNE   outcount,pixelsperword,outcount ; outcount = pixels to do in this word
        MOVNE   R14,#-1
        EORNE   outmask,outmask,R14             ; outmask = which pixels to set

        STRB    outcount,ruboutcount            ; for updating ruboutx
        STR     outmask,startrubmask

        Debug   rub,"getrubaddr: addr,count,mask =",outptr,outcount,outmask

        EXIT
        LTORG

;................................................................................

; Routine to debug a string passed to Font_Paint or Font_ScanString

      [ debugxx

debugpaintparams EntryS "R0-R11"

        Debug   xx,"Font_Paint: font,string,flags,x,y =",R0,R1,R2,R3,R4
        Debug   xx,"            coordblk,matrix,length=",R5,R6,R7
        TST     R2,#paint_coordblk
        BEQ     %FT01
        LDMIA   R5!,{R7-R10}
        Debug   xx,"coordblk: x/yspace, x/yletter =",R7,R8,R9,R10
        LDMIA   R5!,{R7-R10}
        Debug   xx,"          ruboutbox =",R7,R8,R9,R10
01
        TST     R2,#paint_matrix
        BEQ     %FT02
        DebugM  xx,"Paint matrix",R6
02
        Debuga  xx,"String = "

        MOV     R2,#0                   ; 0 => not started at all yet

03      LDRB    R0,[R1],#1
        TEQ     R0,#&7F
        BEQ     %FT10
        CMP     R0,#32
        BLT     %FT10

        TEQ     R2,#1
        BLEQ    dbg_comma
        CMP     R2,#1
        BLLE    dbg_quote
        MOV     R2,#2                   ; 2 => in string
        BL      Neil_WriteC
        B       %BT03

10      TEQ     R2,#2
        BLEQ    dbg_quote
        TEQ     R2,#0
        BLNE    dbg_comma

        BL      dbg_number

        TEQ     R0,#21
        BEQ     skipcomment
        TEQ     R0,#27
        BEQ     skip4
        TEQ     R0,#28
        BEQ     skip6

        TEQ     R0,#cr
        TEQNE   R0,#lf
        TEQNE   R0,#0
        BLEQ    Neil_NewLine
        EXITS   EQ

        EORS    R2,R0,#&7F
        ADRNE   R14,dbgtab
        LDRNEB  R2,[R14,R0]
        TEQ     R2,#0
        BEQ     %FT30

20      BL      dbg_comma
        LDRB    R0,[R1],#1
        BL      dbg_hexnumber
        SUBS    R2,R2,#1
        BNE     %BT20

30      MOV     R2,#1                   ; 1 => last thing was a ctrl char
        B       %BT03

dbgtab  DCB     0,0,0,0,0,0,0,0,0,3,0,3,0,0,0,0,0,1,3,7,0,&FF,0,0,0,2,1,&FF,&FF,0,0,0
        ASSERT  (.-dbgtab) = 32

skipcomment
        Debuga  xx,",<comment>"

50      LDRB    R0,[R1],#1
        CMP     R0,#32
        BGE     %BT50
        B       %BT30

skip4   ADD     R1,R1,#3
        BIC     R1,R1,#3
        LDMIA   R1!,{R3-R6}
        Debuga  xx,",",R3,R4,R5,R6
        B       %BT30

skip6   ADD     R1,R1,#3
        BIC     R1,R1,#3
        LDMIA   R1!,{R3-R8}
        Debuga  xx,",",R3,R4,R5,R6,R7,R8
        B       %BT30

dbg_comma Entry "R0"

        MOV     R0,#","
        BL      Neil_WriteC

        EXITS

dbg_quote Entry "R0"

        MOV     R0,#""""
        BL      Neil_WriteC

        EXITS

dbg_number Entry "R0-R2",12

        MOV     R1,sp
        MOV     R2,#12
        SWI     XOS_ConvertInteger4

        BLVC    Neil_Write0

        EXITS

dbg_hexnumber Entry "R0-R2",12

        MOV     R0,#"&"
        BL      Neil_WriteC
        LDR     R0,[sp,#12]

        MOV     R1,sp
        MOV     R2,#12
        SWI     XOS_ConvertHex2

        BLVC    Neil_Write0

        EXITS

      ]

        END
