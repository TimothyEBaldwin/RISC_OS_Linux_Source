; Copyright 2020 RISC OS Open Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors
        GET     Hdr:ZLib

        AREA    |Asm$$Code|, CODE, READONLY

; definition for a basic SWI wrapper
; all ZLib SWIs return status in R0 and preserve the rest

        MACRO
$label  Wrap    $swi

$label  Push    "R14"
        SWI     $swi
        Pull    "PC"
        EXPORT  $label
        MEND

; These SWIs expect R2 to be a pointer to the end of data
; but the ZLib library functions provide length instead

crc32
        Push    "R14"
        ADD     R2,R2,R1
        SWI     XZLib_CRC32
        Pull    "PC"
        EXPORT  crc32

adler32
        Push    "R14"
        ADD     R2,R2,R1
        SWI     XZLib_Adler32
        Pull    "PC"
        EXPORT  adler32

; These SWIs can have a very simple wrapper

inflateInit_    Wrap XZLib_InflateInit
inflateInit2_   Wrap XZLib_InflateInit2
inflate         Wrap XZLib_Inflate
inflateEnd      Wrap XZLib_InflateEnd
inflateReset    Wrap XZLib_InflateReset

deflate         Wrap XZLib_Deflate
deflateEnd      Wrap XZLib_DeflateEnd
deflateReset    Wrap XZLib_DeflateReset

; Finally, deflateInit2_ has 8 parameters, so we need
; to retrieve the final 4 of them from the stack

deflateInit2_
        MOV     R12,R13
        Push    "R4-R7,R14"
        LDMFD   R12,{R4-R7}
        SWI     XZLib_DeflateInit2
        Pull    "R4-R7,PC"
        EXPORT  deflateInit2_

        END
