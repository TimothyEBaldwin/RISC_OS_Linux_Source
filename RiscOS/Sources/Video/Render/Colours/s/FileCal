; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; > FileCal

;..............................................................................

; Handle desktop saving messages, ie. write calibration/loadings to a file

; in    R1 service number
;       R2 handle
;       WP ->private word (may be zero)
;
; out   R1 Service_Servied if failed to save, else leave intact

save_desktop_to_file ROUT

        LDR     WP,[WP]                 ;Has any workspace been allocated
        CMP     WP,#0
        MOVEQ   PC,LR

        Push    "R0-R2,LR"

        MOV     R1,R2                   ;Move handle to a sensible register
        MOV     R0,#0                   ;..and reset the flags word
        BL      WriteCalibrationToFile_Code
        BLVC    WriteLoadingsToFile_Code

        STRVS   R0,[SP]                 ;Store the error pointer if went boing!
        Pull    "R0-R2"
        MOVVS   R1,#Service_Serviced
        Pull    "PC"                    ;If an error occurs then ensure R0 ->error and service is claimed

;..............................................................................

ColourTransMapSize_Code ROUT

        Entry   "R1-R7"

        BL      validateworkspace
        EXIT    VS

        MOV     R1,R0                   ;->Command line
        BL      getnumber
        MOVVC   R4,R0
        BLVC    getnumber
        MOVVC   R5,R0
        BLVC    getnumber
        MOVVC   R6,R0                   ;Read all three commands

        ADDVC   R3,R4,R5
        ADDVC   R3,R3,R6
        MOVVC   R3,R3,LSL #2
        ADDVC   R3,R3,#12
        MOVVC   R0,#ModHandReason_Claim
        SWIVC   XOS_Module              ;Attempt to claim buffer to put data into
        EXIT    VS

        MOV     R1,R2
        LDR     R2,[WP,#Calibration_pending]
        STR     R1,[WP,#Calibration_pending]
        CMP     R2,#0                   ;Do I need to free any pending blocks?
        BEQ     %FT10                   ;..no as it's already free'd

        MOV     R0,#ModHandReason_Free
        SWI     XOS_Module
10
        STMIA   R1,{R4-R6}
        STR     R3,[WP,#Calibration_remaining]
        EXIT

;..............................................................................

ColourTransMap_Code ROUT

        Entry   "R1-R7"

        BL      validateworkspace
        EXIT    VS                      ;Give up if unable to validate workspace

        MOV     R1,R0                   ;->Command line to be scanned
        LDR     R4,[WP,#Calibration_pending]
10
        LDRB    LR,[R0]                 ;Is there a character left for me to read?
        CMP     LR,#32
        BLT     %FT20                   ;No, so give up now yer big sother poof!

        LDR     R5,[WP,#Calibration_remaining]
        CMP     R5,#12                  ;Is there enough space?
        BLE     %FT90                   ;Nope, so give up its all a pile of glop!

        BL      getnumber               ;Read a number and update line pointer
        SUBVC   R5,R5,#4
        STRVC   R0,[R4,R5]
        STRVC   R5,[WP,#Calibration_remaining]
        BVC     %BT10                   ;Loop until no more numbers left to read

        EXIT                            ;Return if it errors during the above
20
        LDR     R5,[WP,#Calibration_remaining]
        CMP     R5,#12                  ;Is there any more to come (will clear V)
        EXIT    NE                      ;Yes there is so just return

        LDR     R0,[WP,#Calibration_pending]
        BL      SetCalibration_Code
        EXIT    VS                      ;Return now if failed to setup correctly

        LDR     R2,[WP,#Calibration_pending]
        MOV     R0,#ModHandReason_Free
        SWI     XOS_Module              ;Attempt to release the block
        MOVVC   R1,#0
        STRVC   R1,[WP,#Calibration_pending]

        EXIT
90
        ADR     R0,ErrorBlock_CTBadCalib
        BL      LookupError
        EXIT                            ;Return error if setting the table is invalid

        MakeInternatErrorBlock CTBadCalib

;..............................................................................

; Write the calibration block to a file consisting of *commands, this is
; used as part of the desktop boot sequence.

; in    R0 bit 0 clear => if saving to be done if default calibration
;          bit 0 set => set if saving is always to be done
;
;       R1 file handle

WriteCalToFileFlag_SaveAlways * 1:SHL:0

WriteCalibrationToFile_Code ROUT

        Debug   input1,"WriteCalibrationToFile: R0,R1",R0,R1
        Debug   input2,"WriteCalibrationToFile: flag, handle",R0,R1

        Entry   "R1-R7"

        LDR     R4,[WP,#Calibration_ptr]
        CMP     R4,#0                   ;Is there a calibration table?
        TSTEQ   R0,#WriteCalToFileFlag_SaveAlways
        EXIT    EQ

      [ newcalibration
        LDR     LR,[WP,#Calibration_newtable]
        CMP     LR,#0                   ; new format table - if so then ignore the command
        EXIT    NE
      ]

        CMP     R4,#0                   ;Do I need to address the default table
        ADDR    R4,default_calibration,EQ

        LDR     R2,=?mapsizecommand
        ADR     R3,mapcommand           ;->String to be spat
        BL      output_string_r3        ;And then send it now
        LDRVC   R3,[R4]
        MOVVC   R5,R3
        BLVC    output_number_r3
        LDRVC   R3,[R4,#4]
        ADDVC   R5,R5,R3
        BLVC    output_number_r3
        LDRVC   R3,[R4,#8]
        ADDVC   R5,R5,R3
        BLVC    output_number_r3        ;Send the parameters for setting up the map
        EXIT    VS
05
        MOV     R0,#10
        SWI     XOS_BPut                ;Write the new line
        EXIT    VS

        MOV     R5,R5,LSL #2
        ADD     R5,R5,#8                ;Calculate the size of the calibration table

        LDR     R2,=?mapcommand
        ADR     R3,mapcommand
        BL      output_string_r3        ;Send the map command
        EXIT    VS

        MOV     R6,R5
        SUB     R5,R5,#4*16             ;16 items, not 4 as it used to be
10
        CMP     R6,#12                  ;Have we finished yet?
        BLT     %FT20

        CMP     R6,R5                   ;Finished a line of data, need to mush onto the next one
        BLE     %BT05

        LDR     R3,[R4,R6]
        BL      output_number_r3        ;Output numbers at R4+R6
        SUBVC   R6,R6,#4
        BVC     %BT10                   ;Loop back until finished

        EXIT
20
        MOV     R0,#10
        SWI     XOS_BPut                ;Write the final new line
        EXIT                            ;...and then exit

mapsizecommand  = "ColourTransMapSize"
mapcommand      = "ColourTransMap"
loadingcommand  = "ColourTransLoadings"

        ALIGN

;..............................................................................

; Write error loadings to a file as a single *command with three parameters.

; in    R1 handle
; out   all must be preserved, unless error (V set, R0 -> block)

WriteLoadingsToFile_Code ROUT

        Debug   input1,"WriteLoadingsToFile: R1",R1
        Debug   input2,"WriteLoadingsToFile: file handle",R1

        Entry   "R1-R5"

        MOV     R2,#(?loadingcommand)
        ADR     R3,loadingcommand
        BL      output_string_r3        ;Spit the *ColourTransLoadings
        LDRVC   R4,[WP,#ColourErrorLoading]
        MOVVC   R5,#3                   ;Spit three numbers out => R,G and B
10
        MOVVC   R4,R4,LSR #8
        ANDVC   R3,R4,#&FF              ;Get the loading value being applied
        BLVC    output_number_r3        ;and then spit the value out
        BVS     %FT20

        SUBS    R5,R5,#1
        BNE     %BT10

        MOV     R0,#10                  ;Terminate the line
        SWI     XOS_BPut                ;Write the end of line
20
        EXIT                            ;Return any possible errors (R0 and flags passed out)

;..............................................................................

ColourTransLoadings_Code ROUT

        Entry   "R1-R3"

        BL      validateworkspace
        EXIT    VS                      ;Attempt to resolve the workspace pointer and exit if fails

        MOV     R1,R0
        BL      getnumber               ;Attempt to read the red value from the command
        ANDVC   R2,R0,#&FF
        BLVC    getnumber               ;Attempt to read the green value from the command
        ANDVC   R3,R0,#&FF
        BLVC    getnumber               ;Attempt to read the blue value from the command
        ANDVC   R0,R0,#&FF
        EXIT    VS

        MOV     R1,R0,LSL #24           ;&BBxxxxxx
        ORR     R1,R1,R2,LSL #8         ;&BBxxRRxx
        ORR     R1,R1,R3,LSL #16        ;&BBGGRRxx

        MOV     R2,#0
        MOV     R0,#CTransMiscOp_SetWeights
        SWI     XColourTrans_MiscOp     ;Setup the error loading value correctly

        EXIT

;..............................................................................

; in    R1 handle
;       R3 number to output
; out   V set => R0 -> error block

output_number_r3 ROUT

        Push    "R1-R2,LR"

        MOV     R0,#" "                 ;Prefix ' &'
        SWI     XOS_BPut
        MOVVC   R0,#"&"
        SWIVC   XOS_BPut
        Pull    "R1-R2,PC",VS

        MOV     R0,R3
        ADD     R1,WP,#text_buffer12
        MOV     R2,#12
        SWI     XOS_ConvertHex8         ;Convert number to nice wibble hex value

        Pull    "R1-R2,LR"              ;Restore values from the stack

        ADD     R3,WP,#text_buffer12    ;Now strip the leading zeros
        MOV     R2,#8
10
        LDRB    R0,[R3]
        TEQ     R0,#48
        BNE     output_string_r3        ;When we reach a non-zero character then exit

        ADD     R3,R3,#1                ;Increase index into string

        SUB     R2,R2,#1                ;And don't ever remove the final zero
        CMP     R2,#1
        BGT     %BT10                   ;loop back until finished

; flow down into 'output_string_r3'

;..............................................................................

; in    R1 handle
;       R2 size
;       R3 ->string

output_string_r3 ROUT

        Entry   ""
10
        CMP     R2,#0                   ;End of the string reached yet (clears V)
        EXIT    EQ

        LDRB    R0,[R3],#1              ;Get a character from the string
        SWI     XOS_BPut                ;Write the character
        SUBVC   R2,R2,#1
        BVC     %BT10                   ;Loop back until all the blighters are written

        EXIT                            ;..return if it errors

;..............................................................................
;
; getnumber
;
; Extract a number from the current command line.
;
; in    R1 ->command line
; out   V =1 => R0 -> error block
;       else,   R0 contains number obtained
;               R1 updated to point to either next non-white space or termination character

getnumber ROUT

        Entry   "R2"

        MOV     R0,#10 :OR: (1:SHL:31)
        SWI     XOS_ReadUnsigned        ;Get the number from the line
        EXIT    VS

        MOV     R0,R2                   ;Copy number to suitable register
10
        LDRB    R2,[R1,#1]!
        CMP     R2,#32
        BEQ     %BT10

        EXIT

        END
