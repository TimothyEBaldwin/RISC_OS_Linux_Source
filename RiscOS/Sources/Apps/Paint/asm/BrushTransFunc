; Copyright 2021 RISC OS Open Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros

        AREA |C$$code|, CODE, READONLY

        EXPORT brush_transfunc

        ; ColourTrans Transfer Function for the Brush Tool
        ; Entry R0  = palette entry
        ;       R12 = workspace
        ; Exit  R0  = processed palette entry
brush_transfunc
        Push      "R1-R7"
        LDMIA     R12,{R1-R4}        ; Load our arguments
        CMP       R1,#2              ; 2 means the colour will be left unchanged
        Pull      "R1-R7",EQ
        MOVEQ     PC,R14             ; Exit the function

        CMP       R1,#1              ; 1 means set all colours from R2 (Shape feature)
        MOVEQ     R0,R2              ; Just output the supplied colour (and alpha)
        Pull      "R1-R7",EQ
        MOVEQ     PC,R14             ; Exit the function

        AND       R5,R0,#&FF00       ; Copy red to R5
        AND       R6,R0,#&FF0000     ; Copy green to R6
        AND       R7,R0,#&FF000000   ; Copy blue to R7
        ADD       R5,R2,R5,LSR #1    ; Blend 50% with red tint
        ADD       R6,R3,R6,LSR #1    ; Blend 50% with green tint
        ADD       R7,R4,R7,LSR #1    ; Blend 50% with blue tint
        AND       R0,R0,#&FF         ; Clear all except source alpha
        AND       R5,R5,#&FF00       ; Clear all except red
        AND       R6,R6,#&FF0000     ; Clear all except green
        AND       R7,R7,#&FF000000   ; Clear all except blue
        ADD       R0,R0,R5           ; Set final red in output
        ADD       R0,R0,R6           ; Set final green in output
        ADD       R0,R0,R7           ; Set final blue in output
        Pull      "R1-R7"
        MOV       PC,R14             ; Exit the function

        END
