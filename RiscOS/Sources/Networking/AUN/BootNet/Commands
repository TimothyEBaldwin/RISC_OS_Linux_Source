	SUBT	==> &.Bauble.BootNet.Commands

ModuleTitle
  ;	DCB	"BootNet", 0				; Now shared with the command table
  ;	ALIGN

UtilsCommands

	; Configuration and Status commands

	DCB	"BootNet", 0
	ALIGN
	DCD	ConfigureBootNet - Origin
;
; 950318 KWelton
;
; if minimum OS Version supported is less than 3.50, then
; we cannot us the international help support in the kernel
; 'cause it is not there.
;
  [	KernelInternationalisation
	InfoWord 1, 1, Status_Keyword_Flag :OR: International_Help
	DCD	SyntaxOfConfigureBootNet - Origin
	DCD	HelpForConfigureBootNet - Origin
  |
	InfoWord 1, 1, Status_Keyword_Flag :OR: Help_Is_Code_Flag
	DCD	SyntaxOfConfigureBootNet - Origin
	DCD	DoConfigureBootNetHelp - Origin
  ]

	DCD	0					; Terminate table

ConfigureBootNet ROUT
	Push	"r0-r4, lr"
	LDR	wp, [ r12 ]
	CMP	r0, #1
	BHI     DoConfigureBootNet			; Configure
	BEQ	StatusOfBootNet				; Print status
PrintSyntaxOfConfigureBootNet
  [ International_Help <> 0
	ADRL	r1, SyntaxOnlyOfConfigureBootNet
	BL	MessageTransLookupOnce
	MOV	r0, r2
	MOV	r1, r3
	SWI	XOS_WriteN
	B	PrintNewLineAndExit
  |
	ADRL	r0, SyntaxOnlyOfConfigureBootNet
	B	PrintMessageAndExit
  ]

StatusOfBootNet						; Print the status
	ADR	r0, Status_Common
	SWI	XOS_Write0
	BLVC	ReadConfiguration
	BVS	ExitConfigure
	ADREQ	r0, Status_BootNetOff
	ADRNE	r0, Status_BootNetOn
PrintMessageAndExit
	SWI	XOS_Write0
PrintNewLineAndExit
	SWIVC	XOS_NewLine
	B	ExitConfigure

  [ :LNOT: KernelInternationalisation
DoConfigureBootNetHelp
	STMFD	sp!, {lr}
	ADR	r0, HelpForConfigureBootNet
    [	International_Help <> 0
	BL	MessageTransLookupOnce
	MOV	r0, r2
	MOV	r1, r3
	SWI	XOS_WriteN
    |
	SWI	XOS_Write0
    ]
	SWI	XOS_NewLine
	MOV	r0, #0
        CLRV
	LDMFD	sp!, {pc}
  ]

DoConfigureBootNet
	LDRB	r1, [ r0 ], #1
	TEQ	r1, #"o"
	TEQNE	r1, #"O"
	BNE	SyntaxError
	LDRB	r1, [ r0 ], #1
	TEQ	r1, #"f"
	TEQNE	r1, #"F"
	BNE	TestForN
	LDRB	r1, [ r0 ], #1
	TEQ	r1, #"f"
	TEQNE	r1, #"F"
	BNE	SyntaxError
20
	LDRB	r1, [ r0 ], #1
	CMP	r1, #" "
	BEQ	%20					; Skip trailing spaces
	BGT	SyntaxError

	MOV	r0, #ReadCMOS
	MOV	r1, #SystemSpeedCMOS
	SWI	XOS_Byte
	BICVC	r2, r2, #1				; Reset the relevant bit
	MOVVC	r0, #WriteCMOS
	SWIVC	XOS_Byte
	B	ExitConfigure

TestForN
	TEQ	r1, #"n"
	TEQNE	r1, #"N"
	BNE	SyntaxError
40
	LDRB	r1, [ r0 ], #1
	CMP	r1, #" "
	BEQ	%40					; Skip trailing spaces
	BGT	SyntaxError

	MOV	r0, #ReadCMOS
	MOV	r1, #SystemSpeedCMOS
	SWI	XOS_Byte
	ORRVC	r2, r2, #1				; Set the relevant bit
	MOVVC	r0, #WriteCMOS
	SWIVC	XOS_Byte
ExitConfigure
	STRVS	r0, [ sp ]
	Pull	"r0-r4, pc"

SyntaxError
 [ International_Help <> 0
	ADRL	r0, SyntaxOfConfigureBootNetError
	BL	MessageTransErrorLookupOnce
	B	ExitConfigure
 |
	DEC	sp, (EndOfSyntaxOfConfigureBootNet - SyntaxOfConfigureBootNet + 8) :AND: &FFFFFFFC
	MOV	r0, sp
	MOV	r1, #ErrorNumber_Syntax
	STR	r1, [ r0 ], #4
	ADR	r14, SyntaxOfConfigureBootNet
ErrorCopyLoop
	LDRB	r1, [ r14 ], #1
	STRB	r1, [ r0 ], #1
	TEQ	r1, #0
	BNE	ErrorCopyLoop
	MOV	r0, sp
	SWI	XMessageTrans_CopyError
	INC	sp, (EndOfSyntaxOfConfigureBootNet - SyntaxOfConfigureBootNet + 8) :AND: &FFFFFFFC
	B	ExitConfigure
 ]

ReadConfiguration
	;	Returns the bit in R0 and Z is the result
	;	of TEQing with zero
	;	Bit 0 => Acorn Universal Network ROMBoot (0=>disabled, 1=>enabled)
	;	All other registers preserved
	Push	"r1, r2, lr"
	MOV	r0, #ReadCMOS
	MOV	r1, #SystemSpeedCMOS
	SWI	XOS_Byte
	ANDVCS	r0, r2, #1
	Pull	"r1, r2, pc"

	LTORG

	END
