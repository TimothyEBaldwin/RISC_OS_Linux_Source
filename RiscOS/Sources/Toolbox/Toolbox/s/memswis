; Copyright 1998 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; Title:   s.memswis
; Purpose: Assembler veneers onto OS_Heap for speed
; Author:  KJB
; History: 23-Nov-96: KJB: Created
;

;XOS_Heap	*	&2001D
;XOS_Module	*	&2001E

	AREA	MemSWIs,CODE,READONLY,PIC

	EXPORT	xosheap_initialise
	EXPORT	xosheap_alloc
	EXPORT	xosheap_free
	EXPORT	xosheap_realloc
	EXPORT	xosheap_resize
	EXPORT	xosheap_read_size
	EXPORT	xosmodule_realloc

        GET     hdr:ListOpts
        GET     hdr:Macros
        GET     hdr:System
        GET     hdr:Proc
        GET     hdr:ResourceFS
        GET     hdr:Wimp
        GET     hdr:Toolbox
        GET     hdr:Window
        GET     hdr:FSNumbers
        GET     hdr:NewErrors
        GET     hdr:Territory
        GET     hdr:Services

        MACRO
        MOVRet  $lr,$cond
        [ {CONFIG}=26
        MOV$cond.S    pc,$lr
        |
        MOV$cond      pc,$lr
        ]
        MEND

        MACRO
        LDMRet
        [ {CONFIG}=26
        LDMFD         sp!,{pc}^
        |
        LDMFD         sp!,{pc}
        ]
        MEND


xosheap_initialise
	MOV	R12,LR
	MOV	R3,R1
	MOV	R1,R0
	MOV	R0,#0
	SWI	XOS_Heap
	; MOVVC	R0,#0		; Not needed because R0 will be preserved
        MOVRet	R12

xosheap_alloc
	STMFD	R13!,{LR}
	MOV	R12,R2
	MOV	R3,R1
	MOV	R1,R0
	MOV	R0,#2
	SWI	XOS_Heap
	STRVC	R2,[R12]
	MOVVC	R0,#0
        LDMRet

xosheap_free
	MOV	R12,LR
	MOV	R2,R1
	MOV	R1,R0
	MOV	R0,#3
	SWI	XOS_Heap
	MOVVC	R0,#0
        MOVRet	R12

xosheap_realloc
	STMFD	R13!,{LR}
	MOV	R12,R3
	MOV	R3,R2
	MOV	R2,R1
	MOV	R1,R0
	MOV	R0,#4
	SWI	XOS_Heap
	STRVC	R2,[R12]
	MOVVC	R0,#0
        LDMRet

xosheap_resize
	MOV	R12,LR
	MOV	R3,R1
	MOV	R1,R0
	MOV	R0,#5
	SWI	XOS_Heap
	MOVVC	R0,#0
        MOVRet  R12

xosheap_read_size
	STMFD	R13!,{LR}
	MOV     R12,R2
	MOV	R2,R1
	MOV	R1,R0
	MOV	R0,#6
	SWI	XOS_Heap
	STRVC	R3,[R12]
	MOVVC	R0,#0
        LDMRet

xosmodule_realloc
	MOV	R12,LR
	MOV	R3,R1
	MOV	R1,R2
	MOV	R2,R0
	MOV	R0,#13
	SWI	XOS_Module
	STRVC	R2,[R1]
	MOVVC	R0,#0
        MOVRet  R12

	END
