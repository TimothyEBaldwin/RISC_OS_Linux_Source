; Copyright 1997 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
	AREA    |C$$code|, CODE, READONLY

	GET	hdr:ListOpts
	GET	hdr:Macros
        GET     Hdr:System
        GET     Hdr:ColourTran
        GET     Hdr:APCS.<APCS>

; Gets the graphics window
; Prototype:	BBox get_graphics_window(void);
; Equiv. to:	void get_graphics_window(&BBox box);
;
; On entry:	a1 = pointer to BBox (4 byte struct)
; On exit:	nowt
get_graphics_window
	EXPORT	get_graphics_window

        FunctionEntry "r4-r7", makeframe

	MOV	r7, a1

        SUB     sp, sp, #(end_of_variable_list - get_graphics_window_data)
	MOV	r1, sp
	ADR	r0, get_graphics_window_data
	SWI	XOS_ReadVduVariables

	LDMIA	r1, {r0, r2-r6}
	MOV	r0, r0, LSL r5
	MOV	r2, r2, LSL r6
	MOV	r3, r3, LSL r5
	MOV	r4, r4, LSL r6

	STMIA	r7, {r0, r2-r4}
        Return  "r4-r7", fpbased

get_graphics_window_data
	DCD	128		; xmin
	DCD	129		; ymin
	DCD	130		; xmax
	DCD	131		; ymax
	DCD	4		; x eig factor
	DCD	5		; y eig factor
end_of_variable_list
	DCD	-1

; set_graphics_window ------------------------------------------------------
; Sets the graphics window
;
; Prototype:	void set_graphics_window(BBox box);
; On entry:	a1 = xmin
;		a2 = ymin
;		a3 = xmax
;		a4 = ymax
set_graphics_window
	EXPORT	set_graphics_window

	mov	ip, lr

; Check xmin < xmax
	cmp	a1, a3
	eorgt	a1, a1, a3
	eorgt	a3, a1, a3
	eorgt	a1, a1, a3
; ...and ymin < ymax
	cmp	a2, a4
	eorgt	a2, a2, a4
	eorgt	a4, a2, a4
	eorgt	a2, a2, a4

; VDU 24 - set graphics window
	eor	a1, a1, a2, LSL #16
	eor	a3, a3, a4, LSL #16
	stmdb	sp!, {a1, a3}

	mov	lr, #24 << 24
        str     lr, [sp, #-4]!

	add	a1, sp, #3
	mov	a2, #9
	swi	OS_WriteN

	add	sp, sp, #12

        Return  ,LinkNotStacked,,ip

; set_graphics_window_in_window --------------------------------------------
; Sets the graphics window inside another window
;
; Prototype:	int set_graphics_window_in_window(BBox inside, BBox outside);
; On entry:	a1 = inside.xmin
;		a2 = inside.ymin
;		a3 = inside.xmax
;		a4 = inside.ymax
;		a5-a8 on stack
; On exit:	a1 = 1 if graphics window exists, 0 if zero sized.
set_graphics_window_in_window
	EXPORT	set_graphics_window_in_window

	mov	ip, sp
	stmfd	sp!, {v1-v4, lr}

	ldmia	ip, {v1-v4}

	cmp	a1, v3
	cmplt	a2, v4
	bge	zero_window

	cmp	a3, v1
	cmpgt	a4, v2
	ble	zero_window

	cmp	a1, v1
	eorlt	a1, a1, v1
	eorlt	v1, a1, v1
	eorlt	a1, a1, v1

	cmp	a3, v3
	eorgt	a3, a3, v3
	eorgt	v3, a3, v3
	eorgt	a3, a3, v3

	cmp	a2, v2
	eorlt	a2, a2, v2
	eorlt	v2, a2, v2
	eorlt	a2, a2, v2

	cmp	a4, v4
	eorgt	a4, a4, v4
	eorgt	v4, a4, v4
	eorgt	a4, a4, v4

	bl	set_graphics_window

	mov	a1, #1
        Return  "v1-v4"

zero_window
	mov	a1, #0
	mov	a2, #0
	mov	a3, #0
	mov	a4, #0
	bl	set_graphics_window
	mov	a1, #0
        Return  "v1-v4"

; os_plot ------------------------------------------------------------------
; Performs an OS_Plot SWI
;
; Prototype:	_kernel_oserror *os_plot(unsigned short command,
;					long x, long y)
; On entry:	a1 = command code
;		a2 = x
;		a3 = y
; On exit:	a1 = on error, pointer to error block, else NULL
os_plot
	EXPORT	os_plot

	MOV	ip, lr

	SWI	XOS_Plot
	MOVVC	a1, #0

        Return  ,LinkNotStacked,,ip

; colourtrans_set_gcol -----------------------------------------------------
; Performs a ColourTrans_SetGCOL SWI.
;
; Prototype:	_kernel_oserror *colourtrans_set_gcol(unsigned int colour,
;			unsigned int flags, unsigned int action);
;
; On entry:	r0 = colour
;		r1 = flags
; 		r2 = action
;
; On exit:	r0 = pointer to error block if error, else NULL
colourtrans_set_gcol
	EXPORT	colourtrans_set_gcol

        FunctionEntry "r4"

	MOV	r4, r2
	MOV	r3, r1
	SWI	XColourTrans_SetGCOL

        Return  "r4"

; colourtrans_set_font_colours ---------------------------------------------
; Performs a ColourTrans_SetFontColours SWI
;
; Prototype:	_kernel_oserror *colourtrans_set_font_colours(
;			unsigned int font_handle,
;			unsigned int background, unsigned int foreground,
;			unsigned int max_offset);
;
; On entry:	r0 = font handle
;		r1 = background colour
;		r2 = foreground colour
;		r3 = max offset
colourtrans_set_font_colours
	EXPORT	colourtrans_set_font_colours

	MOV	ip, lr
	SWI	XColourTrans_SetFontColours

	MOVVC	r0, #0

        Return  ,LinkNotStacked,,ip

	END
