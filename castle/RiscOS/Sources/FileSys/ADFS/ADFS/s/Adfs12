; This source code in this file is licensed to You by Castle Technology
; Limited ("Castle") and its licensors on contractual terms and conditions
; ("Licence") which entitle you freely to modify and/or to distribute this
; source code subject to Your compliance with the terms of the Licence.
; 
; This source code has been made available to You without any warranties
; whatsoever. Consequently, Your use, modification and distribution of this
; source code is entirely at Your own risk and neither Castle, its licensors
; nor any other person who has contributed to this source code shall be
; liable to You for any loss or damage which You may suffer as a result of
; Your use, modification or distribution of this source code.
; 
; Full details of Your rights and obligations are set out in the Licence.
; You should have received a copy of the Licence with this source code file.
; If You have not received a copy, the text of the Licence is available
; online at www.castle-technology.co.uk/riscosbaselicence.htm
; 
        SUBT    Hard disc driver common entry points etc. -> Adfs12

; Change record
; =============
;
; CDP - Christopher Partington, Cambridge Systems Design
; LVR - Lawrence Rust, Cambridge Beacon
;
; 25-Oct-90
; ADFS version 2.06
;
; 28-Nov-90         LVR
; File created. Winchester entry point code split from other source
; files to facilitate addition of IDE drivers.
;
; 30-Nov-90  18:00  CDP
; Minor changes to make existing drivers compatible with previous
; version of ADFS (conditionals removed by LR added back, comments
; corrected).
;
; 07-Jan-91  10:00  CDP
; IDE driver started.
;
; 11-Mar-91  15:48  CDP
; DoSwiWinControllerType now assumes that the drive number passed is >3.
; 82710 initialisation, commented out when drivers merged, now removed.
; Code using MachineID adjusted to use new values.
;
; 12-Mar-91  11:33  CDP
; IDE power state variables initialised. This will need to be changed
; when the initial power state is read from CMOS.
;
; 12-Mar-91  14:05  CDP
; Added WinIDEPoduleDying.
;
; 18-Mar-91  18:03  CDP
; Initialisation of WinIDECommandActive added.
;
; 20-Mar-91  13:01  CDP
; Added code to read power state from CMOS and setup a timer event
; to initialise drives when they become ready.
;
; 27-Mar-91  10:36  CDP
; Added initialisation of WinIDEPowerSetupActive so PowerSetup can tell
; when it has added a callback to remove itself and avoid doing it again.
;
; 08-Apr-91  17:22  CDP
; Internationalised error return from DoSwiWinControllerType.
; Now installs WinIDEPowerSetup even if there appears to be no IDE
; controller (but IDEdrives configured != 0). Podule will notify driver
; of its presence later and the power setup stuff must run then.
;
; 10-Jan-92  12:01  CDP
; The way FileCore mounts a disc has been changed and it no longer provides
; the defect list read from the disc by the mount entry when reading other
; bits of the disc when trying to understand the disc format. The IDE driver
; used this to access the drive init flag so that InitDriveParms could be
; issued before accessing other bits of the disc. The driver has now been
; changed to save the flag from the data read by the mount as FileCore is
; guaranteed to read the boot block by the call to the mount entry.
; WinLowLevel/WinMount changed to clear/set flag to indicate mount before
; calling WinLowLevelIDE. WinMount changed to extract init flag from data
; read from disc.
;
; 09-Mar-92  16:50  CDP
; Mount changed again as FileCore does two reads of the boot block before
; validating it and the second read was causing the driver to initialise
; the drive (only a problem with discs with a trashed boot block).
; Driver now validates the boot block itself in the Mount entry.
;
; 02-Apr-92  16:18  CDP
; WinInit now zeroes WinIDEDriveSecsPerTrk for each drive.
; Mount entry now saves drive parameters from (validated) boot block so that
; any op following reset of drive will reinit drive shape.
;
; 06-12-94 16:49 SBP
; WinMount altered so that init flag in debug output is shown correctly.
;
;*End of change record*

;*********************************************************************
;
; This file contains the following routines:
;
; WinInit
;    Initialise the driver.
;    Called when the module receives the initialization call.
;
; WinDie
;    Shutdown the driver.
;    Called when the module receives a finalisation call.
;
; WinReset
;    Called when the module receives a post reset service call.
;
; WinIDEPoduleDying
;    Called when the module receives an IDE podule dying service call.
;
; WinLowLevel
;    Called when the module receives a FileCore low-level call with a
;    drive number in the range 4 to 7.
;
; WinMount
;    Called when the module receives a FileCore miscellaneous call
;    with a reason code of 0 (R0 = 0, mount disc).
;
; WinUpdateCMOSMapSize
;    Updates the approximate map size of a disc, held in CMOS RAM.
;
; DoSwiWinControllerType
;    Returns the controller type of a hard disc.
;
;*********************************************************************

        GBLS    GetDebugIDE

 [ Debug20
GetDebugIDE     SETS    "GET     s.DebugIDE"
 |
GetDebugIDE     SETS    "; IDE debugging routines not required"
 ]

        $GetDebugIDE

;*********************************************************************

WinInit ROUT
;
; Initialize the driver.
; Called when the module receives the initialization call.
;
; Entry:
;    R4  = bit map from CMOS giving number of IDE and ST506 hard discs
;    R11 = -> Instantiation number, 0= first init
;    SB  = -> Local storage
;    LR  = Return address
;
; Exit:
;    VC => no error
;          R0 undefined
;          R1 = No. of responding drives
;          WinnieSizes = approx map sizes (1 byte per drive)
;    VS => error
;          R0 -> error pointer/code
;          R1 undefined
;    R2 undefined

        Push    "R3-R9,LR"              ; Save caller's regs

 [ Debug21
        DREG    R4,"WinInit: R4="
        LDR     R0,MachineID
        DREG    R0,"MachineID:"
 ]

; Initialise variables - this could be moved to the preinitialised
; data area to save space.

; Invalidate the IDE data transfer code sitting in RAM

        MOV     R0,#&FF
        STRB    R0,WinIDERAMOp

; Set IDE features to 0

        MOV     R0,#0
        STRB    R0,WinIDEPrecomp

; Indicate not currently in IDE IRQ
; R0 = 0

        STRB    R0,WinIDEIRQFlag

; Indicate no cmd currently running
; R0 = 0

        STR     R0,WinIDECommandActive

 [ IDEDMA
; Zero out our sector-padding data
        sbaddr  LR,WinIDEDMAZeroes
        MOV     R1,#?WinIDEDMAZeroes
01      STR     R0,[LR],#4
        SUBS    R1,R1,#4
        BNE     %BT01

        STR     R0,WinIDECopiedScatterList
        STR     R0,WinIDECopiedScatterSize
 ]

; Flag both IDE drives not initialised
; R0 = 0

        ASSERT  WinIDEDriveStateReset = 0
 [ TwinIDEHardware
        ASSERT  WinIDEMaxDrives = 4
 |
        ASSERT  WinIDEMaxDrives = 2
 ]
        STRB    R0,WinIDEDriveState+0
        STRB    R0,WinIDEDriveState+1
 [ TwinIDEHardware
        STRB    R0,WinIDEDriveState+2
        STRB    R0,WinIDEDriveState+3
 ]

; Zero SecPerTrk for each drive to indicate not valid
; R0 = 0

 [ TwinIDEHardware
        ASSERT  WinIDEMaxDrives = 4
 |
        ASSERT  WinIDEMaxDrives = 2
 ]
        STRB    R0,WinIDEDriveSecsPerTrk+0
        STRB    R0,WinIDEDriveSecsPerTrk+1
 [ TwinIDEHardware
        STRB    R0,WinIDEDriveSecsPerTrk+2
        STRB    R0,WinIDEDriveSecsPerTrk+3
 ]

; Initialise IDE hardware flag
; R0 = 0

        STRB    R0,WinIDEHardware+WinIDEIRQDevNo       ; no hardware yet
 [ TwinIDEHardware
        STRB    R0,WinIDEHardware+SzWinIDEHardware+WinIDEIRQDevNo
 ]

; Split configured IDE and ST506 drives from passed parameter


        AND     R0,R4,#2_00000111       ; mask to get ST506 drives
        STRB    R0,Winnies              ; save ST506 drives
 [ :LNOT:AutoDetectIDE
        MOV     R4,R4,LSR #3            ; shift to get IDE drives
        STRB    R4,WinIDEDrives

; See if there's any IDE to setup

        TEQS    R4,#0                   ; any?
        BEQ     %FT20                   ; branch if not (R4=0)
 ]

; Some configured IDE drives
; Claim TickerV to do timers

        BL      WinClaimTickerV         ; (->R0,V)
        BVS     %FT80                   ; branch if error

 [ HAL
      [ IDEDMA
        SUB     SP,SP,#4*4
        MOV     R3,#4*4
        MOV     R2,SP
        MOV     R1,#-1
        LDR     R0,HAL_IDEDevice_pointer
        Push    "R12"
        MOV     LR,PC
        LDR     PC,[R0,#HALDevice_IDEControllerInfo]
        Pull    "R12"
        MOV     R6,#0
        CMPVC   R0,#4*4
        SETV    LO
        ADD     SP,SP,#4
        Pull    "R0-R2"
        BLVC    CreateBusMaster
        STR     R6,WinIDEDMADeviceHandle

        SUB     SP,SP,#12*4
        MOV     R3,#12*4
      |
        SUB     SP,SP,#8*4
        MOV     R3,#8*4
      ]
        MOV     R2,SP
        MOV     R1,#0                   ; bus 0
        LDR     R0,HAL_IDEDevice_pointer
        Push    "R12"
        MOV     LR,PC
        LDR     PC,[R0,#HALDevice_IDEControllerInfo]
        Pull    "R12"
        Pull    "R2-R8,R14"
        BVS     %FT05
        CMP     R0,#8*4
        BLO     %FT05

        ADR     R9,WinIDEHardware
        STR     R2,[R9,#WinIDEHWFlags]
        STR     R3,[R9,#WinIDEPtr]
        STR     R4,[R9,#WinIDECtrlPtr]
        STRB    R6,[R9,#WinIDEHWDevNo]

        TST     R2,#WinIDEHWFlag_32bitPIO
        ADDR    R7,WinIDEReadASector,EQ
        ADDR    R7,WinIDEReadASector32,NE
        STR     R7,[R9,#WinIDEReadPtr]
        ADDR    R7,WinIDEWriteASector,EQ
        ADDR    R7,WinIDEWriteASector32,NE
        STR     R7,[R9,#WinIDEWritePtr]

        MOV     R14,#WinIDEHW_HAL
        STRB    R14,[R9,#WinIDEHWType]
        MOV     R14,#0
        STRB    R14,[R9,#WinIDEBusNo]

      [ IDEDMA
        MOV     R1,R0
      ]
        MOV     R0,#0
        BL      WinIDESetDefaultTimings

        TEQ     R3,#0
        BLNE    WinClaimIDEIRQs
        BVS     %FT80

      [ IDEDMA
        Push    "R1-R5"
        ADDR    R0, WinIDEDMAEnable
        ADDR    R1, WinIDEDMADisable
        ADDR    R2, WinIDEDMAStart
        ADDR    R3, WinIDEDMACompleted
        ADDR    R4, WinIDEDMASync
        ADR     R5, WinIDEDMAHandlers
        STMIA   R5, {R0-R4}
        Pull    "R1-R5"
      ]

        MOV     R0,#0                   ; -SRST IEN
        STRB    R0,[R4,#:INDEX:IDERegDevCtrl]

      [ IDEDMA
        CMP     R1,#12*4
        LDRHS   R0,[SP,#3*4]
        CMPHS   R0,#-1
        MOVEQ   R0,#1
        SWIEQ   XDMA_AllocateLogicalChannels
        MOV     R3,R0
        STR     R3,[R9,#WinIDEDMALC]
        Pull    "R0-R2"                 ; R5 already set up
        ADD     SP,SP,#4
        LDR     R6,WinIDEDMADeviceHandle
        CMP     R1,#12*4
        BLHS    AddBusMasterChannel
      ]

 [ TwinIDEHardware
      [ IDEDMA
        SUB     SP,SP,#12*4
        MOV     R3,#12*4
      |
        SUB     SP,SP,#8*4
        MOV     R3,#8*4
      ]
        MOV     R2,SP
        MOV     R1,#1                   ; bus 1
        LDR     R0,HAL_IDEDevice_pointer
        Push    "R12"
        MOV     LR,PC
        LDR     PC,[R0,#HALDevice_IDEControllerInfo]
        Pull    "R12"
        Pull    "R2-R8,R14"
        BVS     %FT05
        CMP     R0,#32
        BLO     %FT05

        ADR     R9,WinIDEHardware + SzWinIDEHardware
        STR     R2,[R9,#WinIDEHWFlags]
        STR     R3,[R9,#WinIDEPtr]
        STR     R4,[R9,#WinIDECtrlPtr]
        STRB    R6,[R9,#WinIDEHWDevNo]

        TST     R2,#WinIDEHWFlag_32bitPIO
        ADDR    R7,WinIDEReadASector,EQ
        ADDR    R7,WinIDEReadASector32,NE
        STR     R7,[R9,#WinIDEReadPtr]
        ADDR    R7,WinIDEWriteASector,EQ
        ADDR    R7,WinIDEWriteASector32,NE
        STR     R7,[R9,#WinIDEWritePtr]

        MOV     R14,#WinIDEHW_HAL
        STRB    R14,[R9,#WinIDEHWType]
        MOV     R14,#1
        STRB    R14,[R9,#WinIDEBusNo]

      [ IDEDMA
        MOV     R1,R0
      ]
        MOV     R0,#2
        BL      WinIDESetDefaultTimings

        TEQ     R3,#0
        BLNE    WinClaimIDEIRQs
        BVS     %FT80

        MOV     R0,#0                   ; -SRST IEN
        STRB    R0,[R4,#:INDEX:IDERegDevCtrl]

      [ IDEDMA
        CMP     R1,#12*4
        LDRHS   R0,[SP,#3*4]
        CMPHS   R0,#-1
        MOVEQ   R0,#1
        SWIEQ   XDMA_AllocateLogicalChannels
        MOV     R3,R0
        STR     R3,[R9,#WinIDEDMALC]
        Pull    "R0-R2"                 ; R5 already set up
        ADD     SP,SP,#4
        LDR     R6,WinIDEDMADeviceHandle
        CMP     R1,#12*4
        BLHS    AddBusMasterChannel
      ]
 ]
      [ IDEDMA
        TEQ     R6,#0
        BLNE    RegisterBusMaster

        ADR     R9,WinIDEHardware
        BL      WinIDERegisterDMAChannel
       [ TwinIDEHardware
        ADR     R9,WinIDEHardware + SzWinIDEHardware
        BL      WinIDERegisterDMAChannel
       ]
      ]

 [ :LNOT:AutoDetectIDE
        LDRB    R4,WinIDEDrives         ; needed later
        B       %FT15
 ]

05
 ]

 [ ByteAddressedHW
        B       %FT15                   ; no alternative to HAL call (yet?)
 |
; Set up regs to point to default (motherboard) IDE hardware

 [ HAL
        sbaddr  R9,WinIDEHardware
        ADD     R1,R9,#WinIDEPtr
 |
        baddr   R1,DefaultIDEHardware
 ]
        LDMIA   R1,{R2-R6}
        MOV     R7,#0                   ; initialise data in ptr
        MOV     R8,#0                   ; and data out ptr

; Issue a service call to let an IDE podule change the registers to
; point to its hardware

        LDR     R1,=Service_ADFSPoduleIDE
        SWI     XOS_ServiceCall

 [ Debug21
        DREG    R1,"Service_ADFSPoduleIDE:R1:"
 ]

; If IDE podule present, regs will have been changed to point to
; podule hardware info
; Sort out the pointers to the data transfer routines

        TEQS    R7,#0                   ; default data in routine?
        ADDR    R7,WinIDEReadASector,EQ ; if yes, set it up
        TEQS    R8,#0                   ; default data out routine?
        ADDR    R8,WinIDEWriteASector,EQ ; if yes, set it up

 [ HAL
        TEQ     R1,#Service_Serviced
        BNE     %FT15                   ; no IDE podule present

        ADD     R14,R9,#WinIDEPtr
 |
        LDR     R14, =DefaultIDE
 ]
        TEQ     R2, R14
 [ HAL
        MOVEQ   R14, #IOMD_IDE_DevNo
 [ Override_PDevNo <> -1
        MOVNE   R14, #Override_PDevNo
 |
        MOVNE   R14, #Podule_DevNo
 ]
        STRB    R14, WinIDEHardware+WinIDEHWDevNo
 ]
        MOVEQ   R14, #WinIDEHW_Default
        MOVNE   R14, #WinIDEHW_Podule

; Save info

      [ HAL
        ASSERT  WinIDEPtr = 4
      |
        ASSERT  WinIDEPtr = 0
      ]
        ASSERT  WinIDEPollPtr = WinIDEPtr + 4
        ASSERT  WinIDEPollBits = WinIDEPollPtr + 4
        ASSERT  WinIDEIRQPtr = WinIDEPollBits + 4
        ASSERT  WinIDEIRQBits = WinIDEIRQPtr + 4
        ASSERT  WinIDEReadPtr = WinIDEIRQBits + 4
        ASSERT  WinIDEWritePtr = WinIDEReadPtr + 4

        sbaddr  R9,WinIDEHardware
      [ HAL
        STMIB   R9,{R2-R8}
      |
        STMIA   R9,{R2-R8}
      ]
        STRB    R14,WinIDEHardware+WinIDEHWType

 [ HAL
        ADD     R14,R2,#IDERegCtlDefaultOffset
        STR     R14,WinIDEHardware+WinIDECtrlPtr
 ]

; Check whether there really is IDE hardware to talk to (yet - podule
; may tell us about it later).
; Set R4 = configured drives as, even if we find no hardware, podule may
; tell us later about it and we must have allowed for it

        LDRB    R4,WinIDEDrives         ; needed later

; IF service call claimed OR 710-based machine THEN IDE present

        TEQS    R1,#Service_Serviced    ; service call claimed? EQ => claimed
        LDRNE   R1,MachineID            ; not claimed, IDE on motherboard?
        TEQNES  R1,#MachHas82710        ; EQ => yes
        BNE     %FT15                   ; if no IDE yet, branch
                                        ; (assume podule will appear later)
 ]

10
; Some IDE hardware present - claim the device vector

        BL      WinClaimIDEIRQs         ; (R9->R0,V)
        BVS     %FT80                   ; branch if error

; Enable interrupts in the controller.
; This is ok as it is not until the IRQ is enabled in IOC or on the
; podule that it will actually be able to interrupt.

 [ HAL
        LDR     R2,WinIDEHardware+WinIDECtrlPtr
 ]
        MOV     R0,#0                   ; -SRST IEN
        STRB    R0,[R2,#:INDEX:IDERegDevCtrl]

15
; Read CMOS spin down delay and save

        MOV     R0,#OsByte_ReadCMOS
        MOV     R1,#ADFSSpinDownCMOS
        SWI     XOS_Byte                ; (R0,R1->R2,V)
        MOVVS   R2,#0                   ; if error, spindown = 0
 [ TwinIDEHardware
        ASSERT  WinIDEMaxDrives = 4
 |
        ASSERT  WinIDEMaxDrives = 2
 ]
        STRB    R2,WinIDEPowerState+0
        STRB    R2,WinIDEPowerState+1
 [ TwinIDEHardware
        STRB    R2,WinIDEPowerState+2
        STRB    R2,WinIDEPowerState+3
 ]

; If the power state indicates that the drives should have
; autospindown enabled, install handler to do this when they become
; ready.
; R2 = spindown parameter, 0 => no spindown

        STRB    R2,WinIDEPowerSetupActive ; so can tell when to remove it
                                        ; !0 => not removed
        TEQS    R2,#0                   ; need to add CallEvery?
        MOVNE   R0,#100                 ; if so, R0 = centisecond count
        ADDR    R1,WinIDEPowerSetup,NE  ; ...R1 -> handler
        MOVNE   R2,SB                   ; ...R2 = R12 value
        SWINE   XOS_CallEvery           ; ...(R0-R2->V) ignore error

 [ IDEPower
; Iff on a portable - set up a handler to examine the Winnie, on finding it spun down,
;                     the handler will power down the drive and interface.
;
        SWI     XPortable_ReadFeatures  ;returns flags in R1
        BVS     %FT20                   ;VS: not a portable, so skip handler setup code

        MOV     R0,#1000                ; R0 = centisecond count (10 seconds)
        ADDR    R1,WinIDEPowerShutdown  ; ...R1 -> handler
        MOV     R2,SB                   ; ...R2 = R12 value
        SWI     XOS_CallEvery           ; ...(R0-R2->V) ignore error
 ]

20
 [ AutoDetectIDE
        BL      ProbeIDEDevices
        STRB    R4, WinIDEDrives
 ]
; R4 = number of IDE drives present

; Set up the drive map to indicate which driver should be called
; for each logical drive

        LDR     R1,MachineID
        LDRB    R2,Winnies              ; ST506 drives

; R1=0 => ST506-based machine
; R2 = ST506 drives
; R4 = IDE drives

        TEQS    R1,#MachHas82710        ; 710 based machine?

        MOVEQ   R5,#0                   ; yes, IDE starts at 0...
        MOVEQ   R3,R4                   ; ...ST506 after

        MOVNE   R3,#0                   ; no, ST506 starts at 0...
        MOVNE   R5,R2                   ; ...IDE after

; R2 = ST506 drives
; R3 = logical drive number start for ST506
; R4 = IDE drives
; R5 = logical drive number start for IDE

        ADR     R1,WinDriveTypes
        MVN     R0,#0                   ; R0 = &FFFFFFFF
        STR     R0,[R1]

        ADD     R3,R3,R1
        ADD     R5,R5,R1

; R3 -> where ST506 drives should go
; R5 -> where IDE drives should go

; Do ST506 drives
; These are stored with &08 ORred into them so that, when shifted to
; bits 31..29 as for a FileCore op, carry is set to indicate ST506.

        TEQS    R2,#0                   ; any ST506 drives?
        MOVNE   R0,#8                   ; yes, store &08 for first...
        STRNEB  R0,[R3,#0]

        TEQS    R2,#2                   ; 2 drives?
        MOVEQ   R0,#9                   ; yes, store &09 for second
        STREQB  R0,[R3,#1]

; do IDE drives

 [ AutoDetectIDE
        MOV     LR,#0
        sbaddr  R3,WinIDEDeviceMappings
30      LDRB    R0,[R3,LR]
 [ Debug23
        DREG    LR, "Physical drive ",cc,Integer
        BREG    R0, " has class "
 ]
        TEQ     R0,#0
        BNE     %FT35
        SUB     R0,R5,R1
        ADD     R0,R0,#4
        STRB    R0,[R3,LR]
        STRB    LR,[R5],#1
 [ Debug23
        DREG    R0, "Logical drive ",cc,Integer
        DREG    LR, " is IDE physical drive ",,Integer
 ]
35
        ADD     LR,LR,#1
        CMP     LR,#WinIDEMaxDrives
        BLO     %BT30
 |
        CMPS    R4,#1                   ; any IDE drives?
        MOVHS   R0,#0                   ; yes, store &00 for first
        STRHSB  R0,[R5,#0]

        MOVHI   R0,#1                   ; 2 drives?
        STRHSB  R0,[R5,#1]              ; yes, store &01 for second

 [ TwinIDEHardware
        CMPS    R4,#3                   ; 3 drives?
        MOVHS   R0,#2                   ; yes, store &02 for third
        STRHSB  R0,[R5,#2]

        MOVHI   R0,#3                   ; 4 drives?
        STRHSB  R0,[R5,#3]              ; yes, store &03 for fourth
 ]
 ]

; save total drives

        ADD     R4,R4,R2                ; R4 = ST506 + IDE drives

; Read approx map sizes from CMOS and save them
; ***** contrary to comments here and elsewhere,
; ***** the next call NEVER returns error

 [ :LNOT:BigDisc
        BL      ReadWinnieSizeCMOS      ;(->R0,R6,V)
        STR     R6,WinnieSizes          ; Save approx map size
 ]

        MOV     R1,R4                   ; Return no. of drives from CMOS
        sbaddr  LR,WinIDEDriveLBAFlags  ;
        MOV     R3,#0                   ; clear LBA usage flags
        STRB    R3,[LR]
        STRB    R3,[LR,#1]

 [ AutoDetectIDE
        MOV     R0, #0
        BL      WinIDESetTimings
  [ TwinIDEHardware
        MOV     R0, #2
        BL      WinIDESetTimings
  ]
 ]

        CLRV
        Pull    "R3-R9,PC"              ; Return to caller with no error

;****** Never fall through

80
; Error initialising IDE

        Push    "R0"                    ; save error pointer
        BL      WinDie                  ; release vectors
        SETV
        Pull    "R0,R3-R9,PC"           ; return error

        LTORG

;*********************************************************************

WinDie  ROUT
;
; Shutdown the driver.
; Called when the module receives a finalisation call.
;
; Entry:
;    R11 = -> Instantiation number
;    SB  = -> Local storage
;    LR  = Return address
;
; Exit:
;    All registers preserved
;    Flags undefined

        Push    "R0,R9,LR"

 [ Debug21

        DLINE   "WinDie"
 ]

 [ :LNOT: AutoDetectIDE
; check if any IDE drives and ignore rest if none

        LDRB    LR,WinIDEDrives         ; any IDE drives?
        TEQS    LR,#0
        Pull    "R0,R9,PC",EQ           ; return if not
 ]

; release the device vector for the IDE driver (if owned)

        sbaddr  R9,WinIDEHardware
        BL      WinReleaseIDEIRQs       ; (R9->R0,V)
 [ IDEDMA
        BL      WinIDEDeregisterDMAChannel
 ]
 [ TwinIDEHardware
        ADD     R9,R9,#SzWinIDEHardware
        BL      WinReleaseIDEIRQs       ; (R9->R0,V)
 [ IDEDMA
        BL      WinIDEDeregisterDMAChannel
 ]
 ]

 [ IDEDMA
        Push    "R2,R6"

; deregister DMA devices (if registered)
        LDR     R6,WinIDEDMADeviceHandle
        TEQ     R6,#0
        BLNE    DeleteBusMaster

; free up our copied scatter list
        MOV     R0,#ModHandReason_Free
        LDR     R2,WinIDECopiedScatterList
        TEQ     R2,#0
        SWINE   XOS_Module

        Pull    "R2,R6"
 ]

; release TickerV (if owned)

        BL      WinReleaseTickerV       ; (->R0,V)

; remove the power setup routine (it may not be installed)

        BL      WinIDEKillPowerSetup    ; all regs/flags preserved
 [ IDEPower
        BL      WinIDEKillPowerShutdown ; all regs/flags preserved
 ]
        Pull    "R0,R9,PC"

;*********************************************************************

WinReset        ROUT
;
; Called when the module receives a post reset service call.
;
; Entry:
;    R11 = -> Instantiation number
;    SB  = -> static workspace
;    LR  = Return address
;
; Exit:
;    All registers preserved
;    Flags undefined

        Push    "R0,R9,LR"              ; Save caller's regs

 [ Debug21

        DLINE   "WinReset"
 ]

        MOV     LR,#&FF                 ; HDC has been reset
        STRB    LR,SpecifiedWinnie

10
 [ :LNOT: AutoDetectIDE
; Check if IDE drives configured

        LDRB    LR,WinIDEDrives
        TEQS    LR,#0
        Pull    "R0,R9,PC",EQ
 ]

; IDE drives configured

; Indicate not currently in IDE IRQ

        MOV     R0,#0
        STRB    R0,WinIDEIRQFlag

; Flag drives as not initialised as, on a soft reset, the init code will
; not have been called but the drives will still have been reset
; R0 = 0

        ASSERT  WinIDEDriveStateReset = 0
 [ TwinIDEHardware
        ASSERT  WinIDEMaxDrives = 4
 |
        ASSERT  WinIDEMaxDrives = 2
 ]
        STRB    R0,WinIDEDriveState+0
        STRB    R0,WinIDEDriveState+1
 [ TwinIDEHardware
        STRB    R0,WinIDEDriveState+2
        STRB    R0,WinIDEDriveState+3
 ]

; Release and reclaim Tickerv

        BL      WinReleaseTickerV       ; (->R0,V)
        BL      WinClaimTickerV         ; (->R0,V)

; If we have a device, release and reclaim it

        sbaddr  R9,WinIDEHardware
        LDRB    R0,[R9,#WinIDEIRQDevNo]
        TEQS    R0,#0                   ; got device?
        BEQ     %FT30

; Got a device so release and reclaim it

        BL      WinReleaseIDEIRQs       ; (R9->R0,V)
        BL      WinClaimIDEIRQs         ; (R9->R0,V)

 [ IDEDMA
        BL      WinIDEDeregisterDMAChannel
        BL      WinIDERegisterDMAChannel
 ]

30
 [ TwinIDEHardware
        ADD     R9,R9,#SzWinIDEHardware
        LDRB    R0,[R9,#WinIDEIRQDevNo]
        TEQS    R0,#0                   ; got device?
        Pull    "R0,R9,PC",EQ

; Got a device so release and reclaim it

        BL      WinReleaseIDEIRQs       ; (R9->R0,V)
        BL      WinClaimIDEIRQs         ; (R9->R0,V)

 [ IDEDMA
        BL      WinIDEDeregisterDMAChannel
        BL      WinIDERegisterDMAChannel
 ]
 ]

        Pull    "R0,R9,PC"              ; Return to caller

;*********************************************************************

WinIDEPoduleDying       ROUT
;
; Called when the module receives an IDE podule dying service call.
;
; Entry:
;    R11 = -> Instantiation number
;    SB  = -> static workspace
;    LR  = Return address
;
; Exit:
;    All registers and flags preserved

        Push    "R0,R9,LR"

; Ignore all this if no drives configured

        LDRB    R0,WinIDEDrives
        TEQS    R0,#0
        Pull    "R0,R9,PC",EQ

; Some IDE drives configured
; If currently using a device, release its IRQ

        sbaddr  R9,WinIDEHardware
        BL      WinReleaseIDEIRQs       ; (->R0,V)

; If release failed, must still stop hardware being used so
; set device number to 0

        MOVVS   R0,#0
        STRVSB  R0,[R9,#WinIDEIRQDevNo]

        Pull    "R0,R9,PC"

;*********************************************************************

WinLowLevel  ROUT
;
; Called when the module receives a FileCore low-level call with a
; drive number in the range 4 to 7.
;
; Entry:
;    R1 = b0-3: reason code
;            0= verify, 1= read, 2= write sectors
;            3= verify track, 4= format track,
;            5= Seek, 6= Restore, 7= Step in, 8= Step out,
;            15= specify
;         b4 = Alternate defect map
;         b5 = R3 -> scatter list
;         b6 = Ignore escape
;         b7 = No ready timeout
;         b8 = Background op
 [ BigDisc
;    R2 = sector disc address (sector/track aligned),
 |
;    R2 = byte disc address (sector/track aligned),
 ]
;         top 3 bits = drive (4..7, not 0..3)
;    R3 -> transfer buffer
;    R4 = length in bytes
;    R5 -> disc record
;    R6 -> defect list
;    R12 = SB
;
; Exit:
;    VS => error
;          R0 = error pointer/code
;    VC => no error
;          R0 = 0 (definition)
;    R2 = disc address of next byte to transfer
;    R3 -> Next buffer address
;    R4 = Number of bytes left in buffer


        Push    "R5-R9,LR"                      ; Save caller's regs


 [ Debug21
        DREG    R0,"WinLowLevel: R0-R6: ",cc
        DREG    R1,,cc
        DREG    R2,,cc
        DREG    R3,,cc
        DREG    R4,,cc
        DREG    R5,,cc
        DREG    R6
 ]

 [ :LNOT: BigDisc
; Check that map size held in CMOS is up-to-date and correct it if not

        LDRB    R9,[R5,#Zones]          ; get number of zones on disc
        LDRB    R8,[R5,#SectorSize]     ; and sector size (log2)
        MOV     R8,R9,LSL R8            ; get number of bits for zone (number of zones x size of sector)
        MOVS    R8,R8,LSR #8            ; make into number of bytes(?)
        MOVEQ   R8,#2                   ; if zero, then make 2.
        sbaddr  R7,WinnieSizes-4        ;
        LDRB    LR,[R7,R2,LSR #(32-3)]  ;
        TEQS    LR,R8                   ; if not same as old cmos value...
        BLNE    WinUpdateCMOSMapSize    ; ...go update it
 ]

; Determine which driver (ST506/IDE) should handle this
; and the physical drive number

        MOV     R0,R2,LSR #(32-3)               ; R0 = drive 4..7
        STRB    R0,WinLogicalDrive              ; save it

        ADD     R0,R0,#:INDEX:WinDriveTypes - 4
        LDRB    R0,[SB,R0]                      ; get code for this drive
        BIC     R2,R2,#DiscBits                 ; remove disc bits
        ORRS    R2,R2,R0,LSL #(32-3)            ; put in drive number
                                                ; will set carry if ST506

; Call the driver C=1 => ST506
; The disc address passed to the driver includes a physical drive number,
; not the logical drive number passed by FileCore. The drivers return
; with the LOGICAL drive number substituted back in. That substitution
; is done in the driver rather than here because other things e.g. process
; error may also need to include it and that is best done in the
; driver.

        BL      WinLowLevelIDE

;~~~~~~~~~~~~~~~

10

; Calls to drivers return here
; Don't need to put logical drive number back in disc address as
; driver will have done so.

 [ Debug21
        DREG    R0,"LowLevel end R0-R4: ",cc
        DREG    R1,,cc
        DREG    R2,,cc
        DREG    R3,,cc
        DREG    R4
 ]
        Pull    "R5-R9,PC"

;*********************************************************************

WinMount        ROUT
;
; Called when the module receives a FileCore miscellaneous call
; with a reason code of 0 (R0 = 0, mount disc).
;
; Entry:
;    R0 = 0
;    R1 = drive (4..7)
;    R2 = disc byte address to read (sector aligned)
;    R3 -> transfer buffer (word aligned)
;    R4 = length in bytes
;   (R5 -> disc record to fill in: floppies only)
;    R12 = SB
;    LR = Return address
;
; Exit:
;    VS => error
;          R0 = error pointer/code
;    VC => no error
;          R0 undefined
;    All other registers preserved

 [ Debug21
        DREG    R1,"WinMount R1-R5: ",cc
        DREG    R2,,cc
        DREG    R3,,cc
        DREG    R4,,cc
        DREG    R5
 ]

        Push    "R0-R9,LR"              ; save caller's regs

; Save logical drive number for driver's use

        STRB    R1,WinLogicalDrive      ; 4..7

; determine which driver to call

        ADD     R1,R1,#:INDEX:WinDriveTypes - 4
        LDRB    R0,[SB,R1]              ; get code for this drive
        BIC     R2,R2,#DiscBits         ; remove disc bits
 [ BigDisc
        MOV     R2,R2,LSR #9
 ]
        ORRS    R2,R2,R0,LSL #(32-3)    ; put in drive number
                                        ; will set carry if ST506

; Call the driver: C=1 => ST506
; The disc address passed to the driver includes a physical drive number,
; not the logical drive number passed by FileCore. The drivers return
; with the LOGICAL drive number substituted back in. That substitution
; is done in the driver rather than here because other things e.g. process
; error may also need to include it and that is best done in the
; driver.

        MOV     R1,#DiscOp_ReadSecs :OR: DiscOp_Op_IgnoreEscape_Flag ; read sectors
        MOV     R6,#NIL                 ; no defect list yet

        MOVCS   LR,#&FF                 ; if ST506, say no disc specified
        STRCSB  LR,SpecifiedWinnie

        baddr   R5,WinnieTestDiscRec    ; R5 -> test disc record

; IDE: we need to get the InitDriveParms flag from the boot block on
; completion of the read

        Push    "R2-R3"                 ; save dataptr and disc addr
        BL      WinLowLevelIDE          ; call IDE driver
        Pull    "R2-R3"                 ; restore dataptr and disc addr

 [ Debug21

        DLINE   "Boot block read ",cc
        BVS     %F01
        DLINE   "OK"
        B       %F02
01
        DLINE   "FAIL"
02
 ]

 [ BigDisc:LAND::LNOT:NewErrors
        BVS     %FT82                   ; branch if read failed
 |
        BVS     %FT80                   ; branch if read failed
 ]

; IDE: read of boot block succeeded
; Check it and, if it's valid, init drive if flag says we should
; (can use almost any regs now as they get restored on exit)
;
; First check defect list
; R3 -> boot block read from disc

 [ BigDisc
        MOV     LR,R2,LSL#9             ; turn back into a byte offet (knocks out DiscBits too)
 |
        BIC     LR,R2,#DiscBits
 ]
        RSB     R5,LR,#DefectListDiscAdd
        ADD     R3,R3,R5                ; Offset from dataptr to boot block
        MOV     R5,R3                   ; R5 -> boot block
        MOV     R1,#0                   ; zero checksum
        MOV     R4,#SzDefectList - MaxStruc
10
        LDR     R0,[R5],#4              ; get defect
        TSTS    R0,#DiscBits            ; valid defect?
        BNE     %FT11                   ; branch if not
        EOR     R1,R0,R1,ROR #13        ; work into checksum
        SUBS    R4,R4,#4                ; make sure don't run off end
        BGT     %BT10

; Hit end of defect list and not found terminator

 [ Debug21

        DLINE   "Defect list missing terminator"
 ]
        B       %FT85                   ; error

;~~~~~~~~~~~~~~~~
11
        EOR     R1,R1,R1,LSR #16
        EOR     R1,R1,R1,LSR #8

; R1 = calculated checksum, R0 = checksum from disc

        EOR     R1,R1,R0
        TSTS    R1,#&FF                 ; checksum is LSB only

 [ Debug21

        DLINE   "Defect list ",cc
        BNE     %F01
        DLINE   "VALID"
        B       %F02
01
        DLINE   "BAD"
02
 ]

        BNE     %FT85                   ; branch if bad checksum

; Defect list OK
; Check checksum of boot block
; R3 -> boot block

        MOV     R1,#SzDefectList        ; R1 = size of boot block
        ADDS    R5,R3,R1                ; R5 -> end + 1, C=0
        SUB     R1,R5,#1                ; R1 -> check byte
        MOV     R6,#0                   ; sum
        B       %FT30

;~~~~~~~~~~~~~~~

20
        LDRB    R4,[R1,#-1]!            ; get next byte
        ADC     R6,R6,R4                ; add into checksum
        MOVS    R6,R6,LSL #24           ; bit 8 into carry
        MOV     R6,R6,LSR #24           ; restore R6
30
        TEQS    R1,R3                   ; finished?
        BNE     %BT20                   ; branch if not
        LDRB    R4,[R5,#-1]             ; get stored checksum
        TEQS    R6,R4                   ; matches?

 [ Debug21
        DREG    R5,"R5="
        BREG    R4,"Stored="
        BREG    R6,"Calced="
        DLINE   "Boot block checksum ",cc
        BNE     %F01
        DLINE   "VALID"
        B       %F02
01
        DLINE   "BAD"
02
 ]

        BNE     %FT85                   ; branch if boot block invalid

; Boot block VALID, save parameters
; R3 -> boot block
; R2 still holds disc address from above

        MOV     R1,R2,LSR#(32-3)                ; R1 = drive number

        LDRB    R0,[R3,#SzDefectList - MaxStruc + SecsPerTrk]
        sbaddr  LR,WinIDEDriveSecsPerTrk        ; set up WinIDEDriveSecsPerTrk
        STRB    R0,[LR,R1]

        LDRB    R0,[R3,#SzDefectList - MaxStruc + Heads]
        sbaddr  LR,WinIDEDriveHeads             ; set up WinIDEDriveHeads
        STRB    R0,[LR,R1]

        LDRB    R0,[R3,#WinIDENeedsInit]        ; get flag read from disc
        sbaddr  LR,WinIDEDriveInitFlags         ; set up init flags
        STRB    R0,[LR,R1]

 [ Debug21

        DLINE   "Init flag ",cc
        TSTS    R0,#1
        BNE     %F01
        DLINE   "CLEAR"
        B       %F02
01
        DLINE   "SET"
02
 ]

; SBP 23-Aug-1994 Added support for IDE LBA flag

        LDRB    R0,[R3,#WinIDEUseLBA]           ; get flag read from disc
        sbaddr  LR,WinIDEDriveLBAFlags          ; set up LBA flags
        STRB    R0,[LR,R1]

; SBP 23-Aug-1994 end of LBA flag support

; Parameters saved: the next op will initialise the drive shape if the
; drive needs it.

        CLRV


80
; Calls to drivers return here
; Don't need to put logical drive number back in disc address as
; driver will have done so.

        STRVS   R0,[SP]                 ; save error code if error
 [ Debug21
        Pull    "R0-R9,LR"              ; restore caller's regs and return
        DREG    R0,"   EndMount R0: "
        MOV     PC,LR
 |
        Pull    "R0-R9,PC"              ; restore caller's regs and return
 ]

 [ BigDisc :LAND: :LNOT:NewErrors
82
; a nicety - Mount doesn't use sector addressing, and when called,
; FileCore doesn't know the sector size yet, so if we return a new-style
; disc error, it will not print a correct address. So, convert a any
; new-style disc errors to old-style.
        AND     LR,R0,#ExternalErrorBit+DiscErrorBit
        TEQ     LR,#ExternalErrorBit+DiscErrorBit
        BNE     %BT80                   ; V still set
        BIC     R0,R0,#ExternalErrorBit+DiscErrorBit
        LDMIA   R0,{R0,R1}              ; R0=error code, R1=disc/addr
        AND     R0,R0,#MaxDiscErr       ; mask out error
        MOV     R0,R0,LSL #24           ; put error in bits 24-29
        ORR     R0,R0,#DiscErrorBit     ; set bit 31
        AND     LR,R1,#DiscBits
        BIC     R1,R1,#&FF000000        ; isolate bottom 20 bits of sector address
        BIC     R1,R1,#&00F00000        ; (29 bits of byte address)
        ORR     R0,R0,LR,LSR #8         ; put disc in bits 21-23
        ORR     R0,R0,R1,LSL #1         ; put byte address / 256 in bits 0-20
        B       %BT80                   ; V still set
 ]

;~~~~~~~~~~~~~~~

85
; invalid defect list or checksum

 [ {TRUE}
        CLRV
 |
        MOV     R0,#BadDiscErr
        SETV
 ]
        B       %BT80

 [ :LNOT:BigDisc
;*********************************************************************

WinUpdateCMOSMapSize    ROUT
;
; Update map size held in CMOS
;
; Entry:
;    R2 = b31..b29 drive number (4..7)
;    R7-> RAM copy of map sizes - 4
;    R8 = map size for this drive
;
; Exit:
;    All registers and flags preserved

        Push    "R0-R2,LR"

        STRB    R8,[R7,R2,LSR #(32-3)]
        MOV     R0,#OsByte_WriteCMOS
        MOV     R1,R2,LSR #(32-3)

 [ Debug2
        wrhex   R1
        wrhex   R8
        mess    ,"oldsize drive newsize",NL
 ]

        ADD     R1,R1,#WinniSizeCMOS-4
        MOV     R2,R8
        SWI     XOS_Byte                ; (R0-R2->R0-R2,V)
        Pull    "R0-R2,PC"
 ]

;*********************************************************************

DoSwiWinControllerType  ROUT
;
; Returns the controller type of a hard disc.
;
; Entry:
;    R0 = drive number (4..7)
;
; Exit:
;    VC => valid drive number
;          R0 = controller type
;               0 => disc not present
;               3 => ST506
;               4 => IDE
;    VS => error
;          R0 -> error block
;    All other registers preserved

        Push    "LR"

; Check drive number

        CMPS    R0,#7                           ; valid drive number?
        BHI     %FT80                           ; branch if not

; Drive number valid
; Get type from drive map

        ADD     R0,R0,#:INDEX:WinDriveTypes - 4
        LDRB    LR,[SB,R0]

; R0 = &FF => no drive
;      0/1 => IDE
;      8/9 => ST506

        TSTS    LR,#bit3                        ; 8/9?
        MOVNE   R0,#3                           ; yes, ST506
        MOVEQ   R0,#4                           ; no, IDE

        CMPS    LR,#&FF                         ; not valid? (clears V)
        MOVEQ   R0,#0

        Pull    "PC"

;****** Never fall through

80
; return bad drive error using international stuff

        baddr   R0,BadDriveErrBlk               ; R0 -> error block
        BL      copy_error                      ; (R0->R0)
        SETV
        Pull    "PC"

;*********************************************************************

        END
