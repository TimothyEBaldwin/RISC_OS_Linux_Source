REM This source code in this file is licensed to You by Castle Technology
REM Limited ("Castle") and its licensors on contractual terms and conditions
REM ("Licence") which entitle you freely to modify and/or to distribute this
REM source code subject to Your compliance with the terms of the Licence.
REM
REM This source code has been made available to You without any warranties
REM whatsoever. Consequently, Your use, modification and distribution of this
REM source code is entirely at Your own risk and neither Castle, its licensors
REM nor any other person who has contributed to this source code shall be
REM liable to You for any loss or damage which You may suffer as a result of
REM Your use, modification or distribution of this source code.
REM
REM Full details of Your rights and obligations are set out in the Licence.
REM You should have received a copy of the Licence with this source code file.
REM If You have not received a copy, the text of the Licence is available
REM online at www.castle-technology.co.uk/riscosbaselicence.htm
REM > GBPB_bash
D%=4
BS%=1024*128
F$="adfs::"+STR$D%+".$.Junk"
H%=OPENINF$
IF H%=0 THEN
 PRINT"Creating scrap file"
 H%=OPENOUTF$
 IF H%=0 PRINTF$": cannot create":END
 CLOSE#H%
 H%=OPENUPF$
 IF H%=0 PRINTF$": cannot open":END
 SYS"ADFS_FreeSpace",":"+STR$D% TO S%
 EXT#H%=S%
ELSE
 CLOSE#H%
 H%=OPENUPF$
 IF H%=0 PRINTF$": cannot open":END
 S%=EXT#H%
ENDIF
:
DIM B1% BS%,B2% BS%
PROCasm
PRINT"Initialising buffer"
FOR I%=0 TO BS%-1:B1%?I%=RND(256):NEXT
:
pass%=0
REPEAT
 PRINT"Pass ";pass%;
 P%=RND(S%)-1
 REPEAT
  L%=RND(BS%)
 UNTIL L%<=(S%-P%)
 PRINT", pos &"FNHEX(P%,8)", len &"FNHEX(L%,8)
 SYS"OS_GBPB",1,H%,B1%,L%,P% TO ,,,n%
 IF n%<>0 PRINT;n%" bytes not written":CLOSE#H%:STOP
 SYS"OS_GBPB",3,H%,B2%,L%,P% TO ,,,n%
 IF n%<>0 PRINT;n%" bytes not read":CLOSE#H%:STOP
 B%=B1%:C%=B2%:D%=L%
 IF NOT USRcheck% PROCdataerror:CLOSE#H%:STOP
 pass%+=1
UNTIL INKEY(0)<>-1
CLOSE#H%
END
:
:
DEF PROCdataerror
LOCAL K%
K%=!errpos%
PRINT"Data error at &";~K%
FOR I%=0 TO 15:PRINTFNHEX(B1%?(K%+I%),2)" ";:NEXT:PRINT
FOR I%=0 TO 15:PRINTFNHEX(B2%?(K%+I%),2)" ";:NEXT:PRINT
ENDPROC
:
:
DEF PROCasm
DIM code% &100
FOR pass%=0 TO 2 STEP 2
P%=code%
[ OPT pass%
.check%
\ Entry
\  R1 -> buf1
\  R2 -> buf2
\  R3 = size
\ Exit
\  R0 = 0 => error at !errpos%
 MOV   R0,#0
.loop%
 TEQS  R3,#0
 BEQ   done%
 LDRB  R4,[R1,R0]
 LDRB  R5,[R2,R0]
 TEQS  R4,R5
 SUBEQ R3,R3,#1
 ADDEQ R0,R0,#1
 BEQ   loop%
 STR   R0,errpos%
 MOV   R0,#0
 MOV   PC,R14
\
.done%
 MVN   R0,#0
 MOV   PC,R14
\
.errpos%
 DCD   0
]
NEXT pass%
ENDPROC
:
:
DEF FNHEX(A%,W%)=RIGHT$("0000000"+STR$~A%,W%)
