; This source code in this file is licensed to You by Castle Technology
; Limited ("Castle") and its licensors on contractual terms and conditions
; ("Licence") which entitle you freely to modify and/or to distribute this
; source code subject to Your compliance with the terms of the Licence.
; 
; This source code has been made available to You without any warranties
; whatsoever. Consequently, Your use, modification and distribution of this
; source code is entirely at Your own risk and neither Castle, its licensors
; nor any other person who has contributed to this source code shall be
; liable to You for any loss or damage which You may suffer as a result of
; Your use, modification or distribution of this source code.
; 
; Full details of Your rights and obligations are set out in the Licence.
; You should have received a copy of the Licence with this source code file.
; If You have not received a copy, the text of the Licence is available
; online at www.castle-technology.co.uk/riscosbaselicence.htm
; 
; >MyMacros


        MACRO
$lab    SemEntry   $action,$pre
$lab
        Push    "R0,R1,SB,LR"
        BL      $pre.Entry$action.Err
        MEND


        MACRO
$lab    MinOps  $firstop, $repeatop, $destreg, $srcreg, $const, $cond
        LCLA    Const
Const   SETA    $const
        LCLA    Rotate
Rotate  SETA    0
        LCLL    First
        LCLA    Bits8
        LCLA    Bit
Bit     SETA    2

        WHILE   Bit<32
         [ ($const :ROR: Bit) < Const
Const    SETA    $const :ROR: Bit
Rotate   SETA    Bit
         ]
Bit     SETA    Bit+2
        WEND

Bit     SETA    0
Bits8   SETA    0
First   SETL    {TRUE}

        WHILE   (Const <> Bits8) :LOR: First
Const   SETA    Const - Bits8

         WHILE   (Const :AND: (2_11 :SHL: Bit) = 0 ) :LAND: Bit<24
Bit      SETA    Bit+2
         WEND

Bits8   SETA    Const :AND: ( &FF :SHL: Bit )

         [ First

          [ "$srcreg"=""
$lab      $firstop.$cond $destreg, #Bits8 :ROL: Rotate
          |
$lab      $firstop.$cond $destreg, $srcreg, #Bits8 :ROL: Rotate
          ]

         |

$lab     $repeatop.$cond $destreg, $destreg, #Bits8 :ROL: Rotate
         ]

First   SETL    {FALSE}
        WEND
        MEND

;put address of $dest in $reg
        MACRO
$lab    myaddr  $reg,$dest,$cond
        ASSERT  $reg<>PC
        ASSERT  ($dest-{PC}-8)<&10000
$lab    ADD$cond $reg,PC,#($dest-{PC}-8) :AND: &FF
        ADD$cond $reg,$reg,#($dest-{PC}-4) :AND: &FF00
        MEND


        GBLA    boff
        MACRO
$lab    baddr   $reg,$dest,$cond
        ASSERT  $reg<>PC
boff    SETA    {PC}+8-($dest)
$lab    MinOps  SUB, SUB, $reg, PC, boff, $cond
        MEND


;put absolute address of $sboff in $reg
        MACRO
$lab    sbaddr  $reg,$sboff,$cond
$lab    MinOps  ADD, ADD, $reg, SB, :INDEX: ($sboff), $cond
        MEND


        MACRO
$lab    aw      $size           ;allocate word aligned
        ASSERT  {VAR} :MOD: 4=0
$lab    #       $size
        MEND


        MACRO
$lab    bit     $bitnum
$lab    *       1 :SHL: ($bitnum)
        MEND


        MACRO
        getSB
        LDR     SB,[SB]
        MEND


;point Rptr to start of disc record Rindex, must preserve flags
        MACRO
$lab    DiscRecPtr $Rptr,$Rindex,$cond
        ASSERT  $Rptr<>SB
        ASSERT  $Rptr<>PC
 [ BigDisc
 [ DebugDR
        Push    "SB,LR, R0"
        MOV     R0, $Rindex
        SavePSR SB
        BICS    LR, R0, #7
        BEQ     %FT01
        DREG    R0, "DiscRecPtr: illegal disc:"
        DREG    PC, "PC:"
01
        RestPSR SB,,f
        Pull    "SB,LR,R0"
 ]
  [ BigShare
   [ BigDir
      ASSERT    SzDiscRec=56
      RSB$cond  $Rptr, $Rindex, $Rindex, ASL #3 ;*7
      ADD$cond  $Rptr, SB, $Rptr, ASL #3         ;7*8=56
   |
        ASSERT  SzDiscRec=48
        ADD$cond $Rptr, $Rindex, $Rindex, ASL #1 ;*3
        ADD$cond $Rptr,SB,$Rptr,ASL #4           ;SB+Rindex*SzDiscRec
   ]
  |
        ASSERT  SzDiscRec=44
   [ $Rptr<>$Rindex
        ADD$cond $Rptr, $Rindex, $Rindex, ASL #1 ;*3
        ADD$cond $Rptr, $Rptr, $Rindex, ASL #3   ;+*8=*11
        ADD$cond $Rptr, SB, $Rptr, ASL #2        ;SB+Rindex*SzDiscRec
   |
        Push    "SB"
        ADD$cond SB, $Rindex, $Rindex, ASL #1    ;*3
        ADD$cond $Rptr, SB, $Rindex, ASL #3      ;+*8=*11
        Pull    "SB"
        ADD$cond $Rptr, SB, $Rptr, ASL #2        ;SB+Rindex*SzDiscRec
   ]
  ]
 |
        ASSERT  SzDiscRec=40
        ADD$cond $Rptr,$Rindex,$Rindex,ASL #2   ;*5
        ADD$cond $Rptr,SB,$Rptr,ASL #3          ;SB+Rindex*SzDiscRec
 ]
        MinOps  ADD, ADD, $Rptr, $Rptr, (:INDEX:DiscRecs), $cond
        MEND


;point Rptr to start of drive record Rindex, must preserve flags
        MACRO
$lab    DrvRecPtr $Rptr,$Rindex,$cond
        ASSERT  $Rptr<>SB
        ASSERT  $Rptr<>PC

 [ DebugDR
        Push    "SB,LR, R0"
        MOV     R0, $Rindex
        SavePSR SB
        BICS    LR, R0, #7
        BEQ     %FT01
        DREG    R0, "DrvRecPtr: illegal disc:"
        DREG    PC, "PC:"
01
        RestPSR SB,,f
        Pull    "SB,LR,R0"
 ]

 [ DynamicMaps
        ASSERT  SzDrvRec=36
        ADD$cond        $Rptr, $Rindex, $Rindex, LSL #3
        ADD$cond        $Rptr, SB, $Rptr, LSL #2
 |

   [ BigDisc
        ASSERT  SzDrvRec=24
        ADD$cond $Rptr,$Rindex,$Rindex,LSL #1
        ADD$cond $Rptr,SB,$Rptr,LSL#3
   |
        ASSERT  SzDrvRec=20
        ADD$cond $Rptr,$Rindex,$Rindex,LSL #2
        ADD$cond $Rptr,SB,$Rptr,LSL#2
   ]
 ]
        MinOps  ADD, ADD, $Rptr, $Rptr, (:INDEX:DrvRecs), $cond
        MEND


        GBLS    CaseConvertReg
        GBLS    CaseConvertType
        MACRO
        Internat_CaseConvertLoad  $UR,$Type
CaseConvertReg SETS    "$UR"
CaseConvertType SETS   "$Type"
        LDR     $UR, $Type.CaseTable
        MEND


        MACRO
        Internat_UpperCase      $Reg, $UR
        ASSERT  $UR = $CaseConvertReg
        ASSERT  CaseConvertType = "Upper"
        CMP     $UR, #BadPtr
        LDRNEB  $Reg, [$UR, $Reg]
        BNE     %FT01
        ASCII_UpperCase $Reg, $UR
        MOV     $UR, #BadPtr ; Restore $UR after use as a temp register
01
        MEND


;FOLLOWING MACROS ONLY FOR DEBUG

        MACRO
        mess    $cond,$text,$newline
      [ "$cond" <> ""
        B$cond  %FT01
        B       %FT02
      ]
01
      [ "$newline"="NL"
        DLINE   $text
      |
        DLINE   $text,cc
      ]
02
        MEND


        MACRO
        regdump $cond
      [ "$cond" <> ""
        B$cond  %FT01
        B       %FT02
      ]
01
        DLINE   " R0       R1       R2       R3       R4       R5       R6       R7"
        DREG    r0, " ",cc
        DREG    r1, " ",cc
        DREG    r2, " ",cc
        DREG    r3, " ",cc
        DREG    r4, " ",cc
        DREG    r5, " ",cc
        DREG    r6, " ",cc
        DREG    r7, " "
        DLINE   " R8       R9       R10      R11      R12      R13"
        DREG    r8, " ",cc
        DREG    r9, " ",cc
        DREG    r11, " ",cc
        DREG    r12, " ",cc
        DREG    r13, " "
02
        MEND

        MACRO
        FileBufs
        Push    "R0-R8"
        mess    ,"Buffer   Flags    Next     Prev     Pri Off  Fcb      Older    Younger",NL
        MOV     R0, R9
00
        LDMIA   R0, {R1-R8}
        EOR     R4, R4, R8, LSL #24
        DREG    R0
        DREG    R1
        DREG    R2
        DREG    R3
        DREG    R4
        DREG    R5
        DREG    R6
        DREG    R7
        DLINE   " "
        MOV     R0, R3
        TSTS    R0, #BadPtrBits
        BNE     %FT01
        TEQS    R0, R9
        BNE     %BT00
01
        Pull    "R0-R8"
        MEND


        MACRO
        DebugError $PrefixStr
        BVC     %FT01
      [ {FALSE} ; Used with internal error numbers all over the place
        Push    "R0"
        LDR     R0, [R0]
        DREG    R0, "$PrefixStr",cc
        LDR     R0, [SP]
        ADD     R0, R0, #4
        DSTRING R0, " = "
        Pull    "R0"
      |
        DREG    R0, "$PrefixStr ="
      ]
01
        MEND


        MACRO
$l      Read3   $cond
$l
      [ NoARMv6 :LOR: NoUnaligned
        BL$cond Read3
      |
        LDR$cond LR, [R0]
        BIC$cond LR, LR, #&FF000000
      ]
        MEND


        MACRO
$l      ReadWord $cond
$l
      [ NoARMv6 :LOR: NoUnaligned
        BL$cond ReadWord
      |
        LDR$cond LR, [R0]
      ]
        MEND


        MACRO
$l      Write3  $cond
$l
      [ NoARMv6 :LOR: NoUnaligned
        BL$cond Write3
      |
        STR$cond.H R1, [R0]
        MOV$cond   R1, R1, ROR #16 ; note that this corrupts R1, unlike the BL
        STR$cond.B R1, [R0, #2]
      ]
        MEND


        MACRO
$l      WriteWord $cond
$l
      [ NoARMv6 :LOR: NoUnaligned
        BL$cond WriteWord
      |
        STR$cond R1,[R0]
      ]
        MEND


        END
