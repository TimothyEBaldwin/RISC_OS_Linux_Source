; This source code in this file is licensed to You by Castle Technology
; Limited ("Castle") and its licensors on contractual terms and conditions
; ("Licence") which entitle you freely to modify and/or to distribute this
; source code subject to Your compliance with the terms of the Licence.
; 
; This source code has been made available to You without any warranties
; whatsoever. Consequently, Your use, modification and distribution of this
; source code is entirely at Your own risk and neither Castle, its licensors
; nor any other person who has contributed to this source code shall be
; liable to You for any loss or damage which You may suffer as a result of
; Your use, modification or distribution of this source code.
; 
; Full details of Your rights and obligations are set out in the Licence.
; You should have received a copy of the Licence with this source code file.
; If You have not received a copy, the text of the Licence is available
; online at www.castle-technology.co.uk/riscosbaselicence.htm
; 

        GET MixerDevice

; Workspace
                     ^ 0
                     # HALDevice_Mixer_Size_0_1 ; Mixer device at start of WS
SoftMixSoundRMA      # 4 ; Will be set to 0 if device is orphaned (OS_Hardware remove device call failed)
SoftMixMute          # 4 ; as per SetMix
SoftMixGain          # 4 ; as per SetMix
SoftMixWorkSpaceSize * @

SoftMixInit ROUT
        ; In: r3 = audio controller device
        ;     r12=SoundRMA
        ; Out: r0 = created device, or error
        ; Create & register a software mixer if the device doesn't have a hardware mixer
        Entry   "r0-r3,r8,r11"
        MOV     r11, r3
        MOV     r0, #ModHandReason_Claim
        MOV     r3, #SoftMixWorkSpaceSize
        SWI     XOS_Module
        FRAMSTR r0, VS
        EXIT    VS
        MOV     r0, #0
10
        SUBS    r3, r3, #4
        STR     r3, [r2]
        BNE     %BT10
        LDR     r0, =HALDeviceType_Audio+HALDeviceAudio_Mixer
        STRH    r0, [r2, #HALDevice_Type]
        LDR     r0, =HALDeviceID_Mixer_Software
        STRH    r0, [r2, #HALDevice_ID]
        LDR     r0, =HALDeviceBus_Pro + HALDeviceProBus_Core
        STR     r0, [r2, #HALDevice_Location]
        LDR     r0, =1 ; API 0.1
        STR     r0, [r2, #HALDevice_Version]
        ADR     r0, SoftMixDev_Desc
        STR     r0, [r2, #HALDevice_Description]
        ADR     r0, SoftMixDev_Activate
        STR     r0, [r2, #HALDevice_Activate]
        ADR     r0, SoftMixDev_Deactivate
        STR     r0, [r2, #HALDevice_Deactivate]
        ADR     r0, SoftMixDev_Reset
        STR     r0, [r2, #HALDevice_Reset]
        ADR     r0, SoftMixDev_Sleep
        STR     r0, [r2, #HALDevice_Sleep]
        MVN     r0, #0
        STR     r0, [r2, #HALDevice_Device]
        STR     r11, [r2, #HALDevice_MixerCtrlr]
        MOV     r0, #1
        STR     r0, [r2, #HALDevice_MixerChannels]
        ADR     r0, SoftMixDev_GetFeatures
        STR     r0, [r2, #HALDevice_MixerGetFeatures]
        ADR     r0, SoftMixDev_SetMix
        STR     r0, [r2, #HALDevice_MixerSetMix]
        ADR     r0, SoftMixDev_GetMix
        STR     r0, [r2, #HALDevice_MixerGetMix]
        ADR     r0, SoftMixDev_GetMixLimits
        STR     r0, [r2, #HALDevice_MixerGetMixLimits]
        STR     r12, [r2, #SoftMixSoundRMA]
        MOV     r0, #0
        STR     r0, [r2, #SoftMixMute]
        STR     r0, [r2, #SoftMixGain]
        MOV     r0, r2
        ; Register as a HAL device
        MOV     r8, #OSHW_DeviceAdd
        SWI     XOS_Hardware
        MOVVC   r0, r2
        FRAMSTR r0
        EXIT    VC
        MOV     r0, #ModHandReason_Free
        SWI     XOS_Module
        SETV
        EXIT

SoftMixDev_Desc
        = "SoundDMA software mixer", 0
        ALIGN

SoftMixShutdown ROUT
        ; In: r0 -> mixer device
        ;     r12=SoundRMA
        ; Deregister and free soft mixer device
        Entry   "r0-r2,r8"
        ; Deregister device
        MOV     r8, #OSHW_DeviceRemove
        SWI     XOS_Hardware
        BVS     %FT50
        ; Free memory
        MOV     r0, #ModHandReason_Free
        FRAMLDR r0,,r2
        SWI     XOS_Module
        EXIT
50
        ; Couldn't release device - orphan it by setting SoftMixSoundRMA to 0
        MOV     r0, #0
        STR     r0, [r2, #SoftMixSoundRMA]
        EXIT

SoftMixLimit * -40*16 ; -40dB

SoftMixDev_Activate
        LDR     a1, [a1, #SoftMixSoundRMA]
        CMP     a1, #0
        MOVNE   a1, #1
        ; Fall through...
SoftMixDev_Deactivate
        MOV     pc, lr

SoftMixDev_Reset
        ; Program default mix params
        MOV     a2, #0
        MOV     a3, #0
        MOV     a4, #0
        B       SoftMixDev_SetMix

SoftMixDev_Sleep
        MOV     a1, #0
        MOV     pc, lr

SoftMixDev_GetFeatures
        LDR     a1, =MixerCategory_System :SHL: MixerFeaturesShift_Category
        MOV     pc, lr

SoftMixDev_GetMixLimits
        LDR     a1, =SoftMixLimit
        MOV     a2, #0
        MOV     a4, #1
        STMIA   a3, {a1-a2,a4}
        MOV     pc, lr

SoftMixDev_SetMix ROUT
        Entry   "v1-v5"
        ; a1 = mixer device
        ; a2 = channel
        ; a3 = mute flag
        ; a4 = gain, in dB*16
        LDR     v1, [a1, #SoftMixSoundRMA]
        CMP     v1, #0
        EXIT    EQ
        ; Clamp to within limits
        CMP     a4, #0
        MOVGT   a4, #0
        CMP     a4, #SoftMixLimit
        LDRLT   a4, =SoftMixLimit
        AND     a3, a3, #MixerMixFlag_Mute
        STR     a3, [a1, #SoftMixMute]
        STR     a4, [a1, #SoftMixGain]
        ; Apply settings if this is the active device
        LDR     a2, [a1, #HALDevice_MixerCtrlr]
        LDR     a1, [v1, #HALDevice]
        CMP     a1, a2
        EXIT    NE

10
        CMP     a3, #0
        LDR     v5, =SoundLevel0Base
        MOVNE   a3, #0
        BNE     %FT20
        ; Calculate scale factor
        RSB     a4, a4, #0
        ADR     v2, SoftMixTable
        MOV     v3, a4, LSR #4
        ADD     v2, v2, v3, LSL #1
        LDRH    v3, [v2], #2
        LDRH    v4, [v2]
        AND     a4, a4, #&F
        RSB     a3, a4, #16
        MUL     v3, a3, v3
        MLA     v3, a4, v4, v3
        MOV     a3, v3, LSR #4
20
        ; IRQs off to avoid buffer fill code reading new value while we're updating it (could cause glitches with NEON version due to 32768 overflow)
        PHPSEI
        STR     a3, [v1, #SoftMixAmount]
        LDRB    a2, [v5, #Flags]
        CMP     a3, #32768 ; +0 dB?
        BICEQ   a2, a2, #DoSoftMix
        ORRNE   a2, a2, #DoSoftMix
        STRB    a2, [v5, #Flags]
        PLP
        EXIT

SoftMixDev_Apply ALTENTRY
        ; In:
        ;  r2 = mute flag
        ;  r3 = gain
        ;  r5 = SoundRMA
        ; Out:
        ;  r0-r3, r12 corrupt
        ; Calculate scale factor and apply, regardless of whether mixer currently in use
        MOV     v1, r5
        B       %BT10

SoftMixDev_GetMix
        ; a1 = mixer device
        ; a2 = channel
        LDR     a2, [a1, #SoftMixGain]
        LDR     a1, [a1, #SoftMixMute]
        MOV     pc, lr

; Volume table for the software mixer. The table gives the required sample
; scale factor for each decibel level, for sub-decibel accuracy we simply
; interpolate between table entries.
;
; Calculated with:
;
;   10 FOR DB=0 TO -40 STEP -1
;   20 PRINT "        DCW ";INT(32768*10^(DB/20))
;   30 NEXT DB
;
; Note that for DB=0 we never actually run the software mixer, this is both
; to avoid unnecessary overhead in the IRQ handler, and to avoid an overflow
; in the NEON routine (due to use of VQDMULH)

SoftMixTable
        DCW 32768
        DCW 29204
        DCW 26028
        DCW 23197
        DCW 20675
        DCW 18426
        DCW 16422
        DCW 14636
        DCW 13045
        DCW 11626
        DCW 10362
        DCW 9235
        DCW 8230
        DCW 7335
        DCW 6538
        DCW 5827
        DCW 5193
        DCW 4628
        DCW 4125
        DCW 3676
        DCW 3276
        DCW 2920
        DCW 2602
        DCW 2319
        DCW 2067
        DCW 1842
        DCW 1642
        DCW 1463
        DCW 1304
        DCW 1162
        DCW 1036
        DCW 923
        DCW 823
        DCW 733
        DCW 653
        DCW 582
        DCW 519
        DCW 462
        DCW 412
        DCW 367
        DCW 327
        ALIGN

        END
