REM    BufferWriteC

REM  This program stops output line being intermingled
REM  by character when doing parallel builds.

REM  Copyright (c) 2013, Timothy Baldwin
REM
REM Licensed under the Apache License, Version 2.0 (the "License");
REM you may not use this file except in compliance with the License.
REM You may obtain a copy of the License at
REM
REM     http://www.apache.org/licenses/LICENSE-2.0
REM
REM Unless required by applicable law or agreed to in writing, software
REM distributed under the License is distributed on an "AS IS" BASIS,
REM WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
REM See the License for the specific language governing permissions and
REM limitations under the License.
REM

code_size% = 256
line_size% = 1024 * 1024
gettimeofday% = &4E
REM Allocate memory from Module Area
SYS "OS_Module",6,,,code_size% + line_size% TO ,,code%

FOR opt% = 0 TO 2 STEP 2
P%=code%
[OPT opt%

.word_handler%
; Read time?
teq     r0, #14
movne   pc, lr

stmfd   sp!, {r0-r4, r7, lr}

; Read 5 byte time?
ldrb    r0, [r1]
teq     r0, #3
ldmnefd sp!, {r0-r4, r7, pc}
mov     r2, r1 ; Save result pointer

; Call gettimeofday()
sub     sp, sp, #8
mov     r7, #gettimeofday%
mov     r0, sp
mov     r1, #0
swi     "IXSupport_LinuxSyscall"

ldr     r3, magic%
ldr     r4, [sp, #4]
mov     r7, #0
umull   r1, r4, r3, r4
mov     r3, #100
ldr     r0, [sp], #8
mov     r4, r4, lsr #13
umlal   r4, r7, r3, r0
ldr     r1, lo%
adds    r4, r4, r1
str     r4, [r2]
adc     r7, r7, #&33
strb    r7, [r2, #4]
msr     cpsr_f, #0
ldmfd   sp!, {r0-r4, r7, lr, pc}

.magic%
dcd     -776530087

.lo%
dcd     &6E996A00

.vector_handler%
push    {r0, r1, r2, lr}

; Check for redirection
mvn     r0, #0
mvn     r1, #0
swi     "OS_ChangeRedirection"
teq     r1, #0
popne   {r0, r1, r2, pc}

; Reload r0
ldr     r0, [sp], #4

; Check for CR
teq     r0, #13
popeq   {r1, r2, lr, pc}

; Add to line buffer
adr     r1, line%
ldr     r2, index%
strb    r0, [r1, r2]

teq     r0, #7
addne   r2, r2, #1
streqb  r0, found7%
ldrb    lr, found7%
teqne   r0, lr ; EOL?
teqne   r2, #line_size% ; Too large?
str     r2, index%
popne   {r1, r2, lr, pc}

; Empty buffer?
teq     r2, #0
popeq   {r1, r2, lr, pc}

push    {r0, r3, r4, r7}

; Write to linux standard output
.write%
mov     r0, #1
mov     r7, #4
swi     "IXSupport_LinuxSyscall"
add     r1, r1, r0
sub     r2, r2, r0
cmp     r2, #1 ; More?
cmplt   r0, #1 ; Error or EOF?
bge     write%

; Check for *Spool file
mov     r0, #199
mov     r1, #0
mvn     r2, #0
swi     "OS_Byte"
teq     r1, #0

; Reload and zero index pointer
ldr     r3, index%
mov     r4, #0
str     r4, index%

; Write to *Spool file
mov     r0, #2
adr     r2, line%
swine   "OS_GBPB"

; Return
msr     cpsr_f, #0
pop     {r0, r3, r4, r7}
pop     {r1, r2, lr, pc}

.index%
dcd     0

.found7%
dcd     10

.line%

]:NEXT

REM Claim Wordv and Wrchv
PRINT '"Installing BufferWriteC, RISC OS screen disabled.";
SYS "OS_Claim",7,word_handler%,0
SYS "OS_Claim",3,vector_handler%,0
SYS "OS_RemoveCursors"
PRINT "..done"'
END
