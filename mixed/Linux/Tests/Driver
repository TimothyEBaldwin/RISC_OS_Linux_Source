LIBRARY "RAM:Lib"
PROCinit("Test Driver")
PROCnull
Z%=FNhimem_test

PROCprint("Testing new task...")
SYS "Wimp_StartTask", "WimpSlot -min 32K -max 64K" TO task1%
PROCok
PROCassert("task1% = 0")
PROCnull

PROCprint("Starting slaves...")
SYS "Wimp_StartTask", "/RAM:Slave" TO tasks%(1)
SYS "Wimp_StartTask", "/RAM:Slave" TO tasks%(2)
PROCassert("tasks%(1)<>0 AND tasks%(2)<>0")
PROCok
PROCnull

PROCcall(1,"FNhimem_test")

PROCcall_all("FNcheck_zero(&20000,&10000)")


FOR I%=0 TO 2
PROCprint(CHR$10+"Testing Wimp_TransferBlock from "+STR$I%+" to "+STR$((I%+1) MOD 3)+"."+CHR$10)
PROCcall_all("FNset_mem(&20000,&10000,tasks%(0))")
PROCprint("Running Wimp_TransferBlock from "+STR$I%+" to "+STR$((I%+1) MOD 3)+"...")
SYS "Wimp_TransferBlock", tasks%(I%), &24000, tasks%((I%+1) MOD 3), &28000, &4000
PROCok
PROCcall(I%,"FNcheck_mem(&20000,&10000,tasks%(0))")
PROCcall(I%+1,"FNcheck_mem(&20000,&8000,tasks%(0))")
PROCcall(I%+1,"FNcheck_mem(&28000,&4000,&"+STR$~(tasks%(I%) EOR &C000)+")")
PROCcall(I%+1,"FNcheck_mem(&2C000,&4000,tasks%(0))")
PROCcall(I%+2,"FNcheck_mem(&20000,&10000,tasks%(0))")
NEXT

SYS "OS_PlatformFeatures", 0 TO F%
PROCprint(CHR$10+"OS_PlatformFeatures 0 flags: "+FNbin(F%)+CHR$10)


PROCprint("Tests Completed"+FNnl)
SYS "IXSupport_LinuxSyscall",0,,,,,,,1
STOP

DEF PROCnull
PROCprint("Waiting for null poll...")
REPEAT
  SYS "Wimp_Poll",0,buffer% TO R%
UNTIL R%=0
PROCok
ENDPROC

DEF PROCcall_all(C$)
LOCAL T%
FOR T%=0 TO 2
PROCcall(T%,C$)
NEXT
ENDPROC

DEF PROCcall(T%,C$)
T%=T% MOD 3
IF T%=0 THEN Z%=EVAL(C$):ENDPROC
buffer%!0=256
buffer%!12=0
buffer%!16=test_message%
$(buffer%+20) = C$
SYS "Wimp_SendMessage", 17, buffer%, tasks%(T%)

REPEAT
  SYS "Wimp_Poll",0,buffer% TO R%
UNTIL (R%=17 OR R%=18) AND buffer%!16=test_message%
REM =VAL($(buffer%+20))
ENDPROC
