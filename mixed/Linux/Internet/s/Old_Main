        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:ModHand
	GET	Hdr:LinuxSyscalls

	AREA    |!!Module$$Code|, CODE, READONLY
module
	DCD	0
	DCD	0
	DCD	0
	DCD	0
	DCD	title - module
	DCD	help - module
	DCD	0
	DCD	0x41200
	DCD	swi - module
	DCD	swi_table - module
	DCD	0
	DCD	0
	DCD	flags - module

        PVOpsImports

title	DCB	"Internet",0
help	DCB	"Internet",9,"0.00 (01 Jan 2018) Linux thunks"
swi_table
	DCB	"Socket",0
	DCB	"Creat",0
	DCB     "Bind",0
	DCB     "Listen",0
	DCB     "Accept",0
	DCB     "Connect",0
	DCB     "Recv",0
	DCB     "Recvfrom",0
	DCB     "Recvmsg",0
	DCB     "Send",0
	DCB     "Sendto",0
	DCB     "Sendmsg",0
	DCB     "Shutdown",0
	DCB     "Setsockopt",0
	DCB     "Getsockopt",0
	DCB     "Getpeername",0
	DCB     "Getsockname",0
	DCB     "Close",0
	DCB     "Select",0
	DCB     "Ioctl",0
	DCB     "Read",0
	DCB     "Write",0
	DCB     "Stat",0
	DCB     "Readv",0
	DCB     "Writev",0
	DCB     "Gettsize",0
	DCB     "Sendtosm",0
	DCB     "Sysctl",0
	DCB     "Accept_1",0
	DCB     "Recvfrom_1",0
	DCB     "Sendmsg_1",0
	DCB     "Getpeername_1",0
	DCB     "Getsockname_1",0
	DCB     "InternalLookup",0
	DCB     0
	ALIGN

flags	DCD	ModuleFlag_32bit

swi	STMFD	sp!, {r0-r7, r11, lr}
        CMP     r11, #(%FT20 - %FT10) / 4
        ADDLO   pc, pc, r11, lsl #2
        B       bad_swi
10      B       socket_creat
        B       socket_bind
        B       socket_listen
        B       socket_accept
        B       socket_connect
        B       socket_recv
        B       bad_swi
        B       bad_swi
        B       socket_send
        B       bad_swi
        B       bad_swi
        B       socket_shutdown
        B       socket_setsockopt
        B       socket_getsockopt
        B       socket_getpeername
        B       socket_getsockname
        B       socket_close
        B       bad_swi
        B       bad_swi
        B       socket_read
        B       socket_write
        B       bad_swi
        B       socket_readv
        B       socket_writev
20

        MACRO
        Simple  $name
socket_$name
        LDR     r7, =__NR_$name
        B       syscall_and_out
        MEND

        Simple  bind
        Simple  listen
        Simple  accept
        Simple  connect
        Simple  recv
        Simple  send
        Simple  shutdown
        Simple  getpeername
        Simple  getsockname
        Simple  close
        Simple  read
        Simple  write
        Simple  readv
        Simple  writev

socket_getsockopt
        LDR     r7, =__NR_getsockopt
        B       %FT10
socket_setsockopt
        LDR     r7, =__NR_setsockopt
10      TEQ     r1, #6
        BEQ     syscall_and_out
        ; FIXME IP and socket options
        B       bad_swi

socket_creat
        TEQ     r0, #2
        BNE     no_protocol ; Must be AF_INET
        TEQ     r1, #1
        TEQNE   r1, #2
        BNE     no_protocol ; Must be SOCK_STREAM or SOCK_DGRAM
        LDR     r7, =__NR_socket
        B       syscall_and_out

syscall_and_out
        BL      __PVirt_LinuxSyscall
        CMP     r0, #-4096
        BHI     error_return
        MSR     CPSR_f, #0
        ADD     sp, sp, #4
        LDMFD	sp!, {r1-r7, r11, pc}

no_protocol
	LDR    r0, =-ix_EPROTONOSUPPORT
        B       error_return

bad_swi
	LDR    r0, =-ix_ENOSYS
error_return
        MOV     r8, r0        ; Save error number

	MOV	r4, sp        ; r4 = registers to print
	SUB	sp, sp, #256  ; Reserve workspace
	MOV     r2, #256      ; Buffer size for OS_ConvertHex8
	MOV	r1, sp        ; Buffer pointer
	MOV	r5, #32       ; Space
	MOV	r6, #10       ; Number of registers to print

30
        fvSWI	OS_ConvertHex8
	STRB	r5, [r1], #1
	LDR	r0, [r4], #4
	SUBS	r6, r6, #1
	BNE	%BT30

	; Add newline to end of string
	MOV	r5, #10
	STRB	r5, [r1, #-1]

	; Write string to Linux standard error stream
	SUB	r2, r1, sp
	MOV	r1, sp
	MOV	r0, #2
	MOV	r7, #__NR_write
	BL	__PVirt_LinuxSyscall

	; Return to RISC OS
	ADD	sp, sp, #256 + 4
        MOV     r0, r8        ; Restore error number
	fvSWI   0x0e0204 ; XIXSupport_ConvertError
	LDMFD	sp!, {r1-r7, r11, lr}
	myBX   lr

no_sys
        DCD     0x1E6
        DCB     "SWI not implemented by Linux Internet", 0

no_protocol_message
        DCD     0x20E2B
        DCB     "Protocol not supported", 0
	END
