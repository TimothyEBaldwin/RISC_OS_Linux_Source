|================================================================|
RUNVERSION
|                                                                |
| Copyright (C) Acorn Computers Ltd., 1996                       |
|================================================================|

IF "<Inet$Path>" = "" THEN Set Run$Path <Run$Path>,<Obey$Dir>.bin.
Set Inet$Path <Obey$Dir>.
Set InetDBase$Path <Obey$Dir>.files.
If "<Inet$Started>" = "Yes" THEN Obey

Unset Inet$Error

RMEnsure UtilityModule 3.50 Error This Internet application requires RISC OS 3.50 or later to run
Run Inet:utils.CheckMem
Set System$Path_Message System resources not found. Please run !System before !Internet.
IF "<BootResources$Path>" = "" THEN Set BootResources$Path <Boot$Dir>.Resources.
IF "<System$Path>" = "" THEN IfThere BootResources:!System Then /BootResources:!System

Set Alias$RMIfThere SetEval Inet$TestEval 1|MRMEnsure %%1 0 SetEval Inet$TestEval 0|MIf Inet$TestEval THEN %%0 %%1|MUnset Inet$TestEval
Set Alias$CheckError IF "|<Inet$Error>" <> "" THEN Set Boot$Error |<Inet$Error>||MError |<Inet$Error>

RMIfThere RMKill Net
RMIfThere RMKill NetG
RMIfThere RMKill NetI
RMIfThere RMKill ShareFS
RMIfThere RMKill Freeway
X Unplug InternetA
X Unplug Netmsgs
X Unplug Accmsgs

| An extant Mbuf Manager cannot usually be killed
SetEval Inet$BadMbufManager 0
RMEnsure MbufManager 0.17 SetEval Inet$BadMbufManager 1
IF Inet$BadMbufManager THEN RMEnsure MbufManager 0.00 SetEval Inet$BadMbufManager 0
If Inet$BadMbufManager THEN X Unplug MbufManager
If Inet$BadMbufManager THEN Error Please shutdown the computer, then restart it
Unset Inet$BadMbufManager

| See if AUNMsgs are expected
SetEval Inet$NeedsAUNMsgs 0
RMEnsure Internet 5.61 SetEval Inet$NeedsAUNMsgs 1
If Inet$NeedsAUNMsgs THEN RMEnsure AUNMsgs 0.07 RMLoad System:Modules.Network.AUNMsgs
If Inet$NeedsAUNMsgs THEN RMEnsure AUNMsgs 0.07 Error !Internet requires AUNMsgs 0.07 or later
Unset Inet$NeedsAUNMsgs

| Check the core of the stack is new enough
RMEnsure Internet    5.40 RMLoad System:Modules.Network.Internet
RMEnsure Internet    5.40 Error !Internet requires Internet 5.40 or later
RMEnsure MbufManager 0.17 RMLoad System:Modules.Network.MManager
RMEnsure MbufManager 0.17 Error !Internet requires MbufManager 0.17 or later

SetEval Inet$KickFiler 0

| Bring back up the rest of the (enabled/configured) stack
IfThere Choices:Internet.Startup Then Run Choices:Internet.Startup Else Error Your !Internet application has not yet been configured. Please use InetSetup to configure it.
IfThere Choices:Internet.User Then Run Choices:Internet.User

| Compatibility only
Set Inet$Startup Choices:Internet.Startup

RMIfThere RMReInit NFS

Sysctl -ew net.inet.udp.checksum=1
CheckError

IF "<Inet$IsGateway>" LEFT 1 = "Y" THEN InetGateway on
IF "<Inet$RouteDOptions>" <> "" THEN RMLoad System:Modules.Network.RouteD

If "<Wimp$State>" = "desktop" AND Inet$KickFiler THEN Filer_Run Inet:utils.NewFiler

Unset Inet$KickFiler
Unset Alias$RMIfThere
Unset Alias$CheckError

Set Inet$Started Yes
