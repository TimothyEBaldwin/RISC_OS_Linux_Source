|================================================================|
| Simple AUN BootNet - used only for softloading AUN onto a      |
| standard AUN station.                                          |
|                                                                |
| Only for use of InetSetup-generated startup scripts.           |
|                                                                |
| Copyright (C) Acorn Computers Ltd., 1996                       |
|=================================================================
IF "<Inet$HostName>" <> "" AND "<Inet$HostName>" <> "ARM_NoName" THEN Error BootNet should not be run after !Internet. Use InetSetup to configure AUN + TCP/IP.
IF "<Wimp$State>" = "desktop" THEN Error BootNet should only be run outside the desktop
Set Alias$RMIfThere SetEval Inet$TestEval 1|MRMEnsure %%1 0 SetEval Inet$TestEval 0|MIf Inet$TestEval THEN %%0 %%1|MUnset Inet$TestEval
|
| Check for RISC OS 3.50 or later
|
RMEnsure UtilityModule 3.50 Error This AUN BootNet requires RISC OS 3.50 or later
Set System$Path_Message System resources not found. Please run !System before !BootNet
IF "<System$Path>" = "" THEN IfThere Boot:Resources.!System then Run Boot:Resources.!System
|
| Boot !Internet
|
Run <Obey$Dir>.^.!Boot
|
| Required to get Net to load
|
Set BootNet$File Net
|
| Load Net
|
RMEnsure Net 6.17 RMLoad System:Modules.Network.Net
RMEnsure Net 6.17 Error you need Net 6.17 or later to use AUN
|
| Load a device driver, if required
|
IF "<Net$Device>" <> "" THEN Run System:Modules.Network.<Net$Device>
|
| If necessary, restart Access
|
RMIfThere RMReInit ShareFS
|
| Start up NetFS, etc.
|
RMEnsure NetFS 0 RMReInit NetFS
RMEnsure NetFS 5.79 RMEnsure NetUtils 0.99 Run System:Modules.Network.NetUtils
RMEnsure NetPrint 0 RMReInit NetPrint
RMEnsure NetFiler 0 RMReInit NetFiler

Unset Inet$Restart
Unset Alias$RMIfThere
